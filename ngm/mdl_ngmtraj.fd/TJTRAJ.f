     
      SUBROUTINE TJTRAJ                                                 
C                                                                       
C$$$  SUBPROGRAM DOCUMENTATION BLOCK  ***                               
C                                                                       
C SUBPROGRAM:    TJTRAJ      COMPUTES 3D TRAJECTORIES                   
C   PRGMMR: R. M. REAP       ORG: W/OSD21             DATE: 95-11-01    
C                                                                       
C ABSTRACT:  COMPUTES 3D TRAJECTORIES IN 2 HR STEPS                     
C                                                                       
C PROGRAM HISTORY LOG:                                                  
C   66-12-01  R. M. REAP                                                
C   74-02-01  CONVERT TO IBM SYSTEM                                     
C   92-09-18  CONVERT TO FORTRAN 77                                     
C   95-11-01  CONVERT TO CRAY                                           
C                                                                       
C USAGE:                                                                
C   SEE BELOW FOR MDL STANDARDS                                         
C     PROGRAM TJTRAJ                                                    
C        NOV 1995      R. M. REAP         MDL          CRAY             
C        PURPOSE                                                        
C        COMPUTES THREE-DIMENSIONAL TRAJECTORIES FOR PREDICTION OF      
C        SELECTED PARAMETERS RELATED TO SEVERE STORM OCCURRENCE.        
C        INPUT WIND DATA ARE EXTRACTED FROM DISK DATA SETS GENERATED BY 
C        THE CURRENT NCEP PRIMITIVE EQUATION MODELS.  THE INFLUENCE OF  
C        A DETAILED TERRAIN GEOMETRY IS INCLUDED BY NOT ALLOWING THE    
C        TRAJECTORIES TO GO BELOW GROUND.  X,Y,P CO-ORDINATES DEFINING  
C        THE PARCEL POSITIONS (SIX HOUR INTERVALS) ARE OUTPUT AT        
C        PROGRAM COMPLETION.                                            
C        DATA SET USE                                                   
C           FT81,FT82,FT83 (OUTPUT)                                     
C        COMMON BLOCKS                                                  
C           KDMY,BLOCK4,BLOCK5,BLOCKF                                   
C        SUBPROGRAMS CALLED:                                            
C           REDISK,OVERT,INTPL1,MARIAH,OUTPT3                                  
C          LIBRARY:                                                     
C           COMMON,W3LIB,TDLLIB                                         
C                                                                       
C ATTRIBUTES:                                                           
C   LANGUAGE: FORTRAN 90                                                
C   MACHINE:  CRAY                                                      
C$$$                                                                    
C                                                                       
      COMMON KDMY,PLX(13,17,4),PLY(13,17,4),PLP(13,17,4),NSTAP,AI,AJ,   
     1XCORD(150),YCORD(150),RHUM(13,17,4),INCR                          
      COMMON/BLOCK4/U(5,5,5),V(5,5,5),W(5,5,5),UP(5,5,5),VP(5,5,5),     
     1WP(5,5,5)                                                         
      COMMON/BLOCK5/XXX(13,17,4),YYY(13,17,4),ZZZ(13,17,4)              
      COMMON/BLOCKF/ZDF(13,17,4),JSW                                    
      COMMON/BLOCKP/KPRINT                                              
      DIMENSION TERRA(51,65),LTAU(3),AP(858),APP(26,33),P(5),           
     1KU(26,33,5),KV(26,33,5),KW(26,33,5),LU(26,33,5),LV(26,33,5),      
     2LW(26,33,5),MU(26,33,5),MV(26,33,5),MW(26,33,5)               
      EQUIVALENCE (APP(1,1),AP(1))                              
      INTEGER X                                                         
      DATA P/1000.,850.,700.,500.,300./,LTAU/0,12,24/                   
C     ******************************************************************
C        READ TERRAIN HEIGHTS                                           
      READ(5,10) ((TERRA(I,J),I=1,51),J=1,65)
   10 FORMAT(18F4.0)  
      READ(5,12) INCR                                                   
   12 FORMAT(1I2)                                                       
C        INITIALIZE TRAJECTORY TERMINAL POINTS (GRID POINT OR STATION)  
      SFX=-0.42262                                                      
      CFX=0.90631                                                       
      CH=10.1                                                           
      CK=10.5                                                           
      DO 20 I=1,13                                                      
      DO 20 J=1,17                                                      
      XCR=((J-1)*CFX)-((I-1)*SFX)+CH                                    
      YCR=((I-1)*CFX)+((J-1)*SFX)+CK                                    
      DO 20 N=1,3                                                       
      ZDF(I,J,N)=0.0                                                    
      RHUM(I,J,N)=0.0                                                   
      PLX(I,J,N)=XCR                                                    
      PLY(I,J,N)=YCR                                                    
      PLP(I,J,N)=P(N)                                                   
      GP=PLX(I,J,N)+PLX(I,J,N)-1.                                       
      RP=PLY(I,J,N)+PLY(I,J,N)-1.                                       
      LP=GP                                                             
      KP=RP                                                             
      DX=GP-LP                                                          
      DY=RP-KP                                                          
      LXP1=LP+1                                                         
      KYP1=KP+1                                                         
      LOC=TERRA(KP,LP)                                                  
      PSALT=LOC+(TERRA(KP,LXP1)-LOC)*DX+(TERRA(KYP1,LP)-LOC)*DY         
     1+(LOC+TERRA(KYP1,LXP1)-TERRA(KYP1,LP)-TERRA(KP,LXP1))*DX*DY       
      IF(N-1)14,16,14                                                   
  14  IF(PLP(I,J,N)-PSALT)20,20,16                                      
  16  PLP(I,J,N)=PSALT                                                  
  20  CONTINUE                                                          
C        WRITE TERMINAL POINT X,Y,P ON DISK 82                          
      NRCD=1                                                            
      WRITE(82,REC=NRCD) (((PLX(I,J,N),N=1,4),J=1,17),I=1,13)           
      NRCD=NRCD+1                                                       
      WRITE(82,REC=NRCD) (((PLY(I,J,N),N=1,4),J=1,17),I=1,13)           
      NRCD=NRCD+1                                                       
      WRITE(82,REC=NRCD) (((PLP(I,J,N),N=1,4),J=1,17),I=1,13)           
      IF(KPRINT.EQ.0) GO TO 216                                         
C        PRINT PARCEL TERMINAL POSITIONS                                
      PRINT 208                                                         
  208 FORMAT(1H1,//,56X,'TRAJECTORIES',//,54X,'DATA ARRANGEMENT',//,46X,
     1'ITDL',3X,'JTDL',3X,'INCP',3X,'JNCP',3X,'VERT',//,42X,'ITDL AND JT
     2DL REFERENCE 13 X 17 TDL GRID ',//,42X,'INCP AND JNCP REFERENCE NM
     3C 26 X 33 GRID  ',//,42X,'VERT IS THE PRESSURE LEVEL IN MILLIBARS'
     4,//,41X,'I CORRESPONDS TO NORTH-SOUTH GRID DIRECTION',//,42X,'J CO
     5RRESPONDS TO EAST-WEST GRID DIRECTIONS')                          
      KHR=INCR                                                          
      PRINT 210, KHR                                                    
  210 FORMAT(1H0,54X,I2,' HR POSITIONS')                                
      DO 212 N=1,3                                                      
      PRINT 214, P(N),((I,J,PLY(I,J,N),PLX(I,J,N),PLP(I,J,N),J=1,17),   
     1  I=1,13)                                                         
  212 CONTINUE                                                          
  214 FORMAT(1H0,60X,F5.0,//,1X,4('ITDL',1X,'JTDL',1X,'INCP',3X,'JNCP', 
     12X,'VERT',5X),//,(4(2I4,2F7.1,F6.0,4X)))                          
C        READ U,V AND W WINDS FROM DISK 15                              
  216 FLT=(FLOAT(INCR)/12.)+0.5                                         
      NFP=FLT                                                           
      JNT=0                                                             
      DO 50 X=1,NFP                                                     
      JSW=X                                                             
      MK=1                                                              
      IX=(NFP+1)-X                                                      
      JTT=LTAU(IX)                                                      
      NPROJ=0                                                           
      CALL REDISK(JTT,AP,APP,KU,KV,KW,LU,LV,LW,MU,MV,MW,NPROJ)          
      IF(LTAU(IX))23,22,23                                              
  22  CALL OVERT(KU,KV,KW,LU,LV,LW,MU,MV,MW)                            
  23  IF(JNT)28,24,28                                                   
  24  IF(FLOAT(NFP)-FLT)28,26,28                                        
  26  JNT=1                                                             
      MK=7                                                              
C        START TRAJECTORY COMPUTATIONS                                  
  28  DO 48 M=MK,12,2                                                   
      DO 40 K=3,24,3                                                    
      DO 40 L=3,30,3                                                    
      NTEST=0                                                           
      DO 40 I=1,13                                                      
      DO 40 J=1,17                                                      
      DO 40 N=1,3                                                       
      A=PLX(I,J,N)                                                      
      B=PLY(I,J,N)                                                      
      IF(ABS(A-L)-1.5)34,33,40                                          
  33  PLX(I,J,N)=PLX(I,J,N)-.000001                                     
  34  IF(ABS(B-K)-1.5)36,35,40                                          
  35  PLY(I,J,N)=PLY(I,J,N)-.000001                                     
  36  IF(NTEST)37,37,38                                                 
  37  CALL INTPL1(K,L,M,LTAU(IX),KU,KV,KW,LU,LV,LW,MU,MV,MW)            
  38  INUM=7 
      EHRZ=0.01
      EVER=1.0  
      CALL MARIAH(I,J,N,K,L,INUM,EHRZ,EVER,M,TERRA)                     
      NTEST=1                                                           
  40  CONTINUE                                                          
      DO 41 I=1,13                                                      
      DO 41 J=1,17                                                      
      DO 41 N=1,3                                                       
      PLX(I,J,N)=XXX(I,J,N)                                             
      PLY(I,J,N)=YYY(I,J,N)                                             
      PLP(I,J,N)=ZZZ(I,J,N)                                             
  41  CONTINUE                                                          
      IF(M-5)42,43,42                                                   
  42  IF(M-11)48,43,48                                                  
  43  NRCD=NRCD+1                                                       
      WRITE(82,REC=NRCD) (((PLX(I,J,N),N=1,4),J=1,17),I=1,13)           
      NRCD=NRCD+1                                                       
      WRITE(82,REC=NRCD) (((PLY(I,J,N),N=1,4),J=1,17),I=1,13)           
      NRCD=NRCD+1                                                       
      WRITE(82,REC=NRCD) (((PLP(I,J,N),N=1,4),J=1,17),I=1,13)           
      IF(KPRINT.EQ.0) GO TO 48                                          
      PRINT 47                                                          
  47  FORMAT(1H1)                                                       
C        PRINT 6-HR PARCEL POSITIONS                                    
      KHR=KHR-6                                                         
      PRINT 210, KHR                                                    
      DO 470 N=1,3                                                      
      PRINT 214, P(N),((I,J,PLY(I,J,N),PLX(I,J,N),PLP(I,J,N),J=1,17),   
     1  I=1,13)                                                         
  470 CONTINUE                                                          
  48  CONTINUE                                                          
  50  CONTINUE                                                          
      IF(KPRINT.EQ.0) GO TO 52                                          
      ND=82                                                             
      CALL OUTPT3(ND)                                                   
  52  NSTAP=NRCD                                                        
      NRCD=1                                                            
      WRITE(81,REC=NRCD) (((KU(I,J,N),N=1,5),J=1,33),I=1,26)            
      NRCD=2                                                            
      WRITE(81,REC=NRCD) (((KV(I,J,N),N=1,5),J=1,33),I=1,26)            
      NRCD=1                                                            
      WRITE(83,REC=NRCD) ((TERRA(I,J),J=1,65),I=1,51)                   
      NRCD=30                                                           
      WRITE(82,REC=NRCD) (((ZDF(I,J,N),N=1,4),J=1,17),I=1,13)           
      RETURN                                                            
      END                                                               
