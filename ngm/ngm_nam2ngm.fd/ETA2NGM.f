      PROGRAM ETA2NGM
C$$$  MAIN PROGRAM DOCUMENTATION BLOCK
C
C MAIN PROGRAM: NGM_ETA2NGM
C   PRGMMR: ROGERS           ORG: NP22        DATE: 2000-02-09
C
C ABSTRACT:
C      
C   1) READS IN NGM RESTART FILE BASED ON 6-H GDAS OR AVN FORECAST
C      CREATED BY NGM INPUT CODE
C   2) UNPACKS ETA ANALYSIS (POSTED ON THE NGM SUPER C-GRID #104 AND
C      ON 39 PRESSURE LEVELS IN GRIB FORMAT)
C   3) VERTICALLY INTERPOLATES ETA ANALYSIS ON PRESSURE TO NGM SIGMA
C      LAYERS/SURFACES
C   4) HORIZONTALLY INTERPOLATE WINDS TO NGM WIND POINTS
C   5) UPDATE "FIRST GUESS" RESTART FILE WITH VALUES FROM INTERPOLATED
C      ETA ANALYSIS
C
C PROGRAM HISTORY LOG:
C   98-11-10  ERIC ROGERS
C
C USAGE:
C   INPUT FILES BY UNIT NUMBER:
C
C     UNIT11   -   NGM RESTART FILE FROM GDAS/AVN FORECAST
C     UNIT12   -   DATA CARDS SPECIFYING MODEL OPTIONS.
C     UNIT13   -   ETA ANALYSIS (GRID #104 OUTPUT IN GRIB FORMAT)
C     UNIT14   -   INDEX FILE FOR UNIT 13 GRIB FILE
C
C   OUTPUT FILES BY UNIT NUMBER:
C     UNIT6    -   STANDARD PRINT OUTPUT.
C     UNIT51   -   UPDATED NGM RESTART FILE
C
C   INTERNAL FILES:
C     NONE.
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:      CORNER, CULL, DIAG, ERRSTOP, FIND, GRDSET,
C                  HORINT, INTERP, LOLA, NPOLE, SIGCON,
C                  RESTARTR, RESTARTW,
C                  TASKA, TASKB, TASKC, TASKD,
C                  TASKE, TASKF, TASKH, TASKI.
C
C     LIBRARY:
C       W3LIB  -
C                  W3TAGB 
C                  W3TAGE
C                  IW3JDN
C
C   EXIT STATES:
C     COND = 0     SUCCESSFUL RUN.
C
C REMARKS:       NONE AT THIS TIME.
C
C ATTRIBUTES:
C   LANGUAGE:   FORTRAN 90
C   MACHINE :   CRAY C-90
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
      INCLUDE 'parmodel'
      INCLUDE 'parmg104'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
      COMMON /NGMSIGS/ SIGMID(IKM), SIGINT(IKM+1)
      COMMON /GBLDAT/ IAODAT(8)
C
      COMMON    VBL( INVBL )
C
      COMMON /GVB/NGMZ(200,200),PINT(ILIM,JLIM,IKM+1),
     1     PMID(ILIM,JLIM,IKM),ALPINT(ILIM,JLIM,IKM+1),
     2     UINT(ILIM,JLIM,IKM),LEVATM(ILIM,JLIM),
     3     ALPMID(ILIM,JLIM,IKM)
C
      COMMON /EGRID /PS(ILIM,JLIM),ZS(ILIM,JLIM),
     1 Z(ILIM,JLIM,MAXLEV),TS(ILIM,JLIM),
     2 T(ILIM,JLIM,MAXLEV),QS(ILIM,JLIM),
     3 U(ILIM,JLIM,MAXLEV),
     4 V(ILIM,JLIM,MAXLEV),
     5 Q(ILIM,JLIM,MAXLEV),MASKE(ILIM,JLIM),
     6 MASKV(ILIM,JLIM)
C
C Geoff START
C     establish DUMMY2 as big array in main code
      COMMON /DUMMY2/ FIELD(IIJKMAX)
C Geoff STOP
C
      CHARACTER*35 ERRMSG(4)
      CHARACTER*6  DATANAME
      CHARACTER*32 LAB85IBM
C
      DIMENSION ISUM( INGRDUSE ), JSUM( INGRDUSE )
      DIMENSION AINC(5),IANDAT(8),IEDATE(8)
C
      DATA IFOUR /4/
C
      IOUTUPRT = 6
      INUOPTS  = 11
      INRSTIN0 = 12
      IGRIB    = 13
      IGRIBI   = 14
      IOUTUNIT = 51
C
      DO K = 1, 8
        IANDAT(K) = 0
        IAODAT(K) = 0
        IF(K .LE. 5) THEN
          AINC(K) = 0.0
        ENDIF
      ENDDO
C
C              WRITE TO THE W3LOG.
C
      CALL W3TAGB('NGM_ETA2NGM',2000,0040,0096,'NP22')
C
C        INPUT THE ARRAYS OF THE RESTART FILE.
C
      CALL RESTARTR (INRSTIN0, IOUTUPRT, SPECS, VBL,
     1                  BITSEA, BITSNO, BITWVL)
C
C              PRINT THE CONTENTS OF THE SPECS ARRAY.
      WRITE (IOUTUPRT, 5100)
C
      I1 = -6
      I2 =  0
      DO 630 LINE=1,100
         I1 = I1 + 7
         I2 = I2 + 7
         WRITE (IOUTUPRT, 5200) I1, (SPECS(I), I=I1, I2), I2
         WRITE (IOUTUPRT, 5200) I1, (SPECS(I), I=I1, I2), I2
  630 CONTINUE
C
C     MATERIALIZE SOME OF THE INFORMATION INPUT IN
C     ARRAY SPECS.
      HOUR0    = SPECS(2)
      IHOUR    = NINT (HOUR0)
      DAY0     = SPECS(3)
      IDAY     = NINT (DAY0)
      AMONTH0  = SPECS(4)
      IMONTH   = NINT (AMONTH0)
      CENYEAR0 = SPECS(5)
      ICENYEAR = NINT (CENYEAR0)
      WRITE(IOUTUPRT,*)' DATE FROM AVN BACKGROUND ',HOUR0,DAY0,
     1     AMONTH0,CENYEAR0
C
C     CREATE NCEP DATE WORD USING W3MOVDAT
C     GESPREP CODE UPDATES DATE OF FIRST GUESS TO BE VALID DATE
C     SO AINC(2) IS ALWAYS ZERO HERE
C
      IANDAT(1) = NINT(CENYEAR0)
      IANDAT(2) = NINT(AMONTH0)
      IANDAT(3) = NINT(DAY0)
      IANDAT(5) = NINT(HOUR0)
      CALL W3MOVDAT(AINC,IANDAT,IAODAT)
      WRITE(6,*)' VALID DATE FROM GDAS ',IAODAT
C
C              MATERIALIZE SOME MORE OF THE INFORMATION INPUT IN
C              ARRAY SPECS.
C
      LATG2    = NINT (SPECS(10))
      LAT      = NINT (SPECS(11))
      LONF     = NINT (SPECS(12))
      LEVS     = NINT (SPECS(13))
      KM       = LEVS
      LEVH     = NINT (SPECS(14))
C
      DO 650 K=1,KM
         DELSIG(K) = SPECS(21 + K)
  650 CONTINUE
C
      SIGINT(1) = 1.0
      DO K = 2, KM 
        SIGINT(K) = SIGINT(K-1) - DELSIG(K-1)
      ENDDO
      SIGINT(17) = 0.0
      DO K = 2, KM + 1
        SIGMID(K-1) = 0.5 * (SIGINT(K-1) + SIGINT(K))
      ENDDO
      WRITE(IOUTUPRT,10) SIGINT
      WRITE(IOUTUPRT,10) SIGMID
   10 FORMAT(6G12.5)
C
C              PRINT SOME VALUES OF PARAMETERS THAT WERE IN
C              ARRAY SPECS WHEN IT WAS READ IN.
      WRITE (IOUTUPRT, 660)
     1       CENYEAR0, AMONTH0, DAY0, HOUR0
C
  660 FORMAT ('0', 'THE FOLLOWING FUNDAMENTAL PARAMETERS EXISTED ',
     1             'IN ARRAY SPECS WHEN IT WAS READ FROM DISK:'/
     2        '0', 5X, 'THE TIME OF THE INITIAL CONDITIONS IS ',
     2             '(Y/M/D/H) :',
     3             F5.0,' / ',F3.0,' / ',F3.0,' / ',F3.0,' UTC')
C
      WRITE (IOUTUPRT, 670)
     1       KM, (K, DELSIG(K), K=KM, 1, -1)
C
  670 FORMAT ('0', 5X, 'THE NUMBER OF MODEL LAYERS IS', I3, '.'/
     1        '0', 5X, 'THE SIGMA THICKNESSES OF THESE LAYERS ARE:'/
     2       (' ', 9X, 'DELSIG(', I3, ') =', 1PE14.6))
C
C              MATERIALIZE OTHER PLI PARAMETERS USED
C              IN THIS PROGRAM.
      IJKMAX0  =  IIJKMAX
      IJMAXB0  =  IIJMAXB
      IWORDSB0 =  IIWORDSB
      KM0      =  IKM
      KMP10    =  IKMP1
      NGRDUSE0 =  INGRDUSE
      NGUSM10  =  INGRDUS1
      NH0      =  INH
      NVBL0    =  INVBL
C
C
C              SPECIFY THE STARTING LOCATIONS OF EACH FIELD IN A
C              LATITUDINAL STRIP CONTAINING DATA
C              ON A LATITUDE-LONGITUDE GRID ON SIGMA SURFACES.
      IU      = 1
      IV      = IU + LONF * LEVS
      IT      = IV + LONF * LEVS
      IQ      = IT + LONF * LEVS
      IH      = IQ + LONF * LEVH
      IZ      = IH + LONF
      NSIGSTP = IZ + LONF - 1
C
C
C     SPECIFY THE GRID PARAMETERS OF THE NGM GRID USING
C     INFORMATION ON DATA CARDS.
      READ (INUOPTS, 800) IOPTION, NH, DLAMNGM, NGRDUSE
  800 FORMAT (27X, I7 / 27X, I7 / 27X, F10.0 / 27X, I7)
C
      NGRDUSM1 = NGRDUSE - 1
C
      IF (NGRDUSE .GT. 1) THEN
C
         DO 1200 NG=2, NGRDUSE
           READ (INUOPTS, 1000) IMG(NG), JMG(NG),
     1                         ISUM(NG-1),JSUM(NG-1)
 1000      FORMAT(27X, I7 / 27X, I7 / 27X, I7 / 27X, I7)
 1200    CONTINUE
C
C           ALL DATA CARDS HAVE NOW BEEN READ IN.
C
      END IF
C
C        SPECIFY SEVERAL OTHER GRID CONSTANTS.
         IMG(1) = 2 * NH + 6
         JMG(1) = IMG(1)
C
         NH       = NINT(SPECS(131))
         NGRDUSE  = NINT(SPECS(132))
         NGRDUSM1 = NGRDUSE - 1
         DLAMNGM  = NINT(SPECS(133))
C
         DO 1500 NG=1,NGRDUSE
            IMG(NG) = NINT(SPECS(140 + NG))
            JMG(NG) = NINT(SPECS(150 + NG))
 1500    CONTINUE
C
         DO 1600 NG=1,NGRDUSM1
            IAG(NG) = NINT(SPECS(160 + NG))
            IBG(NG) = NINT(SPECS(170 + NG))
            JAG(NG) = NINT(SPECS(180 + NG))
            JBG(NG) = NINT(SPECS(190 + NG))
C
            ISUM(NG) = IAG(NG) + IBG(NG)
            JSUM(NG) = JAG(NG) + JBG(NG)
C
 1600    CONTINUE
C
C     THE POLAR LOCATIONS OF THE GRIDS ARE CALCULATED.
C
C     FIRST THE H POINTS:
      XPOLEH(1) = REAL(NH + 4)
      YPOLEH(1) = XPOLEH(1)
C
      IF (NGRDUSE .GT. 1) THEN
         DO 2400 NG=2, NGRDUSE
            XPOLEH(NG) = 1.0E0 + 0.5E0 * REAL(IMG(NG))
     1                + 2.0E0 * XPOLEH(NG-1) - REAL(ISUM(NG-1))
            YPOLEH(NG) = 1.0E0 + 0.5E0 * REAL(JMG(NG))
     1                + 2.0E0 * YPOLEH(NG-1) - REAL(JSUM(NG-1))
 2400    CONTINUE
C
      END IF
C
C     NOW THE U AND V POINTS:
      DO 2600 NG=1,NGRDUSE
         XPOLEU(NG) = XPOLEH(NG)
         YPOLEU(NG) = YPOLEH(NG) - 0.5E0
C
         XPOLEV(NG) = XPOLEH(NG) - 0.5E0
         YPOLEV(NG) = YPOLEH(NG)
C
 2600 CONTINUE
C
C              CALCULATE THE LOCATION OF THE CORNERS OF EACH
C              INTERIOR GRID ON THE NEXT OUTER GRID.
C              NOTE THAT THERE SHOULD BE NO REMAINDER
C              WHEN DIVIDING BY 4.
      IF(NGRDUSE .GT. 1) THEN
C
         DO 2800 NG=1, NGRDUSM1
            IAG(NG) = (2 * ISUM(NG) + 13 - IMG(NG+1)) / IFOUR
            IBG(NG) = ISUM(NG) - IAG(NG)
            JAG(NG) = (2 * JSUM(NG) + 13 - JMG(NG+1)) / IFOUR
            JBG(NG) = JSUM(NG) - JAG(NG)
 2800    CONTINUE
C
      END IF
C
C     CALCULATE THE PARAMETER CMUU, WHICH IS RELATED TO
C     GRID INCREMENT.
      DO 3000 NG=1,NGRDUSE
         CMUU(NG) = 1.0E0 / ( (REAL(NH) + 0.5E0) * REAL(2 ** (NG-1)))
         print *,'cmuu ',ng,cmuu(ng)
 3000 CONTINUE
C
C              SPECIFY THE POINTERS IN ARRAY IADDRS FOR THE
C              VARIOUS MODEL VARIABLES.
      INDEXHU  =  1
      INDEXHV  =  2
      INDEXHTH =  3
      INDEXHQ  =  4
      INDEXH   =  5
      INDEXST  =  6
      INDEXSQ  =  7
      INDEXALB =  8
      INDEXSLP =  9
      INDEXPSI = 10
      INDEXCD  = 11
      INDEXCC  = 12
      INDEXGSP = 13
      INDEXRAD = 14
      INDEXDLW = 15
      INDEXDSW = 16
      INDEXCPR = 17
      INDEXMA  = 18
      INDEXSEC = 19
      INDEXZ0  = 20
      INDEXTSS = 21
      INDEXCG  = 22
      INDEXDHQ = 23
      INDEXTSD = 42
C
C              CALCULATE THE STARTING ADDRESSES FOR THE VARIOUS
C              FIELDS OF ARRAY VBL.
      LSTART = 1
C
      DO 3400 NG=1, NGRDUSE
         IJ  = IMG(NG) * JMG(NG)
         IJK = IJ * KM
C
         DO 3200 LVAR=1, 4
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJK
 3200    CONTINUE
C
         DO 3300 LVAR=5, 13
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3300    CONTINUE
C
         LVAR = 14
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3301 LVAR=15, 22
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3301    CONTINUE
C
         LVAR = 23
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3302 LVAR=24, 37
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3302    CONTINUE
C
         LVAR = 38
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3303 LVAR=39, 44
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3303    CONTINUE
C
 3400 CONTINUE
C
      LSTOP = LSTART - 1
C
C              PRINT THE STARTING ADDRESSES.
      WRITE (IOUTUPRT, 3500)
     1      ((LVAR, NG, IADDRG(LVAR, NG), LVAR=1,44), NG=1, NGRDUSE)
C
 3500 FORMAT ('0', 'THE STARTING ADDRESSES FOR THE VARIOUS FIELDS ',
     1             'OF ARRAY VBL ARE LISTED NEXT, WHERE IADDRG IS ',
     2             'IADDRG (VARIABLE NUMBER, GRID NUMBER) :' //
     3       (' ', 5X, 'IADDRG(', I2, ',', I2, ') =', I10 ))
C
      WRITE (IOUTUPRT, 1490) LSTOP
 1490 FORMAT ('0', 5X, 'LAST LOCATION =', I10 / )
C
C
      LVLTOT =  ILVLTOT
C
      IVERINPT = 98111015

C              ADD SOME PARAMETERS TO ARRAY SPECS.
      SPECS(  1) = REAL (IVERINPT)
C
      SPECS(131) = REAL (NH)
      SPECS(132) = REAL (NGRDUSE)
      SPECS(133) = DLAMNGM
C
      DO 4200 NG=1,NGRDUSE
         SPECS(140 + NG) = REAL (IMG (NG))
         SPECS(150 + NG) = REAL (JMG (NG))
         SPECS(200 + NG) = XPOLEH (NG)
         SPECS(210 + NG) = YPOLEH (NG)
 4200 CONTINUE
C
      DO 4300 NG=1,NGRDUSM1
         SPECS(160 + NG) = REAL( IAG( NG))
         SPECS(170 + NG) = REAL( IBG( NG))
         SPECS(180 + NG) = REAL( JAG( NG))
         SPECS(190 + NG) = REAL( JBG( NG))
 4300 CONTINUE
C
      ITIME0     = 0
      TIMEI0     = REAL (ITIME0)
      SPECS(233) = REAL (ITIME0)
C
      NSTEPS0    = 0
      SPECS(234) = REAL (NSTEPS0)
C
      IBUCKET    = 0
      BUCKETI    = REAL (IBUCKET)
      SPECS(243) = REAL (IBUCKET)
C
C              PRINT THE VALUES OF PARAMETERS IN ARRAY SPECS.
      WRITE (IOUTUPRT, 4400)
     1       IVERINPT,
     3       ITIME0, TIMEI0, NSTEPS0,
     4       IBUCKET, BUCKETI
C
 4400 FORMAT ('0', 'THE FOLLOWING FUNDAMENTAL PARAMETERS WERE ',
     1             'SPECIFIED ON INPUT CARDS, WERE CALCULATED, ',
     2             'OR SIMPLY WERE SPECIFIED:' /
     3        ' ', 5X, 'THE VERSION OF THE INPUT MODEL IS', I11, '.' /
     4        '0', 5X, 'THE TIME SINCE THE INITIAL CONDITIONS AT ',
     5             'WHICH THE FORECAST IS BEING STARTED IS',
     6             I8, ' SECONDS =', F8.4, ' HOURS.'/
     7        '0', 5X, 'THE NUMBER OF TIME STEPS INTO THE FORECAST AT',
     8             ' WHICH THE FORECAST IS BEING STARTED IS',
     9             I5, '.'/
     1        '0', 5X, 'THE TIME SINCE THE INITIAL CONDITIONS AT ',
     2             'WHICH THE PRECIPITATION BUCKETS WERE LAST ',
     3             'EMPTIED IS', I8, ' SECONDS =', F8.4, ' HOURS.')
C
      WRITE (IOUTUPRT, 4600) NH, DLAMNGM, NGRDUSE
C
 4600 FORMAT ('0', 5X, 'THE NUMBER OF GRID POINTS (MINUS 0.5) ',
     1             'ON GRID A BETWEEN THE POLE AND THE EQUATOR ',
     2             'IS', I3, '.'/
     3        ' ', 5X, 'THE ROTATION ANGLE OF THE POSITIVE X AXIS ',
     4             'COUNTERCLOCKWISE FROM THE GREENWICH MERIDIAN ',
     5             'IS', F8.3, '.'/
     6        ' ', 5X, 'THE NUMBER OF FORECAST GRIDS IS', I2, '.'/
     7        '0', 5X, 'INDIVIDUAL GRID PARAMETERS:'/
     8        ' ', T10, 'GRID', T20, 'IMG', T30, 'JMG',
     9             T40, 'XPOLEH', T50, 'YPOLEH', T60, 'IAG',
     1             T70, 'IBG', T80, 'JAG', T90, 'JBG' /)
C
      DO 4800 NG = 1, NGRDUSE
         IF (NG .EQ. NGRDUSE) THEN
            WRITE (IOUTUPRT, 4700)
     1             NG, IMG(NG), JMG(NG), XPOLEH(NG), YPOLEH(NG)
C
 4700       FORMAT (' ', T10, I3, T20, I3, T30, I3, T38, F8.3,
     1                   T48, F8.3, T60, I3, T70, I3,
     2                   T80, I3, T90, I3)
C
         ELSE
            WRITE (IOUTUPRT, 4700)
     1             NG, IMG(NG), JMG(NG), XPOLEH(NG), YPOLEH(NG),
     2             IAG(NG), IBG(NG), JAG(NG), JBG(NG)
         END IF
 4800 CONTINUE
C
C     BEGIN PROCESSING OF ETA ANALYSIS
C     READ ETA ANALYSIS ON NGM SUPER-C GRID 104
C
      CALL GETE104(IGRIB,IGRIBI,IEDATE,IRETG)
      IF(IRETG .NE. 0) THEN
       WRITE (IOUTUPRT,*) 'PROBLEMS READING ETA ANALYSIS GRIB FILE'
       WRITE (IOUTUPRT,*) 'ABORT WITH IRETG = ', IRETG
       STOP99
      ELSE
       WRITE (IOUTUPRT,*) 'SUCCESSFUL READ OF ETA ANALYSIS FILE'
       WRITE (IOUTUPRT,*) 'DATE = ',IEDATE
      ENDIF
C
C     BASIC GRID PARAMETERS ARE PRINTED.
C
      CALL PRINTG
C
C     PERFORM VERTICAL INTERPOLATION TO NGM SIGMA LEVELS
C
      CALL VTERP
C
C     SOME DIAGNOSTICS FOR THE NGM FIELDS AND REPRESENTATIVE VALUES
C     OF THE FIELDS ARE PRINTED.
c     CALL DIAG
C
C              PRINT THE CONTENTS OF THE SPECS ARRAY.
      WRITE (IOUTUPRT, 5100)
 5100 FORMAT ('1', 'THE VALUES IN THE SPECS ARRAY ARE:' /)
C
      I1 = -6
      I2 =  0
      DO 5300 LINE=1,100
         I1 = I1 + 7
         I2 = I2 + 7
         WRITE (IOUTUPRT, 5200) I1, (SPECS(I), I=I1, I2), I2
 5200    FORMAT (' ', 'I=', I3, ',',1X,E15.6, 6(1X,E16.6),
     1                ', I=', I3)
 5300 CONTINUE
C
C JUANG START
C
      DATANAME = 'RSTMOD'
C
      CALL RESTARTW (IOUTUNIT, IOUTUPRT, DATANAME, SPECS, VBL,
     1               BITSEA, BITSNO, BITWVL)
C
      WRITE (IOUTUPRT, 5900)
 5900 FORMAT ('0', '***************', ' EXECUTION OF THE ETA2NGM CODE ',
     1             'FOR THE NESTED GRID ',
     2             'MODEL IS COMPLETED. ***************' /)
C
C     STEP IS COMPLETE.  STOP.
      WRITE (IOUTUPRT, 6000)
 6000 FORMAT('0', '***** STEP ENDED AS PLANNED. *****')
C
C     WRITE TO THE W3LOG.
      CALL W3TAGE('NGM_ETA2NGM')
C
      STOP
      END
