      SUBROUTINE ADABAT(TS,PS,ISTO)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    ADABAT      PERFORM ADIABATIC CHECK OF TEMPS
C   PRGMMR: DIMEGO           ORG: W/NMC22    DATE: 86-03-31
C
C ABSTRACT: PERFORMS A CHECK OF TEMPERATURE PROFILES FOR LAYERS
C   WHICH ARE SUPER-ADIABATIC.  A CORRECTION IS CALCULATED AND
C   APPLIED IF THE SWITCH IS SET TO DO SO.  BASED ON RALPH JONES
C   CODE FROM THE ISENTROPIC.
C
C PROGRAM HISTORY LOG:
C   86-03-31  DIMEGO
C   88-06-30  DIMEGO      HALF PRECISION / PL1 / FTN200
C   99-05-20  ROGERS      CHANGED TO DO ONE PROFILE AT A TIME
C
C USAGE:    CALL ADABAT(TS,PS,ISTO)
C   INPUT ARGUMENT LIST:
C     TS       - ARRAY OF TEMPERATURES (ANALYSIS STRIP)
C     PS       - ARRAY OF PRESSURES (ANALYSIS STRIP)
C     ISTO     - SWITCH FOR STORING CORRECTED/ADJUSTED VALUE:
C              - 0 DO NOT STORE ; NON ZERO STORE NEW VALUE.
C
C   OUTPUT ARGUMENT LIST:
C     TS       - WHEN ISTO IS NON-ZERO, THE ADJUSTED VALUES
C              - ARE WRITTEN IN PLACE OF ORIGINAL VALUES.
C
C REMARKS: LAYERS ARE SIMPLY MIXED TO RELIEVE SUPER-ADIABAT
C
C ATTRIBUTES:
C   LANGUAGE: STANDARD FORTRAN
C   MACHINE:
C
C$$$
      IMPLICIT REAL (A-H,O-Z)
      INCLUDE "parmodel"
      PARAMETER(LMAX=IKM)
      DIMENSION TS( LMAX ),PS( LMAX )
      DIMENSION PT( LMAX ),PI( LMAX ),NCOUNT( LMAX )
      DIMENSION DMEAN( LMAX ),RMSTD( LMAX ),TDMAX( LMAX )
      DATA NCOUNT,DMEAN/ LMAX *0, LMAX *0.0/
      DATA RMSTD,TDMAX/ LMAX *0.0, LMAX *0.0/
      LMAXM1= LMAX - 1
      ROCP = 287.05 / 1005.
      XSTO = 0.0
      IF( ISTO.GT.0 )  XSTO = 1.0
C  COMPUTE PI & THETA AND ADJUST ANY SUPERADIABATIC LAPSE RATES
      DO 2 L=1, LMAX
      PI(L) = (.001*PS(L)) ** ROCP
      PT(L) = TS(L)/PI(L)
    2 CONTINUE
      ITER = 0
      ITX = 50
      CMAX = 0.0
      CMEAN = 0.0
      NCNT = 0
      LEVMAX = 0
      DO 4 N=1,ITX
      NHS = 0
      DO 3 L=1,LMAXM1
      DPT = PT(L+1)-PT(L)
      IF(DPT.GT.0.0) GO TO 3
      NHS = NHS+1
      NCNT = NCNT + 1
      COR = 0.5*DPT-0.05
      TCOR = ABS(COR*0.5*(PI(L)+PI(L+1)))
      CMAX = MAX(CMAX,TCOR)
      CMEAN = CMEAN + TCOR
      IF(NCNT.EQ.1) LEVMAX = L
      PT(L+1) = PT(L+1)-COR
      PT(L) = PT(L)+COR
    3 CONTINUE
      IF(NHS.EQ.0) GO TO 5
      ITER = N
    4 CONTINUE
    5 CONTINUE
      IF(ITER.EQ.0) GO TO 7
      IF( NCNT.GT.1 )  CMEAN = CMEAN / FLOAT(NCNT)
C     IF(ITER.GT.3.OR.CMAX.GE.1.0.OR.CMEAN.GT.0.5)
C    + WRITE(6,101) I,J,ITER,NCNT,LEVMAX,CMAX,CMEAN
C 101 FORMAT('  ADIABATIC ADJUSTMENT AT POINT (',I3,',',I2,')',
C    1        ' NEEDED ',I3,' ITERATIONS',2I5,2F10.3)
C  NOW RE-COMPUTE ADJUSTED TEMPERATURES
      DO 6 L=1,LMAX
      TNEW = PT(L) * PI(L)
      TDIF = TNEW - TS(L)
      IF(ABS(TDIF).LE.0.01) GO TO 6
      NCOUNT(L) = NCOUNT(L) + 1
      DMEAN(L) = DMEAN(L) + TDIF
      RMSTD(L) = RMSTD(L) + TDIF*TDIF
      TS(L) = TS(L) + TDIF * XSTO
      IF(ABS(TDIF).LE.TDMAX(L)) GO TO 6
      TDMAX(L) = ABS(TDIF)
    6 CONTINUE
    7 CONTINUE
      RETURN
      END
