      SUBROUTINE CORNER(X,Y,Z,DLAMNGM,IRANGE,IPRINT,ALONG,ALAT)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    CORNER      COMPUTES LATITUDE AND LONGITUDE OF X,Y
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: PRINTS LANGITUDE AND LATITUDE OF POINT I,J WITH
C   WEST LONGITUDE PRINTED AS NEGATIVE
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-01  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL CORNER(X,Y,X,DLAMNGM,IRANGE,IPRINT,ALONG,ALAT)
C   INPUT ARGUMENT LIST:
C     X        - I - I(POLE)
C     Y        - J - J(POLE)
C     Z        - (DELTA OF GRID)/(2*EARTH RADIUS)
C     DLAMNGM  - ROTATION ANGLE
C     IRANGE   - SPECIFIES THE RANGE OF LONGITUDE
C                1;  LONGITUDE VARIES FROM 0 TO 360 DEGREES
C                ELSE;  LONGITUDE VARIES FROM -180 TO 180 DEGREES
C     IPRINT   - SPECIFIES CHOICE OF WRITE TO UNIT 6
C                <> 0;  WRITES LATITUDE AND LONGITUDE OF (X,Y)
C
C   OUTPUT ARGUMENT LIST:
C     ALONG    - LONGITUDE OF INPUT POINT
C     ALAT     - LATITUDE OF INPUT POINT
C
C   OUTPUT FILES:
C     FT06F001 - PRINTS LATITUDE AND LONGITUDE OF (X,Y) IF
C                IPRINT <> 1.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205.
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      PI=3.141592654E0
      IF(X) 5,1,9
C     X=0
    1 IF(Y) 3,2,4
C     X=0 AND Y=0
    2 ALONG=0.E0
      ALAT=90.E0
      GO TO 16
C     X=0,BUT Y NEGATIVE
    3 ALONG=-.5E+0*PI
      GO TO 13
C     X=0, BUT Y POSITIVE
    4 ALONG=.5E+0*PI
      GO TO 13
C     X IS NEGATIVE
    5 IF(Y) 6,7,8
C     THIRD QUADRANT
    6 ALONG=ATAN(Y/X)-PI
      GO TO 13
C     NEGATIVE X-AXIS
    7 ALONG=PI
      GO TO 13
C     SECOND QUADRANT
    8 ALONG=ATAN(Y/X) +PI
      GO TO 13
C     X POSITIVE
    9 IF(Y) 10,11,12
C     FOURTH QUADRANT
   10 ALONG=ATAN(Y/X)
      GO TO 13
C     POSITIVE X-AXIS
   11 ALONG=0.E0
      GO TO 13
C     FIRST QUADRANT
   12 ALONG=ATAN(Y/X)
C
C     LONGITUDE NOW IN PLACE, CONVERT TO DEGREES AND ADD
C     THE ROTATION ANGLE (DLAMNGM).
   13 ALONG=(180.E+0/PI)*ALONG + DLAMNGM
C
C     ENSURE THAT THE LONGITUDE IS WITHIN THE SPECIFIED RANGE.
      IF(IRANGE.NE.1) GO TO 50
C
C     THE LONGITUDE WILL RANGE FROM 0 AT THE GREENWICH MERIDIAN
C     EASTWARD TO 360 DEGREES FOLLOWING THIS SECTION.
C**********IS MOD FUNCTION CORRECT.
      ALONG = MOD(ALONG, 360.E+0)
      IF (ALONG .LT. 0.0E0) ALONG = ALONG + 360.E0
      GO TO 60
C
   50 CONTINUE
C     LONGITUDE RANGE FROM -180. TO +180.
      IF(ALONG .GT. +180.E0) ALONG = ALONG - 360.E0
      IF(ALONG .LT. -180.E0) ALONG = ALONG + 360.E0
   60 CONTINUE
C
C     COMPUTE LATITUDE
      RHO=Z*Z*(X*X +Y*Y)
C
      ALAT=ASIN( (1.E0-RHO)/(1.E0 + RHO) )*(180.E0/PI)
C
   16 IF(IPRINT.NE.0) THEN
         PRINT 15,ALONG,ALAT
   15    FORMAT ('0', '     LONGITUDE AND LATITUDE ARE',2F20.6)
      END IF
C
      RETURN
C
      END
