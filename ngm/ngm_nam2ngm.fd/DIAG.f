      SUBROUTINE DIAG
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    DIAG        PRINTS DIAGNOSTICS AND PART OF EACH FIELD
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: CALCULATES AND PRINTS SOME DIAGNOSTICS FOR THE NGM
C   FIELDS AND PRINTS PART OF A REPRESENTATIVE ROW OF THE FIELD
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-01  BRIAN SCHMIDT ADDED THE DOCBLOCK
C   89-02-03  KEN MITCHELL ADDED SOIL TEMPERATURE
C
C USAGE:    CALL DIAG
C   INPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT FILES:
C     FT06F001 - PRINTS MESSAGES PERTAINING TO ERRORS
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C                  GRDSET
C                  STATS
C     LIBRARY:
C       COMMON   - CONSTN
C                  DUMMY2
C                  CONSTS
C                  BLANK (CONTAINS VBL ARRAY)
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON          VBL ( INVBL )
      COMMON /DUMMY2/ ANAL( ILONFP1 ,  INLAT ,  ILEVS )
C
      DIMENSION FIELD( IIJKMAX )
C
      CHARACTER*8  LABEL(2)
      CHARACTER*8  LVAR(12)
C     LGRID IS DIMENSIONED TO BE BIGGER THAN THE NUMBER OF
C     GRIDS WILL EVER BE.
      CHARACTER*8  LGRID(9)
      CHARACTER*35 ERRMSG(4)
C
      EQUIVALENCE (ANAL(1,1,1), FIELD(1))
C
      DATA LVAR/'NGM U   ','NGM V   ','NGM THET','NGM Q   ','NGM P*  ',
     1          'NGM Z*  ','NGM T*  ','NGM DRAG','NGM Q*  ','NGM ALB ',
     2          'NGM PADJ','NGM TSSL'/
      DATA LGRID /' -GRID 1', ' -GRID 2', ' -GRID 3', ' -GRID 4',
     1            ' -GRID 5', ' -GRID 6', ' -GRID 7', ' -GRID 8',
     2            ' -GRID 9'/
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
C     ENSURE THAT THE SIZE OF ARRAY FIELD IS LESS THAN THE SIZE OF
C     ARRAY ANAL.
      IJKMAX0 =  IIJKMAX
      LONFP1  = LONF + 1
      IF (LONFP1*NLAT*KM .LT. IJKMAX0) THEN
         WRITE (IOUTUPRT, 10)
   10    FORMAT ('0', 'THE SIZE OF ARRAY ANAL IN SUBROUTINE DIAG ',
     1                'IS SMALLER THAN THAT OF ARRAY FIELD.'/
     2           ' ', 'EXECUTION CONTINUING.'//)
C    2           ' ', 'EXECUTION TERMINATING.'//)
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
         ERRMSG(1) = 'IN SUBROUTINE DIAG     NEAR STATEME'
         ERRMSG(2) = 'NT NUMBER 10:                      '
         ERRMSG(3) = 'THE SIZE OF ARRAY ANAL IS SMALLER T'
         ERRMSG(4) = 'HAN THAT OF ARRAY FIELD.           '
C
C        CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
      END IF
C
C     ENSURE THAT THE VALUE OF NGRDUSE IS NOT TOO LARGE
C     FOR THIS SUBROUTINE.
      IF (NGRDUSE .LE. 9) GO TO 60
      WRITE (IOUTUPRT, 30)
   30 FORMAT('0', 'THE VALUE OF NGRDUSE IS TOO LARGE FOR ',
     1            'SUBROUTINE DIAG.  EXECUTION TERMINATED.')
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE DIAG     NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 30:                      '
      ERRMSG(3) = 'THE VALUE OF NGRDUSE IS TOO LARGE F'
      ERRMSG(4) = 'OR THE SUBROUTINE.                 '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
   60 CONTINUE
C
C     NOW THE U COMPONENT IS PROCESSED.
C
      DO 1200 NG=1,NGRDUSE
      CALL GRDSET (NG, INDEXHU, DUM, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ     = IM * JM
      MADDHU = IADDRG(INDEXHU, NG) -1 -IM -IJ
      MADDH  = IADDRG(INDEXH , NG) -1 -IM
C
      DO 1100 J=1,JM
C
      DO 1000 K=1,KM
C
      DO 900 I=1,IM
         IF (J .LT. JM) THEN
            FIELD(I + (J-1)*IM + (K-1)*IJ)
     1               = VBL(MADDHU + IM*J +IJ*K + I) * 2.0E0
     2               / (VBL(MADDH+I+IM*J) + VBL(MADDH+I+IM+IM*J))
         ELSE
C
C     IF THIS IS THE LAST ROW, WE ASSUME THAT
C     THE SURFACE PRESSURE OF THE NONEXISTENT NEXT ROW IS THE
C     SAME AS THE CURRENT ROW.
            FIELD(I + (J-1)*IM + (K-1)*IJ)
     1                   = VBL(MADDHU + IM*J +IJ*K + I)
     2                   / VBL(MADDH+I+IM*J)
         END IF
  900 CONTINUE
C
 1000 CONTINUE
C
 1100 CONTINUE
C
      LABEL(1) = LVAR(1)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,KM,KM, LABEL)
C
 1200 CONTINUE
C
C
C     PROCESS THE V COMPONENT.
      DO 2200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXHV, DUM, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ     = IM * JM
      MADDHV = IADDRG(INDEXHV, NG) -1 -IM -IJ
      MADDH  = IADDRG(INDEXH , NG) -1 -IM
C
      DO 2100 J=1,JM
C
      DO 2050 K=1,KM
      DO 2000 I=1,IM-1
      FIELD(I + (J-1)*IM +(K-1)*IJ)
     1             = VBL(MADDHV + IM*J+IJ*K +I) * 2.0E0
     2             /(VBL(MADDH + I + IM*J) + VBL(MADDH + I + 1 + IM*J))
 2000 CONTINUE
C
      FIELD(IM +(J-1)*IM +(K-1)*IJ)
     1              = VBL(MADDHV +IM*J +IJ*K+IM) / VBL(MADDH+IM+IM*J)
C
 2050 CONTINUE
C
 2100 CONTINUE
C
      LABEL(1) = LVAR(2)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,KM,KM, LABEL)
C
 2200 CONTINUE
C
C
C     PROCESS THE POTENTIAL TEMPERATURE FIELD.
      DO 3200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXHTH, DUM, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ     = IM * JM
      MADDHTH = IADDRG(INDEXHTH, NG) -1 -IM -IJ
      MADDH   = IADDRG(INDEXH  , NG) -1 -IM
C
      DO 3100 J=1,JM
C
      DO 3000 K=1,KM
      DO 3000 I=1,IM
      FIELD(I + (J-1)*IM +(K-1)*IJ)
     1             = VBL(MADDHTH +IM*J + IJ*K +I) / VBL(MADDH+IM*J + I)
 3000 CONTINUE
C
 3100 CONTINUE
C
      LABEL(1) = LVAR(3)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,KM,KM, LABEL)
C
 3200 CONTINUE
C
C
C     PROCESS THE SPECIFIC HUMIDITY FIELD.
      DO 4200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXHQ, DUM, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ     = IM * JM
      MADDHQ = IADDRG(INDEXHQ, NG) -1 -IM -IJ
      MADDH  = IADDRG(INDEXH , NG) -1 -IM
C
      DO 4100 J=1,JM
C
      DO 4000 K=1,KM
      DO 4000 I=1,IM
      FIELD(I + (J-1)*IM +(K-1)*IJ)
     1             = VBL(MADDHQ +IM*J + IJ*K +I) / VBL(MADDH+IM*J + I)
 4000 CONTINUE
C
 4100 CONTINUE
C
      LABEL(1) = LVAR(4)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,KM,KM, LABEL)
C
 4200 CONTINUE
C
C
C     PROCESS THE SURFACE PRESSURE.
      DO 5200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXH, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR SURFACE PRESSURE IN ARRAY VBL.
      MADDH  = MADDRS -1 -IM
C
      DO 5100 J=1,JM
C
      DO 5000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDH + IM*J + I)
 5000 CONTINUE
C
 5100 CONTINUE
C
      LABEL(1) = LVAR(5)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 5200 CONTINUE
C
C
C     PROCESS THE ADJUSTMENT TO THE MEAN-SEA-LEVEL PRESSURE
C     FOR THE PRESSURE TENDENCY METHOD.
C     DO 5900 NG=1,NGRDUSE
C
C     CALL GRDSET(NG, INDEXSLP, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR ADJUSTMENT TO MSLP IN ARRAY VBL.
C     MADDSLP = MADDRS -1 -IM
C
C     DO 5500 J=1,JM
C
C     DO 5300 I=1,IM
C     FIELD(I + (J-1)*IM) = VBL(MADDSLP + IM*J + I)
C5300 CONTINUE
C
C5500 CONTINUE
C
C     LABEL(1) = LVAR(11)
C     LABEL(2) = LGRID(NG)
C     CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
C5900 CONTINUE
C
C
C     PROCESS THE GROUND HEIGHT.
      FACTOR = 1005.E0 / 9.8E0
      DO 6200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXPSI, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR PSI (RELATED TO GROUND HEIGHT)
C     IN ARRAY VBL.
      MADDPSI  = MADDRS -1 -IM
C
      DO 6100 J=1,JM
C
      DO 6000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDPSI + IM*J + I) * FACTOR
 6000 CONTINUE
C
 6100 CONTINUE
C
      LABEL(1) = LVAR(6)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 6200 CONTINUE
C
C
C     PROCESS THE SURFACE TEMPERATURE.
      DO 7200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXST, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR SURFACE TEMPERATURE IN ARRAY VBL.
      MADDST   = MADDRS -1 -IM
C
      DO 7100 J=1,JM
C
      DO 7000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDST +IM*J + I)
 7000 CONTINUE
C
 7100 CONTINUE
C
      LABEL(1) = LVAR(7)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 7200 CONTINUE
C
C
C     PROCESS THE SOIL TEMPERATURE.
      DO 7700 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXTSS, MADDRS, IM ,JM ,DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR SOIL TEMPERATURE IN ARRAY VBL.
      MADDTSS  = MADDRS -1 -IM
C
      DO 7600 J=1,JM
C
      DO 7500 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDTSS+IM*J + I)
 7500 CONTINUE
C
 7600 CONTINUE
C
      LABEL(1) = LVAR(12)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 7700 CONTINUE
C
C
C     PROCESS THE DRAG COEFFICIENT.
      DO 8200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXCD, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR THE DRAG COEFFICIENT IN ARRAY VBL.
      MADDCD  = MADDRS -1 -IM
C
      DO 8100 J=1,JM
C
      DO 8000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDCD + IM*J + I)
 8000 CONTINUE
C
 8100 CONTINUE
C
      LABEL(1) = LVAR(8)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 8200 CONTINUE
C
C
C     PROCESS THE SURFACE MOISTURE.
      DO 9200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXSQ, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR SURFACE MOISTURE IN ARRAY VBL.
      MADDSQ   = MADDRS -1 -IM
C
      DO 9100 J=1,JM
C
      DO 9000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDSQ + IM*J + I)
 9000 CONTINUE
C
 9100 CONTINUE
C
      LABEL(1) = LVAR(9)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
 9200 CONTINUE
C
C
C     PROCESS THE ALBEDO.
      DO 10200 NG=1,NGRDUSE
C
      CALL GRDSET(NG, INDEXALB, MADDRS, IM, JM, DUM, DUM, DUM, DUM)
C
C     SPECIFY THE POINTER FOR THE ALBEDO IN ARRAY VBL.
      MADDALB  = MADDRS -1 -IM
C
      DO 10100 J=1,JM
C
      DO 10000 I=1,IM
      FIELD(I + (J-1)*IM) = VBL(MADDALB +IM*J + I)
10000 CONTINUE
C
10100 CONTINUE
C
      LABEL(1) = LVAR(10)
      LABEL(2) = LGRID(NG)
      CALL STATS(FIELD, 1,IM,IM, 1,JM,JM, 1,1,KM, LABEL)
C
10200 CONTINUE
C
      RETURN
C
      END
