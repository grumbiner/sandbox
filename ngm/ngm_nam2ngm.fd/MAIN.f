      PROGRAM MAIN
C$$$  MAIN PROGRAM DOCUMENTATION BLOCK
C
C MAIN PROGRAM: RGCINP28     PREPARATION OF THE NGM FOR A FORECAST
C   PRGMMR: ROGERS           ORG: NP22        DATE: 1998-06-08
C
C ABSTRACT: PREPARES THE FIELDS THAT ARE NEEDED TO MAKE A
C   FORECAST WITH THE NESTED GRID MODEL (NGM).
C   OPTION 1:
C      ANALYZED ATMOSPHERIC
C      FIELDS ARE INPUT BY THIS PROGRAM, CONVERTED TO THE FIELDS
C      THAT THE NGM ACTUALLY USES WHEN MAKING A FORECAST,
C      INTERPOLATED TO THE NGM GRID CONFIGURATION, AND WRITTEN TO
C      THE CRAY DISK.  ALSO SEVERAL EXTERNAL SURFACE FIELDS ARE INPUT
C      INCLUDING SST (UPDATED DAILY), SNOW/ICE COVER (UPDATED DAILY)
C      LAND-MASS BIT MASK (FIXED), ALBEDO (QUARTERLY CLIMO), SURFACE
C      ROUGHNESS (FIXED), SURFACE MOISTURE AVAILABILITY (FIXED), TERRAIN
C      STANDARD DEVIATION (FIXED), WHICH ARE INTERPOLATED TO THE NGM 
C      GRIDS AND WRITTEN TO DISK.
C   OPTION 2:
C      THE RESTART FILE IS INPUT, ALTERED, AND WRITTEN BACK
C      TO CYBER DISK.
C
C   IMPORTANT REFERENCES FOR THE CODE ARE:
C   PHILLIPS, N.A., 1979:  THE NESTED GRID MODEL.  NOAA TECHNICAL .
C        REPORT NWS 22, DEPARTMENT OF COMMERCE, SILVER SPRING,
C        MARYLAND, 80 PP.
C
C   HOKE, J.E., 1984:  FORECAST RESULTS FOR NMC'S NEW REGIONAL
C        ANALYSIS AND FORECAST SYSTEM.  PREPRINTS, TENTH AMS
C        CONFERENCE ON WEATHER FORECASTING AND ANALYSIS,
C        CLEARWATER BEACH, FLORIDA, 25-29 JUNE 1984, 418-423.
C
C PROGRAM HISTORY LOG:
C   84-05-08  JIM HOKE CODED ORIGINAL SUBROUTINE FOR THE CYBER 205.
C   86-11-12  GRAVITY WAVE DRAG CODING ADDED BUT NOT
C   87-10-09  VBL RESTRUCTURED BY JIM TUCCILLO.
C             TURNED ON BY JIM TUCCILLO.
C   88-08-04  BRIAN SCHMIDT UPDATED THE DOCBLOCK.
C   88-09-13  THE CALL TO TASKG, WHICH CALCULATES THE SURFACE
C             PRESSURE ADJUSTMENT, IS REMOVED BECAUSE IT'S
C             NOT USED.
C   88-11-21  BRIAN SCHMIDT CHANGED THE READ AND WRITE OF THE
C             RESTART FILE TO SUBROUTINE CALLS.
C   89-02-16  JIM HOKE ADDED THE OPTION TO USE VBL TO MODIFY
C             THE RESTART FILE.
C   89-09-12  HANN-MING HENRY JUANG ADDED READING LTBL DATA IN SIGMA T80
C             AND INTERPOLATED TO SIGMA SURFACES AT NGM GRID.
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP AND ADDED OPTION TO
C             SKIP READING LTBL SIGMA T80 DATA AND SIG-TO-SIG CONVERSION
C   91-05-17  GEOFF DIMEGO FIXED SG2SG SO THAT NO EXTRAPOLATION BEYOND
C             TOP OR BOTTOM OF BOUNDING-MODEL'S DOMAIN (IN VERTICAL)
C   92-12-02  KEN MITCHELL ADDED USE OF DAILY USAF SNOW COVER IN TASKC
C             ALSO ADDED USE OF NESDIS 89 X 89 LAND/SEA MASK IN TASKC
C   93-06-02  GEOFF DIMEGO EXTENDED NUMBER OF GUESS LEVELS FROM 18 TO 28
C             THIS IS PARAMETER KS IN SEVERAL PLACES
C   97-03-07  KEN MITCHELL CONVERTED FROM NAS TO CRAY INPUT FILES OF 
C             SFC FIELDS OF SST AND SNOW/ICE.  SWITCHED FROM USE OF
C             2.5 DEG SST TO 1.0 DEG SST AND ITS INTERPOLATION TO NGM.
C             DELETED USE OF NAS 36-DAY ARCHIVE 10-DAY MEAN OF LOWEST
C             NGM SIGMA AIR TEMP FOR SOIL TEMP IN FAVOR OF DEFAULT
C             NCAR MONTHLY SFC AIR TEMP CLIMO
C   98-05-07  ERIC ROGERS EXTENDED NUMBER OF GUESS LEVELS FROM 28 TO 42
C             WITH IMPLEMENTATION OF T170 GDAS/AVN. THIS IS PARAMETER KS IN
C             SEVERAL PLACES
C   98-09-11  HANN-MING HENRY JUANG convert to Y2K AND MODIFIED DATA CARDS
C             TO OUTPUT ONE GRID (SUPER C-GRID 147X111)
C   98-10-05  ERIC ROGERS CONVERTED NUMBER OF GDAS LEVELS BACK TO 28
C
C USAGE:
C   INPUT FILES BY UNIT NUMBER:
C
C   INPUT UNITS NEEDED FOR BOTH OPTIONS:
C     UNIT5    -   DATA CARDS SPECIFYING INPUT AND OUTPUT
C                  UNIT NUMBERS FOR THIS PROGRAM.
C     UNIT11   -   INUOPTS
C                  DATA CARDS SPECIFYING MODEL OPTIONS.
C   INPUT UNITS NEEDED FOR OPTION 1:
C     UNIT20   -   INUSPECS
C                  ARRAY CONTAINING SPECIFICATIONS AND HISTORICAL
C                  INFORMATION FOR THIS MODEL RUN.
C     UNIT21   -   INULOLA
C                  SEQUENTIAL FILE CONTAINING WEATHER-RELATED
C                  FIELDS ON SIGMA SURFACES ON A LATITUDE-
C                  LONGITUDE GRID BY LATITUDINAL STRIP.
C     UNIT22   -   INSNOICE
C                  NESDIS/SAB SNOW-COVER/SEA-ICE FIELD
C     UNIT23   -   INUSLP
C                  MEAN-SEA-LEVEL PRESSURE ANALYSIS.
C     UNIT24   -   INSST1DG
C                  CPC DAILY GLOBAL 1-DEG SST (IN GRIB)
C     UNIT25   -   INDXST
C                  GRIB INDEX FILE FOR UNIT INSST1DG ABOVE
C     UNIT31   -   INUHLWC
C                  N. HEMIS 1-DEG LAND MASK 
C     UNIT32   -   INUDRAG
C                  SURFACE DRAG COEFFICIENT FIELD.
C     UNIT33   -   INUALB
C                  ALBEDO FIELD.
C     UNIT34   -   INUZ0
C                  Z0 FIELD.
C     UNIT35   -   INUMA
C                  MA FIELD.
C     UNIT36   -   INUTSD
C                  TERRAIN STANDARD DEVIATION.
C     UNIT37   -   INSSOILT
C                  CLIMATOLOGICAL SUBSOIL TEMPERATURE
C                  (ACTUALLY CLIMATOLOGICAL SHELTER TEMPERATURE)
C     UNIT39   -   INAFSNO
C                  INPUT N. HEMIS. USAF SNOWDEPTH
C     UNIT40   -   INBND
C                  INPUT LAT. BOUNDARY DATA IN P.
C     UNIT42   -   INMSKAF
C                  AFGWC LAND/SEA MASK ON USAF 512 X 512 GRID
C     UNIT43   -   INMSKSAB
C                  NESDIS AND CPC LAND MASK ON 89 X 89 SNOW/ICE GRID
C
C   INPUT UNITS NEEDED FOR OPTION 2:   :
C     UNIT38   -   INRSTIN0
C                  INPUT RESTART FILE.
C
C   OUTPUT FILES BY UNIT NUMBER:
C     UNIT6    -   PRINTOUT FROM THE CONTROLLEE.
C     UNIT51   -   IOUTUNIT
C                  FIELDS ON THE NGM GRIDS IN THE EXACT FORM
C                  NEEDED TO BEGIN A FORECAST.
C                 (THE RESTART FILE, WHICH CONTAINS THE SPECS ARRAY,
C                  ARRAY VBL, AND BIT VECTORS.)
C     UNIT52   -   IOUTBND
C                  OUTPUT LAT. BOUNDARY DATA IN SIGMA.
C
C   INTERNAL FILES:
C     NONE.
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:      CORNER, CULL, DIAG, ERRSTOP, FIND, GRDSET,
C                  HORINT, INTERP, LOLA, NPOLE, SIGCON,
C                  RESTARTR, RESTARTW,
C                  TASKA, TASKB, TASKC, TASKD,
C                  TASKE, TASKF, TASKH, TASKI.
C
C     LIBRARY:
C       W3LIB  -
C                  W3TAGB 
C                  W3TAGE
C                  IW3JDN
C
C   EXIT STATES:
C     COND = 0     SUCCESSFUL RUN.
C
C REMARKS:       NONE AT THIS TIME.
C
C ATTRIBUTES:
C   LANGUAGE:   FORTRAN 77/90
C   MACHINE :   CRAY Y-MP
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
      INCLUDE 'parmodel'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON    VBL( INVBL )
C
C Geoff START
C     establish DUMMY2 as big array in main code
      COMMON /DUMMY2/ FIELD(IIJKMAX)
C Geoff STOP
C
C
C JUANG START
      PARAMETER(IJLB=2000,KS=28,KSP=KS+1)
      COMMON /LTBL/ ULB(IJLB,KS),VLB(IJLB,KS),TLB(IJLB,KS)
     1             ,QLB(IJLB,KS),HLB(IJLB),HLBNEW(IJLB)
     2             ,SLYR(KS),ZSLB(IJLB),LBUO,LBVO,LBCH
     3             ,LSCRU(IJLB), LSCRV(IJLB), LSCRH(IJLB)
CX    DIMENSION FSCRU(IJLB),FSCRV(IJLB),FSCRH(IJLB)
CX    DIMENSION ONED(IJLB),TWOD(IJLB,KS)
C JUANG STOP
C
      CHARACTER*35 ERRMSG(4)
      CHARACTER*6  DATANAME
      CHARACTER*32 LAB85IBM
C
      DIMENSION ISUM( INGRDUSE ), JSUM( INGRDUSE )
C
      DATA IFOUR /4/
C
C
C              WRITE TO THE W3LOG.
      CALL W3TAGB('RGCINP28',1998,0159,0057,'NP22   ')                  
C
C              READ THE DATA CARDS SPECIFYING THE UNIT NUMBER
C              FOR THE PRINT PRODUCED BY THIS RUN AND INDICATING
C              ON WHAT UNIT ARE THE DATA CARDS SPECIFYING
C              FUNDAMENTAL MODEL OPTIONS.
      READ (5, 200, END=9998) IOUTUPRT, INUOPTS,
     1                        INUSPECS, INULOLA,
     2                        INSNOICE, INUSLP,
     2                        INSST1DG, INDXST,
     3                        INUHLWC,  INUDRAG,
     4                        INUALB,   INUZ0,
     5                        INUMA,    INUTSD,
     6                        INSSOILT, INAFSNO,
     7                        INMSKAF,  INMSKSAB,
     8                        INBND,    IOUTBND,
     9                        INRSTIN0, IOUTUNIT
C
  200 FORMAT (27X, I7 / 27X, I7 / 27X, I7 / 27X, I7 /
     1        27X, I7 / 27X, I7 / 27X, I7 / 27X, I7 /
     2        27X, I7 / 27X, I7 / 27X, I7 / 27X, I7 /
     3        27X, I7 / 27X, I7 / 27X, I7 / 27X, I7 /
     4        27X, I7 / 27X, I7 / 27X, I7 / 27X, I7 /
     5        27X, I7/  27X, I7 )
C
C              PRINT THE CURRENT WALL TIME.
      WRITE (IOUTUPRT, 400)
  400 FORMAT (//////'0', '***************', ' EXECUTION OF THE INPUT ',
     1             'CODE FOR THE NESTED GRID ',
     2             'MODEL IS BEGINNING. ***************' /)
C
C              ECHO CHECK THE DATA CARDS JUST READ IN.
      WRITE (IOUTUPRT, 600)  IOUTUPRT, INUOPTS,
     1                       INUSPECS, INULOLA,  INSNOICE, 
     2                       INUSLP,   INSST1DG, INDXST,
     3                       INUHLWC,  INUDRAG,  INUALB,  INUZ0
C
  600 FORMAT ('0', 'THE UNIT NUMBER FOR THE PRINT PRODUCED BY ',
     1             'THIS RUN IS', I3, '.' /
     2        ' ', 'THE DATA CARDS SPECIFYING FUNDAMENTAL MODEL ',
     3             'OPTIONS WILL BE READ FROM UNIT', I3, '.' /
     4        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     5             'SPECIFICATIONS/HISTORY ARRAY IS       ', I3, '.' /
     6        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     7             'SIGMA FIELDS ON THE LAT/LONG GRID IS  ', I3, '.' /
     8        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     9             'NESDIS WEEKLY SNOW-COVER/SEA-ICE IS   ', I3, '.' /
     1        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     2             'MEAN-SEA-LEVEL PRESSURE ANALYSIS IS   ', I3, '.' /
     2        ' ', 'THE INPUT DISK UNIT NUMBER FOR THE  ',
     2             'DAILY CPC GLOBAL 1-DEG SST ANALYSIS IS', I3, '.' /
     3        ' ', 'THE INPUT DISK UNIT NUMBER FOR THE  ',
     3             'SST GRIB INDEX FILE IS                ', I3, '.'/
     3        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     4             'HORIZONTAL LAND-WATER CONTRAST IS     ', I3, '.' /
     5        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     6             'SURFACE DRAG COEFFICIENT IS           ', I3, '.' /
     7        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     8             'ALBEDO ANALYSIS IS                    ', I3, '.' /
     9        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     1             'Z0 ANALYSIS IS                        ', I3, '.' )
C
      WRITE (IOUTUPRT, 601)
     1                      INUMA,    INUTSD,
     2                      INSSOILT, INAFSNO,
     3                      INMSKAF,  INMSKSAB,
     4                      INBND,    IOUTBND,
     5                      INRSTIN0, IOUTUNIT
C
  601 FORMAT (' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     1             'MOISTURE AVAILABILITY ANALYSIS IS     ', I3, '.' /
     2        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     3             'TERRAIN STANDARD DEVIATION IS         ', I3, '.' /
     4        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     5             'CLIMATOLOGICAL SUBSOIL TEMPERATURE IS ', I3, '.' /
     6        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     7             'DAILY N.H. USAF SNOWCOVER ANALYSIS IS ', I3, '.' /
     8        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     9             'LAND-SEA MASK ON USAF SNO ANAL GRD IS ', I3, '.' /
     1        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     2             'LAND-SEA MSK ON NESDIS SNO ANL GRD IS ', I3, '.' /
     3        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     4             'LATERAL BOUNDARY CONDITIONS ON P IS   ', I3, '.' /
     5        ' ', 'THE OUTPUT DISK UNIT NUMBER FOR THE ',
     6             'LATERAL BOUNDARY CONDITIONS ON SIG IS ', I3, '.' /
     7        ' ', 'THE INPUT  DISK UNIT NUMBER FOR THE ',
     8             'INPUT RESTART FILE IS                 ', I3, '.' /
     9        ' ', 'THE OUTPUT DISK UNIT NUMBER FOR THE ',
     1             'OUTPUT RESTART FILE IS                ', I3, '.' )
C
C     INPUT WHETHER OPTION -1, +1 OR +2 IS TO BE USED.
      READ (INUOPTS, 620) ISOPT
  620 FORMAT (27X, I7)
C
C     ECHO CHECK THE OPTION JUST READ IN.
      WRITE (IOUTUPRT, 625) ISOPT
  625 FORMAT ('0', 'THE OPTION THAT THE INPUT CODE WILL BE RUN ',
     1             'UNDER IS OPTION', I2, '.')
C
C  REMOVE OPTION SIGN WHICH IS ONLY USED TO CONTROL
C  WHETHER TO CALCULATE GRID-C BOUNDARY CONDITIONS FROM THE GDAS
C
      IOPTION = ABS(ISOPT)
C
      IF (IOPTION .EQ. 1) THEN
C        INPUT THE IDENTIFICATION RECORD.
         READ (INUSPECS) SPECS
C
      ELSE IF (IOPTION .EQ. 2) THEN
C        INPUT THE ARRAYS OF THE RESTART FILE.
         CALL RESTARTR (INRSTIN0, IOUTUPRT, SPECS, VBL,
     1                  BITSEA, BITSNO, BITWVL)
C
C
C              PRINT THE CONTENTS OF THE SPECS ARRAY.
C     WRITE (IOUTUPRT, 5100)
C
C     I1 = -6
C     I2 =  0
C     DO 630 LINE=1,100
C        I1 = I1 + 7
C        I2 = I2 + 7
C        WRITE (IOUTUPRT, 5200) I1, (SPECS(I), I=I1, I2), I2
C 630 CONTINUE
C
      ELSE
C        AN UNACCEPTABLE VALUE FOR IOPTION WAS SPECIFIED.
C        TERMINATE THE JOB.
         WRITE (IOUTUPRT, 640)
  640    FORMAT ('0', '***** AN UNACCEPTABLE VALUE OF IOPTION ',
     1                'WAS SPECIFIED.  JOB ',
     2                'TERMINATED.')
C
         CALL W3TAGE('RGCINP28')                                        
C
C        TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
         ERRMSG(1) = 'IN THE MAIN CODE NEAR STATEMENT NUM'
         ERRMSG(2) = 'BER 640:                           '
         ERRMSG(3) = 'IOPTION WAS NOT 1 OR 2.            '
         ERRMSG(4) = '                                   '
C
         CALL ERRSTOP (IOUTUPRT, ERRMSG)
      ENDIF
C
C     MATERIALIZE SOME OF THE INFORMATION INPUT IN
C     ARRAY SPECS.
      HOUR0    = SPECS(2)
      IHOUR    = NINT (HOUR0)
      DAY0     = SPECS(3)
      IDAY     = NINT (DAY0)
      AMONTH0  = SPECS(4)
      IMONTH   = NINT (AMONTH0)
      CENYEAR0 = SPECS(5)
      ICENYEAR = NINT (CENYEAR0)
C
C              MATERIALIZE SOME MORE OF THE INFORMATION INPUT IN
C              ARRAY SPECS.
C
      LATG2    = NINT (SPECS(10))
      LAT      = NINT (SPECS(11))
      LONF     = NINT (SPECS(12))
      LEVS     = NINT (SPECS(13))
      KM       = LEVS
      LEVH     = NINT (SPECS(14))
C
      DO 650 K=1,KM
         DELSIG(K) = SPECS(21 + K)
  650 CONTINUE
C
C              PRINT SOME VALUES OF PARAMETERS THAT WERE IN
C              ARRAY SPECS WHEN IT WAS READ IN.
      WRITE (IOUTUPRT, 660)
     1       CENYEAR0, AMONTH0, DAY0, HOUR0,
     2       LATG2, LAT, LONF, LEVS, LEVH
C
  660 FORMAT ('0', 'THE FOLLOWING FUNDAMENTAL PARAMETERS EXISTED ',
     1             'IN ARRAY SPECS WHEN IT WAS READ FROM DISK:'/
     2        '0', 5X, 'THE TIME OF THE INITIAL CONDITIONS IS ',
     2             '(Y/M/D/H) :',
     3             F5.0, ' / ', F3.0, ' / ', F3.0, ' / ', F3.0,
     4             ' GMT.' /
     5        '0', 5X, 'THE RESOLUTION OF THE INITIAL CONDITIONS ',
     6             'ON THE LATITUDE/LONGITUDE GRID WAS:'/
     7        ' ', 9X, 'LATG2 =', I4 /
     8        ' ', 9X, 'LAT   =', I4 /
     9        ' ', 9X, 'LONF  =', I4 /
     1        ' ', 9X, 'LEVS  =', I4 /
     2        ' ', 9X, 'LEVH  =', I4 )
C
      WRITE (IOUTUPRT, 670)
     1       KM, (K, DELSIG(K), K=KM, 1, -1)
C
  670 FORMAT ('0', 5X, 'THE NUMBER OF MODEL LAYERS IS', I3, '.'/
     1        '0', 5X, 'THE SIGMA THICKNESSES OF THESE LAYERS ARE:'/
     2       (' ', 9X, 'DELSIG(', I3, ') =', 1PE14.6))
C
C     MATERIALIZE THE PL/I PARAMETERS FOR THE INPUT DATA ON THE
C     LATITUDE-LONGITUDE GRID ON SIGMA SURFACES IN LATITUDINAL STRIPS.
C
C              LATG2 IS THE NUMBER OF NORTHERN HEMISPHERIC
C              LATITUDINAL STRIPS ON THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
      LATG20   =  ILATG2
C
C              LONF IS THE NUMBER OF GRID POINTS AROUND A
C              LATITUDE CIRCLE ON THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
      LONF0    =  ILONF
C
C              LEVS IS THE NUMBER OF LEVELS OF THE ANALYSIS
C              OF WINDS AND HEIGHTS/TEMPERATURES ON THE
C              LATITUDE-LONGITUDE GRID OF THE INPUT SIGMA
C              DATA.
      LEVS0    =  ILEVS
C
C              LEVH IS THE NUMBER OF LEVELS OF THE
C              MOISTURE ANALYSIS ON THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
      LEVH0    =  ILEVH
C
C              NLATSH IS THE NUMBER OF
C              SYMMETRIZED SOUTHERN HEMISPHERE
C              LATITUDINAL STRIPS THAT ARE
C              GENERATED FROM THE NORTHERN
C              HEMISPHERIC STRIPS OF FIELDS
C              ON THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
C              NLATSH MUST BE BIG ENOUGH SO THAT THE
C              SOUTHERNMOST SOUTHERN HEMISPHERIC STRIP IS
C              SOUTH OF THE SOUTHERNMOST NGM FORECAST POINT.
      NLATSH0  =  INLATSH
C
C              NLAT IS THE TOTAL NUMBER OF
C              LATITUDINAL STRIPS THAT ARE DERIVED
C              FROM THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
C*****NLAT     = LATG2 + NLATSH + 1
      NLAT0    =  INLAT
      NLAT     = NLAT0
C
C              NSIGSTP IS THE SIZE OF THE VERTICAL
C              LATITUDINAL STRIP ON THE LATITUDE-LONGITUDE
C              GRID OF THE INPUT SIGMA DATA.
      NSIGSTP0 =  INSIGSTP
C
C              MATERIALIZE OTHER PLI PARAMETERS USED
C              IN THIS PROGRAM.
      IJKMAX0  =  IIJKMAX
      IJMAXB0  =  IIJMAXB
      IWORDSB0 =  IIWORDSB
      KM0      =  IKM
      KMP10    =  IKMP1
      NGRDUSE0 =  INGRDUSE
      NGUSM10  =  INGRDUS1
      NH0      =  INH
      NVBL0    =  INVBL
C
C
C              SPECIFY THE STARTING LOCATIONS OF EACH FIELD IN A
C              LATITUDINAL STRIP CONTAINING DATA
C              ON A LATITUDE-LONGITUDE GRID ON SIGMA SURFACES.
      IU      = 1
      IV      = IU + LONF * LEVS
      IT      = IV + LONF * LEVS
      IQ      = IT + LONF * LEVS
      IH      = IQ + LONF * LEVH
      IZ      = IH + LONF
      NSIGSTP = IZ + LONF - 1
C
C              REPLACE THE VERSION GIVEN ON THE DATA CARD
C              WITH AN INTEGER GIVING THE YEAR OF THE CENTURY,
C              MONTH, DAY OF THE MONTH, HOUR OF THE DAY, AND MINUTE
C              THAT THE CONTROLLEE ASSOCIATED WITH THIS MAIN
C              PROGRAM WAS CHANGED.
      IVERINPT = 9703071045
C
C
      IF (IOPTION .EQ. 1) THEN
C        SPECIFY THE GRID PARAMETERS OF THE NGM GRID USING
C        INFORMATION ON DATA CARDS.
         READ (INUOPTS, 800) NH, DLAMNGM, NGRDUSE
  800    FORMAT (27X, I7 / 27X, F10.0 / 27X, I7)
C
         NGRDUSM1 = NGRDUSE - 1
C
         IF (NGRDUSE .GT. 1) THEN
C
            DO 1200 NG=2, NGRDUSE
               READ (INUOPTS, 1000) IMG(NG), JMG(NG),
     1                              ISUM(NG-1),JSUM(NG-1)
 1000          FORMAT(27X, I7 / 27X, I7 / 27X, I7 / 27X, I7)
 1200       CONTINUE
C
C           ALL DATA CARDS HAVE NOW BEEN READ IN.
C
         END IF
C
C        SPECIFY SEVERAL OTHER GRID CONSTANTS.
         IMG(1) = 2 * NH + 6
         JMG(1) = IMG(1)
C
C        FOR OPTION 1,
C        ENSURE THAT THE VALUES FROM THE IDENTIFICATION RECORD
C        ARE CONSISTENT WITH THE PL/I PARAMETERS.
         IF ((LATG2 .NE. LATG20) .OR.
     1       (LONF  .NE. LONF0 ) .OR.
     2       (LEVS  .NE. LEVS0 ) .OR.
     3       (LEVH  .NE. LEVH0 )) THEN
C
            WRITE (IOUTUPRT, 2100)
     1             LATG2, LATG20, LONF, LONF0,
     2             LEVS,  LEVS0,  LEVH, LEVH0
 2100       FORMAT ('0', '***** THE IDENTIFICATION RECORD ',
     1                   'DOES NOT AGREE WITH THE PL/I PARAMETERS.  ',
     2                   'JOB TERMINATED.'/
     3              ' ', 8I10)
C
            CALL W3TAGE('RGCINP28')                                     
C
C           TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
            ERRMSG(1) = 'IN THE MAIN CODE NEAR STATEMENT NUM'
            ERRMSG(2) = 'BER 2100:                          '
            ERRMSG(3) = 'THE IDENTIFICATION RECORD DOES NOT '
            ERRMSG(4) = 'AGREE WITH THE PL/I PARAMETERS.    '
C
            CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
         END IF
C
      ELSE
C
C        SPECIFY THOSE QUANTITITES FOR OPTION 2 THAT WERE
C        READ IN AT THIS POINT IN OPTION 1.  THE VALUES ARE
C        AVAILABLE IN THE SPECS ARRAY FOR OPTION 2.
C
         NH       = NINT(SPECS(131))
         NGRDUSE  = NINT(SPECS(132))
         NGRDUSM1 = NGRDUSE - 1
         DLAMNGM  = NINT(SPECS(133))
C
         DO 1500 NG=1,NGRDUSE
            IMG(NG) = NINT(SPECS(140 + NG))
            JMG(NG) = NINT(SPECS(150 + NG))
 1500    CONTINUE
C
         DO 1600 NG=1,NGRDUSM1
            IAG(NG) = NINT(SPECS(160 + NG))
            IBG(NG) = NINT(SPECS(170 + NG))
            JAG(NG) = NINT(SPECS(180 + NG))
            JBG(NG) = NINT(SPECS(190 + NG))
C
            ISUM(NG) = IAG(NG) + IBG(NG)
            JSUM(NG) = JAG(NG) + JBG(NG)
C
 1600    CONTINUE
C
C        FOR OPTION 2,
C        REPLACE VALUES FROM THE SPECS FILE RELATED TO THE ANALYSIS
C        GRID WITH THEIR PLI COUNTERPARTS.  THIS ENSURES CONSISTENCY
C        BETWEEN DIMENSIONS AS STORED IN VARIABLES AND AS USED IN
C        ACTUAL DIMENSION STATEMENTS.
         LATG2   = LATG20
         LONF    = LONF0
         LEVS    = LEVS0
         LEVH    = LEVH0
         NLATSH  = NLATSH0
         NLAT    = NLAT0
         NSIGSTP = NSIGSTP0
C
      ENDIF
C
C     CHECK FOR CONSISTENCY BETWEEN VALUES SPECIFIED ON PL/I
C     STATEMENTS AND VALUES AS DETERMINED FROM DATA CARDS.
      IF ((NH .NE. NH0) .OR.
     1    (NGRDUSE .NE. NGRDUSE0)) THEN
         WRITE (IOUTUPRT, 2200)
 2200    FORMAT ('0', 'THERE EXISTS AN INCONSISTENCY IN AT ',
     1                'LEAST ONE OF THE FOLLOWING PARAMETERS ',
     2                'AS DETERMINED FROM PL/I AND FROM THE ',
     3                'INPUT DATA CARDS:' /
     4           ' ', 20X, 'NH, NGRDUSE  .' /
     5           ' ', 20X, 'EXECUTION TERMINATED.')
C
         CALL W3TAGE('RGCINP28')                                        
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
         ERRMSG(1) = 'IN THE MAIN CODE NEAR STATEMENT NUM'
         ERRMSG(2) = 'BER 2200:                          '
         ERRMSG(3) = 'THERE IS AN INCONSISTENCY BETWEEN P'
         ERRMSG(4) = 'L/I PARAMETERS AND THE DATA CARDS. '
C
         CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
      END IF
C
C     THE POLAR LOCATIONS OF THE GRIDS ARE CALCULATED.
C
C     FIRST THE H POINTS:
      XPOLEH(1) = REAL(NH + 4)
      YPOLEH(1) = XPOLEH(1)
C
      IF (NGRDUSE .GT. 1) THEN
         DO 2400 NG=2, NGRDUSE
            XPOLEH(NG) = 1.0E0 + 0.5E0 * REAL(IMG(NG))
     1                + 2.0E0 * XPOLEH(NG-1) - REAL(ISUM(NG-1))
            YPOLEH(NG) = 1.0E0 + 0.5E0 * REAL(JMG(NG))
     1                + 2.0E0 * YPOLEH(NG-1) - REAL(JSUM(NG-1))
 2400    CONTINUE
C
      END IF
C
C     NOW THE U AND V POINTS:
      DO 2600 NG=1,NGRDUSE
         XPOLEU(NG) = XPOLEH(NG)
         YPOLEU(NG) = YPOLEH(NG) - 0.5E0
C
         XPOLEV(NG) = XPOLEH(NG) - 0.5E0
         YPOLEV(NG) = YPOLEH(NG)
C
 2600 CONTINUE
C
C              CALCULATE THE LOCATION OF THE CORNERS OF EACH
C              INTERIOR GRID ON THE NEXT OUTER GRID.
C              NOTE THAT THERE SHOULD BE NO REMAINDER
C              WHEN DIVIDING BY 4.
      IF(NGRDUSE .GT. 1) THEN
C
         DO 2800 NG=1, NGRDUSM1
            IAG(NG) = (2 * ISUM(NG) + 13 - IMG(NG+1)) / IFOUR
            IBG(NG) = ISUM(NG) - IAG(NG)
            JAG(NG) = (2 * JSUM(NG) + 13 - JMG(NG+1)) / IFOUR
            JBG(NG) = JSUM(NG) - JAG(NG)
 2800    CONTINUE
C
      END IF
C
C
C
C     CALCULATE THE PARAMETER CMUU, WHICH IS RELATED TO
C     GRID INCREMENT.
      DO 3000 NG=1,NGRDUSE
         CMUU(NG) = 1.0E0 / ( (REAL(NH) + 0.5E0) * REAL(2 ** (NG-1)))
 3000 CONTINUE
C
C              SPECIFY THE POINTERS IN ARRAY IADDRS FOR THE
C              VARIOUS MODEL VARIABLES.
      INDEXHU  =  1
      INDEXHV  =  2
      INDEXHTH =  3
      INDEXHQ  =  4
      INDEXH   =  5
      INDEXST  =  6
      INDEXSQ  =  7
      INDEXALB =  8
      INDEXSLP =  9
      INDEXPSI = 10
      INDEXCD  = 11
      INDEXCC  = 12
      INDEXGSP = 13
      INDEXRAD = 14
      INDEXDLW = 15
      INDEXDSW = 16
      INDEXCPR = 17
      INDEXMA  = 18
      INDEXSEC = 19
      INDEXZ0  = 20
      INDEXTSS = 21
      INDEXCG  = 22
      INDEXDHQ = 23
      INDEXTSD = 42
C
C              CALCULATE THE STARTING ADDRESSES FOR THE VARIOUS
C              FIELDS OF ARRAY VBL.
      LSTART = 1
C
      DO 3400 NG=1, NGRDUSE
         IJ  = IMG(NG) * JMG(NG)
         IJK = IJ * KM
C
         DO 3200 LVAR=1, 4
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJK
 3200    CONTINUE
C
         DO 3300 LVAR=5, 13
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3300    CONTINUE
C
         LVAR = 14
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3301 LVAR=15, 22
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3301    CONTINUE
C
         LVAR = 23
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3302 LVAR=24, 37
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3302    CONTINUE
C
         LVAR = 38
         IADDRG (LVAR, NG) = LSTART
         LSTART = LSTART + IJK
C
         DO 3303 LVAR=39, 44
            IADDRG (LVAR, NG) = LSTART
            LSTART = LSTART + IJ
 3303    CONTINUE
C
 3400 CONTINUE
C
      LSTOP = LSTART - 1
C
C              PRINT THE STARTING ADDRESSES.
      WRITE (IOUTUPRT, 3500)
     1      ((LVAR, NG, IADDRG(LVAR, NG), LVAR=1,44), NG=1, NGRDUSE)
C
 3500 FORMAT ('0', 'THE STARTING ADDRESSES FOR THE VARIOUS FIELDS ',
     1             'OF ARRAY VBL ARE LISTED NEXT, WHERE IADDRG IS ',
     2             'IADDRG (VARIABLE NUMBER, GRID NUMBER) :' //
     3       (' ', 5X, 'IADDRG(', I2, ',', I2, ') =', I10 ))
C
      WRITE (IOUTUPRT, 1490) LSTOP
 1490 FORMAT ('0', 5X, 'LAST LOCATION =', I10 / )
C
C
      LVLTOT =  ILVLTOT
C
      IF (IOPTION .EQ. 1) THEN
C        ZERO THE ARRAY VBL.
C
         DO 3800 NG=1,NGRDUSE
            IM=IMG(NG)
            JM=JMG(NG)
            IJ=IM*JM
C
            IAD = IADDRG(1,NG) - IJ
C
            DO 3700 K=1,LVLTOT
               IAD = IAD + IJ
                 DO 3600  L=1,IJ
                    VBL(IAD+L-1) = 0.E0
 3600            CONTINUE
 3700       CONTINUE
C
 3800    CONTINUE
C
      ENDIF
C
C
C              ADD SOME PARAMETERS TO ARRAY SPECS.
      SPECS(  1) = REAL (IVERINPT)
C
      SPECS(131) = REAL (NH)
      SPECS(132) = REAL (NGRDUSE)
      SPECS(133) = DLAMNGM
C
      DO 4200 NG=1,NGRDUSE
         SPECS(140 + NG) = REAL (IMG (NG))
         SPECS(150 + NG) = REAL (JMG (NG))
         SPECS(200 + NG) = XPOLEH (NG)
         SPECS(210 + NG) = YPOLEH (NG)
 4200 CONTINUE
C
      DO 4300 NG=1,NGRDUSM1
         SPECS(160 + NG) = REAL( IAG( NG))
         SPECS(170 + NG) = REAL( IBG( NG))
         SPECS(180 + NG) = REAL( JAG( NG))
         SPECS(190 + NG) = REAL( JBG( NG))
 4300 CONTINUE
C
      ITIME0     = 0
      TIMEI0     = REAL (ITIME0)
      SPECS(233) = REAL (ITIME0)
C
      NSTEPS0    = 0
      SPECS(234) = REAL (NSTEPS0)
C
      IBUCKET    = 0
      BUCKETI    = REAL (IBUCKET)
      SPECS(243) = REAL (IBUCKET)
C
C              PRINT THE VALUES OF PARAMETERS IN ARRAY SPECS.
      WRITE (IOUTUPRT, 4400)
     1       IVERINPT,
     3       ITIME0, TIMEI0, NSTEPS0,
     4       IBUCKET, BUCKETI
C
 4400 FORMAT ('0', 'THE FOLLOWING FUNDAMENTAL PARAMETERS WERE ',
     1             'SPECIFIED ON INPUT CARDS, WERE CALCULATED, ',
     2             'OR SIMPLY WERE SPECIFIED:' /
     3        ' ', 5X, 'THE VERSION OF THE INPUT MODEL IS', I11, '.' /
     4        '0', 5X, 'THE TIME SINCE THE INITIAL CONDITIONS AT ',
     5             'WHICH THE FORECAST IS BEING STARTED IS',
     6             I8, ' SECONDS =', F8.4, ' HOURS.'/
     7        '0', 5X, 'THE NUMBER OF TIME STEPS INTO THE FORECAST AT',
     8             ' WHICH THE FORECAST IS BEING STARTED IS',
     9             I5, '.'/
     1        '0', 5X, 'THE TIME SINCE THE INITIAL CONDITIONS AT ',
     2             'WHICH THE PRECIPITATION BUCKETS WERE LAST ',
     3             'EMPTIED IS', I8, ' SECONDS =', F8.4, ' HOURS.')
C
      WRITE (IOUTUPRT, 4600) NH, DLAMNGM, NGRDUSE
C
 4600 FORMAT ('0', 5X, 'THE NUMBER OF GRID POINTS (MINUS 0.5) ',
     1             'ON GRID A BETWEEN THE POLE AND THE EQUATOR ',
     2             'IS', I3, '.'/
     3        ' ', 5X, 'THE ROTATION ANGLE OF THE POSITIVE X AXIS ',
     4             'COUNTERCLOCKWISE FROM THE GREENWICH MERIDIAN ',
     5             'IS', F8.3, '.'/
     6        ' ', 5X, 'THE NUMBER OF FORECAST GRIDS IS', I2, '.'/
     7        '0', 5X, 'INDIVIDUAL GRID PARAMETERS:'/
     8        ' ', T10, 'GRID', T20, 'IMG', T30, 'JMG',
     9             T40, 'XPOLEH', T50, 'YPOLEH', T60, 'IAG',
     1             T70, 'IBG', T80, 'JAG', T90, 'JBG' /)
C
      DO 4800 NG = 1, NGRDUSE
         IF (NG .EQ. NGRDUSE) THEN
            WRITE (IOUTUPRT, 4700)
     1             NG, IMG(NG), JMG(NG), XPOLEH(NG), YPOLEH(NG)
C
 4700       FORMAT (' ', T10, I3, T20, I3, T30, I3, T38, F8.3,
     1                   T48, F8.3, T60, I3, T70, I3,
     2                   T80, I3, T90, I3)
C
         ELSE
            WRITE (IOUTUPRT, 4700)
     1             NG, IMG(NG), JMG(NG), XPOLEH(NG), YPOLEH(NG),
     2             IAG(NG), IBG(NG), JAG(NG), JBG(NG)
         END IF
 4800 CONTINUE
C
C     BASIC GRID PARAMETERS ARE PRINTED.
C
      CALL PRINTG
C
C     CALCULATE THE LAYER PRESSURE AS NORMALIZED BY SURFACE
C     PRESSURE (H) AND LAYER EXNER FUNCTION AS NORMALIZED BY
C     H**KAPPA.  THIS WILL EXPEDITE THE CALCULATION OF LAYER
C     PRESSURES AND EXNER FUNCTIONS LATER.
C
      CALL SIGCON (DELSIG, KM, POVH, PIOVHK)
C
      IF (IOPTION .NE. 1) GO TO 5000
C
C     OPTION 1:
C
C     TRANSFER THE FIELDS IN LATITUDINAL STRIPS CONTAINING
C     DATA ON A LATITUDE-LONGITUDE GRID ON SIGMA SURFACES
C     FROM THE SEQUENTIAL FILE (UNIT INULOLA) TO A LARGE
C     ARRAY (IN THE CENTRAL MEMORY) THAT IS STILL IN STRIP FORM.
C     FILL IN SYMMETRIC FIELDS SOUTH OF THE EQUATOR FOR AS
C     FAR SOUTH AS THE NGM WILL EVER REASONABLY REACH.
C
      CALL LOLA (SPECS, LATG2, INULOLA)
C
C     THE WIND COMPONENTS IN THE LATITUDE-LONGITUDE SYSTEM ARE
C     ROTATED TO THOSE IN THE NGM COORDINATE SYSTEM.
C
      CALL TASKA
C
C     EXTRAPOLATE THE FIELDS (IN LATITUDINAL STRIPS CONTAINING DATA
C     ON A LATITUDE-LONGITUDE GRID ON SIGMA SURFACES) TO THE NORTH
C     POLE STRIP.
C
      CALL NPOLE
C
C     HORIZONTAL INTERPOLATION OF THE DRAG COEFFICIENT
C     TO THE NGM FORECAST GRID POINTS FROM THE LATITUDE-
C     LONGITUDE GRIDS.
C
      CALL TASKB (INDEXCD, INUDRAG)
C
C     HORIZONTAL INTERPOLATION OF THE SEA AND GROUND SFC TEMPERATURE,
C     AND SNOW/ICE COVER WHILE DETERMINING SFC HEAT FLX FLAG (BITSEA),
C     LAND/WATER MASK (BITWVL), AND SNOW/ICE MASK (BITSNO).
C
      CALL TASKC (INSNOICE,INAFSNO,INSST1DG,INDXST,
     1                      INUHLWC, INMSKSAB, INMSKAF)
C
C     HORIZONTAL INTERPOLATION OF THE SURFACE PRESSURE, U AND V
C     WIND COMPONENTS, VIRTUAL TEMPERATURE, SPECIFIC HUMIDITY,
C     AND TERRAIN HEIGHT TO THE NGM FORECAST GRID POINTS.
C
      CALL TASKD
C
C     CALCULATION OF THE SATURATION SPECIFIC HUMIDITY AT THE
C     WATER SURFACE.  THEN SPECIFIC HUMIDITY IS BOUND AWAY FROM
C     ZERO AND VALUES ABOVE THE TOP ANALYSIS LAYER FOR MOISTURE ARE
C     DETERMINED BY EXTRAPOLATION.  POTENTIAL TEMPERATURE IS COMPUTED
C     FROM VIRTUAL TEMPERATURE, SPECIFIC HUMIDITY, AND SURFACE
C     PRESSURE, AND THE DRY CONVECTIVE ADJUSTMENT APPLIED.
C     FINALLY, MASS-WEIGHTED THETA AND SPECIFIC HUMIDITY ARE
C     CALCULATED.
C
      CALL TASKE
C
C     MASS-WEIGHTED U AND V WIND COMPONENTS ARE CALCULATED NEXT.
C
      CALL TASKF
C
C     INPUT OF THE FILES CONTAINING THE ALBEDO, SURFACE ROUGHNESS(Z0),
C     AND MOISTURE AVAILABILITY(MA), AND INTERPOLATION TO THE
C     NGM GRIDS. CG, A MEASURE OF THE THERMAL CAPACITY OF THE SKIN,
C     IS ALSO SPECIFIED. TERRAIN STANDARD DEVIATION IS ALSO PROCESSED.
C
      CALL TASKH (INUALB,INUZ0,INUMA,INUTSD)
C
C     INPUT OF THE MONTHLY AIR TEMPERATURE CLIMO, INTERPOLATION
C     IN TIME TO THE CURRENT BASETIME OF RUN, AND INTERPOLATION
C     IN SPACE TO THE NGM GRIDS. 
C  
      CALL TASKI (INSSOILT)
C
C CHK WHETHER TO CALCULATE GRID-C BOUNDARY CONDITIONS FROM GDAS
C
      IF (ISOPT .LT. 0) GO TO 5050
C
C JUANG START
C
C     READ LATERAL BOUNDARY FOR POINTS U H AND V AT 6 HR. INTERVAL
C
      PRINT *,' ****** START  TO  DO  LTBL ****** '
      READ(INBND)    NGLB,LTBN,LBUO,LBVO,LBCH,KMLB,KHLB,NK,IOTM
      PRINT *,' $$ ',NGLB,LTBN,LBUO,LBVO,LBCH,KMLB,KHLB,NK,IOTM
      WRITE(IOUTBND) NGLB,LTBN,LBUO,LBVO,LBCH,KMLB,KHLB
C
C  -------READ LOCATION NUMBER OF LATERAL BOUNDARY POINTS
C
      READ(INBND)    (LSCRU(I),I=1,LBUO)
      WRITE(IOUTBND) (LSCRU(I),I=1,LBUO)
C     print *,' ** LSCRU ',(LSCRU(I),I=1,LBUO)
C
      READ(INBND)    (LSCRV(I),I=1,LBVO)
      WRITE(IOUTBND) (LSCRV(I),I=1,LBVO)
C     print *,' ** LSCRV ',(LSCRV(I),I=1,LBVO)
C
      READ(INBND)    (LSCRH(I),I=1,LBCH)
      WRITE(IOUTBND) (LSCRH(I),I=1,LBCH)
C     print *,' ** LSCRH ',(LSCRH(I),I=1,LBCH)
C
      IS = IMG(NGLB) + 10
C ------------------------------------------------------------
C ------START LOOP READ
      DO 5060 IT=1,IOTM
C
C ------READ TIME STEP
      READ(INBND) ITLB
      PRINT *,' $$ ITLB= ', ITLB
C ------READ SIGMA LAYER FROM T80
      READ(INBND) (SLYR(K),K=1,NK)
C     PRINT *,' $$ SLYR= ',(SLYR(K),K=1,NK)
C
 8800 FORMAT(1X,A10,I5/(1X,8G13.6))
 8811 FORMAT(1X,A10   /(1X,8G13.6))
C
C U POINT
C --------READ U
      READ(INBND) ((ULB(N,K),N=1,LBUO),K=1,NK)
      K = 1
      WRITE(*,8800) 'U AT U K= ',K,ULB(IS,K)
C     print *,' ** level ',K,(N,ULB(N,K),N=1,LBUO)
C --------READ T (THETA)
      READ(INBND) ((TLB(N,K),N=1,LBUO),K=1,NK)
      K = 1
      WRITE(*,8800) 'T AT U K= ',K,TLB(IS,K)
C     print *,' ** level ',K,(N,TLB(N,K),N=1,LBUO)
C --------READ H
      READ(INBND) (HLB(N),N=1,LBUO)
      WRITE(*,8811) 'H AT U    ',HLB(IS)
C     print *,' ** level 1 ',(N,HLB(N),N=1,LBUO)
C --------READ ZS
      READ(INBND) (ZSLB(N),N=1,LBUO)
      WRITE(*,8811) 'ZS AT U   ',ZSLB(IS)
C     print *,' ** level 1 ',(N,ZSLB(N),N=1,LBUO)
C ------- SIG TO SIG
      CALL GETHN(NGLB,LBUO,'U')
      CALL SG2SG(LBUO,'U...')
      WRITE(*,8811) 'HLBNEW AT U    ',HLBNEW(IS)
C     print *,' ** level 1 ',(N,HLBNEW(N),N=1,LBUO)
      DO 5067 K=1,KM
      DO 5066 N=1,LBUO
      ULB(N,K) = ULB(N,K) * HLBNEW(N) * 0.001E0
 5066 CONTINUE
      WRITE(*,8800) 'HU AT U K=',K,ULB(IS,K)
 5067 CONTINUE
      K = 1
C     print *,' ** level ',K,(N,ULB(N,K),N=1,LBUO)
C V POINT
C -------READ V
      READ(INBND) ((VLB(N,K),N=1,LBVO),K=1,NK)
      K = 1
      WRITE(*,8800) 'V AT V K= ',K,VLB(IS,K)
C     print *,' ** level ',K,(N,VLB(N,K),N=1,LBVO)
C -------READ T (THETA)
      READ(INBND) ((TLB(N,K),N=1,LBVO),K=1,NK)
      K = 1
      WRITE(*,8800) 'T AT V K= ',K,TLB(IS,K)
C     print *,' ** level ',K,(N,TLB(N,K),N=1,LBVO)
C -------READ H
      READ(INBND) (HLB(N),N=1,LBVO)
      WRITE(*,8811) 'H AT V    ',HLB(IS)
C     print *,' ** level 1 ',(N,HLB(N),N=1,LBVO)
C -------READ ZS
      READ(INBND) (ZSLB(N),N=1,LBVO)
      WRITE(*,8811) 'ZS AT V   ',ZSLB(IS)
C     print *,' ** level 1 ',(N,ZSLB(N),N=1,LBVO)
C ------- SIG TO SIG
      CALL GETHN(NGLB,LBVO,'V')
      CALL SG2SG(LBVO,'.V..')
      WRITE(*,8811) 'HNEW AT V    ',HLBNEW(IS)
C     print *,' ** level 1 ',(N,HLBNEW(N),N=1,LBVO)
      DO 5077 K=1,KM
      DO 5076 N=1,LBVO
      VLB(N,K) = VLB(N,K) * HLBNEW(N) * 0.001E0
 5076 CONTINUE
      WRITE(*,8800) 'HV AT V K=',K,VLB(IS,K)
 5077 CONTINUE
      K = 1
C     print *,' ** level ',K,(N,VLB(N,K),N=1,LBVO)
C H POINT
C --------READ T
      READ(INBND) ((TLB(N,K),N=1,LBCH),K=1,NK)
      K = 1
      WRITE(*,8800) 'T AT H K= ',K,TLB(IS,K)
C     print *,' ** level ',K,(N,TLB(N,K),N=1,LBCH)
C --------READ Q
      READ(INBND) ((QLB(N,K),N=1,LBCH),K=1,NK)
      K = 1
      WRITE(*,8800) 'Q AT H K= ',K,QLB(IS,K)
C     print *,' ** level ',K,(N,QLB(N,K),N=1,LBCH)
C -------READ H
      READ(INBND) (HLB(N),N=1,LBCH)
      WRITE(*,8811) 'H AT H    ',HLB(IS)
C     print *,' ** level 1 ',(N,HLB(N),N=1,LBCH)
C -------READ ZS
      READ(INBND) (ZSLB(N),N=1,LBCH)
      WRITE(*,8811) 'ZS AT H   ',ZSLB(IS)
C     print *,' ** level 1 ',(N,ZSLB(N),N=1,LBCH)
C ------ SIG TO SIG
      CALL GETHN(NGLB,LBCH,'H')
      CALL SG2SG(LBCH,'..TQ')
      WRITE(*,8811) 'HNEW AT H    ',HLBNEW(IS)
C     print *,' ** level 1 ',(N,HLBNEW(N),N=1,LBCH)
      DO 5086 N=1,LBCH
      HLBNEW(N) = HLBNEW(N) * 0.001E0
 5086 CONTINUE
      WRITE(*,8811) 'H/1000AT H',HLBNEW(IS)
      DO 5089 K=1,KM
      DO 5088 N=1,LBCH
      TLB(N,K) = TLB(N,K) * HLBNEW(N)
      QLB(N,K) = QLB(N,K) * HLBNEW(N)
 5088 CONTINUE
      WRITE(*,8800) 'HT AT H K=',K,TLB(IS,K)
      WRITE(*,8800) 'HQ AT H K=',K,QLB(IS,K)
 5089 CONTINUE
      K = 1
C     print *,' ** level ',K,(N,TLB(N,K),N=1,LBCH)
C     print *,' ** level ',K,(N,QLB(N,K),N=1,LBCH)
C
      WRITE(IOUTBND) ITLB,((ULB(N,K),N=1,LBUO),K=1,KM)
     1                   ,((VLB(N,K),N=1,LBVO),K=1,KM)
     2                   ,((TLB(N,K),N=1,LBCH),K=1,KM)
     3                   ,((QLB(N,K),N=1,LBCH),K=1,KM)
     4                   ,( HLBNEW(N)  ,N=1,LBCH )
 5060 CONTINUE
C
C JUANG STOP
C
      GO TO 5050
C
C
 5000 CONTINUE
C     OPTION 2:
C
C  PUT OPTION 2 TASKS HERE (I.E. TASKS TO MODIFY VBL JUST BEFORE FCST)
C
      CALL TASKC (INSNOICE,INAFSNO,INSST1DG,INDXST,
     1                       INUHLWC,INMSKSAB,INMSKAF)
C
 5050 CONTINUE
C
C     SOME DIAGNOSTICS FOR THE NGM FIELDS AND REPRESENTATIVE VALUES
C     OF THE FIELDS ARE PRINTED.
      CALL DIAG
C
C              PRINT THE CONTENTS OF THE SPECS ARRAY.
      WRITE (IOUTUPRT, 5100)
 5100 FORMAT ('1', 'THE VALUES IN THE SPECS ARRAY ARE:' /)
C
      I1 = -6
      I2 =  0
      DO 5300 LINE=1,100
         I1 = I1 + 7
         I2 = I2 + 7
         WRITE (IOUTUPRT, 5200) I1, (SPECS(I), I=I1, I2), I2
 5200    FORMAT (' ', 'I=', I3, ',', 1PE15.6, 6(1PE16.6),
     1                ', I=', I3)
 5300 CONTINUE
C
C JUANG START
C
C     DETERMINE THE OFFICE NOTE 85 FILENAME.
C     FIRST GET THE DAY OF THE CENTURY FOR THE INITIAL CONDITIONS.
CX    CALL W3FS17 (ICENYEAR, IMONTH, IDAY, ICENDAY)
      PRINT *,' CHECKING IN MAIN FOR DIGITS OF YEAR ',ICENYEAR
      IF(ICENYEAR.LT.100) ICENYEAR=2050-MOD(2050-ICENYEAR,100)
      ICENDAY=IW3JDN(ICENYEAR, IMONTH, IDAY)
C
C     NOW GET THE DAY OF THE CENTURY WHEN THE RAFS WAS CONVERTED
C     FROM THE SELA SPECTRAL INITIALIZATION TO THE TEMPERTON
C     IMPLICIT NORMAL MODE INITIALIZATION.
CX    CALL W3FS17 (88, 12, 14, ICENDAY0)
      ICENDAY0=IW3JDN(1988,12,14)
C JUANG STOP
C
C     WRITE THE FIELDS TO DISK.
      IF (IOPTION .EQ. 1) THEN
         IF (ICENDAY .GT. ICENDAY0) THEN
            DATANAME = 'RSTGES'
         ELSE IF (ICENDAY .LT. ICENDAY0) THEN
            DATANAME = 'RSTINP'
         ELSE IF (IHOUR .EQ. 12) THEN
            DATANAME = 'RSTGES'
         ELSE
            DATANAME = 'RSTINP'
         ENDIF
      ELSE
         DATANAME = 'RSTMOD'
      ENDIF
C
      CALL RESTARTW (IOUTUNIT, IOUTUPRT, DATANAME, SPECS, VBL,
     1               BITSEA, BITSNO, BITWVL)
C
      WRITE (IOUTUPRT, 5900)
 5900 FORMAT ('0', '***************', ' EXECUTION OF THE INPUT CODE ',
     1             'FOR THE NESTED GRID ',
     2             'MODEL IS COMPLETED. ***************' /)
C
C     STEP IS COMPLETE.  STOP.
      WRITE (IOUTUPRT, 6000)
 6000 FORMAT('0', '***** STEP ENDED AS PLANNED. *****')
C
C     WRITE TO THE W3LOG.
      CALL W3TAGE('RGCINP28')                                           
C
      STOP
C
 9998 CONTINUE
C              WE HIT THE INPUT END-OF-FILE.  THIS SHOULD NOT HAVE
C              HAPPENED.
      WRITE (IOUTUPRT, 9999)
 9999 FORMAT ('0', 'INPUT END-OF-FILE ENCOUNTERED AT THE WRONG PLACE ',
     1             'WHEN READING DATA CARDS SPECIFYING ',
     2             'FUNDAMENTAL MODEL OPTIONS.'/
     3        '0', 11X, 'EXECUTION TERMINATING.')
C
         CALL W3TAGE('RGCINP28')                                        
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN THE MAIN CODE NEAR STATEMENT NUM'
      ERRMSG(2) = 'BER 9999:                          '
      ERRMSG(3) = 'INPUT EOF ENCOUNTERED AT WRONG PLAC'
      ERRMSG(4) = 'E WHEN READING DATA CARDS.         '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)

C
      END
