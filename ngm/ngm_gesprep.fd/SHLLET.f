      SUBROUTINE SHLLET (IDATE, SI, LATG2, LONF, LEVS, LEVH,
     1                   ITUNIT, XLAT)
C$$$  SUBPROGRAM DOCUMENTATION  BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SHLLET      ID-RECORD FOR LOLA SIGMA FILE
C   PRGMMR: HOKE             ORG: W/NMC22    DATE: 88-03-31
C
C ABSTRACT: CREATES THE 700 WORD SPECIFICATION
C   FILE USED FOR CONTROL AND HISTORICAL
C   PURPOSES BY THE NGM INPUT CODE.
C
C PROGRAM HISTORY LOG:
C   84-06-07  JIM HOKE
C   88-03-31  DIMEGO      HALF PRECISION FOR ALL BUT
C   90-05-21  DIMEGO      CRAY CONVERSION
C
C USAGE:    CALL SHLLET(IDATE,SI,LATG2,LONF,LEVS,LEVH,
C                       ITUNIT,XLAT)
C
C   INPUT ARGUMENT LIST:
C     IDATE    - THE DATE/TIME GROUP.  (SELA CONVENTION.)
C              -  1)  HOUR OF THE DAY. (1200 GMT IS 12.)
C              -  2)  MONTH OF THE YEAR.
C              -  3)  DAY OF THE MONTH.
C              -  4)  YEAR OF THE CENTURY.
C     SI       -  THE SIGMA VALUES FOR THE INTERFACES STARTING
C              -  FROM THE GROUND, WITH SIGMA DECREASING UPWARD.
C              -  SI IS     F U L L   P R E C I S I O N  * * * *
C     LATG2    -  THE NUMBER OF NORTHERN HEMISPHERIC LATITUDINAL
C              -  STRIPS FROM THE TROPICS TOWARD THE NORTH POLE.
C     LONF     -  THE NUMBER OF NONDUPLICATE LONGITUDES.
C     LEVS     -  THE NUMBER OF LEVELS FOR U, V, AND TV.
C     LEVH     -  THE NUMBER OF LEVELS FOR Q.
C     ITUNIT   -  THE OUTPUT UNIT NUMBER FOR THE IDENTIFICATION
C              -  (SPECIFICATIONS) RECORD.
C     XLAT     -  THE LATITUDES IN DEGREES OF THE LATITUDINAL
C              -  STRIPS, STARTING NEAR THE EQUATOR.
C
C   OUTPUT FILES:
C     ITUNIT   - THE SPECIFICATIONS FILE AS IS READ BY
C              - THE NGM INPUT CODE
C     UNIT6    - LISTING OF THE SPECIFICATIONS FILE
C
C ATTRIBUTES:
C   LANGUAGE: STANDARD FORTRAN
C   MACHINE:
C
C$$$
C
      IMPLICIT REAL (A-H,O-Z)
      REAL SI
C
C     THE ACTUAL DIMENSIONS ARE XLAT(LATG2) AND SI(LEVS+1).
      DIMENSION XLAT(*), SI(*)
      DIMENSION SPECS(700)
      DIMENSION IDATE(4)
C
C     FIRST THE RECORD IS INITIALIZED TO ZERO.
      DO 88890 IQ2W6E=1,700
         SPECS(IQ2W6E)=0.0 E+0
88890 CONTINUE
C
C     HOUR OF THE DAY, IN GMT.
      SPECS(2) = (IDATE(1))
C
C     DAY OF THE MONTH.
      SPECS(3) = (IDATE(3))
C
C     MONTH OF THE YEAR.
      SPECS(4) = (IDATE(2))
C
C     YEAR OF THE CENTURY.
      SPECS(5) = (IDATE(4))
C
C     RESET FORECAST HOUR.
      SPECS(233) = 0. E+0
C
C     NUMBER OF NORTHERN HEMISPHERIC LATITUDINAL STRIPS.
      SPECS(10) = (LATG2)
C
C     TOTAL NUMBER OF LATITUDINAL STRIPS.
      SPECS(11) = (LATG2)
C
C     NUMBER OF NONDUPLICATE GRID POINTS ALONG A LATITUDINAL STRIP.
      SPECS(12) = (LONF)
C
C     NUMBER OF VERTICAL LEVELS FOR WIND AND VIRTUAL TEMPERATURE.
      SPECS(13) = (LEVS)
C
C     NUMBER OF VERTICAL LEVELS FOR THE HUMIDITY FIELD.
      SPECS(14) = (LEVH)
C
C
C     THE SIGMA VALUES FOR THE LAYERS ARE INSERTED.
      DO 275 L=1,LEVS
         SPECS(21 + L) = (SI(L) - SI(L+1))
  275 CONTINUE
C
C     LATITUDES OF THE LATITUDINAL STRIPS.
C     ORDER IS FROM LOW LATITUDE POLEWARD.
C     THE LATITUDES ARE IN UNITS OF DEGREES IN ARRAY SPECS.
      DO 400 J=1, LATG2
         SPECS(500 + J) = XLAT(J)
  400 CONTINUE
C
C     WRITE THE SPECS ARRAY TO DISK.
      WRITE (ITUNIT) SPECS
      ENDFILE ITUNIT
C
C     PRINT THE CONTENTS OF THE SPECS ARRAY.
      WRITE (6, 1200)
 1200 FORMAT ('0', 'THE VALUES IN THE SPECS ARRAY ARE:' /)
C
      I1 = -6
      I2 =  0
      DO 1400 LINE=1,100
         I1 = I1 + 7
         I2 = I2 + 7
         WRITE (6, 1300) I1, (SPECS(I), I=I1, I2), I2
 1300    FORMAT (' ', 'I=', I3, ',',E15.6, 6(E16.6),
     1                ', I=', I3)
 1400 CONTINUE
C
C     TASK IS DONE.  RETURN TO CALLING PROGRAM.
C
      RETURN
C
      END
