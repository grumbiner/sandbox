       SUBROUTINE GETLALO(RLATS,RLONS,WGTS,
     *                    JCAP,NLATH,NLON,NLONEX,
     *       IRS,IIS,AP,BP,AQR,BQR,GR,SLAT,CLAT,PE0,QE0,RO0,
     *       TRIGSout,IFAX)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GETLALO    RETURN GAUSSIAN LATS AND LONS AND TRANCONS
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 90-09-21
C
C ABSTRACT: RETURN GAUSSIAN LATS, LONS, AND TRANSFORM STUFF.
C
C PROGRAM HISTORY LOG:
C   90-09-21  PARRISH
C
C USAGE:  CALL GETLALO(RLATS,RLONS,WGTS,
C    *                    JCAP,NLATH,NLON,NLONEX,
C    *       IRS,IIS,AP,BP,AQR,BQR,GR,SLAT,CLAT,PE0,QE0,RO0,TRIGS,IFAX)
C   INPUT ARGUMENT LIST:
C     JCAP     - TRIANGULAR TRUNCATION
C     NLATH    - NUMBER OF GAUSSIAN LATS IN ONE HEMISPHERE
C     NLON     - NUMBER OF LONGITUDES
C     NLONEX   - GE NLON, TO ALLOW FOR SOME REPEAT LONGITUDES
C
C   OUTPUT ARGUMENT LIST:
C     RLATS    - RLATS(NLATH*2): GRID LATITUDES (RADIANS)
C     RLONS    - RLONS(NLONEX): GRID LONGITUDES (RADIANS)
C     WGTS     - WGTS(NLATH*2): GAUSSIAN INTEGRATION WEIGHTS
C     IRS      - IRS(0:JCAP,0:JCAP): SPECTRAL COEF
C     IIS      - IIS(0:JCAP,0:JCAP):    GATHER ARRAYS
C     AP       - AP(0:JCAP,0:JCAP):  RECURSION
C     BP       - BP(0:JCAP,0:JCAP):    CONSTANTS
C     AQR      - AQR(0:JCAP,0:JCAP):     FOR GENERATING
C     BQR      - BQR(0:JCAP,0:JCAP):       VECTOR SPHERICAL
C     GR       - GR(0:JCAP,0:JCAP):          HARMONICS
C     SLAT     - SLAT(NLATH):  SIN(GAUSSIAN LATITUDES)
C     CLAT     - CLAT(NLATH):  COS(GAUSSIAN LATS)
C     PE0      - PE0(NLATH,0:JCAP): P(L,L)
C     QE0      - QE0(NLATH,0:JCAP): Q(L,L)
C     RO0      - RO0(NLATH,0:JCAP): R(L,L)
C     TRIGS    - TRIGS(NLON*2)
C     IFAX     - IFAX(10)
C
C ATTRIBUTES:
C   LANGUAGE: CFT77
C   MACHINE:  CRAY YMP
C
C$$$
         implicit double precision (a-h,o-z)
         DIMENSION RLATS(NLATH*2),WGTS(NLATH*2),RLONS(NLONEX)
         DIMENSION W1(NLATH-1),W2(NLATH-1),W3(NLATH-1),W4(NLATH-1)
         DIMENSION IRS(0:JCAP,0:JCAP),IIS(0:JCAP,0:JCAP)
         DIMENSION AP(0:JCAP,0:JCAP),BP(0:JCAP,0:JCAP)
         DIMENSION AQR(0:JCAP,0:JCAP),BQR(0:JCAP,0:JCAP)
         DIMENSION GR(0:JCAP,0:JCAP)
         DIMENSION SLAT(NLATH),CLAT(NLATH)
         real*4 TRIGSout(NLON*2)
         integer IFAX(10)
         DIMENSION PE0(NLATH,0:JCAP),QE0(NLATH,0:JCAP),RO0(NLATH,0:JCAP)

         dimension trigs(nlon*2)
C--------
C-------- GET STUFF FOR FFT*S
C--------
         CALL M1FAX(IFAX,NLON,3)
         CALL M1FTRG(TRIGS,NLON,3)
         trigsout=trigs
C--------
C-------- COMPUTE RECURSION CONSTANTS AND INITIALIZE LEGENDRE FUNCTIONS
C--------
         CALL M1RCONS(AP,BP,AQR,BQR,GR,JCAP)
C--------
C-------- GET LATITUDES, LONGITUDES, AND INTEGRATION WEIGHTS.
C--------
         PIH=2.d0*ATAN(1.d0)
         CALL M1GLAT(NLATH-1,W1,W2,W3,W4)
         RLATS(1)=-PIH
         WGTS(1)=0.d0
CDIR$ IVDEP
         DO 100 I=1,NLATH-1
           RLATS(I+1)=W1(I)-PIH
           WGTS(I+1)=W2(I)
100      CONTINUE
         DO 200 I=1,NLATH
           RLATS(NLATH*2+1-I)=-RLATS(I)
           WGTS(NLATH*2+1-I)=WGTS(I)
           SLAT(I)=SIN(RLATS(I))
           CLAT(I)=COS(RLATS(I))
200      CONTINUE
         DLON=8.d0*ATAN(1.d0)/NLON
         DO 300 I=1,NLONEX
           RLONS(I)=(I-1.d0)*DLON
300      CONTINUE
         II=0
         IRS=0
         IIS=0
         DO 500 M=0,JCAP
           DO 400 L=0,JCAP-M
             II=II+1
             IRS(M+L,L)=II
             II=II+1
             IIS(M+L,L)=II
400        CONTINUE
500      CONTINUE
C--------
C-------- FINALLY GET INITIAL P,Q,R.
C--------
         CALL M1IPQR(PE0,QE0,RO0,SLAT,CLAT,NLATH,JCAP)
       RETURN
       END
