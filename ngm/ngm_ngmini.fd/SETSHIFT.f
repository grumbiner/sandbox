       SUBROUTINE SETSHIFT(IBIG,JBIG,
     *        L1, L2, L3, L4, L5,
     *        M1, M2, M3, M4, M5,
     *       IS1,IS2,IS3,IS4,IS5,
     *       LEN,BITA)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SETSHIFT    SET UP FOR SHIFTED VECTOR OPERATIONS
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: SET UP STARTING ADDRESSES, VECTOR LENGTH, AND CONTROL
C   STORE BIT VECTOR FOR OPERATIONS ON UP TO 5 DIFFERENT
C   SHIFTS OF TWO-D FIELDS.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL SETSHIFT(IBIG,JBIG,
C    *        L1, L2, L3, L4, L5,
C    *        M1, M2, M3, M4, M5,
C    *       IS1,IS2,IS3,IS4,IS5,
C    *       LEN,BITA)
C   INPUT ARGUMENT LIST:
C     IBIG     - COMMON DIMENSIONS OF
C     JBIG     - TWO-DIM FIELDS.
C     L1       - I-INDEX SHIFT FOR 1ST FIELD
C     L2       - I-INDEX SHIFT FOR 2ND FIELD
C     L3       - I-INDEX SHIFT FOR 3RD FIELD
C     L4       - I-INDEX SHIFT FOR 4TH FIELD
C     L5       - I-INDEX SHIFT FOR 5TH FIELD
C     M1       - J-INDEX SHIFT FOR 1ST FIELD
C     M2       - J-INDEX SHIFT FOR 2ND FIELD
C     M3       - J-INDEX SHIFT FOR 3RD FIELD
C     M4       - J-INDEX SHIFT FOR 4TH FIELD
C     M5       - J-INDEX SHIFT FOR 5TH FIELD
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     IS1      - STARTING ADDRESS FOR VECTOR OP(1ST FIELD)
C     IS2      - STARTING ADDRESS FOR VECTOR OP(2ND FIELD)
C     IS3      - STARTING ADDRESS FOR VECTOR OP(3RD FIELD)
C     IS4      - STARTING ADDRESS FOR VECTOR OP(4TH FIELD)
C     IS5      - STARTING ADDRESS FOR VECTOR OP(5TH FIELD)
C     LEN      - LENGTH OF VECTOR OP (COMMON FOR 5 FIELDS)
C     BITA     - CONTROL STORE BIT VECTOR FOR VECTOR OP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
         LOGICAL BITA(1)
C--------
         IMIN=MAX(1,1-L1,1-L2,1-L3,1-L4,1-L5)
         JMIN=MAX(1,1-M1,1-M2,1-M3,1-M4,1-M5)
         IMAX=MIN(IBIG,IBIG-L1,IBIG-L2,IBIG-L3,IBIG-L4,IBIG-L5)
         JMAX=MIN(JBIG,JBIG-M1,JBIG-M2,JBIG-M3,JBIG-M4,JBIG-M5)
         ILEN=IMAX-IMIN+1
         LEN=ILEN+IBIG*(JMAX-JMIN)
         IS1=IMIN+L1+IBIG*(JMIN+M1-1)
         IS2=IMIN+L2+IBIG*(JMIN+M2-1)
         IS3=IMIN+L3+IBIG*(JMIN+M3-1)
         IS4=IMIN+L4+IBIG*(JMIN+M4-1)
         IS5=IMIN+L5+IBIG*(JMIN+M5-1)
         DO 50 I=1,LEN
           BITA(I)=.FALSE.
50       CONTINUE
         II=0
         DO 100 I=1,LEN
           II=MOD(II,IBIG)+1
           IF(II.LE.ILEN) BITA(I)=.TRUE.
100      CONTINUE
       RETURN
       END
