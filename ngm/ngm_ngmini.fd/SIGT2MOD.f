       SUBROUTINE SIGT2MOD(TSIG,RLNPSFC,HMODE,S,NV,NMODIN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SIGT2MOD    SIGMA TO VERT MODE TRANSFORM
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-09-08
C
C ABSTRACT: TRANSFORM FROM SIGMA COORDINATE TO VERTICAL MODE
C   AMPLITUDES FOR TEMP, LOG(PSFC).  USE ONLY WITH COEFFICIENTS!
C
C PROGRAM HISTORY LOG:
C   88-09-08  PARRISH
C
C USAGE:    CALL SIGT2MOD(TSIG,RLNPSFC,HMODE,S,NV,NMODIN)
C   INPUT ARGUMENT LIST:
C     TSIG     - INTEGER POINTERS FOR INPUT SIGMA VARIABLES
C     RLNPSFC  - DITTO
C     HMODE    - INTEGER POINTERS FOR OUTPUT MODE VARIABLES
C     NV       - NUMBER OF POINTS TO TRANSFORM
C     NMODIN   - NUMBER OF VERTICAL MODES
C
C   OUTPUT ARGUMENT LIST:
C     S        - ARRAY CONTAINING ALL COEFFICIENT STUFF (POINTERS
C              - ARE USED TO IDENTIFY WHAT IS WHAT).
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         INTEGER TSIG(1),RLNPSFC,HMODE(1)
         REAL S(INCOEFS,1)
C--------
         COMMON/FIXSP03/NSIG,NMODES,
     *      DELSIG(INSIG),
     *      HEINVT(INSIG),HEINV(INSIG,INSIG),
     *      HEINVG(INSIG,INSIG),
     *      HPIBINVE(INSIG),HTBINVE(INSIG,INSIG),
     *      HE(INSIG,INSIG),HDEPTHS(INMODES)
C--------
C-------- INITIALIZE ALL INTERNAL VARIABLES IF NOT ALREADY DONE
         IF(NSIG.NE.INSIG) CALL SETSP03
C-------- MULTIPLY TEMPS BY GASCON BEFORE DOING TRANSFORM
         OVERG=1./9.8062
         GASCON=287.04
         DO 225 KK=1,NMODIN
           DO 208 I=1,NV
             S(I,HMODE(KK))=HEINVT(KK)*S(I,RLNPSFC)
208        CONTINUE
           DO 220 KR=1,NSIG
             K=NSIG+1-KR
             DO 212 I=1,NV
               S(I,HMODE(KK))=S(I,HMODE(KK))
     *            +HEINVG(KK,K)*GASCON*S(I,TSIG(KR))
212          CONTINUE
220        CONTINUE
C---------- DIVIDE H BY G, SO WE HAVE H IN UNITS OF METERS.
           DO 222 I=1,NV
             S(I,HMODE(KK))=OVERG*S(I,HMODE(KK))
222        CONTINUE
225      CONTINUE
       RETURN
       END
