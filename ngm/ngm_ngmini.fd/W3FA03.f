       SUBROUTINE W3FA03(PRESS,HEIGHT,TEMP,THETA)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    W3FA03      COMPUTE STANDARD HEIGHT, TEMP, POT TEMP
C   PRGMMR: MCDONELL, J.     ORG: W345       DATE: 74-07-01
C
C ABSTRACT: COMPUTES THE STANDARD HEIGHT, TEMPERATURE, AND POTENTIAL
C   TEMPERATURE GIVEN THE PRESSURE IN MILLIBARS ( > 8.68 MB ).  FOR
C   HEIGHT AND TEMPERATURE THE RESULTS DUPLICATE THE VALUES IN THE
C   U.S. STANDARD ATMOSPHERE (L962), WHICH IS THE ICAO STANDARD
C   ATMOSPHERE TO 54.7487 MB (20 KM) AND THE PROPOSED EXTENSION TO
C   8.68 MB (32 KM).  FOR POTENTIAL TEMPERATURE A VALUE OF  2/7  IS
C   USED FOR  RD/CP.
C   FOR DETAILS OBTAIN WRITEUP FROM NMC/AD/SAB
C
C PROGRAM HISTORY LOG:
C   74-07-01  MCDONELL, J.
C   84-07-05  JONES, R.E.      UPDATE TO FORTRAN77.
C
C USAGE:    CALL W3FA03 (PRESS, HEIGHT, TEMP, THETA)
C   INPUT ARGUMENT LIST:
C     PRESS    - PRESSURE IN MILLIBARS
C
C   OUTPUT ARGUMENT LIST:
C     A        - OUTPUT UPPER HESSENBERG FORM OF MATRIX
C     HEIGHT   - HEIGHT IN METERS
C     TEMP     - TEMPERATURE IN DEGREES KELVIN
C     THETA    - POTENTIAL TEMPERATURE IN DEGREES KELVIN
C
C   REMARKS: NOT VALID FOR PRESSURES LESS THAN 8.68 MILLIBARS,
C     DECLARE ALL PARAMETERS AS TYPE REAL*4 .
C
C ATTRIBUTES:
C   LANGUAGE: IBM VS FORTRAN
C   SOURCE STATEMENTS:  24    CORE SIZE: 6A8 (HEX) BYTES
C
C$$$
C
      REAL M0
      DATA
     *G     /9.80665/,
     *RSTAR /8314.32/,
     *M0    /28.9644/,
     *PISO  /54.7487/,
     *ZISO  /20000./,
     *SALP  /-.0010/,
     *PZERO /1013.25/,
     *T0    /288.15/,
     *ALP   /.0065/,
     *PTROP /226.321/,
     *TSTR  /216.65/
C
      ROVCP = 2.0 / 7.0
      R     = RSTAR / M0
      ROVG  = R / G
      FKT   = ROVG * TSTR
      AR    = ALP  * ROVG
      PP0   = PZERO ** AR
      IF (PRESS.LT.PISO)  GO TO 100
      IF (PRESS.GT.PTROP) GO TO 200
C
C     COMPUTE ISOTHERMAL CASES
C
      HEIGHT = 11000.0 + FKT * ALOG(PTROP/PRESS)
      TEMP = TSTR
      GO TO 300
C
C     COMPUTE LAPSE RATE = -.0010 CASES
C
 100  AR     = SALP * ROVG
      PP0    = PISO ** AR
      HEIGHT = (TSTR/(PP0 * SALP )) * (PP0-(PRESS ** AR)) +ZISO
      TEMP   = TSTR - (HEIGHT - ZISO) * SALP
      GO TO 300
C
 200  HEIGHT = (T0/(PP0 * ALP)) * (PP0 - (PRESS ** AR))
      TEMP   = T0-HEIGHT*ALP
C
 300  THETA  = TEMP * ((1000./PRESS) ** ROVCP)
      RETURN
      END
