       SUBROUTINE SETUPR00(VBLCOPY,LFRSTGES,
     *    W0U,W0V,W0TH,W0Q,W0H,W00U,W00V,W00TH,W00Q,W00H,
     *    R0U,R0V,R0TH,R0Q,R0H,IADDRGDP)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SETUPR00    COMPUTE TENDENCY OF FIRST GUESS
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: IF AVAILABLE, THEN COMPUTE TENDENCY OF FIRST GUESS
C   FOR INCREMENTAL INITIALIZATION.  ON INPUT, VBLCOPY CONTAINS
C   ANALYSIS.  IF A FIRST GUESS IS AVAILABLE, (LFRSTGES=.TRUE.)
C   THEN IT IS LOCATED IN VBL.  ON RETURN FROM SETUPR00,
C   VBLCOPY CONTAINS R00, THE TENDENCY OF THE FIRST GUESS (=0 FOR
C   NO FIRST GUESS), WHILE VBL CONTAINS THE ANALYSIS.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL SETUPR00(VBLCOPY,LFRSTGES,
C    *    W0U,W0V,W0TH,W0Q,W0H,W00U,W00V,W00TH,W00Q,W00H,
C    *    R0U,R0V,R0TH,R0Q,R0H,IADDRGDP)
C   INPUT ARGUMENT LIST:
C     LFRSTGES - =.TRUE., THEN FIRST GUESS AVAILABLE IN VBL.
C              - =.FALSE., THEN FIRST GUESS NOT AVAILABLE.
C     IADDRGDP - ADDRESS ARRAY FOR WORK SPACE IN VBLCOPY.
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     VBLCOPY  - WORK ARRAY
C     W0U      - WORK ARRAY
C     W0V      - WORK ARRAY
C     W0TH     - WORK ARRAY
C     W0Q      - WORK ARRAY
C     W0H      - WORK ARRAY
C     W00U     - WORK ARRAY
C     W00V     - WORK ARRAY
C     W00TH    - WORK ARRAY
C     W00Q     - WORK ARRAY
C     W00H     - WORK ARRAY
C     R0U      - WORK ARRAY
C     R0V      - WORK ARRAY
C     R0TH     - WORK ARRAY
C     R0Q      - WORK ARRAY
C     R0H      - WORK ARRAY
C
C   SUBPROGRAMS CALLED:  FROMVBL,ONEGRID,SHEM,CONVERTA,CTOVBL,
C     CZEROVBL,SWAPVBL,LATBND
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
C--------
         include "myparam"
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMDIAG/ CONTAINS QUANTITIES SAVED FOR
C          DIAGNOSTIC PURPOSES.
      COMMON /COMDIAG/  KADIAB( IKM, INGRDUSE ),
     1                  NPTSDHQ (2, INGRDUSE),
     2                  KKCLOUD2(2, INGRDUSE), NPTSBUOY(2, INGRDUSE),
     3                  NPTSWATR(2, INGRDUSE), NPTSCC  (2, INGRDUSE),
     4                  KKGSP2  (2, INGRDUSE),
     5                  KKGSE2  (2, INGRDUSE),
     6                  NPTSSAT (IKM, 2, INGRDUSE),
     7                  NPTSEVAP(IKM, 2, INGRDUSE),
     8                  NPTSGSP (2, INGRDUSE), NUMPROF,
     9                  PPTCC   (2, INGRDUSE), PPTGSP  (2, INGRDUSE),
     1                  ALATPR  (IMAXPROF),    ALONPR  (IMAXPROF),
     2                  DESCRP  (IMAXPROF)
C
      CHARACTER*24 DESCRP
C
C     COMMON BLOCK /COMOUT/ CONTAINS OUTPUT CODE PARAMETERS
C               AND ARRAYS.
C
      CHARACTER*20 DESCRIPT
C          DESCRIPTORS USED IN SR  OUTHORIZ
C
      COMMON /COMOUT/
C
C          THE FOLLOWING ARE STORED BY OUT2FILE FROM DATA CARDS.
C          (THE LAST 6 WILL BE OVERWRITTEN BY  OUTPUT  IF KGTYPE
C          IS 5 OR 26 (I.E. AN LFM GRID DEFINED IN TABLE 7 OF
C          OFFICE NOTE 84.))
C
     1     INUNIT, IOUTUNIT,
     2     KGTYPE, IMOUT, JMOUT, DNPEQOUT, DLAMOUT, XIPOUT, YJPOUT,
C
C          THE FOLLOWING COME FROM  MAIN  READING THE DATA CARDS.
     2   LQTYPE(IMAXOUTV),   LSTYPE(IMAXOUTV),
     3   DESCRIPT(IMAXOUTV), LEVELS(ILMOUT,IMAXOUTV),  NOUTVBLS,
C
C          THE FOLLOWING ARE DERIVED BY   OUTPUT  .
     4       IJOUT, LMOUT, NFILLER, NS2, NS3NGM, NS3OUT,
     5       COSOUT,   SINOUT,    DXOUT,    ZSIGLYR(IKM),
C
C           ARRAYS  PLOUT  AND  HTFD  ARE SET BY DATA STATEMENTS IN
C           THE SR  OUTPUT  .  'PADDER' FILLS OUT TO FULL WORD BNDRY.
     6      PLOUT(ILMOUT),   HTFD( 6),  PADDER,
C
C          THE FOLLOWING ADDRESSES AND ADDRESS ARRAYS ARE SET UP
C          BY   OUTPREP  WHEN IT IS CALLED BY   OUTPUT  .
     7  J1(INGRDUSE),          J2(INGRDUSE),   JADDK2L(INGRDUSE,2),
     8  JADDFNGM(INGRDUSE), JHREC(INGRDUSE),   JHKAP(INGRDUSE),
     9  JSCR2(12),    JSCR3NGM(INGRDUSE,12),   JSCR3OUT(12),
     A  LENGD(INGRDUSE), LTYPE(ILMOUT,INGRDUSE), MB1, MB2, MINT,
     B   MSOUT1,  MSOUT2,  MZGNDOUT,  MNGM3,  MHOUT, MZFR, MZTRO,
     C      MSZGND(INGRDUSE), NGRIDATA(INGRDUSE),
C
C            NUMGDREQ, THE NUMBER OF NGM GRIDS NEEDED TO COVER THE
C            OUTPUT GRID, AND ZLOUT, ARE SET BY  OUTPREP  .
     D                NUMGDREQ,    ZLOUT(ILMOUT),
C
C          OUTPREP ALSO ASSIGNS THE FOLLOWING DESCRIPTORS TO BE
C          USED BY  OUTHORIZ  FOR HORIZONTAL INTERPOLATION.
     E    AHORD,BHORD,CHORD,DHORD, ILLD,IULD,  SCRHORD,  OUTHORD,
C
C
C          THE FOLLOWING ARRAY CONTAINS THE INTEGERS NEEDED TO
C          FORM THE LABEL THAT GOES WITH EACH OUTPUT ARRAY.
     F               LABEL84( 27 ),
C
C          THE FOLLOWING SYMBOLS REPRESENT TEMPORARY VALUES OF
C          INTEGERS TO GO INTO  LABEL84 WHEN THE OUTPUT ARRAY
C          HAS BEEN PREPARED FOR PACKING.
C
     G  LABQTYPE, LABSSUB1, LABTMARK,  S1VALUE,  S2VALUE,
     H  LABMLDIF, LABSSUB2, LABFSUB2, LABKGRID, LABNUMPT
C
C
      COMMON /COM1WAY/ ITIMSTEP, ITBOUND, INGLB
         INTEGER IADDRGDP(5,INGRDUSE)
         REAL VBLCOPY(1)
         REAL TBAR(IKM),SIGKAP(IKM),SIGHAT(IKM)
         REAL G(IKM,IKM),TAU(IKM,IKM)
         REAL DSIG(IKM),PIVERT(IKM)
         REAL W0U(IIJMAX,IKM),W0V(IIJMAX,IKM)
         REAL W0TH(IIJMAX,IKM),W0Q(IIJMAX,IKM)
         REAL W0H(IIJMAX)
         REAL W00U(IIJMAX,IKM),W00V(IIJMAX,IKM)
         REAL W00TH(IIJMAX,IKM),W00Q(IIJMAX,IKM)
         REAL W00H(IIJMAX)
         REAL R0U(IIJMAX,IKM),R0V(IIJMAX,IKM)
         REAL R0TH(IIJMAX,IKM),R0Q(IIJMAX,IKM)
         REAL R0H(IIJMAX)
         LOGICAL LFRSTGES
C--------
C--------
         KHINTERP=2*IKM+1
         DO 1000 NG=1,NGRDUSE
           IJ=IMG(NG)*JMG(NG)
           MADDU=IADDRG(1,NG)
           MADDV=IADDRG(2,NG)
           MADDT=IADDRG(3,NG)
           MADDQ=IADDRG(4,NG)
           MADDH=IADDRG(5,NG)
           MADDUC=IADDRGDP(1,NG)
           MADDVC=IADDRGDP(2,NG)
           MADDTC=IADDRGDP(3,NG)
           MADDQC=IADDRGDP(4,NG)
           MADDHC=IADDRGDP(5,NG)
C----------
           IF(LFRSTGES) THEN
C------------
C------------WE HAVE FIRST GUESS FIELD.  COMPUTE ITS TENDENCY AND SAVE.
C------------ CALL ONEGRID TO DO ONE MODEL TIME-STEP.
C------------
             CALL FROMVBL(W0U,W0V,W0TH,W0Q,W0H,NG)
             NPAIR=1
             LASTSTEP=0
C------------ MAKE SURE PHYSICS IS TURNED OFF (SET IPHYSPL=1)
             IPHYSPL=1
C???????????????????????????????????????????????????????????????????????
C???????THIS CALL TO ONEGRID SHOULD USE THE SAME TIME-STEP ON ALL GRIDS?
C???????????????????????????????????????????????????????????????????????
             CALL ONEGRID(NG,NPAIR,LASTSTEP)
             CALL SHEM(KHINTERP)
C------------
C------------ NOW CONVERT VARIABLES.
C------------
             IBITNG2=MIN(NG,2)
             DO 100 K=1,KM
               KR=KM+1-K
               DSIG(K)=DELSIG(KR)
100          CONTINUE
             CPDP=1005.
             GASCON=287.05
             RKAPPA=GASCON/CPDP
             SIGOLD=0.
             DO 112 I=1,KM
               SIGNEW=SIGOLD+DSIG(I)
               SIGHAT(I)=.5*(SIGNEW+SIGOLD)
               SIGKAP(I)=EXP(RKAPPA*LOG(SIGHAT(I)))
               SIGOLD=SIGNEW
C              WRITE(6,111)DSIG(I),SIGHAT(I),SIGKAP(I)
C11            FORMAT(' DSIG,SIGHAT,SIGKAP=',3E12.4)
112          CONTINUE
           CALL CONVERTA(VBL(MADDU),VBL(MADDV),VBL(MADDT),VBL(MADDH),IJ,
     *      IMG(NG),JMG(NG),PR,GASCON,TBAR,BITGRDH(1,IBITNG2,NG),
     *       R0U(1,1),R0V(1,1),R0U(1,2),R0V(1,2),R0U(1,3))
           CALL CONVERTA(W0U,W0V,W0TH,W0H,IJMAX,IMG(NG),JMG(NG),
     *            PR,GASCON,TBAR,BITGRDH(1,IBITNG2,NG),
     *       R0U(1,1),R0V(1,1),R0U(1,2),R0V(1,2),R0U(1,3))
C------------
C------------ NEXT OBTAIN TENDENCIES, STORING IN R0.
C------------
             DELTAXA=(2.*RADIUS)*2./(REAL(NH)+.5)
             DO 204 I=1,NG
               DELTAXA=.5*DELTAXA
204          CONTINUE
             DELTATA=DTOVDX*DELTAXA
             DELTATAI=1./DELTATA
C???????????????????????????????????????????????????????????????
C???????AFTER CHANGING TIME-STEP IN CALL TO ONEGRID ABOVE,??????
C??????? MAKE CORRESPONDING CHANGE HERE.????????????????????????
C???????????????????????????????????????????????????????????????
C            WRITE(6,205)NG,DELTAXA,DELTATA,RADIUS,NH,DTOVDX,
C    *                DELTATAI
C05      FORMAT(' BEFORE OBTAINING FULL MODEL TENDENCIES ON GRID',I4,/,
C    *        ' DELTAXA,DELTATA=',2E12.4,/,
C    *        ' RADIUS,NH=',E12.4,I6,/,' DTOVDX,DELTATAI=',2E12.4)
             DO 210 K=1,KM
               IADDU=MADDU+(K-1)*IJ
               IADDV=MADDV+(K-1)*IJ
               IADDT=MADDT+(K-1)*IJ
               DO 21002 I=1,IJ
                 R0U(I,K)=(VBL(IADDU+I-1)-W0U(I,K))*DELTATAI
                 R0V(I,K)=(VBL(IADDV+I-1)-W0V(I,K))*DELTATAI
                 R0TH(I,K)=(VBL(IADDT+I-1)-W0TH(I,K))*DELTATAI
21002          CONTINUE
210          CONTINUE
             DO 21004 I=1,IJ
               R0H(I)=(VBL(MADDH+I-1)-W0H(I))*DELTATAI
21004        CONTINUE
C------------
C------------ ANALYSIS WILL ALWAYS BE FULL FIELDS, SO JUST COPY
C------------ VBLCOPY TO VBL.
C------------
             CALL CTOVBL(VBLCOPY,NG,IADDRGDP)
C------------
C------------ PUT TENDENCIES OF FIRST GUESS INTO VBLCOPY NOW
C------------
             DO 230 K=1,KM
               IADDUC=MADDUC+(K-1)*IJ
               IADDVC=MADDVC+(K-1)*IJ
               IADDTC=MADDTC+(K-1)*IJ
               IADDQC=MADDQC+(K-1)*IJ
               DO 23002 I=1,IJ
                 VBLCOPY(IADDUC+I-1)=R0U(I,K)
                 VBLCOPY(IADDVC+I-1)=R0V(I,K)
                 VBLCOPY(IADDTC+I-1)=R0TH(I,K)
                 VBLCOPY(IADDQC+I-1)=R0Q(I,K)
23002          CONTINUE
230          CONTINUE
             DO 23004 I=1,IJ
               VBLCOPY(MADDHC+I-1)=R0H(I)
23004        CONTINUE
C------------
C------------END OPTION WHERE FIRST GUESS WAS AVAILABLE
C------------
           ELSE
C------------
C------------ FIRST GUESS NOT AVAILABLE. MOVE STUFF FROM VBLCOPY.
C------------
             CALL CTOVBL(VBLCOPY,NG,IADDRGDP)
             CALL CZEROVBL(VBLCOPY,NG,IADDRGDP)
C------------
C------------ END SECOND OPTION, NO FIRST GUESS.
C------------
           END IF
C----------
C---------- END BIG LOOP OVER GRIDS.
C----------
1000     CONTINUE
C--------
C-------- FIX UP TENDENCIES IN GRID OVERLAP AREAS.
C--------
         CALL SWAPVBL(VBLCOPY,IADDRGDP)
         DO 1100 NG=1,INGRDUSE
           IF(NG.GT.1) THEN
             NGM=NG-1
             CALL LATBND(NGM,KHINTERP)
           ELSE
             CALL SHEM(KHINTERP)
           END IF
1100     CONTINUE
         CALL SWAPVBL(VBLCOPY,IADDRGDP)
       RETURN
       END
