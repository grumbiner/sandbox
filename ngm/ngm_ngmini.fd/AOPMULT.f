       SUBROUTINE AOPMULT(Z,D,H,AZ,AD,AH,JCAPIN,DEPTH)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    AOPMULT     APPLY "A" OPERATOR
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-06-13
C
C ABSTRACT: APPLY "A" OPERATOR, CORRESPONDING TO TEMPERTON*S "B"
C   LINEARIZATION.
C
C PROGRAM HISTORY LOG:
C   88-06-13  PARRISH
C
C USAGE:    CALL AOPMULT(Z,D,H,AZ,AD,AH,JCAPIN,DEPTH)
C   INPUT ARGUMENT LIST:
C     Z        - VORTICITY COEFFICIENTS.
C     D        - DIVERGENCE COEFFICIENTS.
C     H        - HEIGHT COEFFICIENTS.
C     JCAPIN   - TRIANGULAR TRUNCATION
C     DEPTH    - SCALE HEIGHT.
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     AZ       - VORTICITY COEFFICIENTS.
C     AD       - DIVERGENCE COEFFICIENTS.
C     AH       - HEIGHT COEFFICIENTS.
C
C REMARKS: MUST INCLUDE BLOCK DATA BLOCKSP02 AND DEFINITION OF
C           PL1 VARIABLES BEFORE COMPILING.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         real Z(1),D(1),H(1),AZ(1),AD(1),AH(1)
         INTEGER WORK
C--------
         COMMON/DAVEBUFF/ COF(INCOEFS,INUMCOEF),GRD(INLOLA,INUMGRID)
         real COF,GRD
C--------
         COMMON/FIXSP02/JCAP,NCOEFS,NFOP,JFOP,
     *      ISCALZD,IUNSCLZD,
     *      IBSHAT,IBVHAT,IBVIHAT,
     *      IBOP,ICOP,IFOP,ICINVOP,
     *      IRFOP,IRCINVOP,
     *      BFOP(INCOEFS),BONES(INCOEFS)
         LOGICAL BFOP,BONES
         LOGICAL BWORK(INCOEFS)
C--------
C-------- CHECK TO SEE IF INITIALIZATION OF INTERNAL VARIABLES NEEDED
         IF(JCAP.NE.JCAPIN) CALL SETSP02(JCAPIN)
C--------
C-------- VORTICITY FIRST
C--------
         CALL GETCOEF(WORK,1)
         CALL FOPMULT(D,AZ,COF(1,IFOP),NCOEFS,NFOP,JFOP,COF(1,WORK),
     *           BFOP,BONES,BWORK)
C--------
C-------- DIVERGENCE NEXT
C--------
         CALL FOPMULT(Z,AD,COF(1,IFOP),NCOEFS,NFOP,JFOP,COF(1,WORK),
     *           BFOP,BONES,BWORK)
         C0=SQRT(9.8062*DEPTH)/(6.3712E6*7.2921E-5)
         do 10002 i=1,ncoefs
         COF(i,WORK)=COF(i,IBOP)*D(i)
         AD(i)=AD(i)+COF(i,WORK)
         COF(i,WORK)=COF(i,ICOP)*H(i)
         AD(i)=AD(i)+C0*COF(i,WORK)
10002    continue
C--------
C-------- FINALLY HEIGHT
C--------
         do 10004 i=1,ncoefs
         COF(i,WORK)=COF(i,ICOP)*D(i)
         AH(i)=C0*COF(i,WORK)
10004    continue
         CALL FREECOEF(WORK,1)
       RETURN
       END
