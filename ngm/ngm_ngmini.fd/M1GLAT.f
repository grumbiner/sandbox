      SUBROUTINE M1GLAT(KHALF,COLRAD,WGT,WGTCS,RCS2)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    M1GLAT      COMPUTE GAUSSIAN LATS AND WEIGHTS
C   PRGMMR: SELA             ORG: W/NMC22    DATE: 79-03-03
C
C ABSTRACT: COMPUTE GAUSSIAN LATITUDES AND WEIGHTS.
C   SEE P 887, 25.4.29, HANDBOOK OF MATH FUNCTIONS, ABRAMOWITZ AND
C   STEGUN, FOR DETAILS.
C
C PROGRAM HISTORY LOG:
C   79-03-03  SELA
C   88-04-08  PARRISH   ADD DOCBLOCK
C
C USAGE:    CALL M1GLAT(KHALF,COLRAD,WGT,WGTCS,RCS2)
C   INPUT ARGUMENT LIST:
C     KHALF    - NUMBER OF GAUSSIAN LATITUDES TO COMPUTE (POLE TO
C              - EQUATOR)
C
C   OUTPUT ARGUMENT LIST:
C     COLRAD   - GAUSSIAN COLATITUDES IN RADIANS (FULL PRECISION)
C     WGT      - INTEGRATION WEIGHTS (FULL PRECISION)
C     WGTCS    - WGT/(COS(LAT))**2
C     RCS2     - 1/(COS(LAT))**2
C
C ATTRIBUTES:
C   LANGUAGE: FTN200
C   MACHINE:  CYBER
C
C$$$
      implicit double precision (a-h,o-z)
      double precision COLRAD(1),WGT(1),WGTCS(1),RCS2(1)
      EPS=1.d-12
      SI=1.d0
      K2=2*KHALF
      RK2=K2
      SCALE=2.d0  /(RK2*RK2)
      K1=K2-1
      PI=ATAN(SI)*4.d0
      DRADZ=PI/360.d0
      RAD=0.d0
      DO 1000 K=1,KHALF
      ITER=0
      DRAD=DRADZ
1     CALL M1POLY(K2,RAD,P2)
2     P1 =P2
      ITER=ITER+1
      RAD=RAD+DRAD
      CALL M1POLY(K2,RAD,P2)
      IF(SIGN(SI,P1).EQ.SIGN(SI,P2))GO TO 2
      IF(DRAD.LT.EPS)GO TO 3
      RAD=RAD-DRAD
      DRAD=DRAD*0.25d0
      GO TO 1
3     CONTINUE
      COLRAD(K)=RAD
      PHI=RAD   *180.d0/PI
      CALL M1POLY(K1,RAD,P1)
      X=  COS(RAD)
      W=SCALE*(1.d0-X*X)/(P1*P1)
      WGT(K)=    W
      SN=  SIN(RAD)
      W=W/(SN*SN)
      WGTCS(K)=    W
      RC=1.d0/(SN*SN)
      RCS2(K)=   RC
      CALL M1POLY(K2,RAD,P1)
      PRPHI =    PHI
      PRCOL =    COLRAD(K)
1000  CONTINUE
C     THIS LINE COMES FROM META FOR DEBUG
      RETURN
      END
