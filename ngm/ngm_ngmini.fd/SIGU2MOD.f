       SUBROUTINE SIGU2MOD(USIG,UMODE,S,NV,NMODIN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SIGU2MOD    SIGMA TO VERT MODE TRANSFORM
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-09-08
C
C ABSTRACT: TRANSFORM FROM SIGMA COORDINATE TO VERTICAL MODE
C   AMPLITUDES FOR WIND OR MOISTURE COMPONENT.  USE ONLY WITH
C   COEFFICIENTS!
C
C PROGRAM HISTORY LOG:
C   88-09-08  PARRISH
C
C USAGE:    CALL SIGU2MOD(USIG,UMODE,S,NV,NMODIN)
C   INPUT ARGUMENT LIST:
C     USIG     - INTEGER POINTERS FOR INPUT SIGMA VARIABLES
C     UMODE    - INTEGER POINTERS FOR OUTPUT MODE VARIABLES
C     NV       - NUMBER OF POINTS TO TRANSFORM
C     NMODIN   - NUMBER OF VERTICAL MODES
C
C   OUTPUT ARGUMENT LIST:
C     S        - ARRAY CONTAINING ALL COEFFICIENT STUFF (POINTERS
C              - ARE USED TO IDENTIFY WHAT IS WHAT).
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         INTEGER USIG(1),UMODE(1)
         REAL S(INCOEFS,1)
C--------
         COMMON/FIXSP03/NSIG,NMODES,
     *      DELSIG(INSIG),
     *      HEINVT(INSIG),HEINV(INSIG,INSIG),
     *      HEINVG(INSIG,INSIG),
     *      HPIBINVE(INSIG),HTBINVE(INSIG,INSIG),
     *      HE(INSIG,INSIG),HDEPTHS(INMODES)
C--------
C-------- INITIALIZE ALL INTERNAL VARIABLES IF NOT ALREADY DONE
         IF(NSIG.NE.INSIG) CALL SETSP03
         DO 225 KK=1,NMODIN
           DO 208 I=1,NV
             S(I,UMODE(KK))=0.
208        CONTINUE
           DO 220 KR=1,NSIG
             K=NSIG+1-KR
             DO 212 I=1,NV
             S(I,UMODE(KK))=S(I,UMODE(KK))
     *           +HEINV(KK,K)*S(I,USIG(KR))
212          CONTINUE
220        CONTINUE
225      CONTINUE
       RETURN
       END
