       SUBROUTINE USCLCOFS(ZIN,DIN,HIN,ZOUT,DOUT,HOUT,DEPTH,JCAPIN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    USCLCOFS    INVERSE OF "SCALCOFS"
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-06-13
C
C ABSTRACT: INVERSE OF SUBROUTINE "SCALCOFS".
C
C PROGRAM HISTORY LOG:
C   88-06-13  PARRISH
C
C USAGE:    CALL USCLCOFS(ZIN,DIN,HIN,ZOUT,DOUT,HOUT,DEPTH)
C   INPUT ARGUMENT LIST:
C     ZIN      - SCALED VORTICITY COEFFICIENTS.
C     DIN      - SCALED DIVERGENCE COEFFICIENTS.
C     HIN      - SCALED HEIGHT COEFFICIENTS.
C     DEPTH    - SCALE HEIGHT.
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     ZOUT     - UNSCALED VORTICITY COEFFICIENTS.
C     DOUT     - UNSCALED DIVERGENCE COEFFICIENTS.
C     HOUT     - UNSCALED HEIGHT COEFFICIENTS.
C
C REMARKS: MUST INCLUDE BLOCK DATA BLOCKSP02 AND DEFINITION OF
C           PL1 VARIABLES BEFORE COMPILING.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         real ZIN(1),DIN(1),HIN(1),ZOUT(1),DOUT(1),HOUT(1)
C--------
         COMMON/DAVEBUFF/ COF(INCOEFS,INUMCOEF),GRD(INLOLA,INUMGRID)
         real COF,GRD
C--------
         COMMON/FIXSP02/JCAP,NCOEFS,NFOP,JFOP,
     *      ISCALZD,IUNSCLZD,
     *      IBSHAT,IBVHAT,IBVIHAT,
     *      IBOP,ICOP,IFOP,ICINVOP,
     *      IRFOP,IRCINVOP,
     *      BFOP(INCOEFS),BONES(INCOEFS)
         LOGICAL BFOP,BONES
C--------
C-------- CHECK TO SEE IF INITIALIZATION OF INTERNAL VARIABLES NEEDED
         IF(JCAP.NE.JCAPIN) CALL SETSP02(JCAPIN)
C--------
C-------- MAKE SURE ZONAL PART OF COEFS HAS PROPER FORM
C--------
         do 10002 i=1,ncoefs
         ZIN(i)=COF(i,IBVHAT)*ZIN(i)
         DIN(i)=COF(i,IBVIHAT)*DIN(i)
         HIN(i)=COF(i,IBSHAT)*HIN(i)
10002    continue
         UNSCLH=SQRT(DEPTH/9.8062)
         do 10004 i=1,ncoefs
         ZOUT(i)=COF(i,IUNSCLZD)*ZIN(i)
         HOUT(i)=UNSCLH*HIN(i)
10004    continue
C-------- MULTIPLY DIVERGENCE BY -SQRT(-1)
         DO 100 I=1,NCOEFS,2
           DOUT(I)=DIN(I+1)
           DOUT(I+1)=-DIN(I)
100      CONTINUE
         do 10006 i=1,ncoefs
         DOUT(i)=COF(i,IUNSCLZD)*DOUT(i)
10006    continue
       RETURN
       END
