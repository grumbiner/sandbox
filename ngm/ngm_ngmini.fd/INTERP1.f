       SUBROUTINE INTERP1(F,G,RK,IK,BK,NF,NX,NY,NKX,NKY,NKZ,IZERO)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    INTERP1     INTERPOLATE FROM FIELD G TO FIELD F
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: INTERPOLATE FROM FIELD G TO FIELD F, USING PREVIOUSLY
C   DETERMINED INTERPOLATION CONSTANTS AND GATHER INDICES.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL INTERP1(F,G,RK,IK,BK,NF,NX,NY,NKX,NKY,NKZ,IZERO)
C   INPUT ARGUMENT LIST:
C     F        - INPUT INTERPOLATEES (RESULT INTERPOLATEES MAY BE
C              - ADDED ON IF DESIRED)
C     G        - INPUT INTERPOLATORS
C     RK       - INTERPOLATOR WEIGHTS
C     IK       - INTERPOLATOR BASE GATHER INDEX
C              -                          NK=NXK*NKY*NKZ
C              -             F(IOBS)=SUM(RK(IOBS,K)*G(IK(IOBS)+IS(K)))
C              -                         K=1
C     BK       - =0, POINT IS EXTRAPOLATEE (DO NOTHING).
C              - =1, POINT IS INTERPOLATEE (SO INTERPOLATE).
C     NF       - TOTAL NUMBER OF INTERPOLATEES.
C     NX       - X DIMENSION OF INTERPOLATOR GRID
C     NY       - Y DIMENSION OF INTERPOLATOR GRID
C     NKX      - ORDER OF INTERPOLATION IN X-DIRECTION.
C     NKY      - ORDER OF INTERPOLATION IN Y-DIRECTION.
C     NKZ      - ORDER OF INTERPOLATION IN Z-DIRECTION.
C     IZERO    - =0, THEN ZERO F BEFORE ACCUMULATING RESULT.
C              - =1, THEN ACCUMULATE INTERPOLATEE TO CURRENT
C              -                CONTENTS OF F.
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     F        - OUTPUT INTERPOLATEES
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
C--------
         include "myparam"
         REAL F(NF),G(1)
         REAL RK(NF,1)
C--------
         INTEGER IK(NF)
         LOGICAL BK(NF)
C--------
         NXY=NX*NY
         IF(IZERO.EQ.0) F=0.
         K=0
         DO 130 IKZ=1,NKZ
           DO 120 IKY=1,NKY
             DO 110 IKX=1,NKX
               K=K+1
               IS=IKX-1+(IKY-1)*NX+(IKZ-1)*NXY
               DO 11002 I=1,NF
                 IF(BK(I)) F(I)=F(I)+RK(I,K)*G(IK(I)+IS)
11002          CONTINUE
110          CONTINUE
120        CONTINUE
130      CONTINUE
       RETURN
       END
