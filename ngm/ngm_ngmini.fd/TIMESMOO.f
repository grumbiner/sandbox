       SUBROUTINE TIMESMOO(VBLCOPY,NUMVERT,
     *    W0U,W0V,W0TH,W0Q,W0H,W00U,W00V,W00TH,W00Q,W00H,
     *    R0U,R0V,R0TH,R0Q,R0H,IADDRGDP,LOOPBIG,NONLOOPB)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TIMESMOO    DO ONE ITERATION OF INITIALIZATION
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: DO ONE ITERATION OF INITIALIZATION.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL TIMESMOO(VBLCOPY,NUMVERT,W0U,W0V,W0TH,W0Q,W0H,
C                   W00U,W00V,W00TH,W00Q,W00H,R0U,R0V,R0TH,R0Q,R0H,
C                   IADDRGDP,LOOPBIG,NONLOOPB)
C   INPUT ARGUMENT LIST:
C     NUMVERT  - NUMBER OF VERTICAL MODES TO INITIALIZE.
C     IADDRGDP - ADDRESS ARRAY FOR WORK SPACE IN VBLCOPY.
C     LOOPBIG  - ITERATION NUMBER
C     NONLOOPB - TOTAL NUMBER OF ITERATIONS
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     VBLCOPY  - WORK ARRAY
C     W0U      - WORK ARRAY
C     W0V      - WORK ARRAY
C     W0TH     - WORK ARRAY
C     W0Q      - WORK ARRAY
C     W0H      - WORK ARRAY
C     W00U     - WORK ARRAY
C     W00V     - WORK ARRAY
C     W00TH    - WORK ARRAY
C     W00Q     - WORK ARRAY
C     W00H     - WORK ARRAY
C     R0U      - WORK ARRAY
C     R0V      - WORK ARRAY
C     R0TH     - WORK ARRAY
C     R0Q      - WORK ARRAY
C     R0H      - WORK ARRAY
C
C   SUBPROGRAMS CALLED: LATBND,SHEM,FROMVBL,ONEGRID,
C     CONVERTA,GETVIRTT,VTRANSFR,TOVBL,SFROMVBL,STOVBL,
C     TEMPERTN,GETACTT,UNAVERTA
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
C--------
         include "myparam"
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMDIAG/ CONTAINS QUANTITIES SAVED FOR
C          DIAGNOSTIC PURPOSES.
      COMMON /COMDIAG/  KADIAB( IKM, INGRDUSE ),
     1                  NPTSDHQ (2, INGRDUSE),
     2                  KKCLOUD2(2, INGRDUSE), NPTSBUOY(2, INGRDUSE),
     3                  NPTSWATR(2, INGRDUSE), NPTSCC  (2, INGRDUSE),
     4                  KKGSP2  (2, INGRDUSE),
     5                  KKGSE2  (2, INGRDUSE),
     6                  NPTSSAT (IKM, 2, INGRDUSE),
     7                  NPTSEVAP(IKM, 2, INGRDUSE),
     8                  NPTSGSP (2, INGRDUSE), NUMPROF,
     9                  PPTCC   (2, INGRDUSE), PPTGSP  (2, INGRDUSE),
     1                  ALATPR  (IMAXPROF),    ALONPR  (IMAXPROF),
     2                  DESCRP  (IMAXPROF)
C
      CHARACTER*24 DESCRP
C
C     COMMON BLOCK /COMOUT/ CONTAINS OUTPUT CODE PARAMETERS
C               AND ARRAYS.
C
      CHARACTER*20 DESCRIPT
C          DESCRIPTORS USED IN SR  OUTHORIZ
C
      COMMON /COMOUT/
C
C          THE FOLLOWING ARE STORED BY OUT2FILE FROM DATA CARDS.
C          (THE LAST 6 WILL BE OVERWRITTEN BY  OUTPUT  IF KGTYPE
C          IS 5 OR 26 (I.E. AN LFM GRID DEFINED IN TABLE 7 OF
C          OFFICE NOTE 84.))
C
     1     INUNIT, IOUTUNIT,
     2     KGTYPE, IMOUT, JMOUT, DNPEQOUT, DLAMOUT, XIPOUT, YJPOUT,
C
C          THE FOLLOWING COME FROM  MAIN  READING THE DATA CARDS.
     2   LQTYPE(IMAXOUTV),   LSTYPE(IMAXOUTV),
     3   DESCRIPT(IMAXOUTV), LEVELS(ILMOUT,IMAXOUTV),  NOUTVBLS,
C
C          THE FOLLOWING ARE DERIVED BY   OUTPUT  .
     4       IJOUT, LMOUT, NFILLER, NS2, NS3NGM, NS3OUT,
     5       COSOUT,   SINOUT,    DXOUT,    ZSIGLYR(IKM),
C
C           ARRAYS  PLOUT  AND  HTFD  ARE SET BY DATA STATEMENTS IN
C           THE SR  OUTPUT  .  'PADDER' FILLS OUT TO FULL WORD BNDRY.
     6      PLOUT(ILMOUT),   HTFD( 6),  PADDER,
C
C          THE FOLLOWING ADDRESSES AND ADDRESS ARRAYS ARE SET UP
C          BY   OUTPREP  WHEN IT IS CALLED BY   OUTPUT  .
     7  J1(INGRDUSE),          J2(INGRDUSE),   JADDK2L(INGRDUSE,2),
     8  JADDFNGM(INGRDUSE), JHREC(INGRDUSE),   JHKAP(INGRDUSE),
     9  JSCR2(12),    JSCR3NGM(INGRDUSE,12),   JSCR3OUT(12),
     A  LENGD(INGRDUSE), LTYPE(ILMOUT,INGRDUSE), MB1, MB2, MINT,
     B   MSOUT1,  MSOUT2,  MZGNDOUT,  MNGM3,  MHOUT, MZFR, MZTRO,
     C      MSZGND(INGRDUSE), NGRIDATA(INGRDUSE),
C
C            NUMGDREQ, THE NUMBER OF NGM GRIDS NEEDED TO COVER THE
C            OUTPUT GRID, AND ZLOUT, ARE SET BY  OUTPREP  .
     D                NUMGDREQ,    ZLOUT(ILMOUT),
C
C          OUTPREP ALSO ASSIGNS THE FOLLOWING DESCRIPTORS TO BE
C          USED BY  OUTHORIZ  FOR HORIZONTAL INTERPOLATION.
     E    AHORD,BHORD,CHORD,DHORD, ILLD,IULD,  SCRHORD,  OUTHORD,
C
C
C          THE FOLLOWING ARRAY CONTAINS THE INTEGERS NEEDED TO
C          FORM THE LABEL THAT GOES WITH EACH OUTPUT ARRAY.
     F               LABEL84( 27 ),
C
C          THE FOLLOWING SYMBOLS REPRESENT TEMPORARY VALUES OF
C          INTEGERS TO GO INTO  LABEL84 WHEN THE OUTPUT ARRAY
C          HAS BEEN PREPARED FOR PACKING.
C
     G  LABQTYPE, LABSSUB1, LABTMARK,  S1VALUE,  S2VALUE,
     H  LABMLDIF, LABSSUB2, LABFSUB2, LABKGRID, LABNUMPT
C
C
      COMMON /COM1WAY/ ITIMSTEP, ITBOUND, INGLB
         INTEGER IADDRGDP(5,INGRDUSE)
         REAL VBLCOPY(1)
         REAL TBAR(IKM),SIGKAP(IKM),SIGHAT(IKM)
         REAL HDEPTHS(IKM)
         REAL HEINVT(IKM),HEINV(IKM,IKM)
         REAL HEINVG(IKM,IKM)
         REAL HPIBINVE(IKM),HE(IKM,IKM)
         REAL HTBINVE(IKM,IKM)
         REAL DSIG(IKM)
         REAL W0U(IIJMAX,IKM),W0V(IIJMAX,IKM)
         REAL W0TH(IIJMAX,IKM),W0Q(IIJMAX,IKM)
         REAL W0H(IIJMAX)
         REAL W00U(IIJMAX,IKM),W00V(IIJMAX,IKM)
         REAL W00TH(IIJMAX,IKM),W00Q(IIJMAX,IKM)
         REAL W00H(IIJMAX)
         REAL R0U(IIJMAX,IKM),R0V(IIJMAX,IKM)
         REAL R0TH(IIJMAX,IKM),R0Q(IIJMAX,IKM)
         REAL R0H(IIJMAX)
         REAL UTSAVE(IIJMAX,INUMVERT,INGRDUSE)
         REAL VTSAVE(IIJMAX,INUMVERT,INGRDUSE)
         REAL PTSAVE(IIJMAX,INUMVERT,INGRDUSE)
         REAL U0SAVE(IIJMAX,INUMVERT,INGRDUSE)
         REAL V0SAVE(IIJMAX,INUMVERT,INGRDUSE)
         REAL P0SAVE(IIJMAX,INUMVERT,INGRDUSE)
C--------
         w0u=0.
         w0v=0.
         w0th=0.
         w0q=0.
         w0h=0.
         w00u=0.
         w00v=0.
         w00th=0.
         w00q=0.
         w00h=0.
         r0u=0.
         r0v=0.
         r0th=0.
         r0q=0.
         r0h=0.
        DO 9100 LOOP=1,2
C--------
C-------- ON FIRST TIME AROUND, GET TENDENCIES ON ALL GRIDS.
C-------- AT END OF FIRST TIME (LOOP=1,NG=NGRDUSE), COMPUTE TEMPERTON
C-------- CORRECTIONS.  ON SECOND TIME AROUND, ADD INCREMENTS
C-------- BACK TO FULL FIELDS.
C--------
        DO 9000 NG=1,INGRDUSE
         IBITNG2=MIN(NG,2)
         WRITE(6,10)NG
 10      FORMAT(' COMPUTING TENDENCY FOR NG=',I4)
         KHINTERP=2*KM+1
         IJ=IMG(NG)*JMG(NG)
         MADDU=IADDRG(1,NG)
         MADDV=IADDRG(2,NG)
         MADDT=IADDRG(3,NG)
         MADDQ=IADDRG(4,NG)
         MADDH=IADDRG(5,NG)
         MADDUC=IADDRGDP(1,NG)
         MADDVC=IADDRGDP(2,NG)
         MADDTC=IADDRGDP(3,NG)
         MADDQC=IADDRGDP(4,NG)
         MADDHC=IADDRGDP(5,NG)
        IF(LOOP.EQ.1) THEN
C--------
C-------- STEP 0. MAKE SURE BOUNDARIES ARE CORRECT.
C--------
         IF(NG.GT.1) THEN
           NGM=NG-1
           CALL LATBND(NGM,KHINTERP)
         ELSE
           CALL SHEM(KHINTERP)
         END IF
C----------
C---------- OBTAIN TENDENCIES ON ALL GRIDS, TRANSFORMED TO
C---------- VERT MODES AND SAVED IN UTSAVE,VTSAVE,PTSAVE.
C---------- CALL ONEGRID TO DO ONE MODEL TIME-STEP.
C----------
C---------- SAVE W0 IN SCRATCH ARRAY.
C----------
           CALL FROMVBL(W0U,W0V,W0TH,W0Q,W0H,NG)
           CALL FROMVBL(W00U,W00V,W00TH,W00Q,W00H,NG)
           NPAIR=1
           LASTSTEP=0
C------------ MAKE SURE PHYSICS IS TURNED OFF (SET IPHYSPL=1)
           IPHYSPL=1
           CALL ONEGRID(NG,NPAIR,LASTSTEP)
             IF(NG.EQ.1) CALL SHEM(KHINTERP)
C----------
C---------- NOW CONVERT VARIABLES.
C----------
           DO 100 K=1,KM
             KR=KM+1-K
             DSIG(K)=DELSIG(KR)
100        CONTINUE
           CPDP=1005.
           GASCON=287.05
           RKAPPA=GASCON/CPDP
           SIGOLD=0.
           DO 112 I=1,KM
             SIGNEW=SIGOLD+DSIG(I)
             SIGHAT(I)=.5*(SIGNEW+SIGOLD)
             SIGKAP(I)=EXP(RKAPPA*LOG(SIGHAT(I)))
             SIGOLD=SIGNEW
c            WRITE(6,111)DSIG(I),SIGHAT(I),SIGKAP(I)
c11          FORMAT(' DSIG,SIGHAT,SIGKAP=',3E12.4)
112        CONTINUE
           CALL CONVERTA(VBL(MADDU),VBL(MADDV),VBL(MADDT),VBL(MADDH),IJ,
     *      IMG(NG),JMG(NG),PR,GASCON,TBAR,BITGRDH(1,IBITNG2,NG),
     *       R0U(1,1),R0V(1,1),R0U(1,2),R0V(1,2),R0U(1,3))
           CALL CONVERTA(W0U,W0V,W0TH,W0H,IJMAX,IMG(NG),JMG(NG),
     *            PR,GASCON,TBAR,BITGRDH(1,IBITNG2,NG),
     *       R0U(1,1),R0V(1,1),R0U(1,2),R0V(1,2),R0U(1,3))
           TBAR=300.*GASCON
C----------
C---------- NEXT OBTAIN TENDENCIES, STORING IN R0.
C----------
         DELTAXA=(2.*RADIUS)*2./(NH+.5)
         DELTAXA0=DELTAXA
         DO 204 I=1,NG
           DELTAXA=.5*DELTAXA
204      CONTINUE
         DELTATA=DTOVDX*DELTAXA
         DELTATAI=1./DELTATA
c        WRITE(6,205)NG,DELTAXA,DELTATA,RADIUS,NH,DTOVDX,
c    *                DELTATAI
c05      FORMAT(' BEFORE OBTAINING FULL MODEL TENDENCIES ON GRID',I4,/,
c    *        ' DELTAXA,DELTATA=',2E12.4,/,
c    *        ' RADIUS,NH=',E12.4,I6,/,' DTOVDX,DELTATAI=',2E12.4)
           DO 210 K=1,KM
             IADDU=MADDU+(K-1)*IJ
             IADDV=MADDV+(K-1)*IJ
             IADDT=MADDT+(K-1)*IJ
             IADDUC=MADDUC+(K-1)*IJ
             IADDVC=MADDVC+(K-1)*IJ
             IADDTC=MADDTC+(K-1)*IJ
             DO 21002 I=1,IJ
               R0U(I,K)=(VBL(IADDU+I-1)-W0U(I,K))*DELTATAI
     *           -VBLCOPY(IADDUC+I-1)
               R0V(I,K)=(VBL(IADDV+I-1)-W0V(I,K))*DELTATAI
     *           -VBLCOPY(IADDVC+I-1)
               R0TH(I,K)=(VBL(IADDT+I-1)-W0TH(I,K))*DELTATAI
     *           -VBLCOPY(IADDTC+I-1)
21002        CONTINUE
210        CONTINUE
           DO 21004 I=1,IJ
             R0H(I)=(VBL(MADDH+I-1)-W0H(I))*DELTATAI
     *           -VBLCOPY(MADDHC+I-1)
21004      CONTINUE
C--------
C-------- CONVERT TEMP TENDENCIES TO VIRTUAL TEMP TENDENCIES
C--------
         CALL GETVIRTT(R0TH,W00H,W00Q,IJ,IJMAX,KM)
C--------
C--------  OBTAIN EIGENVALUES, TRANSFORM MATRICES.
C--------
C        CALL VMATHS(TBAR,DSIG,PR,CPDP,RKAPPA,GASCON,
C    *                   SIGHAT,HDEPTHS,HEINVT,HEINV,HEINVG,
C    *                   HPIBINVE,HTBINVE,HE)
         CALL VTRANSFR(HDEPTHS,HEINVT,HEINV,HEINVG,HPIBINVE,HTBINVE,HE)
C--------
C-------- TRANSFORM TO VERTICAL MODE SPACE
C--------
         DO 225 KK=1,NUMVERT
           DO 22502 I=1,IJ
             W0TH(I,KK)=HEINVT(KK)*R0H(I)
             W0U(I,KK)=0.
             W0V(I,KK)=0.
22502      CONTINUE
           DO 220 KR=1,IKM
             K=IKM+1-KR
             DO 22002 I=1,IJ
               W0U(I,KK)=W0U(I,KK)+HEINV(KK,K)*
     *                       R0U(I,KR)
               W0V(I,KK)=W0V(I,KK)+HEINV(KK,K)*
     *                       R0V(I,KR)
               W0TH(I,KK)=W0TH(I,KK)+HEINVG(KK,K)*
     *                       R0TH(I,KR)
22002        CONTINUE
220        CONTINUE
225      CONTINUE
C--------SAVE ALL GRIDS IN SPECIAL HOLDING ARRAYS PTSAVE,UTSAVE,VTSAVE
         DO 235 KK=1,NUMVERT
           DO 23502 I=1,IJ
             PTSAVE(I,KK,NG)=W0TH(I,KK)
             UTSAVE(I,KK,NG)=W0U(I,KK)
             VTSAVE(I,KK,NG)=W0V(I,KK)
23502      CONTINUE
235      CONTINUE
C--------
C-------- RESTORE ORIGINAL VBL.
C--------
         CALL TOVBL(W00U,W00V,W00TH,W00Q,W00H,NG)
C--------
C-------- SAVE FIRST NUMVERT LEVELS OF U,V,T FROM VBL IN U0SAVE,ETC.
C--------
         CALL SFROMVBL(U0SAVE(1,1,NG),V0SAVE(1,1,NG),P0SAVE(1,1,NG),NG)
C--------
C-------- IF NG=NGRDUSE, THEN DO INITIALIZATION ON ALL GRIDS.
C--------
         IF(NG.EQ.INGRDUSE) THEN
C--------
C-------- 1. FIX UP BOUNDARIES FOR TENDENCIES
C--------
           DO 241 NGG=1,INGRDUSE
             CALL STOVBL(UTSAVE(1,1,NGG),VTSAVE(1,1,NGG),
     *          PTSAVE(1,1,NGG),NGG)
241        CONTINUE
           DO 242 NGG=1,INGRDUSE
             IF(NGG.LT.INGRDUSE) CALL LATBND(NGG,NUMVERT)
             IF(NGG.EQ.1) CALL SHEM(NUMVERT)
             CALL SFROMVBL(UTSAVE(1,1,NGG),VTSAVE(1,1,NGG),
     *          PTSAVE(1,1,NGG),NGG)
242        CONTINUE
C----------
C---------- 2. DO TEMPERTON INITIALIZATION ON ALL GRIDS
C----------
           CALL TEMPERTN(UTSAVE,VTSAVE,PTSAVE,
     *        IMG,JMG,XPOLEH,YPOLEH,DELTAXA0,LOOPBIG,NUMVERT)
C----------    LAST ITERATION IS FOR DIAGNOSTICS ONLY, DON*T UPDATE
           IF(LOOPBIG.EQ.NONLOOPB) THEN
             DO 260 NGG=1,INGRDUSE
               DO 250 KK=1,NUMVERT
                 DO 25002 I=1,IIJMAX
                   UTSAVE(I,KK,NGG)=0.
                   VTSAVE(I,KK,NGG)=0.
                   PTSAVE(I,KK,NGG)=0.
25002            CONTINUE
250            CONTINUE
260          CONTINUE
           END IF
C--------
C-------- 5. FINALLY EXCHANGE U0SAVE,ETC. WITH VBL(U,ETC.)
C--------   THUS RESTORING VBL TO ORIGINAL STATE.
C--------
           DO 325 NGG=1,INGRDUSE
             CALL STOVBL(U0SAVE(1,1,NGG),V0SAVE(1,1,NGG),
     *          P0SAVE(1,1,NGG),NGG)
325        CONTINUE
         END IF
C----------------------
C----------------HERE FOR LOOP=2:  ONE STEP OF INITIALIZATION
C----------------               COMPLETE.  NOW ADD INCREMENT TO FULL
C------------------                FIELDS.
        ELSE
C--------
C-------- RETURN INCREMENTS FROM VERTICAL MODE SPACE
C--------
         DO 535 KK=1,NUMVERT
           DO 53502 I=1,IJ
             W0TH(I,KK)=PTSAVE(I,KK,NG)
             W0U(I,KK)=UTSAVE(I,KK,NG)
             W0V(I,KK)=VTSAVE(I,KK,NG)
53502      CONTINUE
535      CONTINUE
         CALL FROMVBL(W00U,W00V,W00TH,W00Q,W00H,NG)
         CALL TOVBL(W0U,W0V,W0TH,W0Q,W0H,NG)
         DO 550 K=1,IKM
           DO 55002 I=1,IJ
             W0U(I,K)=0.
             W0V(I,K)=0.
             W0TH(I,K)=0.
55002      CONTINUE
550      CONTINUE
         DO 55004 I=1,IJ
           W0H(I)=0.
55004    CONTINUE
         DO 575 K=1,NUMVERT
           IADDT=MADDT+(K-1)*IJ
           IADDU=MADDU+(K-1)*IJ
           IADDV=MADDV+(K-1)*IJ
           DO 57502 I=1,IJ
             W0H(I)=W0H(I)+HPIBINVE(K)*VBL(IADDT+I-1)
57502      CONTINUE
           DO 570 KKR=1,IKM
             KK=IKM+1-KKR
             DO 57002 I=1,IJ
               W0TH(I,KKR)=W0TH(I,KKR)+HTBINVE(KK,K)*VBL(IADDT+I-1)
               W0U(I,KKR)=W0U(I,KKR)+HE(KK,K)*VBL(IADDU+I-1)
               W0V(I,KKR)=W0V(I,KKR)+HE(KK,K)*VBL(IADDV+I-1)
57002        CONTINUE
570        CONTINUE
575      CONTINUE
C--------
C-------- CONVERT VIRT TEMP CORRECTIONS TO ACTUAL TEMP CORRECTIONS
C--------
         CALL GETACTT(W0TH,W00H,W00Q,IJ,IJMAX,KM)
C--------
C-------- 6. CONVERT AND ADD INCREMENTS BACK TO ORIGINAL VARIABLES
C--------      (W0U,V,TH,H CONTAINS INCREMENTS, W00U,.. FULL FIELDS)
C--------
           CALL UNAVERTA(VBL(MADDU),VBL(MADDV),VBL(MADDT),VBL(MADDH),
     *           VBL(MADDQ),IJ,
     *                  IMG(NG),JMG(NG),PR,GASCON,
     *                  W0U,W0V,W0TH,W0H,
     *                  W00U,W00V,W00TH,W00H,W00Q,
     *                  R0Q(1,1),R0Q(1,2),R0Q(1,3),R0Q(1,4),
     *                  R0Q(1,5),R0Q(1,6),R0Q(1,7),R0Q(1,8))
        END IF
9000    CONTINUE
9100   CONTINUE
       RETURN
       END
