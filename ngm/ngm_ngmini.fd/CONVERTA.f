       SUBROUTINE CONVERTA(U,V,TH,H,IJ,IMG,JMG,PR,GASCON,TBAR,BITH,
     *    WORKS,PIS,PSINVS,HU,HV)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    CONVERTA    CONVERT VARIABLES TO HOSKINS-SIMMONS FORM
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: CONVERT VARIABLES FROM PHILLIPS FORM TO HOSKINS-SIMMONS
C   FORM.  (INPUT SFCP IS IN BARS).  CONVERT POTTEMP USING ARAKAWA SIG.
C   (PR = .5*SIG**KAPPA).
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL CONVERTA(U,V,TH,H,IJ,IMG,JMG,PR,GASCON,TBAR,BITH,
C                   WORKS,PIS,PSINVS,HU,HV)
C   INPUT ARGUMENT LIST:
C     U        - H*U, PHILLIPS FORM OF U-WIND
C     V        - H*V, PHILLIPS FORM OF V-WIND
C     TH       - H*THETA, PHILLIPS FORM OF TEMP
C     H        - PHILLIPS SURFACE PRESSURE (IN BARS)
C     IJ       - NUMBER OF GRID POINTS ON GRID (IMG*JMG)
C     IMG      - X-DIMENSION OF GRID
C     JMG      - Y-DIMENSION OF GRID
C     PR       - RELATED TO EXNER FUNCTION (PR=.5*SIG**KAPPA)
C     GASCON   - GAS CONSTANT
C     BITH     - BIT ARRAY TELLING WHICH GRID POINTS OF H ARE FORECAST
C     NONLOOPB - TOTAL NUMBER OF ITERATIONS
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     U        - U, HOSKINS-SIMMONS FORM
C     V        - V, HOSKINS-SIMMONS FORM
C     TH       - T, HOSKINS-SIMMONS FORM
C     H        - HOSKINS-SIMMONS FORM
C     WORKS    - WORK ARRAY
C     PIS      - WORK ARRAY
C     PSINVS   - WORK ARRAY
C     HU       - WORK ARRAY
C     HV       - WORK ARRAY
C
C   SUBPROGRAMS CALLED:  P2KAP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         REAL TBAR(1)
         REAL PR(1)
         REAL WORKS(IIJMAX),PIS(IIJMAX),PSINVS(IIJMAX)
         REAL U(IJ,1),V(IJ,1),TH(IJ,1),H(1)
         REAL HU(IIJMAX),HV(IIJMAX)
         LOGICAL BITH(1)
C--------
c        WRITE(6,110)
c110     FORMAT(' ENTERING SUBROUTINE CONVERTA')
C--------
C-------- OBTAIN HU AND HV (SFCP INTERPOLATED TO U AND V GRIDS)
C--------
         NVECT=IMG*JMG
         DO 11402 I=1,NVECT
           HU(I)=H(I)
11402    CONTINUE
         DO 114 J=1,JMG-1
           DO 112 I=1,IMG
             HU(I+(J-1)*IMG)=.5*(H(I+(J-1)*IMG)+H(I+J*IMG))
112        CONTINUE
114      CONTINUE
         DO 11404 I=1,NVECT
           HV(I)=H(I)
11404    CONTINUE
         DO 118 J=1,JMG
           DO 116 I=1,IMG-1
             HV(I+(J-1)*IMG)=.5*(H(I+(J-1)*IMG)+H(I+1+(J-1)*IMG))
116        CONTINUE
118      CONTINUE
         DO 1182 I=1,NVECT
           HU(I)=1./HU(I)
           HV(I)=1./HV(I)
           PSINVS(I)=1./H(I)
1182     CONTINUE
         CALL P2KAP(H,PIS,NVECT)
         TNORM=0.
         DO 1184 I=1,NVECT
           IF(BITH(I)) TNORM=TNORM+1.
           H(I)=LOG(H(I))
1184     CONTINUE
         TNORM=1./TNORM
         DO 230 KR=1,IKM
           K=IKM+1-KR
           CONST=PR(KR)*GASCON*2.
           TBAR(K)=0.
           DO 2301 I=1,NVECT
             TH(I,KR)=CONST*TH(I,KR)*PIS(I)*PSINVS(I)
             IF(BITH(I)) TBAR(K)=TBAR(K)+TNORM*TH(I,KR)
             U(I,KR)=U(I,KR)*HU(I)
             V(I,KR)=V(I,KR)*HV(I)
2301       CONTINUE
230      CONTINUE
       RETURN
       END
