       SUBROUTINE GINTERP0(RK,IK,BITK,NOBS,XOBS,YOBS,ZOBS,VOBS,
     *            NX,NY,NZ,NVR,NKX,NKY,NKZ,XGRD,YGRD,ZGRD,EXTRAPX,
     *            EXTRAPY,EXTRAPZ,RKWORK,IKWORK)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GINTERPO    GET INTERPOLATION CONSTANTS
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: OBTAIN INTERPOLATION CONSTANTS FOR 1,2, OR 3 DIMENSIONAL
C   INTERPOLATION.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL GINTERP0(RK,IK,BITK,NOBS,XOBS,YOBS,ZOBS,VOBS,
C    *            NX,NY,NZ,NVR,NKX,NKY,NKZ,XGRD,YGRD,ZGRD,EXTRAPX,
C    *            EXTRAPY,EXTRAPZ,RKWORK,IKWORK)
C   INPUT ARGUMENT LIST:
C     NOBS     - TOTAL NUMBER OF INTERPOLATION POINTS.
C     XOBS     - X-COORDINATES OF INTERPOLATION POINTS.
C     YOBS     - Y-COORDINATES OF INTERPOLATION POINTS.
C     ZOBS     - Z-COORDINATES OF INTERPOLATION POINTS.
C     VOBS     - VARIABLE NUMBER FOR EACH INTERPOLATION POINT.
C              -           (ASSUMED TO BE WHOLE NUMBER)
C              -             (FOR MULTIVARIABLE FIELDS)
C     NX       - GRID DIMENSIONS. IT IS ASSUMED HERE THAT
C     NY       - FIELDS BEING INTERPOLATED FROM ARE INDEXED
C     NZ       - F(NX,NY,NZ,NVR)
C     NVR      -
C     NKX      - ORDER OF INTERPOLATION IN EACH DIRECTION
C     NKY      - =1 (ASSIGN VALUE OF NEAREST POINT)
C     NKZ      - =2 (LINEAR INTERPOLATION)
C              - =3 (QUADRATIC INTERPOLATION)
C              - =4 (CUBIC INTERPOLATION)
C              - NOTE THAT ACTUAL ORDER OF INTERPOLATION
C              - IS LE CORRESPONDING GRID DIMENSION.
C     XGRD     - COORDINATE VALUES OF EACH GRID DIMENSION
C     YGRD     -
C     ZGRD     -
C     EXTRAPX  - HOW FAR TO EXTRAPOLATE BEYOND END POINTS IN
C     EXTRAPY  - UNITS OF END INTERVALS.
C     EXTRAPZ  -
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     RK       - INTERPOLATION CONSTANTS.
C     IK       - BASE GATHER INDEX LIST.
C              -               NK=NKX*NKY*NKZ
C              -       F(IOBS)=SUM(RK(IOBS,NK)*G(IK(IOBS)+IS))
C              -               K=1
C              - WHERE F IS INTERPOLATED FROM G.
C              - AND "IS" IS SHIFT,
C              -        IS=IKX-1+(IKY-1)*NX+(IKZ-1)*NX*NY
C              -           IKX=1,NKX, ETC.
C     BITK     - =1 FOR INTERPOLATION POINTS
C              - =0 FOR EXTRAPOLATION POINTS
C     RKWORK   - WORK ARRAY
C     IKWORK   - WORK ARRAY
C
C   SUBPROGRAMS CALLED: INTERCON
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
C--------
         include "myparam"
         REAL RK(NOBS,NKX,NKY,NKZ),RKWORK(NOBS,4)
         REAL XOBS(NOBS),YOBS(NOBS),ZOBS(NOBS),VOBS(NOBS)
         REAL XGRD(NX),YGRD(NY),ZGRD(NZ)
         LOGICAL BITK(NOBS)
C--------
         INTEGER IK(NOBS),IKWORK(NOBS)
C--------
         NXY=NX*NY
         NXYZ=NXY*NZ
C--------
C-------- OBTAIN INTERPOLATION CONSTS FOR EACH DIRECTION.
C--------
C-------- SET ALL BITS TO INDICATE ALL POINTS ARE INTERPOLATEES UNTIL
C-------- DETERMINED OTHERWISE
C--------
         BITK=.TRUE.
C--------------X-DIRECTION
         CALL INTERCON(IK,RK,BITK,XOBS,NOBS,XGRD,NX,NKX,EXTRAPX)
         IK=IK+1
C--------------Y-DIRECTION
         CALL INTERCON(IKWORK,RKWORK,BITK,YOBS,NOBS,YGRD,NY,NKY,EXTRAPY)
         IK=IK+IKWORK*NX
         IF(NKY.GT.1) THEN
           DO 220 IKY=2,NKY
             DO 210 IKX=1,NKX
               DO 21002 I=1,NOBS
                 RK(I,IKX,IKY,1)=RK(I,IKX,1,1)
21002          CONTINUE
210          CONTINUE
220        CONTINUE
           DO 240 IKY=1,NKY
             DO 230 IKX=1,NKX
               DO 23002 I=1,NOBS
                 RK(I,IKX,IKY,1)=RK(I,IKX,NKY,1)*RKWORK(I,IKY)
23002          CONTINUE
230          CONTINUE
240        CONTINUE
         END IF
C--------------Z-DIRECTION
         CALL INTERCON(IKWORK,RKWORK,BITK,ZOBS,NOBS,ZGRD,NZ,NKZ,EXTRAPZ)
         IK=IK+IKWORK*NXY
         IF(NKZ.GT.1) THEN
           DO 330 IKZ=2,NKZ
             DO 320 IKY=1,NKY
               DO 310 IKX=1,NKX
                 DO 31002 I=1,NOBS
                   RK(I,IKX,IKY,IKZ)=RK(I,IKX,IKY,1)
31002            CONTINUE
310            CONTINUE
320          CONTINUE
330        CONTINUE
           DO 360 IKZ=1,NKZ
             DO 350 IKY=1,NKY
               DO 340 IKX=1,NKX
                 DO 34002 I=1,NOBS
                   RK(I,IKX,IKY,IKZ)=RK(I,IKX,IKY,NKZ)*RKWORK(I,IKZ)
34002            CONTINUE
340            CONTINUE
350          CONTINUE
360        CONTINUE
         END IF
         IF(NVR.GT.1) THEN
           DO 36004 I=1,NOBS
             IK(I)=IK(I)+(NINT(VOBS(I))-1)*NXYZ
36004      CONTINUE
         END IF
       RETURN
       END
