       SUBROUTINE LOLARXY(XOBS,YOBS,MC,NC,DX,ERAD,RLAT,RLON,W,
     *                    GAUSLATS)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    LOLARXY     GET POLA COORDS FOR LOLA GRID
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: OBTAIN TWO-DIM FIELDS OF LAT AND LON ON LOLA GRID.
C   THEN CONVERT TO X,Y POLAR STEREO COORDINATES.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL LOLARXY(XOBS,YOBS,MC,NC,DX,ERAD,RLAT,RLON,W,GAUSLATS)
C   INPUT ARGUMENT LIST:
C     MC       - NUMBER OF LONGITUDES ON LOLA GRID
C     NC       - NUMBER OF LATITUDES ON LOLA GRID
C     DX       - LONGITUDE INCREMENT IN RADIANS
C     ERAD     - RADIUS OF EARTH IN METERS
C     GAUSLATS - GAUSSIAN LATITUDES IN RADIANS
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     XOBS     - OUTPUT X-COORDINATES OF LOLA POINTS
C     YOBS     - OUTPUT Y-COORDINATES OF LOLA POINTS
C     RLAT     - WORK ARRAY
C     RLON     - WORK ARRAY
C     W        - WORK ARRAY
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         REAL XOBS(1),YOBS(1),RLAT(1),RLON(1),W(1)
         REAL GAUSLATS(1)
C--------
         PIH=2.*ATAN(1.)
         PI=4.*ATAN(1.)
         MNC=MC*NC
         DO 100 I=1,MC
           RLON(I)=(I-1)*DX
           RLAT(I)=GAUSLATS(1)
100      CONTINUE
         DO 210 J=2,NC
           J0=(J-1)*MC
           DO 207 I=1,MC
             RLON(J0+I)=RLON(I)
             RLAT(J0+I)=GAUSLATS(J)
207        CONTINUE
210      CONTINUE
C--------
C-------- NOW CONVERT TO POLAR-STEREO X,Y.
C--------
         DO 400 I=1,MNC
           W(I)=2.*ERAD*COS(RLAT(I))/
     *         (1.+SIN(RLAT(I)))
           XOBS(I)=W(I)*COS(RLON(I))
           YOBS(I)=W(I)*SIN(RLON(I))
400      CONTINUE
       RETURN
       END
