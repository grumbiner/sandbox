       SUBROUTINE POLORXY(RLON,RLAT,MC,NC,XPOLET,YPOLET,DX,DY,ERAD,
     *          XOBS,YOBS)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    POLORXY     GET LOLA COORDINATES OF POLA GRID POINTS
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-08-08
C
C ABSTRACT: OBTAIN LOLA COORDINATES OF POLA GRID POINTS.
C
C PROGRAM HISTORY LOG:
C   88-08-08  PARRISH
C
C USAGE:    CALL POLORXY(RLON,RLAT,MC,NC,XPOLET,YPOLET,DX,DY,ERAD,
C                         XOBS,YOBS)
C   INPUT ARGUMENT LIST:
C     MC       - X-DIMENSION OF POLA GRID
C     NC       - Y-DIMENSION OF POLA GRID
C     XPOLET   - X-COORDINATE OF POLE
C     YPOLET   - Y-COORDINATE OF POLE
C     DX       - X-GRID INCREMENT
C     DY       - Y-GRID INCREMENT
C     ERAD     - RADIUS OF EARTH
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     RLON     - LONGITUDE COORDINATES OF POLA GRID POINTS (RADIANS)
C     RLAT     - LATITUDE COORDINATES OF POLA GRID POINTS
C     XOBS     - WORK ARRAY
C     YOBS     - WORK ARRAY
C
C   SUBPROGRAMS CALLED: MYVATAN2
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         REAL XOBS(1),YOBS(1),RLAT(1),RLON(1)
C--------
         CALL POLARXY(XOBS,YOBS,MC,NC,XPOLET,YPOLET,DX,DY)
         MNC=MC*NC
         ERAD2I=1./(4.*ERAD*ERAD)
         TPI=8.*ATAN(1.)
         DO 100 I=1,MNC
           RLAT(I)=ERAD2I*(XOBS(I)**2+YOBS(I)**2)
           RLAT(I)=(1.-RLAT(I))/(1.+RLAT(I))
           RLAT(I)=ASIN(RLAT(I))
100      CONTINUE
         CALL MYVATAN2(RLON,YOBS,XOBS,MNC)
C SAMEAS--> RLON(1;MNC)=VATAN2(YOBS(1;MNC),XOBS(1;MNC);MNC)
C------- BUT MYVATAN2 TAKES CARE OF STUPID RULE ABOUT X=Y=0 NOT ALLOWED
C--------
C-------- CHANGE RANGE OF LONGITUDE TO 0-2*PI.
C--------
         DO 200 I=1,MNC
           IF(RLON(I).LT.0.) RLON(I)=RLON(I)+TPI
200      CONTINUE
       RETURN
       END
