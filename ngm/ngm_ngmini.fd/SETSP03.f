       SUBROUTINE SETSP03
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SETSP03     SET ALL VERT MODE INTERNAL VARIABLES
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-09-08
C
C ABSTRACT: SET UP ALL VERTICAL MODE INTERNAL VARIABLES IN
C   COMMON/FIXSP03/.
C
C PROGRAM HISTORY LOG:
C   88-09-08  PARRISH
C
C USAGE:    CALL SETSP03
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         COMMON/FIXSP03/NSIG,NMODES,
     *      DELSIG(INSIG),
     *      HEINVT(INSIG),HEINV(INSIG,INSIG),
     *      HEINVG(INSIG,INSIG),
     *      HPIBINVE(INSIG),HTBINVE(INSIG,INSIG),
     *      HE(INSIG,INSIG),HDEPTHS(INMODES)
C--------
         REAL DSIG(INSIG),SIGHAT(INSIG),SIGINT(INSIGP1)
         REAL PR(INSIG),TBAR(INSIG)
         REAL PRESSMB,STDHIGH,STDTEMP,STDTHETA,CKAPPAP1,ONEMSIG
C--------
         NSIG=INSIG
         NMODES=INMODES
C--------
C-------- SET UP DELSIG IF NOT ALREADY SPECIFIED
C--------
         IF(DELSIG(1).GT.10.) DELSIG=1./NSIG
         GASCON=287.04
         CP=1004.6
         RKAPPA=GASCON/CP
         CKAPPAP1=RKAPPA+1.
C              CALCULATE THE INTERFACE VALUES OF SIGMA.
      SIGINT(1) = 0.0
C
      DO 1800 K=1, NSIG
         SIGINT(K+1) = SIGINT(K) + DELSIG(K)
 1800 CONTINUE
C
      SIGINT(NSIG+1) = 1.
C
C              COMPUTE PR(K), WHICH WHEN MULTIPLIED BY 2 TIMES
C              H ** KAPPA YIELDS PI, THE EXNER FUNCTION.
      PR(1) = 1.
C
      DO 1900 K=2, NSIG
         ONEMSIG = 1.E0 -  SIGINT(K)
         PR(K) = ONEMSIG**CKAPPAP1
 1900 CONTINUE
C
      C1 = 1./(2.*CKAPPAP1)
C
      DO 2000 K=1, NSIG-1
         PR(K) = C1 * (PR(K) - PR(K+1) ) / DELSIG(K)
 2000 CONTINUE
C
      PR(NSIG) = C1 * PR(NSIG) / DELSIG(NSIG)
C--------
         DO 300 K=1,NSIG
           KR=NSIG+1-K
           DSIG(K)=DELSIG(KR)
300      CONTINUE
         SIGOLD=0.
         DO 312 I=1,NSIG
           SIGNEW=SIGOLD+DSIG(I)
           SIGHAT(I)=.5*(SIGNEW+SIGOLD)
           SIGOLD=SIGNEW
312      CONTINUE
         DO 314 K=1,NSIG
C          PRESSMB=1000.*(2.*PR(K))**CKAPPAI
           PRESSMB=1000.*SIGHAT(K)
           PRESSMB=MAX(PRESSMB,8.7)
           CALL W3FA03(PRESSMB,STDHIGH,STDTEMP,STDTHETA)
           TBAR(K)=STDTEMP
c          WRITE(6,313)PRESSMB,TBAR(K),K
313        FORMAT(' PRESSURE = ',F8.1,' TBAR(K) = ',F8.1,' K=',I3)
314      CONTINUE
         CALL VMATHS(TBAR,DSIG,PR,CP,RKAPPA,GASCON,
     *                   SIGHAT,HDEPTHS,HEINVT,HEINV,HEINVG,
     *                   HPIBINVE,HTBINVE,HE)
       RETURN
       END
