      SUBROUTINE SHEM (KHINTERP)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    SHEM        COMPUTE VALUES OF H-,U-, AND V-TYPE VBLS
C   PRGMMR: J TUCCILLO       ORG: W/NMC40    DATE: 90-06-18
C
C ABSTRACT: COMPUTE VALUES OF H-,U-, AND V-TYPE VBLS
C   .       AT THE SOUTHERN HEMISPHERE 'BOUNDARY' PTS.
C   .       OF GRID A OF THE NGM MODEL.
C   .
C   SUBROUTINE  SHEMPRE  HAS PREPARED THE 3 ARRAYS OF
C   GATHER-SCATTER INTEGERS ISHEMH,ISHEMU,ISHEMV, AND THE 5 ARRAYS
C   SHH4H,SHU4U,SHV4U,SHU4V,SHV4V OF BILINEAR INTERPOLATION COEFF-
C   ICIENTS, FOR USE BY THIS ROUTINE.  THIS ROUTINE CARRIES OUT
C   THE CALCULATION OF VALUES AT THESE BOUNDARY POINTS FOR THE KM
C   LEVELS OF HU AND THE KM LEVELS OF HV, AND FOR KHINTERP LEVELS
C   OF THE H-TYPE VBLS, H*THETA, H*Q, H,ETC.
C   .
C   .
C   THIS IS TO BE DONE AFTER EVERY FORECAST TIME STEP ON
C   GRID A.
C
C PROGRAM HISTORY LOG:
C   90-06-18  J TUCCILLO
C
C USAGE:  CALL SHEM (KHINTERP)
C   INPUT ARGUMENT LIST:
C     KHINTERP -  THE NUMBER OF CONTIGUOUS LEVELS OF
C                 H-POINT VARIABLES TO BE INTERPOLATED
C                 AT THE EQUATORIAL BOUNDARY.
C
C   SUBPROGRAMS CALLED:
C     LIBRARY:
C       COMMON   - COMCONST
C                  COMBLANK
C                  COMSHEM
C
C REMARKS:
C   .
C   - - - - - - - - I N P U T   V A R I A B L E S  - - - - - - - - -
C   .
C   NAMES          MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----          ----------------------------------        ---------
C   NSHEMH,NSHEMU,  THE NUMBER OF BOUNDARY POINTS FOR H-,      COMMON
C   AND NSHEMV      HU-, AND HV-TYPE VARIABLES.
C   .
C   ISHEMX,X=H,U,V  ARRAYS OF GATHER-SCATTER INTEGERS          COMMON
C   .
C   SHX4Y(N,4)      4 ARRAYS OF BILINEAR INTERPOLATION COEFF-  COMMON
C   .               ICIENTS TO GET VBL X(=H,U,V) FOR VBL
C   .               Y(=H,U,V)
C   .
C   VBL             ARRAY CONTAINING ALL BASIC NGM FORECAST    COMMON
C   .               VARIABLES
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----       ----------------------------------        ---------
C   .
C   VBL             ARRAY CONTAINING ALL BASIC NGM FORECAST   COMMON
C   .               VARIABLES
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      include 'parmodel'
C
C...TRANSLATED BY FPP 3.00Z36 11/09/90  15:14:03
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMSHEM/ CONTAINS VARIABLES NEEDED
C     FOR THE EQUATORIAL BOUNDARY CONDITIONS.
      COMMON /COMSHEM/  NSHEM, NSHEMH, NSHEMU, NSHEMV,
     1                  ISHEMH(INSHEM, 2), ISHEMU(INSHEM, 3),
     2                  ISHEMV(INSHEM, 3),
     3                  SHH4H (INSHEM, 4), SHU4U (INSHEM, 4),
     4                  SHV4U (INSHEM, 4), SHU4V (INSHEM, 4),
     5                  SHV4V (INSHEM, 4)
C
      DIMENSION X1 ( IIJMAX )
      EQUIVALENCE ( X1, SCR )
C
C          GRID A DIMENSIONS
      IM=2*NH+6
      IMJM=IM*IM
C          NUMBER OF H, HU, AND HV BNDRY POINTS.
      MH=NSHEMH
      MU=NSHEMU
      MV=NSHEMV
C
C                        DO THE H VARIABLE TYPE POINTS.
C          FIRST SET DESCRIPTORS USED FOR THE H COMPUTATIONS.
C          PRESET INITIAL ADDRESS FOR H VBL TYPES OF GRID A
      IAH=IADDRG(3,1)-IMJM-1
C
      DO 10 K=1,KHINTERP
      IAH=IAH+IMJM
C          GATHER H VALUES SURROUNDING IMAGE PT INTO X1,--,X4.
C          COMPUTE BILINEAR INTPD VALUE AT IMAGE POINT
C          SCATTER RESULT FROM X5 INTO H ARRAY IN VBL.
CMIC$ PARALLEL SHARED(MH, IAH, X1, ISHEMH, VBL, IM, SHH4H) PRIVATE(IQ2)
CMIC$ DO PARALLEL VECTOR
      DO 88890 IQ2=1,MH
         X1 ( IQ2 ) =
     *      SHH4H(IQ2,1)*VBL(ISHEMH(IQ2,2)+IAH)+
     *      SHH4H(IQ2,2)*VBL(ISHEMH(IQ2,2)+IAH+1)+
     *      SHH4H(IQ2,3)*VBL(ISHEMH(IQ2,2)+IAH+IM)+
     *      SHH4H(IQ2,4)*VBL(ISHEMH(IQ2,2)+IAH+1+IM)
88890 CONTINUE
CFPP$ PERMUTATION(ISHEMH)
CMIC$ DO PARALLEL VECTOR
      DO 88891 IQ2 = 1, MH
         VBL(ISHEMH(IQ2,1)+IAH) = X1 ( IQ2 )
88891 CONTINUE
CMIC$ END PARALLEL
C
   10 CONTINUE
C
C             COMPUTE HU(N=1) AND HV(N=2) BNDRY VALUES.
C          FIRST THE SPECIAL DESCRIPTORS FOR THE HU OPERATION.
C          PRESET ADDRESS FOR SCATTER INTO HU ARRAY.
      ISC=IADDRG(1,1)-IMJM-1
C          NUMBER OF HU BNDRY POINTS.
      MX=MU
C
      N=0
C          ENTER N LOOP(N=1 FOR HU PTS, N=2 FOR HV PTS).
   20 N=N+1
C          PRESET ADDRESSES FOR GATHERING HU AND HV VALUES  AROUND
C               THE IMAGE POINT.
      IGU=IADDRG(1,1)-IMJM-1
      IGV=IADDRG(2,1)-IMJM-1
C
C          ENTER THE K LOOP
      DO 30 K=1,KM
      IGU=IGU+IMJM
      IGV=IGV+IMJM
      ISC=ISC+IMJM
      IF ( N .EQ. 1 ) THEN
C          GATHER HU VALUES SURROUNDING THE IMAGE POINT.
C          FORM LINEAR COMBINATION OF THESE HU VALUES INTO X5.
CMIC$ PARALLEL SHARED(MX, ISC, X1, ISHEMU, VBL, IGU, IM, IGV, SHU4U,
CMIC$1   SHV4U) PRIVATE(IQ2)
CMIC$ DO PARALLEL VECTOR
      DO 88960 IQ2=1,MX
         X1 ( IQ2 ) =
     *      SHU4U(IQ2,1)*VBL(ISHEMU(IQ2,2)+IGU)+
     *      SHU4U(IQ2,2)*VBL(ISHEMU(IQ2,2)+IGU+1)+
     *      SHU4U(IQ2,3)*VBL(ISHEMU(IQ2,2)+IGU+IM)+
     *      SHU4U(IQ2,4)*VBL(ISHEMU(IQ2,2)+IGU+1+IM)+
     *      SHV4U(IQ2,1)*VBL(ISHEMU(IQ2,3)+IGV)+
     *      SHV4U(IQ2,2)*VBL(ISHEMU(IQ2,3)+IGV+1)+
     *      SHV4U(IQ2,3)*VBL(ISHEMU(IQ2,3)+IGV+IM)+
     *      SHV4U(IQ2,4)*VBL(ISHEMU(IQ2,3)+IGV+1+IM)
88960 CONTINUE
CFPP$ PERMUTATION(ISHEMU)
CMIC$ DO PARALLEL VECTOR
      DO 88961 IQ2 = 1, MX
         VBL(ISHEMU(IQ2,1)+ISC) = X1 ( IQ2 )
88961 CONTINUE
CMIC$ END PARALLEL
      ELSE
C          GATHER HU VALUES SURROUNDING THE IMAGE POINT.
C          FORM LINEAR COMBINATION OF THESE HU VALUES INTO X5.
CMIC$ PARALLEL SHARED(MX, ISC, X1, ISHEMV, VBL, IGU, IM, IGV, SHU4V,
CMIC$1   SHV4V) PRIVATE(IQ2)
CMIC$ DO PARALLEL VECTOR
      DO 88962 IQ2=1,MX
         X1 ( IQ2 ) =
     *      SHU4V(IQ2,1)*VBL(ISHEMV(IQ2,2)+IGU)+
     *      SHU4V(IQ2,2)*VBL(ISHEMV(IQ2,2)+IGU+1)+
     *      SHU4V(IQ2,3)*VBL(ISHEMV(IQ2,2)+IGU+IM)+
     *      SHU4V(IQ2,4)*VBL(ISHEMV(IQ2,2)+IGU+1+IM)+
     *      SHV4V(IQ2,1)*VBL(ISHEMV(IQ2,3)+IGV)+
     *      SHV4V(IQ2,2)*VBL(ISHEMV(IQ2,3)+IGV+1)+
     *      SHV4V(IQ2,3)*VBL(ISHEMV(IQ2,3)+IGV+IM)+
     *      SHV4V(IQ2,4)*VBL(ISHEMV(IQ2,3)+IGV+1+IM)
88962 CONTINUE
CFPP$ PERMUTATION(ISHEMV)
CMIC$ DO PARALLEL VECTOR
      DO 88963 IQ2 = 1, MX
         VBL(ISHEMV(IQ2,1)+ISC) = X1 ( IQ2 )
88963 CONTINUE
CMIC$ END PARALLEL
      END IF
C
   30 CONTINUE
C
C                    ARE WE DONE WITH HU AND HV,,,
      IF( N .EQ. 2 )  GO TO 40
C
C          N=1, MUST DO HV PTS.   SET SPECIAL DESCRIPTORS FOR HV.
C          PRESET ADDRESS FOR SCATTER INTO HV ARRAY
      ISC=IADDRG(2,1)-IMJM-1
C          NUMBER OF HV BNDRY POINTS.
      MX=MV
C          RE-ENTER THE N LOOP
      GO TO 20
C
   40 CONTINUE
C
C
      RETURN
      END
