       SUBROUTINE MODT2SIG(HMODE,TSIG,RLNPSFC,S,NV,NMODIN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    MODT2SIG    VERT MODE TO SIGMA TRANSFORM
C   PRGMMR: PARRISH          ORG: W/NMC22    DATE: 88-09-08
C
C ABSTRACT: TRANSFORM FROM VERTICAL MODE AMPLITUDES TO SIGMA
C   COORDINATES FOR TEMP AND LOG(PSFC). USE ONLY WITH COEFFICIENTS!
C
C PROGRAM HISTORY LOG:
C   88-09-08  PARRISH
C
C USAGE:    CALL MODT2SIG(HMODE,TSIG,RLNPSFC,S,NV,NMODIN)
C   INPUT ARGUMENT LIST:
C     HMODE    - INTEGER POINTERS FOR INPUT MODE VARIABLES
C     TSIG     - INTEGER POINTERS FOR OUTPUT SIGMA VARIABLES
C     RLNPSFC  - DITTO
C     NV       - NUMBER OF POINTS TO TRANSFORM
C     NMODIN   - NUMBER OF VERTICAL MODES
C
C   OUTPUT ARGUMENT LIST:
C     S        - ARRAY CONTAINING ALL COEFFICIENT STUFF (POINTERS
C              - ARE USED TO IDENTIFY WHAT IS WHAT).
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN200
C   MACHINE:  CYBER
C
C$$$
         include "myparam"
C--------
         INTEGER HMODE(1),TSIG(1),RLNPSFC
         REAL S(INCOEFS,1)
C--------
         COMMON/FIXSP03/NSIG,NMODES,
     *      DELSIG(INSIG),
     *      HEINVT(INSIG),HEINV(INSIG,INSIG),
     *      HEINVG(INSIG,INSIG),
     *      HPIBINVE(INSIG),HTBINVE(INSIG,INSIG),
     *      HE(INSIG,INSIG),HDEPTHS(INMODES)
C--------
C-------- INITIALIZE ALL INTERNAL VARIABLES IF NOT ALREADY DONE
         IF(NSIG.NE.INSIG) CALL SETSP03
C--------
         G=9.8062
         DO 550 K=1,NSIG
           DO 548 I=1,NV
             S(I,TSIG(K))=0.
548        CONTINUE
550      CONTINUE
         DO 552 I=1,NV
           S(I,RLNPSFC)=0.
552      CONTINUE
         DO 575 K=1,NMODIN
           DO 558 I=1,NV
             S(I,RLNPSFC)=S(I,RLNPSFC)
     *           +HPIBINVE(K)*S(I,HMODE(K))*G
558        CONTINUE
           DO 570 KKR=1,NSIG
             KK=NSIG+1-KKR
             DO 568 I=1,NV
               S(I,TSIG(KKR))=S(I,TSIG(KKR))
     *             +HTBINVE(KK,K)*S(I,HMODE(K))*G
568          CONTINUE
570        CONTINUE
575      CONTINUE
C-------- DIVIDE TEMPS BY GASCON.
         GASCONI=1./287.04
         DO 600 K=1,NSIG
           DO 598 I=1,NV
             S(I,TSIG(K))=GASCONI*S(I,TSIG(K))
598        CONTINUE
600      CONTINUE
       RETURN
       END
