C$$$  MAIN PROGRAM DOCUMENTATION BLOCK  ***                             
C                                                                       
C MAIN PROGRAM: NGMMOSAKTX
C   PRGMMR: ERICKSON         ORG: W/OSD211    DATE: 2000-03-09
C                            NGM (FWC) BULLETINS        
C                      
C ABSTRACT: READS THE NGM MOS FORECAST FILES AND GENERATES 
C   THE FOAK25 THRU FOAK29 BULLETINS. THESE BULLETINS  
C   ARE FORMATTED FOR TRANSMISSION TO THE
C   VARIOUS ALPHANUMERIC COMMUNICATIONS CIRCUITS. THE
C   BULLETINS CONTAIN THE NGM-BASED MOS GUIDANCE         
C   FOR POP, MX/MN TEMP, WIND, CLOUD AMOUNT, QPF, PRECIP TYPE     
C   AND AMOUNTS, CIG, VIS, AND OBVIS FOR STATIONS IN ALASKA ONLY.  
C   THIS PROGRAM WAS MODIFIED FROM THE REVISED FOUS14TX CODE      
C   DEVELOPED IN OCTOBER 1995. THIS PROGRAM (LIKE FOUS14TX)
C   CONFORMS TO CRAY C90 OPERATIONS. 
C
C PROGRAM HISTORY LOG:                                                  
C   92-05-05  GILBERT - ORIGINAL CODE WAS WRITTEN TO DISSEMINATE        
C                       NGM-BASED MOS GUIDANCE.                         
C   93-06-04  GILBERT - ADDED A SYMBOL FOR BLOWING PHENOMENA FOR THE    
C                       OBVIS ELEMENT.                                  
C                     - CHANGED THE CALL TO CHKDAT TO THE NEW SUBR.     
C                       CHKMOS.                                         
C   93-07-29  GILBERT - CORRECTED AN ERROR SO THAT MISSING FORECASTS    
C                       ARE HANDLED PROPERLY.  NEEDED TO MAKE SURE      
C                       THAT MISSING VALUES WERE ADDED TO THE BULLETIN. 
C   94-04-22  GILBERT - ADDED A PATCH TO WRITE 'TTS' TO THE BULLETIN    
C                       WHEN 'X68' IS USED TO READ THE FORECASTS.       
C   94-06-22  GILBERT - CORRECTED AN ERROR THAT INCORRECTLY MAPPED      
C                       MOSMAT 12-HR SNOW AMT CATEGORIES TO THE FOAK13  
C                       CATEGORIES.
C   95-11-20  WEISS   - HOBBS(HP PLATFORM) AND CRAY C90 CONVERSION
C                       THIS CODE INCORPORATES I/O DEVELOPED TO ACCESS
C                       FORECAST NGM MOS 2000 FORECAST DATA FILES 
C                       USED ON THE HOBBS AND CRAY C90.
C   97-06-05  GILBERT - CHANGED FORTRAN STOPS TO EXIT.
C                     - CHANGED TO READ WIND DIRECTION INSTEAD OF
C                       CALCULATING FROM U AND V.
C                     - ADDED CALL TO RDDICT TO GET ICAO STATION ID'S
C                       TO PUT IN BULLETINS.
C   98-07-07  SHIREY  - CHANGED GTDATE/CHNGDT TO GETDATE (5X) AND
C                       CHNGDATE (4X) FOR Y2K.
C   98-07-20  ERICKSON  CHANGED CALLS TO W3FS13 AND W3FS05 TO 
C                       W3DOXDAT FOR Y2K AND F90.
C   99-08-25  MCE     - ADDED COMMA TO FMT STMT 93 FOR IBM
C                                                                       
C USAGE:                                                                
C
C   SEE BELOW FOR MDL STANDARDS                                         
C                                                                       
C     PROGRAM NGMMOSAKTX                                                
C                                                                       
C     MAY  1992   GILBERT    MDL   NAS9000
C     NOV  1995   WEISS      RDC   HOBBS AND CRAY C90
C                                                                       
C        PURPOSE                                                        
C          TO PREPARE ALPHANUMERIC BULLETIN FOAK25 THRU FOAK29.
C          (NGM MOS GUIDANCE FOR ALASKA)
C                                                                       
C        DATA SET USE                                                   
C     *FORT.05(FTN05) - CONTROL DATA (INPUT)
C     *FORT.12(FTN12) - MOS FORECAST FILE (INPUT)
C     *FORT.05(FTN05) - STATION DIRECTORY (INPUT)
C     *FORT.18(FTN18) - NMC STANDARD DATE FILE (INPUT)
C     *FORT.60(FTN60) - PRINTOUT OF MESSAGE (OUTPUT)
C     *FORT.65(FTN65) - TELETYPE MESSAGE (OUTPUT)
C
C        * CRAY C90(HOBBS) UNIT NUMBER DECLARATIONS 
C
C        VARIABLES
C
C**      SUBROUTINE OPMOS CALLING ARGUMENTS:
C**            CALLS=AN ARRAY CONTAINING THE NSTA NUMBER
C**                  OF AVAILABLE CALL LETTERS. 
C**                  NOTE: SEE LIST( ) 
C**            CDESC=MOS (MODEL) DESCRIPTION (EX. NGM, AVN) 
C**            CFILE=INPUT FILE NAME WITH PATH INCLUDED
C**            CNAME=ARRAY OF STATION NAMES (OPMOS)
C**            CTYPE=FIVE CHARACTER DESCRIPTOR OF TYPE OF DATA
C**                  IN MOS FORECAST FILE
C**             IOPT=WHEN ZERO, READ ONLY OPTION FOR MOS FILE
C**                  USED BY OPMOS AND DAOPEN
C**           JBLOCK=ARRAY OF STATION BLOCK NUMBERS
C**            JNERR=ERROR RETURN CODE OF OPMOS
C**            JWBAN=ARRAY OF STATION WBAN NUMBERS
C**            KUNIT=FILE NUMBER(12) USED FOR READING MOS
C**                  FORECAST DATA FILE.  
C**            NDATE=DATE THE MOS DATA FILE WAS GENERATED (OPMOS)
C**                  (IN MDL FORMAT)
C**             NSTA=NUMBER OF STATIONS WHICH ARE AVAILABLE.
C**                  RETURNED BY OPMOS
C
C**      SUBROUTINE GTMOS CALLING ARGUMENTS:
C**             FCST=THE LIST OF NMOSTA DATA VALUES IN THE SAME
C**                  ORDER AS THE REQUESTED STATION LIST (LIST).
C**            ID(4)=FOUR WORD RECORD IDENTIFIER 
C**            IDATE=DATE THE MOS DATA VALUES WERE GENERATED
C**                  IN MDL FORMAT. OBTAINED FROM THE PACKED
C**                  DATA RECORD.
C**              ISW=INDICATES HOW THE REQUESTED STATIONS ARE
C**                    SPECIFIED (1=CALL LETTERS) (2=JWBAN)
C**                    USE ISW=1
C**         JWBAN(X)=REQUESTED STATION WBAN NUMBERS,
C**                  X IS THE DUMMY ARGUMENT USED WHEN ISW=1. 
C**            KUNIT=FILE NUMBER(12) USED FOR READING MOS
C**                  FORECAST DATA FILE.
C**     CALLS (LIST)=LIST OF NMOSTA CALL LETTERS OF STATIONS 
C**                  FOR WHICH STATIONS ARE REQUESTED (CHAR*8).
C**                  OUTPUT FROM RDWMO AND RDLST
C**             NERR=ERROR RETURN CODE.
C**           NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS 
C**                  PREPARED
C
C**      SUBROUTINE CHKMOS CALLING ARGUMENTS:
C**            LDATE=SEE GETDATE ARGUMENT LIST
C**            IDATE=DATE(DATE THE MOS DATA IN THE MOS DATA
C**                  FILE WERE GENERATED.
C**             NERR=ERROR RETURN FROM GTMOS
C**              KUT=COUNT OF NUMBER OF FORECAST FIELDS RUN FROM THE 
C**                  MOSMAT FILE                           
C**            IFLAG=RETURN FLAGS
C
C**      SUBROUTINE GETDATE CALLING ARGUMENTS:
C**            IUNIT=FILE NUMBER(18) USED FOR THE NMC DATE FILE
C**             IERR=ERROR RETURN CODE.
C**            LDATE=CURRENT OPERATIONAL DATE IN MDL FORMAT
C**              MDG=CURRENT DAY, 2 DIGITS
C**              MHG=CURRENT HOUR, 2 DIGITS
C**              MOG=CURRENT MONTH, 2 DIGITS
C**              MYG=CURRENT YEAR, 4 DIGITS
C
C**      SUBROUTINE RDWMO CALLING ARGUMENTS:
C**            JUNIT=FILE NUMBER(5) USED TO ACCESS 
C**                  STATION DIRECTORY FILE.
C**             LIST=LIST OF NMOSTA CALL LETTERS OF STATIONS,
C**                  LEFT JUSTIFIED (8 CHARACTERS)
C**           MAXSTA=MAXIMUN NUMBER OF STATIONS
C**            NHEAD=NUMBER OF WMO STATION CALL LETTER HEADERS
C**                  (EX. MRFFOX42AZ) ONE FOR ALASKA
C**           NWMO()=NUMBER OF STATIONS IN EACH BULLETIN
C**            WMO()=ALASKA'S ONE WMO HEADER(CHARACTER *8)
C**                  (ALASKA HAS JUST ONE WMO HEADER, BUT STILL NEEDS
C**                  TO BE AN ARRAY)
C
C
C**       SUBROUTINE RDLSTA CALLING ARGUMENTS:
C**              CFMT=CONTAINS FORMAT OF DATA
C**             DTEMP=TEMPORARY ARRAY USED TO READ ONE RECORD
C**        DATA(LIST)=LIST OF NMOSTA CALL LETTERS OF STATIONS
C**            IVALEN=LENGTH OF EACH CHARACTER STRING WHICH
C**                   IS READ (8 IN THIS CASE)
C**             JUNIT=FILE NUMBER(5) SEE RDWMO:
C**          LEFT(ND)=SIZE OF ARRRAY NAME
C**                NT=CHARACTER STRINGS READ PER RECORD(9)
C**              NVAL=COUNT OF ELEMENTS IN ARRAY RETURNED
C**              TERM=CHARACTER STRING TO INDICATE THE
C**                   LETTERS FOR A PARTICULAR WMO HEADER
C
C**        SUBROUTINE MISSNG CALLING ARGUMENTS:
C**              FCST=SEE GTMOS   
C**            NMOSTA=SEE GTMOS
C
C**        SUBROUTINE W3DOXDAT CALLING ARGUMENTS:
C**           IDAT(8)=NCEP ABSOLUTE DATE AND TIME ARRAY
C                     1 - 4 DIGIT YEAR  
C                     2 - MONTH OF YEAR
C                     3 - DAY OF MONTH
C                     4 - HHMM TIME ZONE DIFFERENCE FROM UTC (-0500)
C                     5 - HOUR OF DAYI 24-HR CLOCK
C                     6 - MINUTES OF HOUR
C                     7 - SECONDS OF MINUTE
C                     8 - MILLISECONDS OF SECOND
C                JDOW=INTEGER DAY OF WEEK (1 IS SUNDAY)
C                JDOY=INTEGER DAY OF YEAR (1 IS JAN 1)
C**              JDAY=INTEGER JULIAN DAY (FROM JAN 1, 4713BC)
C
C
C         ADDITIONAL PARAMETERS USED IN NGMMOSAKTX
C
C**            BFILE=AFOS TRANSMISSION FILE: CHARACTER*80 (OUTPUT)
C**           CBULHD=BULLETIN HEADER
C**          CDAY(3)=CHARACTER *3 ARRAY OF CALENDER DAYS
C**        CHARDY(7)=CHARACTER *3 ARRAY OF DAYS OF THE WEEK
C**              CLD=ARRAY OF CHARACTERS REPRESENTING CLD CATEGORIES.
C**            CMNTH=ARRAY OF FIRST FOUR CHARACTERS OF THE MONTHS OF 
C**                  THE YEAR.
C**          FCST( )=FORECASTS RETURNED BY GTMOS                       
C**                  FROM MOSMAT FILES FOR NMOSTA STATIONS
C**              ICY=TIME CYCLE IDENTIFIER (ICY=1 00Z, ICY=2 12Z)
C**           IDA(3)=ARRAY OF 3 CHRONOLOGICAL MONTHLY DAY NUMBERS
C**                  GENERATED BY CALLS TO CHNGDATE.
C**            IDYWK=INTEGER DAY OF THE WEEK (1-7)
C**           IMO(3)=ARRAY OF 3 CHRONOLOGICAL MONTH NUMBERS
C**              INC=TIME INCREMENT
C**               IS=COUNTER VARIABLE (DEPENDENT ON ICY)
C**           IYR(3)=ARRAY USED TO HELP CALCULATE CALENDER DAY (CDAY)
C**            JDATE=DATE CHECK FOR SNOW
C**       KOUNT( , )=INITIALLY SET TO ZERO, INCREASED BY 1
C**                  WHERE A FORECAST IS ENTERED IN THAT LINE.
C**                  1ST DIMENSION = LINE
C**                  2ND DIMENSION = GREATER THAN OR EQUAL TO NMOSTA
C**           LBULHD=NUMBER OF CHARACTERS IN BULLETIN HEADER
C**             LINE=NUMBER OF LINES REQUIRED PER STATION (=20)
C**             LPOS=POSITIONING ARRAY TO INSERT VARIABLE USING PUTMOS.
C**           MAXSTA=MAXIMUM NUMBER OF STATIONS(2500) ALLOWED.
C**            MDATE=SAME AS LDATE(SEE LDATE)
C**            MM( )=ARRAY USED TO HOLD THE MDL IDENTIFIERS FOR THE  
C**                  MAX/MIN TEMPERATURE FORECAST-BOTH 00Z AND 12Z  
C**                  CYCLES.
C**             MPOS=POSITIONING ARRAY TO INSERT VARIABLE USING PUTMOS.
C**        MSG(69, )=AREA INTO WHICH TELETYPE MESSAGE IS BUILT
C**                  LAST DIMENSION=NOLINE SHOULD BE GE LINE*NMOSTA+5
C**             MYGA=LAST TWO DIGITS OF CURRENT YEAR
C**                  GOOD FOR YEAR 2000 AND BEYOND
C**            NBLAK=BLANK CHARACTER
C**            NBSTA=BEGINNING STATION NUMBER (USED TO DELINEATE
C**                  BULLETINS IN WRMSG)
C**             NCAT=CHARACTER*5 CATELOG NUMBER '00000'
C**            NFILL=CHARACTER USED AS FILL IN THE BULLETIN
C**                  TRANSMISSION MESSAGE. THIS CHARACTER IS
C**                  TRANSPARENT TO THE AFOS OR TTY SYSTEM.
C**            NESTA=ENDING STATION NUMBER (USED TO DELINEATE
C**                  BULLETINS IN WRMSG)
C**            NHEAD=NUMBER OF WMO HEADERS
C**           NMOSTA=NUMBER OF STATIONS FOR WHICH BULLETIN IS PREPARED,
C**                  RETURNED FROM RDDATA
C**           NOCHAR=1ST DIMENSION OF MSG( , )-MAX NUMBER OF CHARACTERS
C**           NOHEAD=NUMBER OF LINES IN THE HEADER, INCLUDING
C**                  BLANK LINES.
C**           NOLINE=2ND DIMENSION OF MSG( , )-MAX NUMBER OF LINES
C**             NOUT=DATA SET REFERENCE NUMBER(60) FOR PRINTED BULLETIN
C**            NTEMP=NUMBER OF STATIONS TO PROCESS (USUALLY
C**                  EQUAL TO NMOSTA)
C**            NUNIT=FILE NUMBER(65) USED IN WRMSG FOR THE 
C**                  TRANSMISSION FILE OF THE BULLETINS.
C**                  LINES.
C**            OBVIS=CHARACTERS REPRESENTING OBVIS CATEGORIES.
C**             POPT=CHARACTERS REPRESENTING POPT CATEGORIES.
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCCC          SUBROUTINE W3AG15 IS NOT CALLED AT THE PRESENT TIME: 
C             DDNAME=DDNAME OF THE TRANSMISSION FILE.  THIS VALUE    
C COMMENTED          IS READ FROM THE FT05F001 CONTROL FILE AND IS   
C                    USED IN THE CALL TO THE W3AG15 SUBROUTINE.      
C OUT          KEYWD=KEY WORD USED IN THE CALL TO THE W3AG15 SUBROUTINE
C                    TO INDICATE THAT AN ALPHANUMERIC TRANSMISSION IS 
C                    TO BE SENT TO THE 4341.  THIS VALUE IS READ FROM  
C                    THE FT05F001 CONTROL FILE.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C                                                                       
C        SUBPROGRAMS CALLED:                                            
C            UNIQUE:  RDWMO(CALLS RDLSTA)
C                                                                       
C            LIBRARY: LIST ALPHABETICALLY                               
C              W3LIB: W3DOXDAT
C             TDLLIB: DIR,PRMSG,WRMSG,GETDATE,CHNGDATE,GTMOS,
C                     PUTMOS,PUTCHAR,
C                     CHKMOS,MISSNG,OPMOS,RDLSTA,PUTQ
C
C       CHNGDATE=SEE DOCUMENTATION WRITEUPS
C        
C
C GETDATE,GTMOS,OPMOS=SEE DOCUMENTATION WRITEUPS
C      
C              CHKMOS=CHECKS LDATE(OPERATIONAL DATE) VS 
C                     IDATE(DATE THE MOS DATA IN THE MOS DATA FILE
C                     WERE GENERATED)
C
C                 DIR=TO COMPUTE WIND DIRECTION FROM U AND V WIND
C                     COMPONENT VARIABLES
C
C             PUTCHAR=SUBROUTINE WHICH COPIES CHARACTER STRINGS FROM
C                     STRING TO ANOTHER; AVOIDS HAVING TO USE DO LOOPS.
C
C              PUTMOS=SUBROUTINE WHICH CONVERTS A NUMBER (INTEGER OR 
C                     REAL), CONVERTS THE NUMBER TO A CHARACTER STRING
C                     WHICH CAN THEN BE COPIED TO THE BULLETIN MESSAGE.
C
C                PUTQ=SUBROUTINE WHICH ATTACHES CONTROL CHARACTERS AT
C                     THE END OF EACH RECORD FOR AFOS TRANSMISSION 
C                     (CALLED BY WRMSG)
C
C              MISSNG=INSERTS MISSING INDICATOR VALUE OF 9999. INTO
C                     THE FORECAST ARRAY (FCST).
C
C              RDLSTA=TO READ CHARACTER DATA WITH A GIVEN FORMAT. 
C                     ASSUME A MAX RECORD LENGTH OF 80 BYTES.
C
C               PRMSG=PRINTS AN ASCII(SEQUENTIAL FILE) OUTPUT OF NGM
C                     STATION BULLETINS
C
C               WRMSG=GENERATES AN ASCII(DIRECT ACCESS "FORMATTED")
C                     FILE WITH EBSCDIC CONTROL CHARACTERS "TRANSMISSION
C                     COMPONENTS OF THE NGM BULLETINS
C
C        EXIT STATES:   (STOP OCCURS IN MAIN UNLESS OTHERWISE STATED)
C                  30 = NUMBER OF STATIONS IN LIST ARE EXCEEDED (STOP
C                       OCCURS IN RDWMO)
C                 100 = UNABLE TO OPEN NGM MOS 2000 FORECAST FILE
C                 115 = UNABLE TO READ THE STANDARD NMC DATE FILE  
C                 50  = NO MOS FORECASTS WERE AVAILABLE   
C                                            
C        REMARKS                                                        
C            THIS PROGRAM WAS MODIFIED FROM THE ORIGINAL FOAK13TX.
C
C ATTRIBUTES:                                                           
C   LANGUAGE: FORTRAN90                                              
C   MACHINE:  HP(UNIX) AND IBM SP PLATFORMS
C$$$ 

      PROGRAM NGMMOSAKTX

      PARAMETER (NOCHAR=69,MAXSTA=2500,LINE=20)
      PARAMETER (NOHEAD=1)

      INTEGER KOUNT(LINE,MAXSTA),IDA(3),IUNIT,NUNIT,KUNIT,JUNIT,
     *        LBULHD,NOUT,MM(4,2),MPOS(4),LPOS(3,2),IMO(3),
     *        NMOSTA,JWBAN(MAXSTA),JBLOCK(MAXSTA),IOPT,NDATE,NSTA,
     *        NERR,IDATE,LDATE,MDATE,MDG,MHG,MOG,MYG,ISW,IERR,ID(4),
     *        JNERR,MYGA,NTEMP,IDYWK,IYR(3),NWMO(MAXSTA),
     *        JDATE,NESTA,NBSTA,ICY,INC,IDAT(8),JDAY

      REAL FCST(MAXSTA)

      CHARACTER*1 MSG(NOCHAR,MAXSTA*LINE),
     *            NBLAK,NFILL,POPT(4),OBVIS(5)
      CHARACTER*2 CLD(5)
      CHARACTER*3 CDAY(3),CHARDY(7)
      CHARACTER*4 CMNTH(12)
      CHARACTER*5 CTYPE,NCAT
      CHARACTER*6 CDESC
      CHARACTER*8 LIST(MAXSTA),CALLS(MAXSTA),ICAO(MAXSTA)
      CHARACTER*18 CBULHD,WMO(MAXSTA)
      CHARACTER*20 CNAME(MAXSTA)
      CHARACTER*80 CFILE,BFILE

      DATA IUNIT/18/,NUNIT/65/,KUNIT/12/,JUNIT/5/,NOUT/60/
      DATA LBULHD/11/,MPOS/26,38,50,62/
      DATA LPOS/6,27,51,6,15,39/
      DATA MM/202001006,202011006,202001006,202011006,
     &        202011006,202001006,202011006,202001006/
      DATA NCAT/'00000'/

      DATA NBLAK/' '/,NFILL/'^'/,OBVIS/'B','H','F','N','X'/
      DATA CLD/'CL','SC','BK','OV','XX'/,POPT/'Z','S','R','X'/
      DATA CMNTH/'JAN ','FEB ','MAR ','APR ','MAY ','JUNE','JULY',
     *     'AUG ','SEPT','OCT ','NOV ','DEC '/
      DATA CHARDY/'SUN','MON','TUE','WED','THU','FRI','SAT'/

      DATA NMOSTA/0/,NTEMP/0/,ISW/1/,IOPT/0/,IDATE/0/,LDATE/0/,
     *     MDATE/0/,NDATE/0/,JDATE/0/,NESTA/0/,NBSTA/0/,IDYWK/0/
      DATA IDAT/0,0,0,-0500,0,0,0,0/
C
      CALL W3TAGB('NGMMOSAKTX',2000,0069,0064,'W/OSD211')
C
C        INITIALIZE COUNTER AND FCST ARRAYS
      DO 112 M=1,MAXSTA
	FCST(M)=0.
	NWMO(M)=0
      DO 112 K=1,LINE
        KOUNT(K,M)=0
  112 CONTINUE

C        READ IN THE MOS 2000 DATA FILE NAME(CFILE)
C        AND THE OUTPUT TRANSMISSION FILE(BFILE)
C
      READ(JUNIT,1000) CFILE
      READ(JUNIT,1000) BFILE
 1000 FORMAT(A80)

C        READ IN LIST OF STATION CALL LETTERS 
C        (NHEAD=1 FOR ALASKA)
C
      CALL RDWMO(JUNIT,NMOSTA,LIST,WMO,NWMO,NHEAD,MAXSTA)
      WRITE(6,91)NMOSTA
 91   FORMAT(1X,'NMOSTA=',I10)
      NOLINE=(NMOSTA*LINE)+NOHEAD
      KUT=0
C
C SET THE NUMBER OF STATIONS TO PROCESS
C USUALLY WILL BE NMOSTA 
C
      NTEMP=NMOSTA
      WRITE(6,91)NTEMP
      WRITE(6,93)NHEAD
 93   FORMAT(1X,'NHEAD=',I5)
C
C        GET CORRESPONDING ICAO STATION IDENTIFIERS FOR EACH
C        MOS STATION
C
      CALL RDDICT(LIST,ICAO,NMOSTA)

C        FILL THE MESSAGE ARRAY WITH BLANKS EXCEPT FOR THE 1ST CHARACTER
      DO 110 M=1,NOLINE
      DO 110 K=1,NOCHAR
        MSG(K,M)=NBLAK
      IF(K.EQ.1)MSG(K,M)=NFILL
  110 CONTINUE

C        READ NMC STANDARD DATE FILE
C
      CALL GETDATE(IUNIT,MYG,MOG,MDG,MHG,LDATE,IERR)
      IF(IERR.NE.0) THEN
	 WRITE(6,102)IERR
 102     FORMAT(1X,'PROBLEM READING NMC DATE FILE: NERR=',I5)
      CALL W3TAGE('NGMMOSAKTX')
         CALL EXIT(115)
      ENDIF

C        CALCULATES DAYS FOR HEADER LINE

C        FIND VALID DATES FOR HEADINGS.
C        FOR 3 CALENDER DAYS
      MDATE=LDATE
C        CONVERT 1995 TO 95 (FOR DISPLAY PURPOSES)
      MYGA=MOD(MYG,100)
      ICY=2
      IF (MHG.EQ.0) ICY=1
        IDA(1)=MDG
        IMO(1)=MOG
C        FULL YEAR (EX. 1995, 2005) TO GO INTO IYR ARRAY
        IYR(1)=MYG
	IDAT(1)=MYG
	IDAT(2)=MOG
	IDAT(3)=MDG
	CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(1)=CHARDY(IDYWK)
        IS=2
      DO 116 I=IS,3
        CALL CHNGDATE(-MDATE,24,MDATE)
        IDA(I)=MOD(MDATE,10000)/100
        IMO(I)=MOD(MDATE,1000000)/10000
        IYR(I)=MDATE/1000000
	IDAT(1)=IYR(I)
	IDAT(2)=IMO(I)
	IDAT(3)=IDA(I)
        CALL W3DOXDAT(IDAT,IDYWK,JDY,JDAY)
        CDAY(I)=CHARDY(IDYWK)
 116  CONTINUE


C****
C****    START CONSTRUCTION OF THE
C****    BULLETIN:
C****

C        PLACE HEADER INFORMATION FOR EACH STATION

C     DO 140 K=1,NMOSTA
      DO 140 K=1,NTEMP

C        PLACE TITLE AND RUN DATE ON FIRST LINE.

        CALL PUTCHAR('$',MSG(1,(K-1)*LINE+2),1)
        CALL PUTCHAR(ICAO(K),MSG(2,(K-1)*LINE+2),4)

C        REGIONAL ROUTING CODES NOT NEEDED
C        CALL PUTCHAR(CREG(K),MSG(7,(K-1)*LINE+2),4)                    
        CALL PUTCHAR('NGM MOS GUIDANCE',MSG(12,(K-1)*LINE+2),16)
        CALL PUTCHAR('/  /',MSG(32,(K-1)*LINE+2),4)
        CALL PUTMOS('MOG ',FLOAT(MOG),0.,1.,1,12,MSG(30,(K-1)*LINE+2),
     *       2,0,'99',KX)
        CALL PUTMOS('MDG ',FLOAT(MDG),0.,1.,1,31,MSG(33,(K-1)*LINE+2),
     *       2,2,'99',KX)
        CALL PUTMOS('MYG ',FLOAT(MYGA),0.,1.,0,99,
     *       MSG(36,(K-1)*LINE+2),2,2,'99',KX)
        CALL PUTCHAR('00 UTC',MSG(42,(K-1)*LINE+2),6)
        CALL PUTMOS('MHG ',FLOAT(MHG),0.,1.,0,12,MSG(40,(K-1)*LINE+2),
     *       2,2,'99',KX)
        KOUNT(1,K)=1

C        PLACE DAY & WEEKDAY HEADERS

        DO 135 L=1,3
          CALL PUTCHAR('DAY',MSG(2,(K-1)*LINE+3),3)
          CALL PUTCHAR('/',MSG(LPOS(L,ICY),(K-1)*LINE+3),1)
	  CALL PUTCHAR(CDAY(L),MSG(LPOS(L,ICY)+1,(K-1)*LINE+3),3)
	  IF ((L.NE.1).OR.(MHG.EQ.0))THEN
          CALL PUTCHAR(CMNTH(IMO(L)),MSG(LPOS(L,ICY)+5,(K-1)*LINE+3),4)
          CALL PUTMOS('DAY',FLOAT(IDA(L)),0.,1.,0,31,MSG(LPOS(L,ICY)+10,
     *         (K-1)*LINE+3),2,0,'99',KX)
	  ELSE
	  CALL PUTMOS('DAY',FLOAT(IDA(L)),0.,1.,0,31,MSG(LPOS(L,ICY)+5,
     *         (K-1)*LINE+3),2,0,'99',KX)
	  ENDIF
 135    CONTINUE
        IF (MHG.EQ.12) CALL PUTCHAR('/',MSG(63,(K-1)*LINE+3),1)
        KOUNT(2,K)=1

C        PLACE HOUR HEADERS

        CALL PUTCHAR('UTC',MSG(2,(K-1)*LINE+4),3)
        IHR=MHG+3
        IPOS=6
        DO 136 L=1,19
          IPOS=IPOS+3
          IHR=IHR+3
          IF (IHR.EQ.24) IHR=0
          CALL PUTMOS('HOUR',FLOAT(IHR),0.,1.,0,24,MSG(IPOS,
     *    (K-1)*LINE+4),2,2,'99',KX)
 136    CONTINUE
        KOUNT(3,K)=1

C        PLACE FORECAST DESCRIPTION

        IF(MHG.EQ.0)CALL PUTCHAR('MX/MN',MSG(2,(K-1)*LINE+5),5)
        IF(MHG.NE.0)CALL PUTCHAR('MN/MX',MSG(2,(K-1)*LINE+5),5)
        CALL PUTCHAR('TEMP',MSG(2,(K-1)*LINE+6),4)
        CALL PUTCHAR('DEWPT',MSG(2,(K-1)*LINE+7),5)
        CALL PUTCHAR('CLDS',MSG(2,(K-1)*LINE+8),4)
        CALL PUTCHAR('WDIR',MSG(2,(K-1)*LINE+9),4)
        CALL PUTCHAR('WSPD',MSG(2,(K-1)*LINE+10),4)
        CALL PUTCHAR('POP06',MSG(2,(K-1)*LINE+11),5)
        CALL PUTCHAR('POP12',MSG(2,(K-1)*LINE+12),5)
        CALL PUTCHAR('QPF',MSG(2,(K-1)*LINE+13),3)
C        CALL PUTCHAR('TSV06',MSG(2,(K-1)*LINE+14),5)                   
C        CALL PUTCHAR('TSV12',MSG(2,(K-1)*LINE+15),5)                   
        CALL PUTCHAR('PTYPE',MSG(2,(K-1)*LINE+14),5)
        CALL PUTCHAR('POZP',MSG(2,(K-1)*LINE+15),4)
        CALL PUTCHAR('POSN',MSG(2,(K-1)*LINE+16),4)
        CALL PUTCHAR('SNOW',MSG(2,(K-1)*LINE+17),4)
        CALL PUTCHAR('CIG',MSG(2,(K-1)*LINE+18),3)
        CALL PUTCHAR('VIS',MSG(2,(K-1)*LINE+19),3)
        CALL PUTCHAR('OBVIS',MSG(2,(K-1)*LINE+20),5)
        KOUNT(LINE,K)=-1
C        ABOVE STATEMENT PROVIDES FOR BLANK LINE.                       
 140  CONTINUE

C        OPEN MOS FORECAST FILE USING ROUTINE OPMOS. SPECIFIC
C        DATA VALUES WILL THEN BE READ FROM THE FILE USING 
C        SUBROUTINE GTMOS.

C        OPEN THE MOS FORECAST DATA FILE:

      CALL OPMOS(KUNIT,CFILE,IOPT,CDESC,NDATE,CTYPE,NSTA,CALLS,
     &           JWBAN,JBLOCK,CNAME,JNERR)
      IF(JNERR.NE.0)THEN
	 WRITE(6,101)JNERR
 101     FORMAT(1X,'ERROR OPENING NGM MOS 2000 FCST FILE: JNERR=',I5)
      CALL W3TAGE('NGMMOSAKTX')
	 CALL EXIT(100)
      ENDIF


CCCCCCCCCCCC
CCCCCCCCCCCC

C        PLACE MAX/MIN (00Z) OR MIN/MAX (12Z) FCSTS
      INC=12
      ID(4)=0
      ID(3)=12
      ID(2)=0
      DO 150 K=1,4
	ID(1)=MM(K,ICY)
	ID(3)=ID(3)+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
	CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 145 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(MPOS(K),(J-1)*
     *         LINE+5),3,0,'999',KOUNT(4,J))
 145    CONTINUE
 150  CONTINUE

C        PLACE 3-HR TEMPERATURES
      INC=3
      ID(1)=202000006
      ID(2)=0
      ID(3)=3
      IPOS=5
      DO 160 K=1,19
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 155 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(IPOS,(J-1)*
     *         LINE+6),3,0,'999',KOUNT(5,J))
 155    CONTINUE
 160  CONTINUE

C        PLACE 3-HR SURFACE DEW POINTS
      INC=3
      ID(1)=203100006
      ID(2)=0
      ID(3)=3
      IPOS=5
      DO 170 K=1,19
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 165 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,-99,999,MSG(IPOS,(J-1)*
     *         LINE+7),3,0,'999',KOUNT(6,J))
 165    CONTINUE
 170  CONTINUE

C        PLACE CLOUD CATEGORIES
      INC=3
      ID(1)=208300006
      ID(2)=50000
      ID(3)=3
      IPOS=6
      DO 180 K=1,19
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 175 J=1,NTEMP
          IF (NINT(FCST(J)).EQ.9999) THEN
            FCST(J)=5.
          ELSE
            KOUNT(7,J)=1
          ENDIF
C        CHARACTER*2 CLD ARRAY 
	  CALL PUTCHAR(CLD(NINT(FCST(J))),MSG(IPOS,(J-1)*LINE+8),2)
 175    CONTINUE
 180  CONTINUE

C        PLACE WIND DIRECTION FORECASTS
C
      INC=3
      ID(1)=204200006
      ID(2)=0
      ID(3)=3
      IPOS=6
      X=0.
      DO 190 K=1,19
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 185 J=1,NTEMP
          IF(FCST(J).NE.9999.)THEN
             X=IFIX(FCST(J)/10.+.5)
             IF(X.EQ.0)X=36.
          ELSE
             X=9999.
          ENDIF
          CALL PUTMOS(LIST(J),X,0.,1.,0,36,MSG(IPOS,(J-1)*LINE+9),
     *         2,2,'99',KOUNT(8,J))
 185    CONTINUE
 190  CONTINUE

C        PLACE WIND SPEED FORECASTS
      INC=3
      ID(1)=204211006
      ID(2)=0
      ID(3)=3
      IPOS=6
      DO 200 K=1,19
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 195 J=1,NTEMP
C        IF WIND SPEED IS LE ZERO, CHANGE THE WIND DIRECTION IN THE     
C        PREVIOUS LINE TO '00'.                                         
          IF (FCST(J).LT.0.5) THEN
            CALL PUTCHAR('00',MSG(IPOS,(J-1)*LINE+9),2)
          ENDIF
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,98,MSG(IPOS,(J-1)*
     *         LINE+10),2,2,'99',KOUNT(9,J))
 195    CONTINUE
 200  CONTINUE

C       PLACE 6-HR POP FORECASTS
      INC=6
      ID(1)=203500006
      ID(2)=10000
C TEMPORARY USE OF QPF CATEGORY 5 UNTIL POP'S 
C FORMUALAS FOR ALASKA ARE COMPLETE (11-21-95)
       ID(1)=203210006
       ID(2)=50000
      ID(3)=6
      IPOS=8
      DO 210 K=1,9
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 205 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(IPOS,
     *    (J-1)*LINE+11),3,0,'999',KOUNT(10,J))
 205    CONTINUE
 210  CONTINUE

C       PLACE 12-HR POP FORECASTS
      INC=12
      ID(1)=203505006
      ID(2)=10000
C TEMPORARY USE OF QPF CATEGORY 6 UNTIL POP'S
C FORMUALAS FOR ALASKA ARE COMPLETE (11-21-95)
C        ALASKA HOURS ARE 18,30,42,54
      ID(1)=203220006
      ID(2)=60000
      ID(3)=6
      IPOS=8
      DO 220 K=1,4
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 215 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(IPOS,
     *    (J-1)*LINE+12),3,0,'999',KOUNT(11,J))
 215    CONTINUE
 220  CONTINUE

C        PLACE 6-HR QPF CATEGORIES
C
C  POSSIBLE CHANGE OF ID(1)
C 
      INC=6
      ID(1)=203210006
      ID(2)=60000
      ID(3)=6
      IPOS=8
      DO 230 K=1,9
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 225 J=1,NTEMP
          IF (NINT(FCST(J)).NE.9999) FCST(J)=6.-FCST(J)
          CALL PUTCHAR('/',MSG((IPOS+1),(J-1)*LINE+13),1)
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(IPOS,
     *         (J-1)*LINE+13),1,1,'9',KOUNT(12,J))
 225    CONTINUE
 230  CONTINUE

C        PLACE 12-HR QPF CATEGORIES
C        ALASKA HOURS ARE 18,30,42,54
C
C  POSSIBLE CHANGE OF ID(1)
C
      INC=12
      ID(1)=203220006
      ID(2)=70000
      ID(3)=6
      IPOS=10
      DO 240 K=1,4
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 235 J=1,NTEMP
          IF (NINT(FCST(J)).NE.9999) FCST(J)=7.-FCST(J)
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(IPOS,
     *         (J-1)*LINE+13),1,1,'9',KOUNT(12,J))
 235    CONTINUE
 240  CONTINUE

C        TSV06 AND TSV12 NOT INCLUDED

C        SKIP SNOW, FREEZING RAIN AND PRECIP TYPE FORECASTS IF
C        DATE IS FROM JUNE 1 THROUGH AUG 31.

      JDATE=(MOG*100)+MDG
      IF ((JDATE.GE.601).AND.(JDATE.LT.901)) GOTO 301

C        PLACE PRECIPITATION TYPE FORECASTS
      INC=3
      ID(1)=208501006
      ID(2)=40000
      ID(3)=3
      IPOS=7
      DO 260 K=1,15
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 255 J=1,NTEMP
          IF (NINT(FCST(J)).EQ.9999) THEN
            FCST(J)=4.
          ELSE
            KOUNT(13,J)=1
          ENDIF
C        CHARACTER*1 POPT ARRAY
          CALL PUTCHAR(POPT(NINT(FCST(J))),MSG(IPOS,(J-1)*LINE+14),1)
 255    CONTINUE
 260  CONTINUE

C        PLACE POPZ PROBABILITIES
      INC=3
      ID(1)=208501006
      ID(2)=10000
      ID(3)=3
      IPOS=5
      DO 270 K=1,15
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 265 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(IPOS,
     *         (J-1)*LINE+15),3,1,'999',KOUNT(14,J))
 265    CONTINUE
 270  CONTINUE

C        PLACE SNOW PROBABILITIES
      INC=3
      ID(1)=208501006
      ID(2)=20000
      ID(3)=3
      IPOS=5
      INC=3
      DO 280 K=1,15
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 275 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,100.,0,100,MSG(IPOS,
     *         (J-1)*LINE+16),3,1,'999',KOUNT(15,J))
 275    CONTINUE
 280  CONTINUE

C        PLACE 6-HR SNOW AMOUNTS
      INC=6
      ID(1)=208401006
      ID(2)=30000
      ID(3)=6
      IPOS=8
      DO 290 K=1,9
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 285 J=1,NTEMP
          IF (NINT(FCST(J)).NE.9999) FCST(J)=3.-FCST(J)
          CALL PUTCHAR('/',MSG((IPOS+1),(J-1)*LINE+17),1)
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,0,9,MSG(IPOS,
     *         (J-1)*LINE+17),1,1,'9',KOUNT(16,J))
 285    CONTINUE
 290  CONTINUE

C        PLACE 12-HR SNOW AMOUNTS
C        ALASKA HOURS ARE 18,30,42,54
      INC=12
      ID(1)=208402006
      ID(2)=50000
      ID(3)=6
      IPOS=10
      DO 300 K=1,4
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 295 J=1,NTEMP
          IFCST=NINT(FCST(J))
          IF (IFCST.NE.9999) THEN
            IF (IFCST.EQ.1) THEN
              IFCST=6
            ELSEIF (IFCST.EQ.2) THEN
              IFCST=4
            ELSEIF (IFCST.EQ.3) THEN
              IFCST=2
            ELSEIF (IFCST.EQ.4) THEN
              IFCST=1
            ELSEIF (IFCST.EQ.5) THEN
              IFCST=0
            ELSE
              IFCST=9
            ENDIF
          ENDIF
          CALL PUTMOS(LIST(J),FLOAT(IFCST),0.,1.,0,9,MSG(IPOS,
     *         (J-1)*LINE+17),1,1,'9',KOUNT(16,J))
 295    CONTINUE
 300  CONTINUE

C        PLACE CIG CATEGORIES
 301  INC=3
      ID(1)=208000006
      ID(2)=80000
      ID(3)=3
      IPOS=7
      DO 310 K=1,13
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 305 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,1,7,MSG(IPOS,
     *         (J-1)*LINE+18),1,1,'9',KOUNT(17,J))
 305    CONTINUE
 310  CONTINUE

C        PLACE VIS CATEGORIES
      INC=3
      ID(1)=208100006
      ID(2)=60000
      ID(3)=3
      IPOS=7
      DO 320 K=1,13
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 315 J=1,NTEMP
          CALL PUTMOS(LIST(J),FCST(J),0.,1.,1,5,MSG(IPOS,
     *         (J-1)*LINE+19),1,1,'9',KOUNT(18,J))
 315    CONTINUE
 320  CONTINUE

C        PLACE OBVIS CATEGORIES
      INC=3
      ID(1)=208250006
      ID(2)=50000
      ID(3)=3
      IPOS=7
      DO 330 K=1,13
        IF (K.EQ.12) INC=6
        ID(3)=ID(3)+INC
        IPOS=IPOS+INC
        CALL GTMOS(KUNIT,ID,IDATE,NMOSTA,FCST,ISW,LIST,X,NERR)
        CALL CHKMOS(LDATE,IDATE,NERR,KUT,IFLAG)
        IF (IFLAG.NE.0) CALL MISSNG(NMOSTA,FCST)
        DO 325 J=1,NTEMP
          IF (NINT(FCST(J)).EQ.9999) THEN
            FCST(J)=5.
          ELSE
            KOUNT(19,J)=1
          ENDIF
C         CHARACTER*1 OBVIS ARRAY
          CALL PUTCHAR(OBVIS(NINT(FCST(J))),MSG(IPOS,(J-1)*LINE+20),1)
 325    CONTINUE
 330  CONTINUE

CCCCCCCC END OF CONSTRUCTION 
CCCCCCCC OF BULLETIN
CCCCCCCC

C        CLOSE THE MOS 2000 FORECAST DATA FILE USING DACLOS
C        NOT NECESSARY SINCE FILE WAS ONLY OPEN FOR READING

      IF (KUT.LE.0) THEN
        WRITE(6,500)
 500    FORMAT(//'0NO FORECAST FOUND ON NGM MOS FCST FILE.')
      CALL W3TAGE('NGMMOSAKTX')
        CALL EXIT(50)
      ENDIF
C
C        WRITE OUT BULLETINS TO TRANSMISSION FILE.
C
      DO 600 N=1,NHEAD
        NBSTA=NESTA+1
        NESTA=NESTA+NWMO(N)
        CBULHD=WMO(N)
        CALL WRMSG(MSG,NOCHAR,NOLINE,NBSTA,NESTA,LINE,NCAT,KOUNT,LINE,
     *       NTEMP,CBULHD,LBULHD,MDG,MHG,NOHEAD,NUNIT,BFILE,NHEAD)
 600  CONTINUE

      CALL PRMSG(MSG,NOCHAR,NOLINE,KOUNT,LINE,NMOSTA,NOUT,NOHEAD)

      CALL W3TAGE('NGMMOSAKTX')

      STOP
      END
