      SUBROUTINE SFCEXCH ( NG, SPD, DENSITY, CD )
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    SFCEXCH     COMPUTE SFC EXCHANGE & DRAG COEF FIELD
C   PRGMMR: J TUCCILLO       ORG: W/NMC22    DATE: 90-06-18
C
C ABSTRACT: THE SURFACE EXCHANGE COEFFICIENT IS
C   .       DEFINED AS THE PRODUCT OF DENSITY,
C   .       DRAG COEFFICIENT AND WIND SPEED.
C   .       (THIS IS OUTPUT IN ARRAY 19 IN VBL)
C   .       DRAG COEFFICIENT IS ALSO COMPUTED
C   .       AND RETURNED IN ARRAY  CD  .
C   .       (  RESULTS ARE AT  H-POINTS )
C   .
C   .
C   .       CALLED BY PHYSICS IN FORECASTING, AND BY THE
C   .       INITIAL CALL TO THE (LAND) SURFACE TEMPERATURE
C   .       BALANCE ROUTINE.
C
C PROGRAM HISTORY LOG:
C   90-06-18  J TUCCILLO
C
C USAGE:  CALL SFCEXCH ( NG, SPD, DENSITY, CD )
C   INPUT ARGUMENT LIST:
C     NG       -  GRID NUMBER
C     SPD      -  SPEED OF WIND IN FIRST LAYER (M/SEC)
C     DENSITY  -  SURFACE DENSITY (TON/M**3= GM/CC)
C
C   OUTPUT ARGUMENT LIST:
C     CD       -  DRAG COEFFICIENT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - CHARNOCK    TO GET DRAG COEFF AS FCN OF SPEED
C                - HQCOEF      TO COMPUTE THE STABILITY DEPENDENT
C                              DRAG OVER LAND WHEN THE RADIATION
C                              ( ICALLRAD=1 ) IS AVAIALABLE
C     LIBRARY:
C       COMMON   - COMCONST
C                  COMBLANK
C
C REMARKS:
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----       ----------------------------------        ---------
C   .
C   VBL         SFC EXCHANGE COEFF                         COMMON
C   .             (TONS/METER**3) TIMES ( METER/SEC )
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C...TRANSLATED BY FPP 3.00Z36 11/09/90  15:10:15  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
      LOGICAL PMPON
      DIMENSION SPDD(IIJMAX)
      DIMENSION SPD(1),DENSITY(1),CD(1)
C
C
      DATA ICALL/0/
      DATA AMINSP / 2.E0 /
C     DATA PMPON / .FALSE. /
      DATA PMPON / .TRUE. /
C
C--------------------------------------------------------------
C
      IF( ICALL .GT. 0 ) GO TO 10
      ICALL = 1
C
C           GET THE QUADRATIC POLYNOMIAL COEFFS TO FIT THE
C           CHARNOCK-V.KARMAN-PRANDTL DRAG COEFFICIENT FORMULA
C           FOR NEUTRAL STRATIFICATION OVER THE OCEAN.
C
C              GET THE AVERAGE HEIGHT ABOVE GROUND FOR LAYER 1
      Z1 = 287.05E0 * 288.E0 * DELSIG(1) / ( 2.E0 * 9.81E0 )
C            CHARN IS THE CHARNOCK NON-DIMENSIONAL COEFFICIENT
C                ( IN COMCONST FROM DATA CARDS )
C
      CALL CHARNOCK( Z1, CHARN, CH0,CH1,CH2 )
C
   10 CONTINUE
C                END OF PRELIMINARY WORK ON FIRST CALL
C---------------------------------------------------------------
C---------------------------------------------------------------
C
      IM = IMG(NG)
      JM = JMG(NG)
      IJ = IM*JM
C
      IF(PMPON) THEN
        MADDZ0=IADDRG(20,NG)
CMIC$ DO ALL VECTOR SHARED(IJ, NG, Z1, MADDZ0, BITSEA, SPD, VBL)
CMIC$1    PRIVATE(IQ2, U3S, Z0S)
        DO 88950 IQ2=1,IJ
         IF ( BITSEA(IQ2,NG) ) THEN
             U3S=SPD(IQ2)*SPD(IQ2)+1.E0
             Z0S=Z1/(U3S*2.1E-6)
             VBL(MADDZ0+IQ2-1)=
     *           3.E-4*U3S/ ( ALOG(Z0S) ** 2)
         END IF
88950 CONTINUE
      ENDIF
C
C----------PUT DRAG COEFFICIENT INTO ARRAY CD
      IF ( ICALLRAD .EQ. 1 ) THEN
         CALL HQCOEF ( NG , SPD , CD , AMINSP )
      ELSE
C           CONSTANT TO MODIFY CRESSMAN DISTRIBUTION
C           (SAME AS OPERATIONAL VERSION VALID IN MARCH 1986)
         CONST = 0.7E0/ 1.296E0
C           ONE-TENTH OF THIS TO MULTIPLY SPEED
         CONST1 = CONST * 0.1E0
C           ADDRESS OF CRESSMAN DRAG COEFFICIENT
         IADCR = IADDRG(11,NG)
CMIC$ DO ALL VECTOR SHARED(IJ, CONST1, IADCR, CONST, SPD, CD, VBL)
CMIC$1    PRIVATE(IQ2)
      DO 88960 IQ2=1,IJ
         CD(IQ2)=CONST1*SPD(IQ2)
         CD(IQ2)=VBL(IADCR+IQ2-1)*(CONST+CD(IQ2))
88960 CONTINUE
      END IF
C
      IF(.NOT.PMPON) THEN
C         REPLACE THIS WITH CHARNOCK ET AL VALUE OVER WATER
CMIC$ DO ALL VECTOR SHARED(IJ, NG, CH2, CH1, CH0, BITSEA, SPD, CD)
CMIC$1    PRIVATE(IQ2)
      DO 88980 IQ2=1,IJ
         IF ( BITSEA(IQ2,NG) ) THEN
            CD(IQ2)=CH2*SPD(IQ2)
            CD(IQ2)=SPD(IQ2)*(CH1+CD(IQ2))
            CD(IQ2)=CH0+CD(IQ2)
         END IF
88980 CONTINUE
      ENDIF
CMIC$ DO ALL VECTOR SHARED(IJ, CD, SPD, SPDD) PRIVATE(IQ2)
      DO 89010 IQ2 = 1, IJ
         IF (CD(IQ2) .LT. 0.E0) CD(IQ2) = 0.E0
         SPDD(IQ2) = SPD(IQ2)
89010 CONTINUE
      IF ( ICALLRAD .EQ. 1) THEN
      IF(.NOT.PMPON) THEN
CMIC$ DO ALL VECTOR SHARED(IJ, AMINSP, NG, SPDD, BITSEA) PRIVATE(IQ2)
      DO 89030 IQ2=1,IJ
         IF ( SPDD(IQ2) .LT. AMINSP .AND.
     *      .NOT.BITSEA(IQ2,NG) ) THEN
               SPDD(IQ2)=AMINSP
         END IF
89030 CONTINUE
      ELSE
CMIC$ DO ALL VECTOR SHARED(IJ, AMINSP, SPDD) PRIVATE(IQ2)
      DO 89040 IQ2=1,IJ
         SPDD(IQ2)=AMAX1(SPDD(IQ2),AMINSP)
89040 CONTINUE
      END IF
      END IF
      IADEXC = IADDRG(19,NG)
CMIC$ DO ALL VECTOR SHARED(IJ,IADEXC,DENSITY,CD,SPDD,VBL)PRIVATE(IQ2)
      DO 89050 IQ2=1,IJ
         VBL(IADEXC+IQ2-1)=AMAX1((DENSITY(IQ2)*CD(IQ2)*
     *             SPDD(IQ2)), 0.E0)
89050 CONTINUE
C
C
      RETURN
      END
