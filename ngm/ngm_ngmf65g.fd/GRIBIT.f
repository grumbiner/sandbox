      SUBROUTINE GRIBIT(GRID,IJOUT,PACKED,DESCRIPT,LABEL84,IOUTGRIB)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .     
C SUBPROGRAM:    GRIBIT      POST FIELDS IN GRIB1
C   PRGRMMR: TREADON         ORG: W/NMC2     DATE: 93-06-18       
C     
C ABSTRACT:
C     THIS ROUTINE POSTS THE DATA IN THE PASSED ARRAY GRID
C     TO THE OUTPUT FILE IN GRIB1 FORMAT.  THE ROUTINE
C     GENERATES THE GRIB1 PDS FROM AN ON84 LABEL USING
C     W3FP12.
C   .     
C     
C PROGRAM HISTORY LOG:
C   93-06-18  RUSS TREADON
C   93-11-10  RUSS TREADON - ADAPTED ETA POST-PROCESSOR
C                SUBROUTINE GRIBIT FOR USE IN THE NGM.
C   98-09-14 HANN-MING HENRY JUANG for Y2K
C     
C USAGE:    CALL GRIBIT(GRID,IJOUT,PACKED,DESCRIPT,LABEL84,IOUTGRIB)
C   INPUT ARGUMENT LIST:
C     GRID     - FIELD TO BE POSTED IN GRIB.
C     IJOUT    - NUMBER OF ELEMENTS IN ARRAY GRID.
C     PACKED   - SCRATCH ARRAY TO PACK GRIB VERSION OF FIELD IN.
C     DESCRIPT - CHARACTER DESCRIPTION OF POSTED FIELD.
C     LABEL84  - OFFICE NOTE 84 LABEL OF POSTED FIELD.
C     IOUTGRIB - UNIT TO WHICH TO WRITE GRIB DATA.
C
C
C   OUTPUT ARGUMENT LIST: 
C     NONE
C     
C   OUTPUT FILES:
C     IOUTGRIB   - UNIT TO RECEIVE GRIB1 DATA.
C     
C   SUBPROGRAMS CALLED:
C     UTILITIES:
C     ASNUNIT  - CRAY SUBROUTINE TO ASSIGN FILE ATTRIBUTES.
C     WRYTE    - WRITE DATA OUT BY BYTES.
C     COVNRT   - CONVERT ON84 UNITS TO GRIB1 UNITS.
C     GTBITS   - COMPUTE NUMBER OF BITS AND ROUND FIELD.
C     IDSDEF   - SET DECIMAL SCALING FOR GRIB.
C     I84TGB   - CONVERT ON84 Q INTEGER TO GRIB 1 FIELD PARAMETER.
C     VARIOUS W3LIB ROUTINES
C
C     LIBRARY:
C       NONE
C     
C   ATTRIBUTES:
C     LANGUAGE: FORTRAN
C     MACHINE : CRAY Y-MP
C$$$  
C     
C
C
C     SET GRIB1 PARAMETERS.
C        MNBIT  = MINIMUM NUMBER OF BITS TO USE IN PACKING.
C        MXBIT  = MAXIMUM NUMBER OF BITS TO USE IN PACKING.
C        LENPDS = LENGTH OF GRIB1 PDS.
C        LENGDS = LENGTH OF GRIB1 GDS.
C     
      PARAMETER (MNBIT=0,MXBIT=16,LENPDS=28,LENGDS=32)
C     
C     DECLARE VARIABLES.
C     
      LOGICAL RITEHD, KBMS(IJOUT)
      CHARACTER*1  KBUF(30+LENPDS+LENGDS+IJOUT*(MXBIT+2)/8)
      CHARACTER*1  IFLAG
      CHARACTER*7  NMCGR
      CHARACTER*8  NMCGRI
      CHARACTER*20 DESCRIPT
      CHARACTER*28 PDS
      CHARACTER*1  ch1pds(28)
      CHARACTER*48 LAB84PAK
      CHARACTER*50 ENVAR
      CHARACTER*57 FILGR
      CHARACTER*6 CRUN
      INTEGER STDOUT,IBDSFL(9),IBMAP(IJOUT),IGRD(IJOUT)
      INTEGER(8) LABPAK(6)
      INTEGER ID(25),IGDS(18),KPDS(32),KGDS(20),KPTR(16),IDS(255)
      INTEGER LABEL84(27)
      INTEGER(8) LABEL84_8(27)
      
      REAL GRID(IJOUT),PACKED(IJOUT),DATAFLD(IJOUT),gridout(ijout)
C     
C     SET EQUIVALENCE.
      EQUIVALENCE (LABPAK(1),LAB84PAK)
      EQUIVALENCE (pds,ch1pds(1))
C
C     SET DEFAULT RUN NAME.
      DATA CRUN /'RGL   '/

C     
C     SET DEFAULT GRIB1 PARAMETERS.  
C     PARAMETERS MNBIT, MXBIT, IBX, AND NBIT ARE USED IN THE CALL TO GTBITS.
C        IBX    = DESIRED BINARY PRECISION.
C        NBIT   = NUMBER OF BITS TO USE IN PACKING DATA.
C     
      DATA IBX,NBIT / 0, 12 /
C
C     SET STANDARD OUT UNIT.
      DATA STDOUT / 6 /
C     
C     THE ETA POST PROCESSOR KEYS ON LOGICIAL FLAG RITEHD TO SEE IF A NEW
C     UNIT NEEDS TO BE OPENED.  THE NGM DOES THINGS DIFFERENTLY.  SET 
C     RITEHD TO .FALSE. AND SAVE THIS VALUE SO THAT THE ASSIGN/OPEN/CLOSE
C     FILE CODE BELOW NEVER GETS EXECUTED.
C     
c code modified by deaven 6/9/94 assign/open/close removed and
c call to idsdef (first time only) retained
c note idsdef no longer called in OUTOPENGB (ids never was dimensioned)
c
      DATA RITEHD / .true. /
      SAVE RITEHD
C
C     SAVE GRIB1 DECIMAL SCALINGS BETWEEN CALLS TO GRIBIT.
      SAVE IDS
C
C
C*****************************************************************************
C     START GRIBIT HERE.
C     
C
C     ON FIRST ENTRY OPEN OUTPUT FILES.
C
      IF (RITEHD) THEN
C
C	 INITIALIZE GRIB1 DECIMAL SCALINGS.
	 CALL IDSDEF(1,IDS)
         RITEHD = .FALSE.
      ENDIF
C
C     CONVERT ON84 FIELD IDENTIFIER TO GRIB1 IDENTIFIER.
      IDGRB = 255
      CALL I84TGB(LABEL84(1),IDGRB)
      IF (IDGRB.EQ.255) THEN
         WRITE(STDOUT,1020) DESCRIPT,LABEL84(1),LABEL84(2)
 1020    FORMAT('GRIBIT:  NO GRIB1 PARAMETER FOR ',A20,
     X        ' ON84 Q AND S ARE ',2(I3,1X))
         WRITE(STDOUT,*)'GRIBIT:  DID NOT POST THIS FIELD'
         RETURN
      ENDIF
C     
C     CONVERT ON84 UNITS TO GRIB1 UNITS (IF NEED BE).
      CALL CONVRT(LABEL84(1),GRID,IJOUT,gridout)
C     
C     SET DECIMAL SCALING (IDECI) AS SPECIFIED IN SUBROUTINE IDSDEF.
C     A CALL TO GTBITS WILL COMPUTE THE NUMBER OF BITS NECESSARY TO
C     PACK THE DATA BASED ON THE RANGE OF THE FIELD.  THE FIELD IS
C     SCALED TO THIS PRECISION AND RETURNED FOR PACKING BY THE GRIB
C     PACKER.
C     
      IBM   = 0
      IDECI = 0
      IDECI = IDS(IDGRB)
CMIC$ DO ALL VECTOR AUTOSCOPE
      DO K = 1,IJOUT
         PACKED(K) = GRIDout(K)
         IBMAP(K)  = 1
      END DO
      CALL GTBITS(IBM,IDECI,IJOUT,IBMAP,GRIDout,PACKED,GMIN,GMAX,NBIT)
C     
C     GENERATE PRODUCT DEFINITION SECTION (PDS) FOR GRIB1 MESSAGE
C     FROM THE CORRESPONDING ON84 LABEL.  THE ON84 LABEL MUST BE
C     PACKED.  THE CALL TO W3FI32 ACCOMPLISHES THIS.  SET IFLAG
C     TO CHAR(128) TO INDICATE THAT THE GRIB1 MESSAGE WILL CONTAIN
C     ONLY A GDS AND NO BMS.  IF BOTH A GDS AND BMS ARE TO BE PACKED,
C     THEN SET IFLAG EQUAL TO CHAR(192).  SET ICENT EQUAL TO THE
C     CENTURTY OF YEAR.  CALL W3FP12 TO CONVERT ON84 LABEL TO GRIB1 PDS.
C
CHMHJ THERE ARE FULL DIGITS FOR YEAR IN LABEL84(21) FROM MAIN.f----
      IYR4=LABEL84(21)
c get year of the century for pak
      LABEL84(21)=MOD(IYR4-1,100)+1
      do k = 1, 27
        print *,'label84 ',k,label84(k)
      enddo
      label84_8 = label84
      do k = 1, 27
        print *,'label84 ',k,label84(k)
      enddo
      CALL W3FI32(LABEL84_8,LAB84PAK)
C return full digit of year
      LABEL84(21)=IYR4
c get century for w3fp12
      ICENT=(IYR4-1)/100+1
      IFLAG = CHAR(128)
      print *,'gribit ',labpak(1),labpak(2),labpak(3),labpak(4),
     1     labpak(5),labpak(6)
      write(6,1001) lab84pac
1001  format(1x,' gribit ',48a1)
      CALL W3FP12(LABPAK,IFLAG,PDS,ICENT,IDECI,IER)
C     
C     IF W3FP12 CAN NOT GENERATE A PDS, DO NOT POST THIS FIELD IN GRIB.
      IF (IER.NE.0) THEN
         WRITE(STDOUT,1030) IER,DESCRIPT,LABPAK(1)
 1030    FORMAT('GRIBIT:  ***W3FI12 ERROR IER=',I1,
     X        ' FOR ',A20,' ON84 ',2(Z16,1X))
         WRITE(STDOUT,*)'GRIBIT:  DID NOT POST THIS FIELD'
         RETURN
      ENDIF
C     
C     GENERATE COMPLETE GRIB1 MESSAGE USING W3FI72.
C        ITYPE  = 0 SPECIFIES REAL DATA TO BE PACKED.
C        IGRD   = DUMMY ARRAY FOR INTEGER DATA.
C        IBITL  = NBIT TELLS W3FI72 TO PACK DATA USING NBIT BITS.
C        IPFLAG = 0 IS PDS INFORMATION IN USER ARRAY ID.
C                 1 IS PDS (GENERATED ABOVE BY W3FP12).
C        ID     = (DUMMY) ARRAY FOR USER DEFINED PDS.
C        IGFLAG = 0 TELLS W3FI72 TO MAKE GDS USING IGRID.
C                 1 IS GDS GENERATED BY USER IN ARRAY IGDS
C        IGRID  = GRIB1 GRID TYPE (TABLE B OF ON388).
C        IGDS   = ARRAY FOR USER DEFINED GDS.
C        ICOMP  = 0 FOR EARTH ORIENTED WINDS,
C                 1 FOR GRID ORIENTED WINDS.
C        IBFLAG = 0 TELLS W3FI72 TO MAKE BIT MAP FROM USER
C                 SUPPLIED DATA.
C        IBMAP  = ARRAY CONTAINING USER DEFINED BIT MAP.
C        IBLEN  = LENGTH OF ARRAY IBMAP.
C        IBDSFL = ARRAY CONTAINING TABLE 11 (ON388) FLAG INFORMATION.
C        NPTS   = LENGTH OF ARRAY GRID OR IGRD.  MUST AGREE WITH IBLEN.
C     
C     INTIALIZE VARIABLES.
      ITYPE  = 0
      DO 5 K = 1,IJOUT
         IGRD(K) = 0
 5    CONTINUE
      IBITL  = MIN(NBIT,MXBIT)
C
      IPFLAG = 0
      IF (IER.EQ.0) THEN
         IPFLAG = 1
         DO 10 K = 1,25
            ID(K) = 0
 10      CONTINUE
      ENDIF
C
      IGFLAG = 0
      IGRID  = LABEL84(17)
      IF (IGRID.EQ.26) IGRID=6
      DO 20 K = 1,18
         IGDS(K) = 0
 20   CONTINUE
      ICOMP  = 1
      IBFLAG = 0
      IBLEN  = IJOUT
      DO 30 K = 1,9
         IBDSFL(K) = 0
 30   CONTINUE
C     
      CALL W3FI72(ITYPE,PACKED,IGRD,IBITL,
     X            IPFLAG,ID,PDS,
     X            IGFLAG,IGRID,IGDS,ICOMP,
     X            IBFLAG,IBMAP,IBLEN,
     X            IBDSFL,
     X            NPTS,KBUF,ITOT,IER)
      IF (IER.NE.0) THEN
         WRITE(STDOUT,1040) IER,DESCRIPT,LABPAK(1)
 1040    FORMAT('GRIBIT:  ***W3FI72 ERROR IER=',I1,
     X        ' FOR ',A20,' ON84 ',2(Z16,1X))
         WRITE(STDOUT,*)'GRIBIT:  DID NOT POST THIS FIELD'
         RETURN
      ENDIF
C     
C     UNPACK GRIB1 MESSAGE FOR DIAGNOSTIC MESSAGE.
      CALL W3FI63(KBUF,KPDS,KGDS,KBMS,DATAFLD,KPTR,KRET)
      NPTS = KGDS(2)*KGDS(3)
      FMIN = +1.E32
      FMAX = -1.E32
      DO 40 K=1,NPTS
	IF (KBMS(K)) THEN
	   FMIN = AMIN1(FMIN,DATAFLD(K))
	   FMAX = AMAX1(FMAX,DATAFLD(K))
	ENDIF
40    CONTINUE
      WRITE(85,1050) DESCRIPT,(KPDS(K),K=1,22)
 1050 FORMAT(A20,22(I5,1X))
C     
C     WRITE GRIB1 MESSAGE TO OUTPUT FILE.
      CALL WRYTE(IOUTGRIB,ITOT,KBUF)
C     
C     WRITE DIAGNOSTIC MESSAGE.
      WRITE(STDOUT,1060) KPDS(5),DESCRIPT,KPDS(6),KPDS(7),
     X	FMIN,FMAX,KPDS(3)
 1060 FORMAT('GRIBIT:  ',I3,1X,A20,1X,I3,1X,I5,1X,2(G12.6,1X),I5)
C     
C     END OF ROUTINE.
C     
      RETURN
      END
