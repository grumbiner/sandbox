      SUBROUTINE OUTSIGMA( DAT,SCR,LQQTYPE)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    OUTSIGMA    SEND APPROP SIGMA LEVEL DATA TO PACKER
C   PRGMMR: N PHILLIPS       ORG: W/NMC22    DATE: 83-12-24
C
C ABSTRACT: A THREE-DIMENSIONAL ARRAY OF DATA ORGANIZED AS KM
C   CONSECUTIVE HORIZONTAL ARRAYS OF DATA AT SIGMA LEVELS, IS
C   CONSIDERED FOR SENDING TO THE PACKER CODE WITH APPROPRIATE
C   LABELLING.
C   EACH LEVEL IS SENT SEPARATELY, BUT ONLY IF THERE IS A 1
C   IN LEVELS(K,NCARD) WHERE NCARD IS THE 'DATA CARD' FOR THE
C   VARIABLE DEFINED BY LQQTYPE AND A SIGMA SURFACE.
C   .
C PROGRAM HISTORY LOG:
C   83-12-24  N PHILLIPS
C   88-08-26  B SCHMIDT REVISED THE DOCBLOCK
C
C USAGE:    CALL OUTSIGMA( DAT , SCR, LQQTYPE )
C   INPUT ARGUMENT LIST:
C     DAT      - THREE DIMENSIONAL ARRAY OF SIGMA DATA
C                (ARRANGED IN SEQUENCE AS
C                DAT(I=(1,IMOUT),J=(1,JMOUT),K=(1,KM) )
C     SCR      - HORIZONTAL SCRATCH ARRAY (SIZE=IJOUT)
C                THAT WILL BE USED BY THE  PACKER CODE
C     LQQTYPE  - NUMERICAL VALUE ASSIGNED THE VARIABLE IN
C                TABLE 1 OF OFFICE NOTE 84.
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - OUTNULAB
C                  OUTPACK
C     LIBRARY:
C       COMMON   - COMCONST
C                  COMOUT
C
C REMARKS:
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES         MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----         ----------------------------------        ---------
C   DAT(IADOUT)   DESIRED HORIZONTAL ARRAY AT LEVEL K IN    ARGUMENT
C                 THE INPUT ARRAY THAT IS TO BE PACKED      OF CALL TO
C                                                           OUTPACK
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
C     COMMON BLOCK /COMOUT/ CONTAINS OUTPUT CODE PARAMETERS
C               AND ARRAYS.
C
      CHARACTER*20 DESCRIPT
C
      COMMON /COMOUT/
C
C          THE FOLLOWING ARE STORED BY OUT2FILE FROM DATA CARDS.
C          (THE LAST 6 WILL BE OVERWRITTEN BY  OUTPUT  IF KGTYPE
C          IS 5 OR 26 (I.E. AN LFM GRID DEFINED IN TABLE 7 OF
C          OFFICE NOTE 84.))
C
     1     INUNIT, IOUTUNIT, IOUTGRIB,
     2     KGTYPE, IMOUT, JMOUT, DNPEQOUT, DLAMOUT, XIPOUT, YJPOUT,
C
C          THE FOLLOWING COME FROM  MAIN  READING THE DATA CARDS.
     2   LQTYPE(IMAXOUTV),   LSTYPE(IMAXOUTV),
     3   DESCRIPT(IMAXOUTV), LEVELS(ILMOUT,IMAXOUTV),  NOUTVBLS,
C
C          THE FOLLOWING ARE DERIVED BY   OUTPUT  .
     4       IJOUT, LMOUT, NFILLER, NS2, NS3NGM, NS3OUT,
     5       COSOUT,   SINOUT,    DXOUT,    ZSIGLYR(IKM),
C
C           ARRAYS  PLOUT  AND  HTFD  ARE SET BY DATA STATEMENTS IN
C           THE SR  OUTPUT  .  'PADDER' FILLS OUT TO FULL WORD BNDRY.
     6      PLOUT(ILMOUT),   HTFD( 6),  PADDER,
C
C          THE FOLLOWING ADDRESSES AND ADDRESS ARRAYS ARE SET UP
C          BY   OUTPREP  WHEN IT IS CALLED BY   OUTPUT  .
     7  J1(INGRDUSE),          J2(INGRDUSE),   JADDK2L(INGRDUSE,2),
     8  JADDFNGM(INGRDUSE), JHREC(INGRDUSE),   JHKAP(INGRDUSE),
     9  JSCR2(12),    JSCR3NGM(INGRDUSE,12),   JSCR3OUT(12),
     A  LENGD(INGRDUSE), LTYPE(ILMOUT,INGRDUSE), MB1, MB2, MINT,
     B   MSOUT1,  MSOUT2,  MZGNDOUT,  MNGM3,  MHOUT, MZFR, MZTRO,
     C      MSZGND(INGRDUSE), NGRIDATA(INGRDUSE),
C
C            NUMGDREQ, THE NUMBER OF NGM GRIDS NEEDED TO COVER THE
C            OUTPUT GRID, AND ZLOUT, ARE SET BY  OUTPREP  .
     D                NUMGDREQ,    ZLOUT(ILMOUT),
C
C          OUTPREP ALSO ASSIGNS THE FOLLOWING DESCRIPTORS TO BE
C          USED BY  OUTHORIZ  FOR HORIZONTAL INTERPOLATION.
     E    AHORD,BHORD,CHORD,DHORD, ILLD,IULD,  SCRHORD,  OUTHORD,
C
C
C          THE FOLLOWING ARRAY CONTAINS THE INTEGERS NEEDED TO
C          FORM THE LABEL THAT GOES WITH EACH OUTPUT ARRAY.
     F               LABEL84( 27 ),
C
C          THE FOLLOWING SYMBOLS REPRESENT TEMPORARY VALUES OF
C          INTEGERS TO GO INTO  LABEL84 WHEN THE OUTPUT ARRAY
C          HAS BEEN PREPARED FOR PACKING.
C
     G  LABQTYPE, LABSSUB1, LABTMARK,  S1VALUE,  S2VALUE,
     H  LABMLDIF, LABSSUB2, LABFSUB2, LABKGRID, LABNUMPT
CER
      INTEGER IDGRBE(25)
C
      DIMENSION DAT(1),SCR(1)
C          OFFICE NOTE 84 LABEL FOR SURFACE TYPE = SIGMA
      DATA LSSTYPE/148/
C
C          LOOK FOR A DATA CARD REQUESTING THIS DATA
C
      DO 10 N=1,NOUTVBLS
      NCARD=N
      IF( LQTYPE(N) .NE. LQQTYPE ) GO TO 10
C           RIGHT DATA TYPE,,, IS SIGMA OUTPUT WANTED?
      IF( LSTYPE(N) .EQ. LSSTYPE ) GO TO 20
C
   10 CONTINUE
C
C          NO DATA CARD FOUND
      RETURN
C
   20 CONTINUE
C
C           LOOK FOR LEVELS THAT ARE WANTED
C
      IADOUT=1-IJOUT
      DO 30 K=1,KM
      IADOUT=IADOUT+IJOUT
      IF( LEVELS(K,NCARD) .NE. 1 ) GO TO 30
C
      LABQTYPE=LQQTYPE
      LABSSUB1=LSSTYPE
      S1VALUE=PRESS(K)
C
        CALL OUTNULAB
C
C  SET UP PDS INFO
C
      CALL LAB2PDS(LABEL84,IDGRBE)
      IDGRBE(7) =   0
      IF(LABEL84(1).EQ.16) IDGRBE(8) = 11
      IF(LABEL84(1).EQ.17) IDGRBE(8) = 17
      IF(LABEL84(1).EQ.19) IDGRBE(8) = 13
      IF(LABEL84(1).EQ.40) IDGRBE(8) = 39
      IF(LABEL84(1).EQ.42) IDGRBE(8) = 40
      IF(LABEL84(1).EQ.48) IDGRBE(8) = 33
      IF(LABEL84(1).EQ.49) IDGRBE(8) = 34
      IF(LABEL84(1).EQ.88) IDGRBE(8) = 52
      IF(LABEL84(1).EQ.95) IDGRBE(8) = 51
      IDGRBE(9) =   107
      IDGRBE(10) =  0
      ALEV1 = REAL(LABEL84(5))
      ARGL1 = REAL(LABEL84(6))
      XLEV1 = ALEV1 * 10.**ARGL1
      IDGRBE(11) =  INT((XLEV1*10000.)+0.5)
      IDGRBE(18) =  LABEL84(3)
      IDGRBE(19) =  0
      IDGRBE(20) =  0
C
C
c     CALL OUTPACK( DAT(IADOUT),IJOUT,SCR,DESCRIPT(NCARD),
c    1   LABEL84, IOUTUNIT )
      CALL GRIBITN ( DAT(IADOUT),IJOUT,SCR,DESCRIPT(NCARD),
     1   IDGRBE,IOUTGRIB,LABEL84 )
C
   30 CONTINUE
C
      RETURN
      END
