      SUBROUTINE SMOOTH
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    SMOOTH      DO HORIZONTAL SMOOTHING OF THE NGM GRIDS
C   PRGMMR: J TUCCILLO         ORG: W/NMC4      DATE: 90-06-17
C
C ABSTRACT: DO HORIZONTAL SMOOTHING OF THE NGM GRIDS
C   .       BEGINNING WITH THE FINEST AND ENDING
C   .       WITH THE COARSEST.
C   .
C   THE FIELDS OF HU, HV, HTHETA, AND HQ ARE SMOOTHED WITH A
C   4TH-ORDER SHAPIRO/SHUMAN FILTER.
C   THE SURFACE PRESSURE (H) IS NOT SMOOTHED SINCE IT
C   MOSTLY REFLECTS OROGRAPHY. (THE MULTIPLICATIVE FACTOR H IS
C   HOWEVER DIVIDED OUT OF HU, HV, ETC. BEFORE SMOOTHING AND
C   RESTORED AFTERWARDS.)   AFTER GRID NG IS SMOOTHED, SUB-
C   ROUTINE LATBND IS CALLED TO UPDATE THE BOUNDARIES OF THE
C   NEXT COARSER GRID WITH THE SMOOTHED VALUES FROM THE GRID
C   JUST SMOOTHED.  SUBROUTINE FILLHOLE IS THEN CALLED TO FILL
C   THE HOLE ON THE NEXT COARSER GRID WITH THE JUST-SMOOTHED
C   DATA FROM GRID NG.
C   SUBROUTINE SMOOTHA IS CALLED TO SMOOTH GRID A.
C   .
C
C PROGRAM HISTORY LOG:
C   90-06-17  J TUCCILLO
C   98-09-11  HANN-MING HENRY JUANG recode and convert to f90 
C
C USAGE:  CALL SMOOTH
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - LATBND
C                  FILLHOLE
C                  SMOOTHA
C                  SMUSCR
C     LIBRARY:
C       COMMON   - COMCONST
C                  COMBLANK
C
C REMARKS:
C   - - - - - - - - I N P U T   V A R I A B L E S  - - - - - - - - -
C   .
C   NAMES      MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----      ----------------------------------        ---------
C   IMG,JMG    HORIZONTAL GRID DIMENSIONS OF ALL GRIDS   COMMON
C   .
C   IAG,IBG,   LOCATION OF HOLES IN GRIDS 1,2,---        COMMON
C   JAG,JBG    TO (NGRIDUSE -1)
C   .
C   IADDRG     INITIAL ADDRESSES OF DIFFERENT TYPE
C   .          VARIABLES AND GRIDS IN THE ARRAY VBL      COMMON
C   .
C   VBL        ARRAY CONTAINING ALL VBLS FOR ALL GRIDS   COMMON
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES      MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----      ----------------------------------        ---------
C   VBL        SMOOTHED VARIABLES.                       COMMON
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 90
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C...TRANSLATED BY FPP 3.00Z36 11/09/90  14:46:47  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
      COMMON /COM1WAY/ ITIMSTEP, ITBOUND, INGLB
C
      IF ( NGRDUSE .EQ. 1 ) THEN
          CALL SMOOTHA
          RETURN
      ENDIF
C
C          START WITH THE FINEST GRID
C          (FOR WHICH THERE IS NO HOLE TO FILL FIRST.)
C
      NG=NGRDUSE
C
C ***** ***** ENTER LOOP ON GRIDS ***** *****
C          SMOOTH GRID NG.
      DO 100 NG=NGRDUSE,2,-1
C
      IM=IMG(NG)
      JM=JMG(NG)
      IJ=IM*JM
      IJM1=IJ-1
C          ALL SMOOTHED FIELDS HAVE THE SAME INITIAL LIMITS
C          FOR I AND J.  THEY ARE--
      I1=5
      J1=5
C
C          FIRST SMOOTH THE KM LEVELS OF HU.
C          NEED H AND 1/H AT U POINTS(J=2,JM-1) TO DIVIDE OUT H FACTOR.
      IADDH=IADDRG(5,NG)
      LENH=IJ-2*IM
CMIC$ DO ALL VECTOR SHARED(LENH, IADDH, IM, VBL, SCR) PRIVATE(I)
      DO I=1,LENH
         SCR(IM+I,1)=.5E0*(VBL(IADDH+IM+I-1)+VBL(IADDH+2*IM+I-1))
         SCR(IM+I,2)=1.E0/SCR(IM+I,1)
      ENDDO
C          END LIMITS FOR U-SMOOTHING
      I2=IM-3
      J2=JM-4
      IADDU=IADDRG(1,NG)-IJ
C
      DO K=1,KM
        IADDU=IADDU+IJ
C          DIVIDE OUT H-FACTOR BEFORE SMOOTHING.
CMIC$ DO ALL VECTOR SHARED(LENH, IADDU, IM, VBL, SCR) PRIVATE(I)
        DO I=1,LENH
           SCR(IM+I,3)=VBL(IADDU+IM+I-1)*SCR(IM+I,2)
        ENDDO
C          GO TO SMOOTHING ROUTINE
        CALL SMUSCR(IM,JM,J1,J2,IIJMAX,INSCR,SCR)
C          REMULTIPLY BY H AND STORE BACK IN VBL.
CMIC$ DO ALL VECTOR SHARED(LENH, IM, IADDU, SCR, VBL) PRIVATE(I)
        DO I=1,LENH
           VBL(IADDU+IM+I-1)=SCR(IM+I,3)*SCR(IM+I,1)
        ENDDO
C
      ENDDO
C
C          SMOOTH HV VALUES,,GET H AND 1/H AT V PTS(J=2,JM).
      LENH=IJ-IM
CMIC$ DO ALL VECTOR SHARED(LENH, IADDH, IM, VBL, SCR) PRIVATE(I)
      DO I = 1, LENH
         SCR(IM+I,1) = 0.5*(VBL(IADDH+IM+I-1)+VBL(IADDH+IM+I))
         SCR(IM+I,2) = 1.E0/SCR(IM+I,1)
      ENDDO
C          END LIMITS FOR V-SMOOTHING
      I2=IM-4
      J2=JM-3
      IADDV=IADDRG(2,NG)-IJ
C
      DO K=1,KM
        IADDV=IADDV+IJ
C          DIVIDE OUT THE H FACTOR.
CMIC$ DO ALL VECTOR SHARED(LENH, IADDV, IM, VBL, SCR) PRIVATE(I)
        DO I=1,LENH
           SCR(IM+I,3)=VBL(IADDV+IM+I-1)*SCR(IM+I,2)
        ENDDO
C          GO TO SMOOTHING ROUTINE
        CALL SMUSCR(IM,JM,J1,J2,IIJMAX,INSCR,SCR)
C          REMULTIPLY BY H AND STORE IN VBL.
CMIC$ DO ALL VECTOR SHARED(LENH, IM, IADDV, SCR, VBL) PRIVATE(I)
        DO I=1,LENH
           VBL(IADDV+IM+I-1)=SCR(IM+I,3)*SCR(IM+I,1)
        ENDDO
      ENDDO
C ---------------------------------------------------------------
C          SMOOTH THETA AND Q VALUES.
C           1/H FOR J=2,JM.
      LENH=IJ-IM
CMIC$ DO ALL VECTOR SHARED(LENH, IADDH, IM, VBL, SCR) PRIVATE(I)
      DO I=1,LENH
         SCR(IM+I,2)=1.E0/VBL(IADDH+IM+I-1)
      ENDDO
C          END LIMITS FOR SMOOTHING THETA AND Q
      I2=IM-3
      J2=JM-3
      IADDT=IADDRG(3,NG)-IJ
C
C           WE DO THE 2*KM HORIZONTAL ARRAYS FOR THETA AND Q
C
      DO K=1,2*KM
        IADDT=IADDT+IJ
C          DIVIDE OUT H
CMIC$ DO ALL VECTOR SHARED(LENH, IM, IADDT, SCR, VBL) PRIVATE(I)
        DO I=1,LENH
           SCR(IM+I,3)=SCR(IM+I,2)*VBL(IADDT+IM+I-1)
        ENDDO
C          GO TO SMOOTHING ROUTINE
        CALL SMUSCR(IM,JM,J1,J2,IIJMAX,INSCR,SCR)
C
C          MULTIPLY BY H AND RESTORE IN VBL.
CMIC$ DO ALL VECTOR IF (ABS(IADDH-IADDT).GE.LENH .OR. IADDH-IADDT.EQ.0)
CMIC$1    SHARED(LENH, IM, IADDH, IADDT, SCR, VBL) PRIVATE(I)
        DO I=1,LENH
           VBL(IADDT+IM+I-1)=SCR(IM+I,3)*VBL(IADDH+IM+I-1)
        ENDDO
      ENDDO
C
C          DONE WITH SMOOTHING ON THIS GRID,,RESTORE THE
C          CORRUPTED OUTER BNDRY VALUES USING LATBND.
C ---------------------------------------------------------------
C          LATBND NEED DO ONLY 2*KM H-TYPE VBLS(I.E.NOT H ITSELF).
C          IT WORKS WITH GRIDS NC AND NC+1.
C
      NC=NG-1
C
      KLAT=2*KM
C
      CALL LATBND(NC,KLAT)
C
C
C          FILL THE HOLE ON THE NEXT COARSER GRID, INCLUDING
C          THE H FIELD
C
      KFILL=2*KM+1
C
C          WHILE ONEWAY OFF, FILLHOLE AND BITHOLE ARE ON.
C
      IF( INGLB .EQ. 0 ) CALL FILLHOLE(NC,KFILL)
C
 100  CONTINUE
C
      RETURN
      END


      SUBROUTINE SMUSCR(IM,JM,J1,J2,IIJMAX,INSCR,SCR)
      DIMENSION SCR(IIJMAX,INSCR)
      IJ=IM*JM
      IJM1=IJ-1
C********************************
C           GO TO SUBROUTINE FOR SMOOTHING FIELD IN SCR(N,3)
C           FIRST DO SMOOTHING IN I-DIRECTION(J=1,JM)
CMIC$ PARALLEL SHARED(IJM1, SCR) PRIVATE(I)
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJM1
         SCR(I,4)=.5E0*(SCR(I+1,3)-SCR(I,3))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJM1
         SCR(I+1,5)=.5E0*(SCR(I+1,4)-SCR(I,4))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJM1
         SCR(I,4)=.5E0*(SCR(I+1,5)-SCR(I,5))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJM1
         SCR(I+1,6)=.5E0*(SCR(I+1,4)-SCR(I,4))
      ENDDO
C          THAT PUT 4-TH DERIVATIVE INTO SCR(N,6).
C             GET X-SMOOTHED RESULT INTO SCR(N,3) FOR ROWS
C             J=1 THROUGH JM.
C
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJM1
         SCR(I,3)=SCR(I,3)-SCR(I,6)
      ENDDO
CMIC$ END PARALLEL
C
C          NOW SMOOTH THIS IN Y(J).
      IJ1=IJ-IM
      L=IM+1
CMIC$ PARALLEL SHARED(IJ1, L, SCR) PRIVATE(I)
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJ1
         SCR(I,4)=.5E0*(SCR(L+I-1,3)-SCR(I,3))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJ1
         SCR(L+I-1,5)=.5E0*(SCR(L+I-1,4)-SCR(I,4))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJ1
         SCR(I,4)=.5E0*(SCR(L+I-1,5)-SCR(I,5))
      ENDDO
CMIC$ DO PARALLEL VECTOR
      DO I=1,IJ1
         SCR(L+I-1,6)=.5E0*(SCR(L+I-1,4)-SCR(I,4))
      ENDDO
CMIC$ END PARALLEL
C          THAT PUT THE 4TH DERIV. INTO SCR(N,6)
C            PUT Y-SMOOTHED VALUE INTO SCR(N,3) FOR
C              J = J1 THROUGH J2 .
      IADDS = (J1-1)*IM + 1
      ILEN = (J2 - J1 + 1 ) * IM
C
CMIC$ DO ALL VECTOR SHARED(ILEN, IADDS, SCR) PRIVATE(I)
      DO I=1,ILEN
         SCR(IADDS+I-1,3)=SCR(IADDS+I-1,3)-
     *                        SCR(IADDS+I-1,6)
      ENDDO
C
      RETURN
      END
