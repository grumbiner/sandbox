      SUBROUTINE OUTSVALU (SURFACE, MANTISSA, IEXPON)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    OUTSVALU    CALCULATES PARTS OF O.N. 84
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-25
C
C ABSTRACT: CALCULATES C (= MANTISSA) AND
C   .       E (= IEXPON) OF THE QUANTITY L (=SURFACE)
C   .       ACCORDING TO OFFICE NOTE 84 CONVENTION.
C   .       NOTE THAT:
C   .            SURFACE = MANTISSA * (10 ** IEXPON),
C   .       WHERE MANTISSA IS A 5-DIGIT INTEGER (EXCEPT WHEN
C   .       SURFACE = 0, IN WHICH CASE MANTISSA AND IEXPON
C   .       ARE SET TO ZERO.)  NOTE THAT A LITTLE TRUNCATION
C   .       ERROR CAN OCCUR BECAUSE SURFACE IS A FLOATING
C   .       POINT NUMBER.
C
C PROGRAM HISTORY LOG:
C   85-03-25  JIM HOKE
C   88-08-26  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL OUTSVALU (SURFACE, MANTISSA, IEXPON)
C   INPUT ARGUMENT LIST:
C     SURFACE  - REAL NUMBER DESCRIBING SOME MODEL FIELD
C
C   OUTPUT ARGUMENT LIST:
C     MANTISSA - MANTISSA FOR 'SURFACE' ACCORDING O.N. 84 FORMAT
C     IEXPON   - EXPONENT FOR 'SURFACE' ACCORDING TO O.N. 84 FORMAT
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      IF (SURFACE .EQ. 0.0E0) THEN
C
C              THE SPECIAL CASE.
         MANTISSA = 0
         IEXPON = 0
C
      ELSE
C              THE CASE WHEN SURFACE IS NONZERO.
         EXPON = LOG10 ( ABS (SURFACE) )
         IF (EXPON .GE. 0.0E0) THEN
C
C              THE VALUE OF SURFACE IS GREATER THAN OR EQUAL TO 1.
            IEXPON = INT (EXPON) - 4
C
         ELSE
C              THE VALUE OF SURFACE IS LESS THAN 1.
            IEXPON = INT (EXPON) - 5
         END IF
C
         AMANTISS = SURFACE * (10.0E0 ** (-IEXPON))
C
C              ROUND THE MANTISSA TO THE NEAREST (SIGNED) INTEGER.
         MANTISSA = NINT (AMANTISS)
C
C              ENSURE THAT THE MANTISSA IS WITHIN RANGE.
  200    CONTINUE
         IF (ABS(MANTISSA) .GE. 100000) THEN
            IEXPON = IEXPON + 1
            AMANTISS = SURFACE * (10.0E0 ** (-IEXPON))
C
C              ROUND THE MANTISSA TO THE NEAREST (SIGNED) INTEGER.
            MANTISSA = NINT (AMANTISS)
            GO TO 200
C
         END IF
C
  300    CONTINUE
         IF (ABS (MANTISSA) .LE. 9999) THEN
            IEXPON = IEXPON - 1
            AMANTISS = SURFACE * (10.0E0 ** (-IEXPON))
C
C              ROUND THE MANTISSA TO THE NEAREST (SIGNED) INTEGER.
            MANTISSA = NINT (AMANTISS)
            GO TO 300
         END IF
C
      END IF
C
      RETURN
C
      END
