      SUBROUTINE PRECIP (DHQ, NPAIR, NG, LADD3, LEN31, DTIME)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    PRECIP      COMPUTE THE PRECIP FOR ONE GRID
C   PRGMMR: J TUCCILLO       ORG:W/NMC40    DATE: 90-06-19
C
C ABSTRACT:
C   THIS SUBROUTINE CALCULATES THE EFFECTS OF KUO CONVECTION AND
C   GRID-SCALE PRECIPITATION FOR ONE GRID FOR ONE TIME STEP.
C
C PROGRAM HISTORY LOG:
C   90-06-19  J TUCCILLO
C
C USAGE:  CALL PRECIP ( DHQ, NPAIR, NG, LADD3, LEN31, DTIME)
C INPUT ARGUMENT LIST:
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C
C      DHQ        MOISTURE FLUX CONVERGENCE FOR THIS GRID.  ARGUMENT
C
C      NPAIR      1 FOR THE FIRST OF A PAIR OF CALLS TO     ARGUMENT
C                 THIS GRID, 2 FOR THE SECOND OF A PAIR.
C
C      NG         GRID NUMBER (OUTER HEMISPHERIC GRID HAS   ARGUMENT
C                 NG EQUAL TO 1.)
C
C      LADD3      THE LOCATION OF THE FIRST FORECAST POINT  ARGUMENT
C                 IN A TWO-DIMENSIONAL ARRAY FOR THIS GRID
C                 AND NPAIR.
C
C      LEN31      THE LENGTH OF THE VECTOR ENCOMPASSING     ARGUMENT
C                 ALL FORECAST POINTS IN TWO DIMENSIONS
C                 FOR THIS GRID AND NPAIR.
C
C      DTIME      THE LENGTH OF THE TIME STEP (IN SECONDS)  ARGUMENT
C                 FOR THIS GRID.
C
C      BITGRDH    BIT VECTOR INDICATING THE LOCATION        /COMVBLS/
C                 OF LEGITIMATE FORECAST POINTS.
C
C      DELSIG     SIGMA THICKNESSES FOR EACH                /COMCONST/
C                 LAYER.
C
C      IADDRG     STARTING ADDRESSES IN VBL FOR EACH TYPE   /COMCONST/
C                 OF DATA FOR EACH GRID.
C
C      IMG,JMG    HORIZONTAL DIMENSIONS OF ALL GRIDS.       /COMCONST/
C
C      KM         NUMBER OF HORIZONTAL LEVELS FOR ALL       /COMCONST/
C                 GRIDS.
C
C      NPTSFH     THE ARRAY OF THE NUMBER OF FORECAST       /COMCONST/
C                 POINTS FOR EACH GRID AND NPAIR.
C
C      NTIME      TIME STEP (ADVANCES BY ONE FOR EACH STEP  /COMCONST/
C                 ON THE OUTER GRID (NG = 1) ).
C
C      PR         HALF OF (1-SIGMA) TO THE KAPPA POWER.     /COMCONST/
C
C      PRESS      PRESS(K) * SURFACE PRESSURE IS THE        /COMCONST/
C                 PRESSURE OF LAYER K.
C
C      SIGINT     SIGMA VALUES AT THE KM + 1 INTERFACES.    /COMCONST/
C
C      VBL        ARRAY CONTAINING ALL FORECAST-TYPE        /COMVBLS/
C                 VARIABLES FOR ALL GRIDS.
C                 THE H, HTH, AND HQ FIELDS ARE NEEDED
C                 AS INPUT.
C
C              THE FOLLOWING PRECIPITATION OPTIONS WERE SPECIFIED
C              ON DATA CARDS AND PLACED INTO /COMCONST/.
C
C      KUMULUS:   SPECIFIES WHETHER TO PERFORM THE CUMULUS CONVECTIVE
C                 ADJUSTMENT.  (KUMULUS=1 TO DO CUMULUS CONVECTION.)
C
C      LGRIDPPT:  SPECIFIES WHETHER TO PERFORM THE GRID-SCALE
C                 PRECIPITATION.  (LGRIDPPT=1 TO DO GRID-SCALE
C                 PRECIPITATION.)
C
C      SIGMACC:   SPECIFIESE SIGMA LEVEL BELOW WHICH CUMULUS
C                 CONVECTION WILL BE CALCULATED.  LAYERS UP TO
C                 AND INCLUDING THIS SIGMA LEVEL WILL BE INCLUDED
C                 IN THE CALCULATIONS.
C
C      SIGMAGSP:  SPECIFIES SIGMA LEVEL BELOW WHICH GRID-SCALE
C                 PRECIPITATION WILL BE CALCULATED.  LAYERS UP TO
C                 AND INCLUDING THIS SIGMA LEVEL WILL BE INCLUDED IN
C                 THE CALCULATIONS.
C
C      SIGMADHQ:  SPECIFIES THE SIGMA LEVEL BELOW WHICH THE INTEGRATED
C                 MOISTURE FLUX CONVERGENCE MUST EXCEED SOME THRESHOLD
C                 VALUE IN ORDER TO HAVE CUMULUS CONVECTION.
C                 LAYERS UP TO AND INCLUDING THIS SIGMA LEVEL WILL
C                 BE INCLUDED IN THE CALCULATIONS.
C
C      CRITCONV:  SPECIFIES THE MINIMUM INTEGRATED MOISTURE FLUX
C                 CONVERGENCE RATE (IN CB/S) FOR CUMULUS CONVECTION
C                 TO OCCUR.
C
C      KLIFT1, KLIFT2:
C                 SPECIFY THE LOWEST AND HIGHEST LAYERS USED IN
C                 COMPUTING CLOUD PROFILES IN THE CUMULUS
C                 CONVECTION.
C
C      SATDEL:    SPECIFIES THE DELTA PARAMETER USED TO DETERMINE THE
C                 ONSET OF GRID-SCALE PRECIPITATION.
C
C      RHFACTOR:  SPECIFIES THE FACTOR MULTIPLYING
C                 1/(1+SATDEL) -- THE MINIMUM
C                 RELATIVE HUMIDITY FOR ONSET OF GRID-SCALE
C                 PRECIPITATION -- USED TO DETERMINE THE MAXIMUM
C                 RELATIVE HUMIDITY PRODUCED BY EVAPORATION OF
C                 CONVECTIVE OR GRID-SCALE PRECIPITATION.
C
C      QBOUND:    SPECIFIES THE MINIMUM VALUE OF SPECIFIC HUMIDITY.
C
C
C    OUTPUT ARGUMENT LIST:
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C      KKCLOUD2   ARRAY OF THE TOPMOST LAYER MODIFIED BY    /COMDIAG/
C                 THE CUMULUS CONVECTION FOR EACH NPAIR
C                 AND GRID.
C
C      NPTSBUOY   ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 NPAIR AND GRID THAT HAVE CLOUD
C                 TEMPERATURE GREATER THAN AMBIENT
C                 TEMPERATURE FOR AT LEAST 1 LAYER.
C                 THE POINTS ARE A SUBSET OF THE
C                 POINTS GIVEN BY NPTSDHQ.
C
C      NPTSCC     ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 NPAIR AND GRID AT WHICH CUMULUS
C                 CONVECTIVE ADJUSTMENT IS MADE.
C
C      NPTSDHQ    ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 NPAIR AND GRID AT WHICH THERE IS
C                 SUFFICIENT MOISTURE FLUX CONVERGENCE
C                 UP THROUGH LAYER LEVSDHQ FOR
C                 CUMULUS CONVECTION.
C
C      NPTSGSP    ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 NPAIR AND GRID AT WHICH THERE IS AT
C                 LEAST ONE LAYER WITH GRID-SCALE
C                 SATURATION.
C
C      NPTSWATR   ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 NPAIR AND GRID AT WHICH THERE
C                 IS POSITIVE MOISTURE FLUX CONVERGENCE
C                 FROM LAYER 1 TO THE TOP OF THE CLOUD.
C                 THE POINTS ARE A SUBSET OF THE POINTS
C                 GIVEN BY NPTSDHQ.
C
C      NPTSSAT    ARRAY OF THE NUMBER OF POINTS FOR EACH    /COMDIAG/
C                 LAYER, NPAIR, AND GRID AT WHICH THERE
C                 IS GRID-SCALE SATURATION.
C
C      PPTCC      THE SUM OVER ALL GRID POINTS FOR EACH     /COMDIAG/
C                 NPAIR AND GRID OF THE CUMULUS
C                 CONVECTION (IN METERS).  THE CALCULATION
C                 IS NOT NORMALIZED BY MAP SCALE.
C
C      PPTGSP     THE SUM OVER ALL GRID POINTS FOR EACH     /COMDIAG/
C                 NPAIR AND GRID OF THE GRID-SCALE
C                 PRECIPITATION (IN METERS).  THE
C                 CALCULATION IS NOT NORMALIZED BY
C                 MAP FACTOR.
C
C      VBL        ARRAY CONTAINING ALL FORECAST-TYPE        /COMVBLS/
C                 VARIABLES FOR ALL GRIDS.  ONLY THE
C                 HTH AND HQ FIELDS ARE MODIFIED BY
C                 SUBROUTINE PRECIP.
C
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C    SUBPROGRAMS CALLED:
C     NAMES         LIBRARY
C     -------       -------
C     MSTADB
C     P2KAP
C     VAPTAB
C
C REMARKS:
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C...TRANSLATED BY FPP 3.00Z36 11/09/90  14:56:09  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMDIAG/ CONTAINS QUANTITIES SAVED FOR
C          DIAGNOSTIC PURPOSES.
      COMMON /COMDIAG/  KADIAB( IKM, INGRDUSE ),
     1                  NPTSDHQ (2, INGRDUSE),
     2                  KKCLOUD2(2, INGRDUSE), NPTSBUOY(2, INGRDUSE),
     3                  NPTSWATR(2, INGRDUSE), NPTSCC  (2, INGRDUSE),
     4                  KKGSP2  (2, INGRDUSE),
     5                  KKGSE2  (2, INGRDUSE),
     6                  NPTSSAT (IKM, 2, INGRDUSE),
     7                  NPTSEVAP(IKM, 2, INGRDUSE),
     8                  NPTSGSP (2, INGRDUSE), NUMPROF,
     9                  PPTCC   (2, INGRDUSE), PPTGSP  (2, INGRDUSE),
     1                  ALATPR  (IMAXPROF),    ALONPR  (IMAXPROF),
     2                  DESCRP  (IMAXPROF)
C
      CHARACTER*24 DESCRP
C
      LOGICAL BITSDHQ, BITSCLD, BITWATER, BITCC, BITEVAP, BITSSAT,
     1    BITSGSP, BITCONV, BITEALL
      LOGICAL BITGRDHD, BITWATRD, BITCCD, BITEVAPD, BITCONVD, BITEALLD
C
      DIMENSION BITSDHQ(IIJMAX),  BITSCLD(IIJKMAX),
     1          BITWATER(IIJMAX), BITCC(IIJMAX), BITEVAP(IIJMAX),
     2          BITSSAT(IIJMAX),  BITSGSP(IIJMAX),
     3          BITCONV(IIJMAX),  BITEALL(IIJMAX)
      DIMENSION KCLOUD(IKM)
C
      DIMENSION DHQ(IIJKMAX), DHQ1(IIJKMAX),
     1          DQ1(IIJKMAX), DQ2(IIJKMAX),
     2          QCLOUD2 (IIJKMAX),
     3          QDIFF2(IIJKMAX),
     4          Q1 (IIJKMAX), Q2(IIJKMAX), SCRATCHB(IIJKMAX),
     5          SCRATCHD(IIJKMAX), TCLOUD2(IIJKMAX),
     6          TDIFF2(IIJKMAX), T1(IIJKMAX), T2(IIJKMAX)
C
      DIMENSION ALPHA2  (IIJMAX),A2      (IIJMAX),
     1          BETA2 (IIJMAX),  B2      (IIJMAX),   DENOM2  (IIJMAX),
     2          DHQSUM1(IIJMAX), ESAT1   (IIJMAX),   ESAT2   (IIJMAX),
     3          EVAP2(IIJMAX),   FALL    (IIJMAX)
      DIMENSION FALLSIG2(IIJMAX),FALL1   (IIJMAX),
     5          FALL1K(IIJMAX),
     6          FALL2(IIJMAX),   HINV1   (IIJMAX),
     7          HKAPPA1(IIJMAX), HKAPPA2 (IIJMAX),
     8          HHKAPI1(IIJMAX), HKAPHI1 (IIJMAX)
      DIMENSION H1(IIJMAX),      H2      (IIJMAX),
     1          PRATEPS1(IIJMAX),PRATEPS2(IIJMAX),
     2          PRSDEN1(IIJMAX), PRSDEN2 (IIJMAX),   PRSEPSI1(IIJMAX),
     3          PRSTOT1(IIJMAX), PRSTOT2 (IIJMAX),   QCHANGE2(IIJMAX),
     4          QDEF1(IIJMAX),   QDEF2   (IIJMAX),
     5          QEFF2(IIJMAX),   QSAT1   (IIJMAX)
      DIMENSION QSAT2(IIJMAX),   SATDELF1(IIJMAX),   SATDELF2(IIJMAX),
     7          SCRATCH(IIJMAX), SCRATCHA(IIJMAX,4), SCRATCHC(IIJMAX),
     8          SCRP2KAP(IIJMAX),SCRVAP1 (IIJMAX,2), SCRVAP2 (IIJMAX,2),
     9          TCHANGE2(IIJMAX),COEF2 (IIJMAX),     WATER2  (IIJMAX)
C
      DIMENSION ATDESC(IIJMAX), AQDESC(IIJMAX), ABDESC(IIJMAX)
C
      EQUIVALENCE (SCR3(1, 1), T1(1)),
     1            (SCR3(1, 2), Q1(1)),
     2            (SCR3(1, 3), T2  (1)),
     3            (SCR3(1, 4), DHQ1(1), DQ1(1), DQ2(1)),
     4            (SCR3(1, 5), Q2  (1)),
     5            (SCR3(1, 6), SCRATCHB(1)),
     6            (SCR3(1, 7), SCRATCHD(1)),
     7            (SCR3(1, 8), TCLOUD2(1), TDIFF2(1)),
     8            (SCR3(1, 9), QCLOUD2(1), QDIFF2(1))
C
      EQUIVALENCE
     1            (SCR(1, 1), H1(1)),
     2            (SCR(1, 2), HKAPPA1(1)),
     3            (SCR(1, 3), SCRP2KAP(1), SCRATCHA(1,1), FALL2(1),
     4                        PRSEPSI1(1)),
     5            (SCR(1, 4), HINV1(1), WATER2(1), TCHANGE2(1),
     6                        EVAP2(1)),
     7            (SCR(1, 5), DHQSUM1(1), A2(1), SCRATCH(1),
     8                        SCRVAP1(1,1), PRSTOT2(1), FALL(1),
     9                        PRSTOT1(1), FALL1K(1)),
     1            (SCR(1, 6), HKAPHI1(1),
     2                        B2(1), QCHANGE2(1), PRSDEN2(1),
     3                        PRSDEN1(1), ALPHA2(1)),
     4            (SCR(1, 7), SCRATCHC(1), DENOM2(1), QSAT1(1))
C
      EQUIVALENCE
     1            (SCR(1, 8), QDEF1(1), QDEF2(1),
     2                        SATDELF1(1), SATDELF2(1)),
     3            (SCR(1, 9), H2(1), FALL1(1)),
     4            (SCR(1,10), HKAPPA2(1), ESAT2(1), ESAT1(1),
     5                        BETA2(1), HHKAPI1(1)),
     6            (SCR(1,11), QEFF2(1), PRATEPS2(1)),
     7            (SCR(1,12), SCRVAP2(1,1), QSAT2(1)),
     8            (SCR(1,13), COEF2(1)),
     9            (SCR(1,14), FALLSIG2(1), PRATEPS1(1))
C
      EQUIVALENCE (SCR(1,15), ATDESC(1) ),
     *            (SCR(1,16), AQDESC(1) ),
     *            (SCR(1,17), ABDESC(1) )
C
C
      INTEGER I
      REAL COFD1,COFD2,COFD3,COFD4,COFN1,COFN2,COFN3
      PARAMETER (COFD1 = 1., COFD2 = 5.44053037, COFD3 = 2.27693825, 
     1   COFD4 = -0.0869930591, COFN1 = 0.34757549, COFN2 = 4.36732956, 
     2   COFN3 = 3.91557032)
C
C
      DATA ICALL /0/
      DATA BPARAM  /0.2E0/
      DATA RHCC    /0.5E0/
C
C              SPECIFY THE UNIT NUMBER OF THE PRINTER.
C
C              SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
      IF (ICALL .NE. 0) GO TO 500
C
      ICALL = 1
C
C              CALCULATE THE TOP LAYER USED IN THE VERTICAL
C              INTEGRATION OF THE MOISTURE FLUX CONVERGENCE
C
      KMP1 = KM + 1
C
      DO 100 K=1,KMP1
         IF (SIGINT(K) .GE. SIGMADHQ) THEN
            LEVSDHQ = K - 1
            GO TO 200
         END IF
  100 CONTINUE
C
      WRITE (IOUTUPRT, 150)
  150 FORMAT ('0', 'IMPOSSIBLE BRANCH TAKEN IN SUBROUTINE PRECIP ',
     1             'AFTER STATEMENT NUMBER 100.')
C
       CALL EXIT(16)
C
  200 CONTINUE
C
C              CALCULATE THE TOP LAYER USED IN THE CALCULATION
C              OF CUMULUS CONVECTION.
C
      DO 225 K=1,KMP1
         IF (SIGINT(K) .GE. SIGMACC) THEN
            LEVSCC  = K - 1
            GO TO 250
         END IF
  225 CONTINUE
C
      WRITE (IOUTUPRT, 230)
  230 FORMAT ('0', 'IMPOSSIBLE BRANCH TAKEN IN SUBROUTINE PRECIP ',
     1             'AFTER STATEMENT NUMBER 225.')
C
      CALL EXIT(16)
C
  250 CONTINUE
C
C              CALCULATE THE TOP LAYER USED IN THE CALCULATION
C              OF GRID-SCALE PRECIPITATION.
C
      DO 300 K=1,KMP1
         IF (SIGINT(K) .GE. SIGMAGSP) THEN
            LEVSGSP = K - 1
            GO TO 400
         END IF
  300 CONTINUE
C
      WRITE (IOUTUPRT, 350)
  350 FORMAT ('0', 'IMPOSSIBLE BRANCH TAKEN IN SUBROUTINE PRECIP ',
     1             'AFTER STATEMENT NUMBER 300.')
C
      CALL EXIT(16)
C
  400 CONTINUE
C
C              CALCULATE THE TOP LAYER INVOLVED IN THE PRECIPITATION
C              CALCULATIONS.
      LEVSPPT = MAX (LEVSCC, LEVSGSP)
C
C              SOME CONSTANTS ARE SPECIFIED AND CALCULATED.
      GRAVITY = 9.8   E0
      RGASD   = 287.05E0
      RGASV   = 461.5 E0
      CSUBP   = 1005. E0
      ELCOND  = 2.501 E6
C
      EPS     = RGASD  / RGASV
      ONEMEPS = 1.0E0  - EPS
      CPOVEL  = CSUBP  / ELCOND
      ELOVRD  = ELCOND / RGASD
      ELOVRV  = ELCOND / RGASV
      ELOVCP  = ELCOND / CSUBP
C
      SATDELP1 = SATDEL + 1.0E0
      IF (SATDEL .NE. 0.0E0) SATDELI = 1.0E0 / SATDEL
C
C     INITIALIZE EVAPORATION OF FALLING PRECIP VARIABLES
C
      RHFGSP   = RHFACTOR
      RHFCC    = RHCC
C
      RHCRIT   = RHFGSP *       (1.0E0 / (1.0E0 + SATDEL))
      RHKUOEPS = RHFCC  * EPS * (1.0E0 / (1.0E0 + SATDEL))
C
      CBOVG = 100.0E0 / GRAVITY
C
C***********************************************************************
C
  500 CONTINUE
C              ENTRY AFTER THE FIRST TIME THIS SUBROUTINE IS CALLED.
C
C              IF SIGMACC AND SIGMAGSP (THE SIGMA LEVELS BELOW
C              WHICH CUMULUS CONVECTION AND
C              GRID-SCALE PRECIPITATION ARE TO BE CALCULATED)
C              WERE BOTH SPECIFIED TO BE GROUND LEVEL, THEN
C              NO PRECIPITATION CALCULATIONS ARE DONE.
      IF (LEVSPPT .LE. 0) GO TO 2500
C
C              SPECIFICATION OF STARTING ADDRESSES WITHIN ARRAY VBL
C              FOR THIS GRID.
      MADDT  = IADDRG( 3, NG)
      MADDQ  = IADDRG( 4, NG)
      MADDH  = IADDRG( 5, NG)
      MADDCC = IADDRG(12, NG)
      MADDGSP= IADDRG(13, NG)
C
      IM = IMG(NG)
      JM = JMG(NG)
      IJ = IM * JM
C
C              SPECIFY THE NUMBER OF H, HTH, AND HQ FORECAST POINTS.
      NUMCOL1 = NPTSFH(NPAIR, NG)
C
C              SPECIFY CERTAIN VALUES IN CASE NO CUMULUS
C              CONVECTIVE ADJUSTMENT OR GRID-SCALE PRECIPITATIVE
C              ADJUSTMENT IS PERFORMED.
C              FIRST, INITIALIZE THE CUMULUS CONVECTION COUNTERS.
      KCLOUD2 = 0
      KKCLOUD2(NPAIR, NG) = 0
      NPTSDHQ (NPAIR, NG) = 0
      NPTSBUOY(NPAIR, NG) = 0
      NPTSWATR(NPAIR, NG) = 0
      NPTSCC  (NPAIR, NG) = 0
C
C              INITIALIZE GRID-SCALE PRECIPITATION COUNTERS.
      KGSP2  = 0
      KKGSP2  (NPAIR, NG) = 0
      NPTSGSP (NPAIR, NG) = 0
C
CFPP$ NOCONCUR L
      DO 503 K=1, KM
         NPTSSAT (K, NPAIR, NG) = 0
  503 CONTINUE
C
C              INITIALIZE GRID-SCALE EVAPORATION COUNTERS.
      KGSE2  = 0
      KKGSE2  (NPAIR, NG) = 0
C
C              THE H (N+1), HTH (N+1), HQ (N+1), AND DHQ FIELDS
C              ARE COMPRESSED SO THAT ONLY FORECAST POINTS ARE USED
C              IN THE CUMULUS CONVECTION AND THE GRID-SCALE
C              PRECIPITATION CALCULATIONS.
C
C              COMPRESS THE H (N+1) FIELD.
      JADDH = MADDH - 1 + LADD3
      IN = 1
CFPP$ NOCONCUR L
      DO 10001 IQ2 = 1, LEN31
         IF ( BITGRDH(IQ2+LADD3-1,NPAIR,NG) ) THEN
            H1(IN) = VBL(IQ2+JADDH-1)
            IN = IN + 1
         END IF
10001 CONTINUE
C
C              COMPRESS THE 3-DIMENSIONAL FIELDS OF HTH (N+1),
C              HQ (N+1), AND DHQ.
      JADD  = LADD3 - IJ
      JADDT = MADDT - 1 + LADD3 - IJ
      JADDQ = MADDQ - 1 + LADD3 - IJ
      JADD1 = 1 - NUMCOL1
C
      DO 510 K=1,LEVSPPT
         JADD  = JADD  + IJ
         JADDT = JADDT + IJ
         JADDQ = JADDQ + IJ
         JADD1 = JADD1 + NUMCOL1
         IN = 1
CFPP$ NOCONCUR L
         DO 10002 IQ2 = 1, LEN31
            IF ( BITGRDH(IQ2+LADD3-1,NPAIR,NG) ) THEN
               T1(IN+JADD1-1)     = VBL(IQ2+JADDT-1)
               Q1(IN+JADD1-1)     = VBL(IQ2+JADDQ-1)
               DHQ1(IN+JADD1-1)   = DHQ(IQ2+JADD -1)
               IN = IN + 1
            END IF
10002    CONTINUE
  510 CONTINUE
C
C              CALCULATE THE TEMPERATURE AND
C              SPECIFIC HUMIDITY.  THEY ARE NEEDED IN
C              BOTH THE CALCULATION OF CUMULUS CONVECTION AND
C              GRID-SCALE PRECIPITATION.
C
C*****  Code Expanded From Routine:  P2KAP
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, H1, HKAPPA1) PRIVATE(I)
      DO 77001 I = 1, NUMCOL1
         HKAPPA1(I) = (0.34757549+H1(I)*(4.36732956+H1(I)*3.91557032))/(
     1      1.+H1(I)*(5.44053037+H1(I)*(2.27693825+H1(I)*(-0.0869930591)
     2      )))
77001 CONTINUE
C*****  End of Code Expanded From Routine:  P2KAP
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, H1, HINV1, HKAPPA1, HKAPHI1)
CMIC$1    PRIVATE(IQ2)
      DO 88890 IQ2=1,NUMCOL1
         HINV1(IQ2)=1.0E0/H1(IQ2)
         HKAPHI1(IQ2)=HKAPPA1(IQ2)*HINV1(IQ2)
88890 CONTINUE
C
C              FINALLY, WE CAN GET THE TEMPERATURE
C              AND SPECIFIC HUMIDITY.
      JADD1 = 1 - NUMCOL1
C
CMIC$ DO ALL SHARED(LEVSPPT, JADD1, NUMCOL1, PR, T1, HKAPHI1, Q1, HINV1)
CMIC$1    PRIVATE(K, C1, IQ2)
      DO 800 K = 1, LEVSPPT
         C1 = 2.0E0*PR(K)
         DO 77002 IQ2 = 1, NUMCOL1
            T1(JADD1+IQ2-1+K*NUMCOL1) = T1(JADD1+IQ2-1+K*NUMCOL1)*
     1         HKAPHI1(IQ2)*C1
            Q1(JADD1+IQ2-1+K*NUMCOL1) = Q1(JADD1+IQ2-1+K*NUMCOL1)*HINV1(
     1         IQ2)
77002    CONTINUE
  800 CONTINUE
C
C
C              SKIP THE CUMULUS CONVECTION, IF SO SPECIFIED.
      IF ((KUMULUS .NE. 1) .OR. (LEVSCC . LE. 0)) GO TO 2250
C
C              NOW CONSIDER THE MOISTURE FLUX CONVERGENCE TEST.
      IF (LEVSDHQ .LE. 0) THEN
C
C              BECAUSE SIGMADHQ (THE SIGMA LEVEL BELOW WHICH THE
C              INTEGRATED MOISTURE FLUX CONVERGENCE MUST EXCEED
C              SOME THRESHOLD IN ORDER TO HAVE CUMULUS
C              CONVECTION) IS SPECIFIED TO BE THE GROUND LEVEL,
C              ALL POINTS ARE ALLOWED TO PASS THIS TEST.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, BITSDHQ) PRIVATE(IQ2)
         DO 90002 IQ2 = 1, NUMCOL1
            BITSDHQ(IQ2) = .TRUE.
90002    CONTINUE
C
      ELSE
C
C              CALCULATE THE INTEGRATED MOISTURE FLUX CONVERGENCE FROM
C              LAYER 1 THROUGH LAYER LEVSDHQ.
C              THE DIVISION BY 100. CONVERTS FROM CENTIBARS TO BARS.
         C2 = DTIME * CRITCONV / 100.0E0
         JADD1 = 1
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL1,C2,DHQ1,DELSIG,DHQSUM1)PRIVATE(IQ2)
      DO 88940 IQ2 = 1, NUMCOL1
         DHQSUM1(IQ2) = DHQ1(IQ2)*DELSIG(1) - C2
88940 CONTINUE
C
         IF (LEVSDHQ .GE. 2)  THEN
C
            DO 810 K=2,LEVSDHQ
               JADD1 = JADD1 + NUMCOL1
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, JADD1, K, DHQSUM1, DHQ1, DELSIG)
CMIC$1    PRIVATE(IQ2)
      DO 88950 IQ2=1,NUMCOL1
       DHQSUM1(IQ2)=DHQSUM1(IQ2)+DHQ1(JADD1+IQ2-1)*DELSIG(K)
88950 CONTINUE
  810       CONTINUE
C
         END IF
C
C              CREATE THE BIT VECTOR OF POINTS HAVING SUFFICIENT
C              MOISTURE FLUX CONVERGENCE TO WARRANT FURTHER
C              CUMULUS CONVECTION CALCULATION.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, DHQSUM1, BITSDHQ) PRIVATE(IQ2)
      DO 88960 IQ2=1,NUMCOL1
         BITSDHQ(IQ2)=DHQSUM1(IQ2).GT.0.0E0
88960 CONTINUE
C
      END IF
C
      NUMCOL2 = 0
      DO 6542 IQ2 = 1, NUMCOL1
         IF ( BITSDHQ(IQ2) ) NUMCOL2 = NUMCOL2 + 1
6542  CONTINUE
      NPTSDHQ(NPAIR, NG) = NUMCOL2
C
C              SKIP THE CUMULUS CONVECTION IF THERE ARE NO POINTS
C              WITH SUFFICIENT MOISTURE FLUX CONVERGENCE.
      IF (NUMCOL2 .LE. 0) GO TO 2250
C
C              ONLY THE POINTS WITH SUFFICIENT MOISTURE FLUX
C              CONVERGENCE ARE CONSIDERED FOR CUMULUS CONVECTION.
C
C              THE COMPRESS/DECOMPRESS METHOD IS USED TO COLLECT
C              THE POINTS PASSING THE CONVERGENCE CRITERION.
C
C              COMPRESS THE H (N+1) FIELD AND H TO THE KAPPA POWER.
      IN = 1
CFPP$ NOCONCUR L
      DO 10003 IQ2 = 1, NUMCOL1
         IF ( BITSDHQ(IQ2)) THEN
            H2(IN) = H1(IQ2)
            HKAPPA2(IN) = HKAPPA1(IQ2)
            IN = IN + 1
         END IF
10003 CONTINUE
C
C              COMPRESS THE 3-DIMENSIONAL FIELDS.
      JADD1 = 1 - NUMCOL1
      JADD2 = 1 - NUMCOL2
C
      DO 900 K=1,LEVSCC
C
      JADD1 = JADD1 + NUMCOL1
      JADD2 = JADD2 + NUMCOL2
C
C              FIRST COMPRESS THE TEMPERATURE AT TIME STEP N+1.
      IN = 1
CFPP$ NOCONCUR L
      DO 10004 IQ2 = 1, NUMCOL1
         IF ( BITSDHQ(IQ2)) THEN
            T2(IN+JADD2-1) = T1(IQ2+JADD1-1)
            Q2(IN+JADD2-1) = Q1(IQ2+JADD1-1)
            IN = IN + 1
         END IF
10004 CONTINUE
C
C              NOTE THAT DHQ1 AND DQ1 OCCUPY
C              THE SAME SPACE.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1,JADD1,DHQ1,HINV1,DQ1)PRIVATE(IQ2)
      DO 88970 IQ2=1,NUMCOL1
         DQ1(JADD1+IQ2-1)=DHQ1(JADD1+IQ2-1)*HINV1(IQ2)
88970 CONTINUE
C
C              FINALLY COMPRESS THE MOISTURE CONVERGENCE.
C              NOTE THAT DQ1 AND DQ2 ARE EQUIVALENCED.
      IN = 1
CFPP$ NOCONCUR L
CDIR$ IVDEP
      DO 10005 IQ2 = 1, NUMCOL1
         IF ( BITSDHQ(IQ2)) THEN
            DQ2(IN+JADD2-1) = DQ1(IQ2+JADD1-1)
            IN = IN + 1
         END IF
10005 CONTINUE
C
  900 CONTINUE
C
C              NOW CALCULATE THE CLOUD TEMPERATURE AND THE SPECIFIC
C              HUMIDITY.
      CALL MSTADB (H2, HKAPPA2, T2, Q2, SATDEL, NUMCOL2, LEVSCC, PRESS,
     1             PR, KLIFT1, KLIFT2, SCRATCHA, SCRATCHB, SCRATCHC,
     2             SCRATCHD, TCLOUD2, QCLOUD2, KCLOUD)
C
C               DETERMINE THE BOTTOM LAYER WHERE THE CLOUD
C               TEMPERATURE IS GREATER THAN THE AMBIENT TEMPERATURE
C               AT ANY POINT.
C
      DO 1200 K=LEVSCC,1,-1
         IF (KCLOUD(K) .GT. 0) THEN
            KCLOUD2 = K
            GO TO 1400
         END IF
 1200 CONTINUE
C
C              NO BUOYANT CLOUD SOUNDINGS WERE FOUND.  TERMINATE
C              THE CUMULUS CALCULATIONS FOR THIS GRID ON THIS
C              TIME STEP.
      GO TO 2250
C
 1400 CONTINUE
C
      KKCLOUD2(NPAIR, NG) = KCLOUD2
C
C              CALCULATE THE DIFFERENCE BETWEEN CLOUD AND
C              ENVIRONMENTAL TEMPERATURE AND BETWEEN CLOUD AND
C              ENVIRONMENTAL SPECIFIC HUMIDITY.
C              WHERE THE DIFFERENCE IS NEGATIVE, IT IS SET TO ZERO.
C              SPECIFY IN A BIT ARRAY THE BUOYANT LAYERS FOR EACH
C              SOUNDING.
      JADD2 = 1 - NUMCOL2
C
CMIC$ DO ALL SHARED(KCLOUD2, JADD2, NUMCOL2, TCLOUD2, T2, TDIFF2, 
CMIC$1   QCLOUD2, Q2, QDIFF2, BITSCLD) PRIVATE(K, IQ2)
      DO 1500 K = 1, KCLOUD2
C
         DO 77003 IQ2 = 1, NUMCOL2
            TDIFF2(JADD2+IQ2-1+K*NUMCOL2) = TCLOUD2(JADD2+IQ2-1+K*
     1         NUMCOL2) - T2(JADD2+IQ2-1+K*NUMCOL2)
            QDIFF2(JADD2+IQ2-1+K*NUMCOL2) = QCLOUD2(JADD2+IQ2-1+K*
     1         NUMCOL2) - Q2(JADD2+IQ2-1+K*NUMCOL2)
            BITSCLD(JADD2+IQ2-1+K*NUMCOL2) = TDIFF2(JADD2+IQ2-1+K*
     1         NUMCOL2) .GT. 0.0E0
C              ZERO OUT TDIFF AND QDIFF WHERE EITHER IS NEGATIVE.
            TDIFF2(JADD2+IQ2-1+K*NUMCOL2) = AMAX1(TDIFF2(JADD2+IQ2-1+K*
     1         NUMCOL2),0.0E0)
            QDIFF2(JADD2+IQ2-1+K*NUMCOL2) = AMAX1(QDIFF2(JADD2+IQ2-1+K*
     1         NUMCOL2),0.0E0)
77003    CONTINUE
 1500 CONTINUE
C
C
C              BECAUSE THE MOISTURE FLUX CONVERGENCE IS CALCULATED
C              FROM LAYER 1 UP TO THE LAST BUOYANT LAYER OF THE
C              CLOUD, WE MUST ALTER BITSCLD (WHICH WILL BE USED
C              TO CONTROL THE CALCULATION OF THIS CONVERGENCE)
C              FOR ANY NONBUOYANT LAYERS BELOW CLOUD TOP.
C              IF KCLOUD2 IS 1, THIS STEP IS UNNECESSARY.
      IF (KCLOUD2 .GT. 1) THEN
         JADD2 = KCLOUD2 * NUMCOL2 + 1
         JKM1ADD2 = JADD2 - NUMCOL2
C
         DO 1600 K=KCLOUD2, 2, -1
C
            JADD2    = JADD2    - NUMCOL2
            JKM1ADD2 = JKM1ADD2 - NUMCOL2
C
CMIC$ DO ALL VECTOR IF (ABS(JADD2-JKM1ADD2).GE.NUMCOL2 .OR. JADD2-
CMIC$1   JKM1ADD2.EQ.0) SHARED(NUMCOL2, JADD2, JKM1ADD2, BITSCLD)
CMIC$2    PRIVATE(IQ2)
      DO 89030 IQ2=1,NUMCOL2
         BITSCLD(JKM1ADD2+IQ2-1)=BITSCLD(JADD2+IQ2-1).OR.BITSCLD
     *   (JKM1ADD2+IQ2-1)
89030 CONTINUE
C
 1600    CONTINUE
C
      END IF
C
      NUMBUOY = 0
      DO 30001 IQ2 = 1, NUMCOL2
         IF ( BITSCLD(IQ2) ) NUMBUOY = NUMBUOY + 1
30001 CONTINUE
      NPTSBUOY(NPAIR, NG) = NUMBUOY
C
C              NOW ZERO OUT THE FALLING CONVECTIVE PRECIPITATION.
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, FALL2, WATER2) PRIVATE(IQ2)
      DO 89040 IQ2 = 1, NUMCOL2
         FALL2(IQ2) = 0.0E0
         WATER2(IQ2) = 0.0E0
89040 CONTINUE
      JADD2 = 1 - NUMCOL2
C
      DO 1800 K=1,KCLOUD2
         JADD2 = JADD2 + NUMCOL2
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, JADD2, K, BITSCLD, WATER2, DELSIG, 
CMIC$1   DQ2) PRIVATE(IQ2)
      DO 89060 IQ2=1,NUMCOL2
       IF ( BITSCLD(JADD2+IQ2-1)) THEN
         WATER2(IQ2)=WATER2(IQ2)+DELSIG(K)*DQ2(JADD2+IQ2-1)
       END IF
89060 CONTINUE
 1800 CONTINUE
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, WATER2, BITWATER) PRIVATE(IQ2)
      DO 89070 IQ2=1,NUMCOL2
         BITWATER(IQ2)=WATER2(IQ2).GT.0.0E0
89070 CONTINUE
      NUMWATER = 0
      DO 30002 IQ2 = 1, NUMCOL2
         IF ( BITWATER(IQ2) ) NUMWATER = NUMWATER + 1
30002 CONTINUE
      NPTSWATR(NPAIR, NG) = NUMWATER
C
C              CALCULATE HOW TO PARTITION THE CUMULUS HEATING AND
C              MOISTENING , THEN CALCULATE THE NEW TEMPERATURE
C              AND SPECIFIC HUMIDITY.
      JADD2 = 1
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, BITWATER, TDIFF2, DELSIG, A2, QDIFF2
CMIC$1   , B2) PRIVATE(IQ2)
      DO 89080 IQ2 = 1, NUMCOL2
         IF (BITWATER(IQ2)) THEN
            A2(IQ2) = TDIFF2(IQ2)*DELSIG(1)
            B2(IQ2) = QDIFF2(IQ2)*DELSIG(1)
         ENDIF
89080 CONTINUE
C
C              IF THERE IS ONLY ONE LAYER, WE ARE DONE WITH
C              THE SUMMATION.
      IF (KCLOUD2 .GE. 2) THEN
C
         DO 2000 K=2, KCLOUD2
C
            JADD2 = JADD2 + NUMCOL2
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, JADD2, K, BITWATER, A2, TDIFF2, 
CMIC$1   DELSIG, B2, QDIFF2) PRIVATE(IQ2)
      DO 89100 IQ2=1,NUMCOL2
       IF ( BITWATER(IQ2) ) THEN
         A2(IQ2)=A2(IQ2)+TDIFF2(JADD2+IQ2-1)*DELSIG(K)
         B2(IQ2)=B2(IQ2)+QDIFF2(JADD2+IQ2-1)*DELSIG(K)
       END IF
89100 CONTINUE
 2000    CONTINUE
C
      END IF
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, CPOVEL, BITWATER, A2, B2, DENOM2, 
CMIC$1   BITCC) PRIVATE(IQ2)
      DO 89120 IQ2 = 1, NUMCOL2
         IF (BITWATER(IQ2)) DENOM2(IQ2) = CPOVEL*A2(IQ2) + B2(IQ2)
         BITCC(IQ2) = BITWATER(IQ2) .AND. DENOM2(IQ2).GT.0.0E0
89120 CONTINUE
C               FOR DIAGNOSTIC PURPOSES ONLY AT THE THE CURRENT
C               TIME, COUNT AND STORE THE NUMBER OF POINTS WITH
C               KUO CONVECTION.
      NUMXX = 0
      DO 30007 IQ2 = 1, NUMCOL2
         IF ( BITCC(IQ2) ) NUMXX = NUMXX + 1
30007 CONTINUE
      NPTSCC(NPAIR,NG) = NUMXX
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, BPARAM, CPOVEL, BITCC, WATER2, 
CMIC$1   DENOM2, QEFF2, ABDESC, ATDESC, AQDESC, A2, B2) PRIVATE(IQ2)
      DO 89140 IQ2 = 1, NUMCOL2
         IF (BITCC(IQ2)) QEFF2(IQ2) = WATER2(IQ2)/DENOM2(IQ2)
         ABDESC(IQ2) = BPARAM
         ATDESC(IQ2) = 0.E0
         AQDESC(IQ2) = 0.E0
         IF (BITCC(IQ2) .AND. A2(IQ2).GT.1.E-20) ATDESC(IQ2) = ((1.E0-
     1      ABDESC(IQ2))*WATER2(IQ2))/(CPOVEL*A2(IQ2))
         IF (ATDESC(IQ2).GT.1.E0 .AND. BITCC(IQ2)) THEN
            ATDESC(IQ2) = 1.E0
            ABDESC(IQ2) = (WATER2(IQ2)-CPOVEL*A2(IQ2))/WATER2(IQ2)
         ENDIF
         IF (BITCC(IQ2) .AND. B2(IQ2).GT.1.E-20) AQDESC(IQ2) = ABDESC(
     1      IQ2)*WATER2(IQ2)/B2(IQ2)
89140 CONTINUE
C
C      END OF COMPUTATION OF CONVECTIVE HEATING AND MOISTENING
C      COEFFICIENTS
C
      JADD2 = KCLOUD2 * NUMCOL2 + 1
      DO 2100 K=KCLOUD2, 1, -1
C
         JADD2 = JADD2 - NUMCOL2
C
         C3 = DELSIG(K) * CPOVEL
C
C              CREATE THE BIT VECTOR OF POINTS (FOR THIS LAYER)
C              AT OR BELOW CLOUD TOP THAT ARE IN A CONVECTIVE
C              COLUMN.
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, JADD2, C3, BITCC, BITSCLD, BITCONV, 
CMIC$1   TDIFF2, ATDESC, TCHANGE2, T2, QDIFF2, AQDESC, QCHANGE2, Q2, DQ2
CMIC$2   , FALL2) PRIVATE(IQ2)
      DO 89220 IQ2 = 1, NUMCOL2
         BITCONV(IQ2) = BITCC(IQ2) .AND. BITSCLD(JADD2+IQ2-1)
         IF (BITCONV(IQ2)) THEN
            TCHANGE2(IQ2) = TDIFF2(JADD2+IQ2-1)*ATDESC(IQ2)
            T2(JADD2+IQ2-1) = T2(JADD2+IQ2-1) + TCHANGE2(IQ2)
            QCHANGE2(IQ2) = QDIFF2(JADD2+IQ2-1)*AQDESC(IQ2)
            Q2(JADD2+IQ2-1) = Q2(JADD2+IQ2-1) + QCHANGE2(IQ2)
            Q2(JADD2+IQ2-1) = Q2(JADD2+IQ2-1) - DQ2(JADD2+IQ2-1)
            FALL2(IQ2) = FALL2(IQ2) + C3*TCHANGE2(IQ2)
         ENDIF
89220 CONTINUE
C
C
C              NOW WE INCLUDE THE EFFECTS OF EVAPORATING
C              CONVECTIVE PRECIPITATION.
C              FIRST CALCULATE THE SATURATION VAPOR PRESSURE.
         CALL VAPTAB (T2(JADD2), ESAT2, NUMCOL2, 1)
C
         C4 = 100.0E0 * PRESS(K)
         C5 = ELOVCP * ELOVRV
         C6 = 1.0E0 / DELSIG(K)
 
C
CMIC$ PARALLEL SHARED(NUMCOL2, JADD2, ELOVCP, BITEVAP, T2, EVAP2, Q2, C6
CMIC$1   , K, BITCONV, QDEF2, FALL2, FALLSIG2, BITEALL, DELSIG, C4, 
CMIC$2   ONEMEPS, RHKUOEPS, C5, H2, PRSTOT2, ESAT2, PRSDEN2, QSAT2, 
CMIC$3   COEF2, DENOM2) PRIVATE(IQ2)
CMIC$ DO PARALLEL VECTOR
      DO 89290 IQ2=1,NUMCOL2
       IF ( BITCONV(IQ2) ) THEN
         PRSTOT2(IQ2)=C4*H2(IQ2)
         PRSDEN2(IQ2)=PRSTOT2(IQ2)-ONEMEPS*ESAT2(IQ2)
C
         QSAT2(IQ2)=RHKUOEPS*ESAT2(IQ2)
         QSAT2(IQ2)=QSAT2(IQ2)/PRSDEN2(IQ2)
C
C              INCLUDE THE ISOBARIC ADJUSTMENT EFFECT.
         COEF2(IQ2)=T2(JADD2+IQ2-1)*T2(JADD2+IQ2-1)
         COEF2(IQ2)=COEF2(IQ2)*PRSDEN2(IQ2)
         DENOM2(IQ2)=PRSTOT2(IQ2)*QSAT2(IQ2)
         DENOM2(IQ2)=DENOM2(IQ2)*C5+COEF2(IQ2)
C
C              THE COEFFICIENT IS CALCULATED BEFORE MULTIPLICATION
C              BY QDEF2 TO REDUCE MACHINE PRECISION ERROR.
         COEF2(IQ2)=COEF2(IQ2)/DENOM2(IQ2)
         QDEF2(IQ2)=QSAT2(IQ2)-Q2(JADD2+IQ2-1)
         QDEF2(IQ2)=QDEF2(IQ2)*COEF2(IQ2)
       END IF
89290 CONTINUE
C
C              CREATE THE BIT VECTOR OF POINTS (FOR THIS LAYER)
C              AT OR BELOW CLOUD TOP THAT ARE IN A CONVECTIVE
C              COLUMN, ARE DRY ENOUGH FOR POSSIBLE
C              EVAPORATION OF FALLING PRECIPITATION, AND
C              HAVE PRECIPITATION AVAILABLE TO EVAPORATE.
CMIC$ DO PARALLEL VECTOR
      DO 89400 IQ2 = 1, NUMCOL2
         BITEVAP(IQ2) = BITCONV(IQ2) .AND. QDEF2(IQ2).GE.0.0E0 .AND. 
     1      FALL2(IQ2).GE.0.0E0
         FALLSIG2(IQ2) = C6*FALL2(IQ2)
         BITEALL(IQ2) = QDEF2(IQ2) .GE. FALLSIG2(IQ2)
         IF (BITEVAP(IQ2) .AND. BITEALL(IQ2)) THEN
            EVAP2(IQ2) = FALLSIG2(IQ2)
            FALL2(IQ2) = 0.0E0
         ENDIF
         IF (BITEVAP(IQ2) .AND.  .NOT.BITEALL(IQ2)) THEN
            EVAP2(IQ2) = QDEF2(IQ2)
            FALL2(IQ2) = FALL2(IQ2) - EVAP2(IQ2)*DELSIG(K)
         ENDIF
89400 CONTINUE
C
CMIC$ DO PARALLEL VECTOR
      DO 89470 IQ2=1,NUMCOL2
       IF ( BITEVAP(IQ2)  ) THEN
         T2(JADD2+IQ2-1)=T2(JADD2+IQ2-1)-EVAP2(IQ2)*ELOVCP
         Q2(JADD2+IQ2-1)=Q2(JADD2+IQ2-1)+EVAP2(IQ2)
       END IF
89470 CONTINUE
CMIC$ END PARALLEL
C
 2100 CONTINUE
C
C              CALCULATE THE CONVECTIVE PRECIPITATION REACHING THE
C              GROUND. (MULTIPLY BY CBOVG LATER.)
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, BITCC, FALL2, H2) PRIVATE(IQ2)
      DO 89490 IQ2=1,NUMCOL2
       IF ( BITCC(IQ2) ) THEN
         FALL2(IQ2)=FALL2(IQ2)*H2(IQ2)
       END IF
89490 CONTINUE
C
C              NOW WE CAN DECOMPRESS THE TEMPERATURE AND SPECIFIC
C              HUMIDITY.
      JADD2 = 1 - NUMCOL2
      JADD1 = 1 - NUMCOL1
C
      DO 2200 K=1,KCLOUD2
         JADD2 = JADD2 + NUMCOL2
         JADD1 = JADD1 + NUMCOL1
C
         IN = 1
CFPP$ NOCONCUR L
         DO 6901 IQ2 = 1, NUMCOL1
            IF ( BITSDHQ(IQ2) ) THEN
               T1(IQ2+JADD1-1) = T2(IN+JADD2-1)
               Q1(IQ2+JADD1-1) = Q2(IN+JADD2-1)
               IN = IN + 1
            END IF
6901     CONTINUE
C
 2200 CONTINUE
C
C              FOR DIAGNOSTIC PURPOSES ONLY, WE CALCULATE
C              THE SUM OF THE CONVECTIVE WATER FALLING TO THE
C              GROUND FOR THIS TIME STEP AND GRID, IN METERS
C              (NOT NORMALIZED BY MAP FACTOR.)
      SF = 0.0E0
      DO 5421 IQ2 = 1, NUMCOL2
         SF = SF + FALL2(IQ2)
5421  CONTINUE
      PPTCC(NPAIR, NG) = CBOVG * SF
C
C              THE CONVECTIVE PRECIPITATION FOR THIS TIME STEP IS
C              ADDED TO THAT WHICH HAS OCCURRED SINCE THE
C              PRECIPITATION BUCKETS WERE LAST EMPTIED.
C
C              FIRST WE MUST EXPAND THE PRECIPITATION TO THE
C              FORECAST POINTS AND FILL IN ZEROES AT THE FORECAST
C              POINTS NOT HAVING CONVECTIVE PRECIPITATION.
      IN = 1
CFPP$ NOCONCUR L
      DO 6010 IQ2 = 1, NUMCOL1
         IF ( BITSDHQ(IQ2) ) THEN
            FALL1(IQ2) = FALL2(IN)
            IN = IN + 1
         ELSE
            FALL1(IQ2) = 0.0E0
         END IF
6010  CONTINUE
C
C              NOW WE EXPAND TO ALL POINTS FROM ROWS J = J3 TO JM1
C              AND FILL IN ZEROES AT THE NONFORECAST POINTS.
      IN = 1
CFPP$ NOCONCUR L
      DO 6011 IQ2 = 1, LEN31
         IF ( BITGRDH(IQ2+LADD3-1,NPAIR,NG) ) THEN
            FALL(IQ2+LADD3-1) = FALL1(IN)
            IN = IN + 1
         ELSE
            FALL(IQ2+LADD3-1) = 0.0E0
         END IF
6011  CONTINUE
C
C              FINALLY, WE ADD THE PRECIPITATION FOR THIS STEP TO
C              THE ACCUMULATION.
      JADD = MADDCC - 1 + LADD3
CMIC$ DO ALL VECTOR SHARED(LEN31, JADD, CBOVG, LADD3, VBL, FALL)
CMIC$1    PRIVATE(IQ2)
      DO 89500 IQ2=1,LEN31
      VBL(JADD+IQ2-1)=VBL(JADD+IQ2-1)+CBOVG*FALL(LADD3+IQ2-1)
89500 CONTINUE
C
C
C              END OF THE CONVECTIVE PRECIPITATION, EXCEPT FOR
C              THE CALCULATION AND DECOMPRESSION OF HTH AND HQ.
C
 2250 CONTINUE
C              SKIP THE GRID-SCALE PRECIPITATION IF SO SPECIFIED.
      IF ((LGRIDPPT .NE. 1) .OR. (LEVSGSP .LE. 0)) GO TO 2350
C
C
C              BEGIN THE GRID-SCALE PRECIPITATION CALCULATIONS.
C
C              ZERO THE PRECIPITATION FOR THIS GRID AND STEP.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, FALL1) PRIVATE(IQ2)
      DO 89510 IQ2=1,NUMCOL1
         FALL1(IQ2)=0.0E0
89510 CONTINUE
C
      JADD1 = LEVSGSP * NUMCOL1 + 1
C
      DO 2300 K=LEVSGSP, 1, -1
         JADD1 = JADD1 - NUMCOL1
C
C              BOUND THE SPECIFIC HUMIDITY AWAY FROM ZERO.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, JADD1, QBOUND, Q1) PRIVATE(IQ2)
      DO 89520 IQ2=1,NUMCOL1
         Q1(JADD1+IQ2-1) = AMAX1(Q1(JADD1+IQ2-1), QBOUND)
89520 CONTINUE
C
C              CALCULATE THE SATURATION VAPOR PRESSURE FROM THE
C              TEMPERATURE.
         CALL VAPTAB (T1(JADD1), ESAT1, NUMCOL1, 1)
C
         C7 = 100.0E0 * PRESS(K)
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, C7, ONEMEPS, EPS, JADD1, SATDELP1, 
CMIC$1   H1, PRSTOT1, ESAT1, PRSDEN1, PRSEPSI1, QSAT1, Q1, SATDELF1, 
CMIC$2   BITSSAT) PRIVATE(IQ2)
      DO 89530 IQ2 = 1, NUMCOL1
         PRSTOT1(IQ2) = C7*H1(IQ2)
         PRSDEN1(IQ2) = PRSTOT1(IQ2) - ONEMEPS*ESAT1(IQ2)
         PRSEPSI1(IQ2) = EPS/PRSDEN1(IQ2)
         QSAT1(IQ2) = ESAT1(IQ2)*PRSEPSI1(IQ2)
         SATDELF1(IQ2) = Q1(JADD1+IQ2-1)*SATDELP1 - QSAT1(IQ2)
         SATDELF1(IQ2) = SATDELF1(IQ2)*0.5E0
         SATDELF1(IQ2) = SATDELF1(IQ2)/Q1(JADD1+IQ2-1)
         BITSSAT(IQ2) = SATDELF1(IQ2) .GT. 0.0E0
89530 CONTINUE
         NUMCOL2 = 0
         DO 9010 IQ2 = 1, NUMCOL1
            IF ( BITSSAT(IQ2) ) NUMCOL2 = NUMCOL2 + 1
9010     CONTINUE
         NPTSSAT(K, NPAIR, NG) = NUMCOL2
C
C              IF THERE ARE NO SATURATED POINTS FOR THIS LEVEL,
C              SKIP TO THE CALCULATIONS OF THE EVAPORATION OF
C              FALLING GRID-SCALE PRECIPITATION.
         IF (NUMCOL2 .LE. 0) GO TO 2280
C
C              KEEP TRACK OF THE HIGHEST LAYER IN WHICH GRID-SCALE
C              SATURATION OCCURS.
         KGSP2 = MAX (KGSP2, K)
         KKGSP2(NPAIR, NG) = KGSP2
C
C              BEFORE THE COMPRESS ONE PRELIMINARY
C              CALCULATION IS PERFORMED.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, PRSTOT1, PRSEPSI1, PRATEPS1)
CMIC$1    PRIVATE(IQ2)
      DO 89610 IQ2=1,NUMCOL1
         PRATEPS1(IQ2)=PRSTOT1(IQ2)*PRSEPSI1(IQ2)
89610 CONTINUE
C
C              THE TEMPERATURE, SPECIFIC HUMIDITY, SATURATED SPECIFIC
C              HUMIDITY, AND QUANTITIES STORED IN PRATEPS1 AND SATDELF1
C              MUST BE COMPRESSED.
C              NOTE THAT T2 AND Q2 ARE 3-DIMENSIONAL ARRAYS BECAUSE
C              OF THE CUMULUS CONVECTION ALGORITHM, BUT HERE IN
C              THE GRID-SCALE PRECIPITATION CODE ONLY 2-DIMENSIONAL
C              ARRAYS FOR TEMPERATURE AND SPECIFIC HUMIDITY
C              ARE NEEDED AND USED.
         IN = 1
CFPP$ NOCONCUR L
         DO 7801 IQ2 = 1, NUMCOL1
            IF ( BITSSAT(IQ2) ) THEN
               T2(IN)       = T1(IQ2+JADD1-1)
               Q2(IN)       = Q1(IQ2+JADD1-1)
               QSAT2(IN)    = QSAT1(IQ2)
               PRATEPS2(IN)  = PRATEPS1(IQ2)
               SATDELF2(IN)  = SATDELF1(IQ2)
               IN = IN + 1
            END IF
7801     CONTINUE
C
C              CALCULATE THE VALUES FOR ALPHA AND BETA IN THE
C              GRID-SCALE PRECIPITATION.
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, ELOVRD, CPOVEL, SATDEL, SATDELI, 
CMIC$1   PRATEPS2, T2, QSAT2, BETA2, SATDELF2, Q2, TCHANGE2) PRIVATE(IQ2
CMIC$2   , ALPHA2S)
      DO 89630 IQ2 = 1, NUMCOL2
         ALPHA2S = PRATEPS2(IQ2)*ELOVRD/(T2(IQ2)*T2(IQ2))
         BETA2(IQ2) = 1.0/(CPOVEL+ALPHA2S*QSAT2(IQ2))
         IF (SATDELF2(IQ2) .GE. SATDEL) THEN
            TCHANGE2(IQ2) = Q2(IQ2) - QSAT2(IQ2)
C
         ELSE
C              PARTIAL SATURATION.
            TCHANGE2(IQ2) = Q2(IQ2)*SATDELF2(IQ2)*SATDELF2(IQ2)*SATDELI
         ENDIF
89630 CONTINUE
C
         C8 = CPOVEL * DELSIG(K)
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, CPOVEL, C8, TCHANGE2, BETA2, T2, Q2
CMIC$1   , FALL2) PRIVATE(IQ2)
      DO 89710 IQ2=1,NUMCOL2
         TCHANGE2(IQ2)=TCHANGE2(IQ2)*BETA2(IQ2)
C
C               NOW CALCULATE THE CHANGE IN SPECIFIC HUMIDITY AND
C               THE GRID-SCALE PRECIPITATION FOR THIS LAYER FOR
C               THIS TIME STEP.
         T2(IQ2)=T2(IQ2)+TCHANGE2(IQ2)
         Q2(IQ2)=Q2(IQ2)-CPOVEL*TCHANGE2(IQ2)
         FALL2(IQ2)=C8*TCHANGE2(IQ2)
89710 CONTINUE
C
C              NOW WE MUST DECOMPRESS TEMPERATURE AND SPECIFIC HUMIDITY.
         IN = 1
CFPP$ NOCONCUR L
         DO 7901 IQ2 = 1, NUMCOL1
            IF ( BITSSAT(IQ2) ) THEN
               T1(IQ2+JADD1-1) = T2(IN)
               Q1(IQ2+JADD1-1) = Q2(IN)
               IN = IN + 1
            END IF
7901     CONTINUE
C
C              EXPAND THE GRID-SCALE PRECIPITATION FOR THIS LEVEL AND
C              TIME STEP TO THE FORECAST POINTS AND FILL IN ZEROES AT
C              THE FORECAST POINTS NOT HAVING SATURATION.
         IN = 1
CFPP$ NOCONCUR L
         DO 8901 IQ2 = 1, NUMCOL1
            IF ( BITSSAT(IQ2) ) THEN
               FALL1K(IQ2) = FALL2(IN)
               IN = IN + 1
            ELSE
               FALL1K(IQ2) = 0.0E0
            END IF
8901     CONTINUE
C
C              ACCUMULATE THE GRID-SCALE PRECIPITATION FOR THIS GRID
C              AND STEP.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, FALL1, FALL1K) PRIVATE(IQ2)
      DO 89750 IQ2=1,NUMCOL1
         FALL1(IQ2)=FALL1(IQ2)+FALL1K(IQ2)
89750 CONTINUE
C
 2280 CONTINUE
C
C              CONSIDER THE EVAPORATION OF FALLING GRID-SCALE
C              PRECIPITATION.
C
C              FIRST CREATE THE BIT VECTOR OF FORECAST POINTS THAT
C              ARE SUBSATURATED WITH RESPECT TO THE CRITICAL
C              RELATIVE HUMIDITY FOR EVAPORATION AND THAT HAVE
C              SOME PRECIPITATION FALLING INTO THE LAYER.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, RHCRIT, JADD1, QSAT1, Q1, QDEF1, 
CMIC$1   FALL1, BITEVAP) PRIVATE(IQ2)
      DO 89760 IQ2=1,NUMCOL1
         QDEF1(IQ2)=QSAT1(IQ2)*RHCRIT-Q1(JADD1+IQ2-1)
         BITEVAP(IQ2)=((QDEF1(IQ2).GT.0.0E0).AND.(FALL1(IQ2).
     *   GT.0.0E0))
89760 CONTINUE
C
      NUMCOL2 = 0
      DO 4931 IQ2 = 1, NUMCOL1
         IF ( BITEVAP(IQ2) ) NUMCOL2 = NUMCOL2 + 1
4931  CONTINUE
      NPTSEVAP(K, NPAIR, NG) = NUMCOL2
C
      IF (NUMCOL2 . LE. 0 ) GO TO 2298
C
C              KEEP TRACK OF THE HIGHEST LAYER IN WHICH GRID-SCALE
C              EVAPORATION OCCURS.
      KGSE2 = MAX (KGSE2, K)
      KKGSE2(NPAIR, NG) = KGSE2
C
C              THE RELEVANT FIELDS ARE COMPRESSED TO REMOVE
C              POINTS HAVING NO EVAPORATION POTENTIAL.
C              NOTE THAT T2 AND Q2 ARE 3-DIMENSIONAL ARRAYS BECAUSE
C              OF THE CUMULUS CONVECTION ALGORITHM, BUT HERE IN
C              THE GRID-SCALE EVAPORATION CODE ONLY 2-DIMENSIONAL
C              ARRAYS FOR TEMPERATURE AND SPECIFIC HUMIDITY
C              ARE NEEDED AND USED.
         IN = 1
CFPP$ NOCONCUR L
         DO 7802 IQ2 = 1, NUMCOL1
            IF ( BITEVAP(IQ2) ) THEN
               T2(IN)        = T1(IQ2+JADD1-1)
               Q2(IN)        = Q1(IQ2+JADD1-1)
               QSAT2(IN)     = QSAT1(IQ2)
               PRATEPS2(IN)  = PRATEPS1(IQ2)
               FALL2(IN)     = FALL1(IQ2)
               IN = IN + 1
            END IF
7802     CONTINUE
C
C              CALCULATE THE AMOUNT OF SUBSATURATION (THE SPECIFIC
C              HUMIDITY DEFICIT.)
      C9  = (ELOVCP * ELOVRD) * RHCRIT
      C10 = 1.0E0 / DELSIG(K)
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL2, C9, RHCRIT, C10, K, ELOVCP, T2, 
CMIC$1   COEF2, PRATEPS2, QSAT2, DENOM2, Q2, QDEF2, FALL2, FALLSIG2, 
CMIC$2   BITEALL, EVAP2, DELSIG) PRIVATE(IQ2)
      DO 89780 IQ2 = 1, NUMCOL2
         COEF2(IQ2) = T2(IQ2)*T2(IQ2)
         DENOM2(IQ2) = PRATEPS2(IQ2)*QSAT2(IQ2)
         DENOM2(IQ2) = DENOM2(IQ2)*C9 + COEF2(IQ2)
         COEF2(IQ2) = COEF2(IQ2)/DENOM2(IQ2)
         QDEF2(IQ2) = QSAT2(IQ2)*RHCRIT - Q2(IQ2)
         QDEF2(IQ2) = QDEF2(IQ2)*COEF2(IQ2)
         FALLSIG2(IQ2) = C10*FALL2(IQ2)
         BITEALL(IQ2) = QDEF2(IQ2) .GE. FALLSIG2(IQ2)
         IF (BITEALL(IQ2)) THEN
            EVAP2(IQ2) = FALLSIG2(IQ2)
            FALL2(IQ2) = 0.0E0
C
         ELSE
C
            EVAP2(IQ2) = QDEF2(IQ2)
            FALL2(IQ2) = FALL2(IQ2) - EVAP2(IQ2)*DELSIG(K)
         ENDIF
         T2(IQ2) = T2(IQ2) - EVAP2(IQ2)*ELOVCP
         Q2(IQ2) = Q2(IQ2) + EVAP2(IQ2)
89780 CONTINUE
C
C              NOW WE MUST DECOMPRESS TEMPERATURE AND SPECIFIC HUMIDITY.
         IN = 1
CFPP$ NOCONCUR L
         DO 7902 IQ2 = 1, NUMCOL1
            IF ( BITEVAP(IQ2) ) THEN
               T1(IQ2+JADD1-1) = T2(IN)
               Q1(IQ2+JADD1-1) = Q2(IN)
               FALL1(IQ2)      = FALL2(IN)
               IN = IN + 1
            END IF
7902     CONTINUE
C
 2298 CONTINUE
C              THE END OF THE CALCULATION OF THE EVAPORATION OF
C              GRID-SCALE EVAPORATION HAS JUST BEEN PASSED.
C
C
 2300 CONTINUE
C              THE END OF THE DO LOOP ON LAYERS FOR THE GRID-SCALE
C              PRECIPITATION HAS JUST BEEN PASSED.
C
C              MULTIPLY BY THE SURFACE PRESSURE.  (MULTIPLY BY
C              CBOVG LATER.)
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, FALL1, H1, BITSGSP) PRIVATE(IQ2)
      DO 89920 IQ2 = 1, NUMCOL1
         FALL1(IQ2) = FALL1(IQ2)*H1(IQ2)
         BITSGSP(IQ2) = FALL1(IQ2) .GT. 0.0E0
89920 CONTINUE
      NXXX = 0
      DO 3891 IQ2 = 1, NUMCOL1
         IF ( BITSGSP(IQ2) ) NXXX = NXXX + 1
3891  CONTINUE
      NPTSGSP(NPAIR, NG ) = NXXX
C
      SF = 0.0E0
      DO 3892 IQ2 = 1, NUMCOL1
         SF = SF + FALL1(IQ2)
3892  CONTINUE
      PPTGSP(NPAIR, NG) = CBOVG * SF
C
C              EXPAND THE GRID-SCALE PRECIPITATION TO ALL POINTS
C              FROM ROWS J = J3 TO JM1 AND FILL IN ZEROES AT THE
C              NONFORECAST POINTS.
      IN = 1
CFPP$ NOCONCUR L
      DO 3893 IQ2 = 1, LEN31
         IF ( BITGRDH(IQ2+LADD3-1, NPAIR, NG) ) THEN
            FALL(IQ2+LADD3-1) = FALL1(IN)
            IN = IN + 1
         ELSE
            FALL(IQ2+LADD3-1) = 0.0E0
         END IF
3893  CONTINUE
C
C              THE GRID-SCALE PRECIPITATION FOR THIS TIME STEP
C              IS ADDED TO THAT WHICH HAS OCCURRED SINCE THE
C              PRECIPITATION BUCKETS WERE EMPTIED LAST.
      JADD = MADDGSP - 1 + LADD3
CMIC$ PARALLEL SHARED(IJ, CBOVG, NPAIR, NG, FALL, BITGRDH, LEN31, JADD, 
CMIC$1   LADD3, VBL) PRIVATE(IQ2)
CMIC$ DO PARALLEL VECTOR
      DO 89940 IQ2=1,LEN31
         VBL(JADD+IQ2-1)=VBL(JADD+IQ2-1)+CBOVG*FALL(LADD3+IQ2
     *   -1)
89940 CONTINUE
C
CMIC$ DO PARALLEL VECTOR
      DO 89950 IQ2 = 1, IJ
         FALL(IQ2) = CBOVG*FALL(IQ2)
         IF (.NOT.BITGRDH(IQ2,NPAIR,NG)) FALL(IQ2) = 0.0E0
89950 CONTINUE
CMIC$ END PARALLEL
C
C
 2350 CONTINUE
C
C              NOW WE MUST CALCULATE HTH AND HQ AT THE FORECAST
C              POINTS, THEN DECOMPRESS THEM TO THE GRID OF ALL
C              POINTS.
C              IF NO PRECIPITATIVE ADJUSTMENTS WERE MADE, THESE
C              STEPS ARE UNNECESSARY AND ARE NOT PERFORMED.
      KTOPADJ = MAX (KCLOUD2, KGSP2, KGSE2)
      IF (KTOPADJ .LE. 0) GO TO 2500
C
      JADD1 = 1 - NUMCOL1
      JADDT = MADDT - 1 + LADD3 - IJ
      JADDQ = MADDQ - 1 + LADD3 - IJ
C
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, H1, HKAPPA1, HHKAPI1) PRIVATE(IQ2)
      DO 89970 IQ2=1,NUMCOL1
         HHKAPI1(IQ2)=H1(IQ2)/HKAPPA1(IQ2)
89970 CONTINUE
C
      DO 2400 K=1, KTOPADJ
C
         JADD1 = JADD1 + NUMCOL1
         JADDT = JADDT + IJ
         JADDQ = JADDQ + IJ
C
         C11 = 0.5E0 / PR(K)
C
C              CALCULATE HTH AND HQ.
CMIC$ DO ALL VECTOR SHARED(NUMCOL1, JADD1, C11, T1, HHKAPI1, Q1, H1)
CMIC$1    PRIVATE(IQ2)
      DO 89980 IQ2=1,NUMCOL1
         T1(JADD1+IQ2-1)=T1(JADD1+IQ2-1)*HHKAPI1(IQ2)*C11
         Q1(JADD1+IQ2-1)=Q1(JADD1+IQ2-1)*H1(IQ2)
89980 CONTINUE
C
C              DECOMPRESS HTH AND HQ.
         IN = 1
CFPP$ NOCONCUR L
CDIR$ IVDEP
         DO 7831 IQ2 = 1, LEN31
            IF ( BITGRDH(IQ2+LADD3-1, NPAIR, NG) ) THEN
               VBL(IQ2+JADDT-1) = T1(IN+JADD1-1)
               VBL(IQ2+JADDQ-1) = Q1(IN+JADD1-1)
               IN = IN + 1
            END IF
7831     CONTINUE
C
 2400 CONTINUE
C
 2500 CONTINUE
C
C
      RETURN
      END
