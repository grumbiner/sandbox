      SUBROUTINE VAPTAB( T, E, ND, IT )
C...TRANSLATED BY FPP 3.00Z36 11/09/90  15:08:10  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    VAPTAB      COMPUTE SATURATION VAP PRESS FROM TABLE
C   PRGMMR: TUCCILLO         ORG: W/NMC4       DATE:90-05-19
C
C PROGRAM HISTORY LOG:
C   90-05-19  J TUCCILLO
C
C ABSTRACT: COMPUTE SATURATION VAPOR PRESSURE E IN CBS BY LINEAR INTER-
C   POLATION IN THE TEMP. RANGE RT1-RT2.  THE TABLE IS COMPUTED ON
C   THE FIRST CALL.  THE WATER MODEL ASSUMES A PERFECT GAS,
C   CONSTANT SPECIFIC HEATS FOR GAS, LIQUID AND SOLID, AND NEGLECTS
C   THE VOLUME OF THE LIQUID AND SOLID.  TRIPLE POINT RT3 IS AT
C   273.16 WITH PRESSURE RE3=0.611 CBS.
C   .      A FLAG ( =ICEPHASE ) DETERMINES WHETHER LATENT HEAT
C   .             OF FREEZING WILL BE ALLOWED FOR. IF SO,
C   .      SATURATION IS COMPUTED OVER LIQUID FOR T .GT. RT3,
C   .      OVER ICE FOR T .LT. RTI, AND WITH A LINEAR COMBINATION
C   .      (WITH RESPECT TO RT3/T) BETWEEN RT3 AND RTI.
C
C USAGE:  CALL VAPTAB(T, E, ND, IT)
C   INPUT ARGUMENT LIST:
C     T        -  VECTOR OF INPUT TEMPS IN DEG K
C     ND       -  LENGTH OF INPUT VECTOR T AND OUTPUT
C                   VECTOR E
C     IT       -  TEST INTEGER
C                 IF IT .GT.0, VALUES OF T OUTSIDE THE
C                  RANGE RT1-RT2 WILL HAVE E COMPUTED FOR
C                  RT1 OR RT2.
C                 IF IT .LE.0, NO TEST WILL BE MADE FOR
C                  SUCH EXTREMES, AND EXECUTION TIME ABORT
C                  MIGHT RESULT IF THEY OCCUR.
C   OUTPUT ARGUMENT LIST:
C     E        -  VECTOR OF SAT VAP PRESS IN CB
C
C   SUBPROGRAMS CALLED:
C     LIBRARY:
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
C          NT IS THE TABLE SIZE
      LOGICAL DEBUG, DOICE
      PARAMETER ( DEBUG = .FALSE. )
      PARAMETER ( DOICE = .FALSE. )
      PARAMETER ( NT = 2501 )
C
      DIMENSION ET(NT), T(ND), E(ND)
      LOGICAL LFRST
C
      DATA LFRST / .TRUE. /
C
      DATA RT1/100.0/,RT2/350.0/,RTI/253.16/,RT3/273.16/,RE3/0.611/
      DATA RAL/5.0065 /, RAPBL/24.84573/
      DATA RAI/0.09859/, RAPBI/22.58728/
C
C
      IF( LFRST ) THEN
C
      LFRST = .FALSE.
C
      RNT= FLOAT(NT-1)
      RDT=(RT2-RT1)/RNT
C
      REL = RT1 - RDT
      DO 88890 IQ2W6E=1,NT
         REL = REL + RDT
         RTAU=RT3/REL
C          E OVER LIQUID
         RELX=RAPBL*(1.0-RTAU)
         RX=EXP(RELX)
         RELX=RE3*(RTAU**RAL)*RX
C          OVER ICE
         REIX=RAPBI*(1.0-RTAU)
         RX=EXP(REIX)
         REIX=RE3*(RTAU**RAI)*RX
C
C                OPTION TO IGNORE ICEPHASE
      IF( .NOT. DOICE ) THEN
            REIX=RELX
      END IF
C          PUT APPROPRIATE E VALUES IN RXD
      RY=RT3/RTI
      RZ=1.0/(RY-1.0)
C          FIRST DO LINEAR COMBINATION EVERYWHERE
      RX=RZ*(RTAU*(REIX-RELX)-REIX+RY*RELX)
C          SUBSTITUTE PURE VALUES IN THE PROPER RANGES OF T
      IF ( RTAU .LE. 1.0 ) RX=RELX
      IF ( RTAU .GE. RY  ) RX=REIX
C
C          PUT VALUES IN PERMANENT TABLE
      ET(IQ2W6E)=RX
88890 CONTINUE
C
C          FINALLY GET HALF PRECISION CONSTANTS FOR TABLE ENTRIES
      A=1.E0/RDT
      B=1.E0-A*RT1
C
C              PRINT OUT SAMPLING OF TABLE IF DESIRED
C              BY DECOMMENTING THE FLWG 9 CARDS.
      IF ( DEBUG ) THEN
      PRINT 33
  33  FORMAT(1H1,14X,'TEMP',10X,'PRESS',4X,'FROM SR VAPTAB')
  44  FORMAT(1H ,10X,2F14.7)
      RTAB=RT1
      RDT=15.0*RDT
      DO 5 I=1,NT,15
      PRINT 44,RTAB,ET(I)
      RTAB=RTAB+RDT
    5 CONTINUE
      END IF
C
      END IF
C
C       *************************************************************
C
C                ENTRY TO USE THE TABLE
C
CMIC$ DO ALL VECTOR SHARED(ND, A, B, T, ET, E) PRIVATE(I, W1, W2, IND)
      DO 89010 I=1,ND
         W1=AMIN1(AMAX1((A*T(I)+B),1.0),FLOAT(NT-1))
         W2=AINT(W1)
         IND=INT(W2)
         E(I)=ET(IND) + (W1-W2) * ( ET(IND+1) - ET(IND) )
89010 CONTINUE
C
      RETURN
      END
