      SUBROUTINE ALLGRIDS( NGRDUSE, KHINTERP)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    ALLGRIDS    ADVANCES ALL GRIDS IN TIME BY DT
C   PRGMMR: JIM TUCCILLO         ORG: W/NMC4    DATE: 90-06-13
C
C ABSTRACT: INVOKES THE THREE SUBROUTINES
C   ONEGRID,  LATBND,  AND SHEM
C   IN THE APPROPRIATE SEQUENCE IN ORDER TO ADVANCE
C   ALL GRIDS NG=1,2,,,NGRDUSE IN TIME BY THE AMOUNT
C   DT= ONE TIME STEP FOR GRID NG=1(GRID A).
C
C PROGRAM HISTORY LOG:
C   90-06-13  JIM TUCCILLO
C
C USAGE:    CALL ALLGRIDS (NGRDUSE, KHINTERP)
C   INPUT ARGUMENT LIST:
C     NGRDUSE  - PARAMETER TO SPECIFY WHICH GRID IS BEING USED
C     KHINTERP - AN INTERPOLATION PARAMETER
C
C   OUTPUT FILES:
C     IOUTUPRT - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    -
C                  LATBND
C                  ONEGRID
C                  SHEM
C
C REMARKS: NOTE THAT THE ARGUMENTS OF SUBROUTINE ONEGRID ARE:
C   SUBROUTINE ONEGRID (NG, NPAIR, LASTSTEP) ,
C   . WHERE
C   .      NG       = THE GRID NUMBER (1= GRID A, 2= GRID B, ETC.)
C   .      NPAIR    = FIRST OR SECOND TIME STEP OF A PAIR FOR THIS
C   .                 GRID (GRID A HAS ONLY ONE STEP)
C   .      LASTSTEP = FLAG WHICH WHEN EQUAL TO 1 INDICATES THAT THIS
C   .                 IS THE LAST STEP FOR THIS GRID WITHIN A
C   .                 COMPLETE (GRID A) TIME STEP.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      COMMON /COM1WAY/ ITIMSTEP, ITBOUND, INGLB
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
      LASTSTEP = 0
C
      GO TO (100, 200, 300, 400, 500, 600), NGRDUSE
C
C
  100 CONTINUE
C************* ONLY GRID A IS BEING FORECAST.
      CALL ONEGRID(1, 1, 1)
      GO TO 700
C
C
  200 CONTINUE
C************* GRIDS A AND B ARE BEING FORECAST.
      DO 280 NGRIDB=1,2
         IF (NGRIDB .EQ. 2) LASTSTEP = 1
         CALL ONEGRID (2, NGRIDB, LASTSTEP)
         IF(INGLB .EQ. 2 ) CALL ONEWAY (1)
  280 CONTINUE
C
C
      CALL ONEGRID (1, 1, 1)
      CALL LATBND  (1, KHINTERP)
      GO TO 700
C
C
  300 CONTINUE
C************* GRIDS A, B, AND C ARE BEING FORECAST.
      DO 380 NGRIDB=1,2
C
         DO 370 NGRIDC=1,2
            IF ((NGRIDB.EQ.2) .AND. (NGRIDC.EQ.2)) LASTSTEP = 1
            CALL ONEGRID (3, NGRIDC, LASTSTEP)
            IF(INGLB .EQ. 3 ) CALL ONEWAY (1)
  370    CONTINUE
C
         CALL ONEGRID (2, NGRIDB, LASTSTEP)
         CALL LATBND  (2, KHINTERP)
         IF(INGLB .EQ. 2 ) CALL ONEWAY (1)
  380 CONTINUE
C
      CALL ONEGRID (1, 1, 1)
      CALL LATBND  (1, KHINTERP)
      GO TO 700
C
C
  400 CONTINUE
C
C************* GRIDS A, B, C, AND D ARE FORECAST.
      DO 480 NGRIDB=1,2
C
         DO 470 NGRIDC=1,2
C
            DO 460 NGRIDD=1,2
               IF ((NGRIDB.EQ.2) .AND. (NGRIDC.EQ.2) .AND.
     1              (NGRIDD.EQ.2)) LASTSTEP = 1
               CALL ONEGRID (4, NGRIDD, LASTSTEP)
               IF(INGLB .EQ. 4 ) CALL ONEWAY (1)
  460       CONTINUE
C
            CALL ONEGRID (3, NGRIDC, LASTSTEP)
            CALL LATBND  (3, KHINTERP)
            IF(INGLB .EQ. 3 ) CALL ONEWAY (1)
  470    CONTINUE
C
         CALL ONEGRID (2, NGRIDB, LASTSTEP)
         CALL LATBND  (2, KHINTERP)
         IF(INGLB .EQ. 2 ) CALL ONEWAY (1)
  480 CONTINUE
C
      CALL ONEGRID (1, 1, 1)
      CALL LATBND  (1, KHINTERP)
      GO TO 700
C
C
  500 CONTINUE
C************* GRIDS A, B, C, D, AND E ARE FORECAST.
      DO 580 NGRIDB=1,2
C
         DO 570 NGRIDC=1,2
C
            DO 560 NGRIDD=1,2
C
               DO 550 NGRIDE=1,2
                  IF ((NGRIDB.EQ.2) .AND. (NGRIDC.EQ.2) .AND.
     1                (NGRIDD.EQ.2) .AND. (NGRIDE.EQ.2)) LASTSTEP = 1
                  CALL ONEGRID (5, NGRIDE, LASTSTEP)
                  IF(INGLB .EQ. 5 ) CALL ONEWAY (1)
  550          CONTINUE
C
               CALL ONEGRID (4, NGRIDD, LASTSTEP)
               CALL LATBND  (4, KHINTERP)
               IF(INGLB .EQ. 4 ) CALL ONEWAY (1)
  560       CONTINUE
C
            CALL ONEGRID (3, NGRIDC, LASTSTEP)
            CALL LATBND  (3, KHINTERP)
            IF(INGLB .EQ. 3 ) CALL ONEWAY (1)
  570    CONTINUE
C
         CALL ONEGRID (2, NGRIDB, LASTSTEP)
         CALL LATBND  (2, KHINTERP)
         IF(INGLB .EQ. 2 ) CALL ONEWAY (1)
  580 CONTINUE
C
      CALL ONEGRID (1, 1, 1)
      CALL LATBND  (1, KHINTERP)
      GO TO 700
C
C
  600 CONTINUE
C************* THIS SUBROUTINE NOT SET UP TO HANDLE MORE THAN 5 GRIDS.
      WRITE (IOUTUPRT, 650) NGRDUSE
  650 FORMAT ('0', 'SUBROUTINE ALLGRIDS WAS SET UP TO HANDLE ',
     1             'UP TO 5 GRIDS.  YOU SPECIFIED', I3, '.'/
     2        ' ', 'EXECUTION TERMINATED.')
C
      CALL EXIT(16)
C
C
  700 CONTINUE
C************* NOW WE APPLY THE EQUATORIAL BOUNDARY CONDITIONS.
      CALL SHEM (KHINTERP)
C
C     WE ARE DONE WITH ALL GRIDS FOR ONE COMPLETE (GRID A) TIME STEP.
C
      RETURN
      END
