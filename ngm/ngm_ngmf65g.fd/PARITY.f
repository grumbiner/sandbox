      SUBROUTINE PARITY (FIELD, IJOUT, SCRATCH, IPARITY)
C...TRANSLATED BY FPP 3.00Z36 11/09/90  14:48:50  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    PARITY      INCREMENTING THE PARITY COUNTER.
C   PRGMMR: J TUCCILLO       ORG: W/NMC40    DATE: 90-06-21
C
C ABSTRACT:  INCREMENTS THE PARITY NUMBER, GIVEN AN ARRAY
C   .        OF VALUES.  THE INCREMENT IS THE SUM OF THE 7 DIGITS
C   .        OF THE STANDARD DEVIATION OF THE ARRAY OF VALUES.
C
C PROGRAM HISTORY LOG:
C   90-06-21  J TUCCILLO
C
C USAGE:  CALL PARITY (FIELD, IJOUT,  SCRATCH, IPARITY)
C   INPUT ARGUMENT LIST:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ------ --------- -----------------------------------------------
C     FIELD    ARG LIST  THE FIELD OF VALUES FOR CALCULATING THE
C                        INCREMENT TO THE PARITY COUNTER.
C     IJOUT    ARG LIST  THE LENGTH OF THE FIELD OF VALUES.
C     SCRATCH  ARG LIST  SCRATCH SPACE WITH LENGTH AT LEAST IJOUT.
C     IPARITY  ARG LIST  THE PARITY COUNTER.
C
C   OUTPUT ARGUMENT LIST:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ------ --------- -----------------------------------------------
C     IPARITY  ARG LIST  THE PARITY COUNTER.
C
C   SUBPROGRAMS CALLED:
C     NAMES                                                   LIBRARY
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
         DIMENSION FIELD(1), SCRATCH(1)
C
         REF = FIELD(IJOUT/2)
C
CMIC$ DO ALL VECTOR SHARED(IJOUT, REF, FIELD, SCRATCH) PRIVATE(IQ2)
         DO 1000 IQ2 = 1, IJOUT
            SCRATCH(IQ2) = FIELD(IQ2) - REF
1000     CONTINUE
C
C              SUM THE DEVIATION FIELD.
C              SUM THE SQUARE OF THE DEVIATION FIELD.
         SUM1 = 0.E0
         SUM2 = 0.E0
         DO 1001 IQ2 = 1, IJOUT
            SUM1 = SUM1 + SCRATCH(IQ2)
            SUM2 = SUM2 + SCRATCH(IQ2) * SCRATCH(IQ2)
1001     CONTINUE
C              CALCULATE THE MEAN, STANDARD DEVIATION, AND RMS VALUES.
            AMEAN = (SUM1 / FLOAT(IJOUT)) + REF
            RMS2 =  (SUM2 / FLOAT(IJOUT))
     1             + REF * (2.0E0 * AMEAN - REF)
            AMEAN2 = AMEAN * AMEAN
            SD2    = RMS2 - AMEAN2
C
            IF (SD2 .LT. 0.0E0) THEN
               SD  = 0.0E0
               RMS = ABS(AMEAN)
            ELSE
               IF (RMS2 .LT. 0.0E0) THEN
                  SD   = 0.0E0
                  RMS  = 0.0E0
               ELSE
                  SD    = SQRT(SD2)
                  RMS   = SQRT(RMS2)
               END IF
            END IF
C
C     UPDATE THE PARITY COUNTER.
C     THE STANDARD DEVIATION IS NORMALIZED TO OBTAIN A VALUE BETWEEN
C     1 AND 10, WHICH IS THEN MULTIPLIED BY 1,000,000 TO OBTAIN
C     A 7 DIGIT INTEGER.  THE 7 DIGITS ARE SUMMED AND ADDED TO
C     THE ACCUMULATION SO FAR FOR THIS COUNTER.
C     NOTHING IS DONE IF THE STANDARD DEVIATION IS ZERO.
C
            IF (SD .LE. 0.0E0) GO TO 290
C
            EXPON = LOG10(SD)
            IF (EXPON .GE. 0.0E0) THEN
C     THE SD IS GREATER THAN OR EQUAL TO 1.
               FACTOR = 10.0E+0 ** REAL(INT(EXPON))
C
            ELSE
C     THE SD IS LESS THAN 1 (AND GREATER THAN ZERO.)
               FACTOR = 10.0E+0 ** REAL(INT( EXPON  - 1.0E+0))
C
            END IF
C
      NORMAL = INT( (SD / FACTOR) * 1.0E+6)
      IQUOT1 = NORMAL
C
      DO 280 N=1,7
         IQUOT2 = IQUOT1 / 10
         IREM   = IQUOT1 - 10 * IQUOT2
         IPARITY = IPARITY + IREM
         IQUOT1 = IQUOT2
C
C
  280 CONTINUE
C
  290 CONTINUE
C
      RETURN
      END
