      SUBROUTINE O3CLIMO (JD,GMT,NUMB,SIGL,XLAT,QO3,
     *                     DO3V,DO3VP,TTHAN,JJROW)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    O3CLIMO     GENERATES VERTICAL PROFILES OF OZONE
C                            FOR CURRENT BLOCK OF NGM HORIZONTAL
C                            GRID POINTS.  THESE OZONE PROFILES
C                            ARE SET BY SPATIAL AND TEMPORAL
C                            INTERPOLATION FROM A ZONAL MEAN SEASONAL
C                            OZONE CLIMO OBTAINED FROM GFDL AND USED
C                            IN NMC MRF.
C
C   PRGMMR:  J TUCCILLO       ORG: W/NMC4     DATE: 90-06-17
C
C ABSTRACT: THIS CODE IS AN NGM ADAPTATION OF SMALL SEGMENTS OF THE MRF
C   RADIATION ROUTINE RADFS (ORIGINALLY FROM GFDL AND ADAPTED TO THE
C   MRF BY KEN CAMPANA OF NMC).
C
C
C PROGRAM HISTORY LOG:
C   90-06-17  J TUCCILLO
C
C
C USAGE:  CALL O3CLIMO( JD,GMT,NUMB,SIGL,XLAT,QO3,
C                        DO3V,DO3VP,TTHAN,JJROW)
C
C   INPUT ARGUMENT LIST:
C     JD       -  NO. OF DAYS FROM 1 JANUARY INCLUSIVE
C     GMT      -  CURRENT FCST TIME OF DAY (HOURS)
C     NUMB     -  NO. OF HORIZONTAL GRID POINTS IN CURRENT BLOCK
C                 OF VERTICAL PROFILES
C     SIGL     -  MODEL SIGMA LAYER VALUES (K=1:LOWEST LAYER)
C     XLAT     -  LATITUDES OF HORIZONTAL GRID POINTS IN THIS
C                 BLOCK (IN DEG: N.H POS.;S.H. NEG.)
C     DO3V     -  THREE HALF PRECISION WORK ARRAYS
C     DO3VP
C     TTHAN
C     JJROW     - A FULL PRECISION INTEGER WORK ARRAY
C
C   OUTPUT ARGUMENT LIST:
C     QO3      -  VERTICAL OZONE PROFILES  (K=1:HIGHEST LAYER)
C                 (UNITS OF G/G: MASS MIXING RATIO)
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - O3INT
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C
      PARAMETER (IX=IIJRAD,KX=IKM)
      PARAMETER (LNGTH=37*KX)
C...TRANSLATED BY FPP 3.00Z36 11/09/90  15:04:25  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
      DIMENSION QO3(IX,KX),XLAT(IX),SIGL(KX)
      DIMENSION JJROW(IX),DO3V(IX),DO3VP(IX),TTHAN(IX)
C
C====>**************************************************************
C
C       SEASONAL CLIMATOLOGIES OF O3 ON MODEL SIGMA LAYERS OBTAINED
C       FROM ONE-TIME CALL TO SUBROUTINE O3INT BELOW.  CLIMO IS
C       DEFINED AS 5 DEG LAT MEANS N.P.->S.P.
C
      COMMON /SAVMEM/
C.....  ....WINTER....  ...SPRING....  ...SUMMER....  ....FALL.....
     1   DDUO3N(37,KX), DDO3N2(37,KX), DDO3N3(37,KX), DDO3N4(37,KX)
C
      DIMENSION RAD1(LNGTH), RAD2(LNGTH), RAD3(LNGTH), RAD4(LNGTH)
      EQUIVALENCE (RAD1(1),DDUO3N(1,1)),(RAD2(1),DDO3N2(1,1))
      EQUIVALENCE (RAD3(1),DDO3N3(1,1)),(RAD4(1),DDO3N4(1,1))
C
C
      DATA  TPI/6.283185308/,RLAG/14.8125/,YEAR/365.25/
      DATA  IPM/0/
C
C  ............  BEGIN EXECUTION ...........................
C
C  BEGIN WITH ONE-TIME CALCULATIONS ON FIRST CALL ONLY
C
      IF (IPM.EQ.0) THEN
      IPM=10
C
C  VERTICALLY INTERPOLATE OZONE CLIMO TO MODEL SIGMA LAYERS
C  (SIGL INDEXED FROM BOTTOM UP, DDUO3N INDEXED FROM TOP DOWN)
C
      CALL O3INT(DDUO3N,SIGL)
C
C  DO PRELIMINARY CALCULATIONS FOR TEMPORAL
C  INTERPOLATIONS (FINISHED ELSEWHERE BELOW)
C
CMIC$ DO ALL VECTOR SHARED(RAD1,RAD2,RAD3,RAD4)PRIVATE(I,AVG,A1,B1,B2)
      DO 5 I=1,LNGTH
      AVG=.25E0*(RAD1(I)+RAD2(I)+RAD3(I)+RAD4(I))
      A1=.5E0*(RAD2(I)-RAD4(I))
      B1=.5E0*(RAD1(I)-RAD3(I))
      B2=.25E0*((RAD1(I)+RAD3(I))-(RAD2(I)+RAD4(I)))
      RAD1(I)=AVG
      RAD2(I)=A1
      RAD3(I)=B1
      RAD4(I)=B2
    5 CONTINUE
C
C  COMPUTE PARAMETERS NEEDED FOR TEMPORAL INTERPOLATION OF
C  SEASONAL OZONE TO INITIAL TIME OF FCST (WE IGNORE FCST TIME CHANGE)
C
      DATE = JD + GMT/24.E0
      RANG = TPI*(DATE-RLAG)/YEAR
      RSIN1= SIN(RANG)
      RCOS1= COS(RANG)
      RCOS2= COS(2.E0*RANG)
C
C...THIS IS END OF ONE-TIME CALCULATIONS
C
      END IF
C
C  NEXT LOOP CALCULATES QUANTITIES NEEDED FOR LATITUDINAL
C  OZONE INTERPOLATION (NOTE LATITUDES CHANGE FOR EACH BLOCK)
C
CMIC$ DO ALL VECTOR SHARED(NUMB, XLAT, JJROW, TTHAN) PRIVATE(I, TH2)
      DO 40 I=1,NUMB
      TH2=0.2E0*XLAT(I)
      JJROW(I)=19.001E0-TH2
      TTHAN(I)=(19-JJROW(I))-TH2
   40 CONTINUE
C
C  TEMPORAL AND SPATIAL INTERPOLATION OF OZONE DONE BELOW
C  FOR EVERY VERTICAL LEVEL
C
CMIC$ DO ALL SHARED(NUMB, RSIN1, RCOS1, RCOS2, JJROW, DDUO3N, DDO3N2, 
CMIC$1   DDO3N3, DDO3N4, TTHAN, QO3) PRIVATE(DO3V, DO3VP, K, I)
CMIC$2    SAVELAST 
      DO 130 K=1,KX
        DO 125 I=1,NUMB
          DO3V(I)  = DDUO3N(JJROW(I),K) + RSIN1*DDO3N2(JJROW(I),K)
     1                +RCOS1*DDO3N3(JJROW(I),K)
     2                +RCOS2*DDO3N4(JJROW(I),K)
          DO3VP(I) = DDUO3N(JJROW(I)+1,K) + RSIN1*DDO3N2(JJROW(I)+1,K)
     1               +RCOS1*DDO3N3(JJROW(I)+1,K)
     2               +RCOS2*DDO3N4(JJROW(I)+1,K)
C
C  NOW FINISH LATITUDINAL INTERPOLATION AND
C  CONVERT O3 INTO MASS MIXING RATIO(ORIGINAL DATA MPY BY 1.E4)
C  FINAL QO3 IS VERTICALLY INDEXED FROM TOP DOWN (K=1:TOP)
C
      QO3(I,K)=.0001E0 *(DO3V(I)+TTHAN(I)*(DO3VP(I)-DO3V(I)))
C
  125 CONTINUE
  130 CONTINUE
C
C
      RETURN
      END
