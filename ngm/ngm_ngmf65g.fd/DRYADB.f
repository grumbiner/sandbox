      SUBROUTINE DRYADB( NG,NPAIR )
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    DRYADB    ADIABATICALLY ADJUSTS THE H*THETA FLDS
C   PRGMMR: JIM TUCCILLO       ORG: W/NMC4     DATE: 90-06-13
C
C ABSTRACT: MAKE A SINGLE DRY ADIABATIC ADJUSTMENT
C   SWEEP OVER THE H*THETA FLDS OF NGM GRID  NG .
C   FOR GRID  NG , A SINGLE SWEEP, K=1,(KM-1) IS MADE TO
C   FIND PLACES WHERE THETA (POTENTIAL TEMPERATURE) AT LEVEL K
C   EXCEEDS THAT AT LEVEL K+1.  WHERE THIS OCCURS THE VALUES OF
C   H * THETA  ( H=SFC PRESS) LOCATED IN ARRAY  VBL AT LEVELS
C   K AND K+1 ARE BOTH REPLACED BY THE ADJUSTED VALUE
C   .
C   H*THETA(NEW)= ALPHA(K) * (H*THETA AT LVL K)  +
C   .              (1-ALPHA(K) ) * (H*THETA AT LVL K+1)
C   .
C   THE COEFFICIENTS ALPHA(K), K=1,(KM-1),, ARE COMPUTED ON THE
C   FIRST CALL.  THEY ARE BASED ON ROSSBY'S IDEA THAT TURBULENT
C   CONVECTIVE OVERTURNING OF A SUPER-ADIABATIC LAYER WILL, AFTER
C   ALL TURBULENT KINETIC ENERGY HAS BEEN CONVERTED INTO HEAT BY
C   VISCOSITY, RESULT IN A LAYER OF UNIFORM POTENTIAL TEMPERATURE
C   WITH THE SAME (DRY) ENTHALPY AS THAT POSSESSED BY THE
C   ORIGINAL SUPERADIABATIC LAYER.
C   . ALPHA IS GIVEN BY
C   .     ALPHA(K) = A(K) / ( A(K) + A(K+1) )
C   . WHERE
C   .     A(K) = P(K)**C - P(K+1)**C
C   .     P(K) = INTERFACE PRESSURE / SURFACE PRESSURE
C   .        C = 1 + KAPPA,  (KAPPA= R/CSUBP)
C   .
C   NOTE THAT EACH LEVEL ACTS ONLY ONCE AS K IN THE PAIR
C   ( K,K+1)----NEWLY CREATED INSTABILITIES ARE NOT
C   ADJUSTED FURTHER.
C   .
C   THE NUMBER OF UNSTABLE LAYERS IS STORED IN ARRAY KADIAB,
C   FOR POSSIBLE DIAGNOSTIC PRINT OUT BY HIGHER PROGRAMS
C
C PROGRAM HISTORY LOG:
C   90-06-13  J TUCCILLO
C
C USAGE:  CALL DRYADB( NG )
C   INPUT ARGUMENT LIST:
C     NG       - NUMBER OF THE GRID TO BE ADJUSTED
C
C SUBPROGRAMS CALLED:
C   LIBRARY:
C     COMMON   - COMCONST
C                COMBLANK
C                COMDIAG
C
C REMARKS:
C   .
C   - - - - - - - - I N P U T   V A R I A B L E S  - - - - - - - - -
C   .
C   NAMES        MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----        ----------------------------------        ---------
C   .
C   DELSIG       DELTA SIGMA VALUES FOR THE KM LAYERS.     COMMON
C   .
C   HTHETA       SFC PRESSURE TIMES POTENTIAL TEMPERATURE  COMMON
C   .             FOR ALL NGM GRIDS,, IN THE ARRAY  VBL .
C   .
C   SCR(1,1)     THIS SCRATCH ARRAY IS NEEDED TO COMPUTE   COMMON
C   .             THE ADJUSTED VALUES.
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES         MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----         ----------------------------------        ---------
C   HTHETA        ADJUSTED VALUES OF H*THETA                COMMON
C   .
C   KADIAB(K,NG)  NUMBER OF POINTS FOR LEVEL K THAT NEEDED  COMMON
C                 ADJUSTMENT
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C...TRANSLATED BY FPP 3.00Z36 11/09/90  14:58:30  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMDIAG/ CONTAINS QUANTITIES SAVED FOR
C          DIAGNOSTIC PURPOSES.
      COMMON /COMDIAG/  KADIAB( IKM, INGRDUSE ),
     1                  NPTSDHQ (2, INGRDUSE),
     2                  KKCLOUD2(2, INGRDUSE), NPTSBUOY(2, INGRDUSE),
     3                  NPTSWATR(2, INGRDUSE), NPTSCC  (2, INGRDUSE),
     4                  KKGSP2  (2, INGRDUSE),
     5                  KKGSE2  (2, INGRDUSE),
     6                  NPTSSAT (IKM, 2, INGRDUSE),
     7                  NPTSEVAP(IKM, 2, INGRDUSE),
     8                  NPTSGSP (2, INGRDUSE), NUMPROF,
     9                  PPTCC   (2, INGRDUSE), PPTGSP  (2, INGRDUSE),
     1                  ALATPR  (IMAXPROF),    ALONPR  (IMAXPROF),
     2                  DESCRP  (IMAXPROF)
C
      CHARACTER*24 DESCRP
C
      DIMENSION ALPHA(IKM)
C
      LOGICAL BIT1(IIJMAX)
C
      DATA ICALL /0/
C
      IF( ICALL .GT. 0 ) GO TO 100
      ICALL=1
      KMM1=KM-1
C          COMPUTE ALPHA TABLE ON FIRST CALL
C          1-SIGMA(INTERFACE) INTO ALPHA FIRST.
      ALPHA(1)=1.E0
CDIR@ NEXTSCALAR
      DO 10 K=2,KM
      ALPHA(K)=ALPHA(K-1) - DELSIG(K-1)
   10 CONTINUE
C          REPLACE IT WITH ITSELF TO THE (1+KAPPA POWER)
      ZKAP1=(287.05E0 / 1005.E0 ) + 1.E0
CMIC$ DO ALL VECTOR SHARED(KM, ZKAP1, ALPHA) PRIVATE(K)
      DO 20 K=2,KM
         ALPHA(K) = ALPHA(K) ** ZKAP1
   20 CONTINUE
C          REPLACE WITH ITS K-DIFFERENCE
      DO 30 K=1,KMM1
      ALPHA(K)=ALPHA(K) - ALPHA(K+1)
   30 CONTINUE
C          FINALLY GET ALPHA
      DO 40 K=1,KMM1
      ALPHA(K)=ALPHA(K) / ( ALPHA(K)+ALPHA(K+1) )
   40 CONTINUE
C
C    ***** ENTRANCE TO MAKE ADJUSTMENTS ON GRID NG *******
C
  100 CONTINUE
C
      IJ=IMG(NG) * JMG(NG)
      IAD=IADDRG(3,NG)-IJ
C
        DO 200 K=1,KMM1
        IAD=IAD+IJ
C          LOOK FOR POINTS WITH THETA(K) .GT. THETA(K+1)
CMIC$ DO ALL VECTOR SHARED(IJ, IAD, NPAIR, NG, VBL, BITGRDH, BIT1)
CMIC$1    PRIVATE(IQ2W6E)
      DO 88890 IQ2W6E=1,IJ
         BIT1(IQ2W6E)=(VBL(IAD+IQ2W6E-1).GT.VBL(IAD+IJ+IQ2W6E-1))
     *                .AND.BITGRDH(IQ2W6E,NPAIR,NG)
88890 CONTINUE
        ICOUNT = 0
        DO 762 I = 1, IJ
           IF ( BIT1(I) ) ICOUNT= ICOUNT + 1
762     CONTINUE
        KADIAB(K,NG)= ICOUNT
C
C          IF THERE ARE NO SUCH POINTS, GO TO NEXT LAYER.
        IF( ICOUNT .LE. 0 ) GO TO 200
C          WE HAVE SOME UNSTABLE POINTS.
        C1=ALPHA(K)
        C2=1.E0-C1
C          GET THE WEIGHTED MEAN THETA(* H, OF COURSE)
CMIC$ DO ALL VECTOR SHARED(IJ, C1, IAD, C2, VBL, SCR, BIT1) PRIVATE(
CMIC$1   IQ2W6E)
      DO 88910 IQ2W6E = 1, IJ
         SCR(IQ2W6E,1) = C1*VBL(IAD+IQ2W6E-1) + C2*VBL(IAD+IJ+IQ2W6E-1)
         IF (BIT1(IQ2W6E)) THEN
            VBL(IAD+IQ2W6E-1) = SCR(IQ2W6E,1)
            VBL(IAD+IJ+IQ2W6E-1) = SCR(IQ2W6E,1)
         ENDIF
88910 CONTINUE
C
  200   CONTINUE
C
      KADIAB(KM,NG)=0
C
      RETURN
      END
