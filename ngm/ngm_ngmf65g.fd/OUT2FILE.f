      SUBROUTINE OUT2FILE (IOUTUOUT, IOUTUOUTGB, DATASET, INUOUT,
     1                     ISKIPPRT, ISKIPOUT, IOUTUPRT, itrack)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    OUT2FILE    READS GRID PARAMETER CARDS, ETC.
C   PRGMMR: JIM TUCCILLO     ORG: W/NMC4     DATE: 90-06-21
C
C ABSTRACT: 1)  READS THE CARDS SPECIFYING GRID PARAMETERS
C   .          AND ALSO THE FIELDS TO BE POSTPROCESSED FOR ALL
C   .          GRIDS FOR ONE OUTPUT FILE AT THE CURRENT TIME STEP,
C   .       2)  IF DESIRED PRINTS THE CARDS READ IN, AND
C   .       3)  IF DESIRED DOES THE POSTPROCESSING.
C
C PROGRAM HISTORY LOG:
C   90-06-21  JIM TUCCILLO
C   93-12-22  RUSS TREADON - ADDED GRIB UNIT NUMBER AND DATASET
C             NAME TO ARGUMENT LIST
C
C USAGE:    CALL OUT2FILE (IOUTUOUT, IOUTUOUTGB, DATASET, INUOUT,
C                          ISKIPPRT, ISKIPOUT, IOUTUPRT)
C   INPUT ARGUMENT LIST:
C     IOUTUOUT - UNIT NUMBER OF SOMETHING THAT IS NOT USED AT ALL
C     IOUTUOUTGB - UNIT TO WHICH GRIB DATA IS WRITTEN
C     DATASET  - PART OF GRIB FILENAME
C     INUOUT   - UNIT NUMBER OF INPUT FILE AND OUTPUT FILE
C     ISKIPPRT - VARIABLE THAT CONTROLS THE PRINTING OF THE INPUT
C                CARDS READ IN; =0 - PRINT, <>0 - NO PRINT
C     ISKIPOUT - VARIABLE THAT CONTROLS WHETHER OUTPUT IS CALLED
C                =0 - CALL OUTPUT; <>0 - DO NOT CALL OUTPUT
C     IOUTUPRT - UNIT NUMBER FOR PRINTOUT
C
C   INPUT FILES:
C     INUOUT   - FILE FROM WHICH GRID PARAMETERS AND FIELDS ARE READ
C
C   OUTPUT FILES:
C     IOUTUPRT - FOR PRINTOUT OF GRID PARAMETERS
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - OUTPUT
C                  OUTOPENGB
C     LIBRARY:
C       COMMON   - COMOUT
C
C REMARKS: NOTE THAT IF SR OUTPUT IS CALLED, THE OUTPUT UNIT NUMBER
C   IS THE SAME AS THE UNIT NUMBER THAT THE CARDS ARE READ FROM
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C
      CHARACTER*1  CHARFLAG
      CHARACTER*6  DATASET
C
C     COMMON BLOCK /COMOUT/ CONTAINS OUTPUT CODE PARAMETERS
C               AND ARRAYS.
C
      CHARACTER*20 DESCRIPT
C          DESCRIPTORS USED IN SR  OUTHORIZ
C
      COMMON /COMOUT/
C
C          THE FOLLOWING ARE STORED BY OUT2FILE FROM DATA CARDS.
C          (THE LAST 6 WILL BE OVERWRITTEN BY  OUTPUT  IF KGTYPE
C          IS 5 OR 26 (I.E. AN LFM GRID DEFINED IN TABLE 7 OF
C          OFFICE NOTE 84.))
C
     1     INUNIT, IOUTUNIT, IOUTGRIB,
     2     KGTYPE, IMOUT, JMOUT, DNPEQOUT, DLAMOUT, XIPOUT, YJPOUT,
C
C          THE FOLLOWING COME FROM  MAIN  READING THE DATA CARDS.
     2   LQTYPE(IMAXOUTV),   LSTYPE(IMAXOUTV),
     3   DESCRIPT(IMAXOUTV), LEVELS(ILMOUT,IMAXOUTV),  NOUTVBLS,
C
C          THE FOLLOWING ARE DERIVED BY   OUTPUT  .
     4       IJOUT, LMOUT, NFILLER, NS2, NS3NGM, NS3OUT,
     5       COSOUT,   SINOUT,    DXOUT,    ZSIGLYR(IKM),
C
C           ARRAYS  PLOUT  AND  HTFD  ARE SET BY DATA STATEMENTS IN
C           THE SR  OUTPUT  .  'PADDER' FILLS OUT TO FULL WORD BNDRY.
     6      PLOUT(ILMOUT),   HTFD( 6),  PADDER,
C
C          THE FOLLOWING ADDRESSES AND ADDRESS ARRAYS ARE SET UP
C          BY   OUTPREP  WHEN IT IS CALLED BY   OUTPUT  .
     7  J1(INGRDUSE),          J2(INGRDUSE),   JADDK2L(INGRDUSE,2),
     8  JADDFNGM(INGRDUSE), JHREC(INGRDUSE),   JHKAP(INGRDUSE),
     9  JSCR2(12),    JSCR3NGM(INGRDUSE,12),   JSCR3OUT(12),
     A  LENGD(INGRDUSE), LTYPE(ILMOUT,INGRDUSE), MB1, MB2, MINT,
     B   MSOUT1,  MSOUT2,  MZGNDOUT,  MNGM3,  MHOUT, MZFR, MZTRO,
     C      MSZGND(INGRDUSE), NGRIDATA(INGRDUSE),
C
C            NUMGDREQ, THE NUMBER OF NGM GRIDS NEEDED TO COVER THE
C            OUTPUT GRID, AND ZLOUT, ARE SET BY  OUTPREP  .
     D                NUMGDREQ,    ZLOUT(ILMOUT),
C
C          OUTPREP ALSO ASSIGNS THE FOLLOWING DESCRIPTORS TO BE
C          USED BY  OUTHORIZ  FOR HORIZONTAL INTERPOLATION.
     E    AHORD,BHORD,CHORD,DHORD, ILLD,IULD,  SCRHORD,  OUTHORD,
C
C
C          THE FOLLOWING ARRAY CONTAINS THE INTEGERS NEEDED TO
C          FORM THE LABEL THAT GOES WITH EACH OUTPUT ARRAY.
     F               LABEL84( 27 ),
C
C          THE FOLLOWING SYMBOLS REPRESENT TEMPORARY VALUES OF
C          INTEGERS TO GO INTO  LABEL84 WHEN THE OUTPUT ARRAY
C          HAS BEEN PREPARED FOR PACKING.
C
     G  LABQTYPE, LABSSUB1, LABTMARK,  S1VALUE,  S2VALUE,
     H  LABMLDIF, LABSSUB2, LABFSUB2, LABKGRID, LABNUMPT
C
C              PLI PARAMETERS ARE MATERIALIZED.
      LMOUT   = ILMOUT
      MAXOUTV = IMAXOUTV
C
CGRIB         SET UNIT FOR GRIB FILE - PASSED THROUGH COMMON BLOCK
CGRIB         COMOUT.  CLOSE OLD GRIB FILE AND OPEN NEW GRIB FILE
CGRIB         CHANGE MADE 93-12-22 TREADON
cgrib         change made 94-04-19 deaven added logic to open grib file
cgrib         for first track only thus putting all files in the grib file
	      if( itrack.eq.1) then
                  IOUTGRIB = IOUTUOUTGB
                  CLOSE(IOUTGRIB)
                  CALL OUTOPENGB (IOUTGRIB,DATASET,IOUTUPRT)
              end if
C              PROCESS EACH OUTPUT GRID.
      DO 1500 N=1, 1000000
C
C              READ THE PARAMETERS DESCRIBING AN OUTPUT GRID.
C              NOTE THAT ONLY KGTYPE IS USED IF KGTYPE IS 5 (THE LFM
C              ANALYSIS GRID) OR 26 = 1A HEX (THE LFM FORECAST GRID).
C
         READ (INUOUT, 100, END=2000)
     1         KGTYPE, DNPEQOUT, IMOUT, JMOUT,
     2         XIPOUT, YJPOUT, DLAMOUT
  100    FORMAT (27X, I5    / 27X, F15.0 / 27X, I5 / 27X, I5 /
     1           27X, F10.0 / 27X, F10.0 / 27X, F10.0)
C
C              ECHO CHECK THESE CARDS.
         IF (ISKIPPRT .EQ. 0) THEN
            WRITE (IOUTUPRT, 200)
     1             KGTYPE, DNPEQOUT, IMOUT, JMOUT,
     2             XIPOUT, YJPOUT, DLAMOUT
C
  200       FORMAT ('0', 10X, 'ONE OUTPUT GRID FOR THIS DATASET',
     1                   ' HAS GRID TYPE', I4, ' (DECIMAL).'/
     2              ' ', 13X, 'IF THIS IS A GRID TYPE NOT RECOGNIZED ',
     3                   'BY THE OUTPUT CODE, THE FOLLOWING ',
     4                   'GRID PARAMETERS ARE USED:'/
     5              ' ', 13X, 'DNPEQOUT=', 1PE14.6, ', IMOUT=', I5,
     6                   ', JMOUT=', I5, ', XIPOUT=', 1PE14.6,
     7                   ', YJOUT=', 1PE14.6, ', DLAMOUT=', 1PE14.6 /
     8              '0', 13X, 'THE FIELDS TO BE OUTPUT FOR THIS ',
     9                   'OUTPUT GRID ARE:')
C
         END IF
C
         NOUTVBLS = 0
C
C              READ THE FIELDS TO BE OUTPUT FOR THIS OUTPUT GRID.
         DO 800 LFIELD=1, 1000000
C
C              ENSURE THAT THE NUMBER OF OUTPUT VARIABLES (FIELDS)
C              DOES NOT EXCEED THE MAXIMUM PERMITTED BY THE PLI
C              PARAMETER.
            IF (LFIELD .GT. MAXOUTV) THEN
               WRITE (IOUTUPRT, 300) LFIELD, MAXOUTV
  300          FORMAT ('0', 'THE NUMBER OF OUTPUT FIELDS REQUESTED IS',
     1                      I4, ', WHICH IS GREATER THAN THE MAXIMUM ',
     2                      'OF', I4, ' PERMITTED.' /
     3                 ' ', 'THE EXCESS OUTPUT ',
     4                      'FIELDS ARE NOT POSTPROCESSED.' /
     5                 ' ', 'EXECUTION CONTINUING.' /)
            END IF
C
            READ (INUOUT, 600, END=1000)
     1            CHARFLAG, DESCRIPT(LFIELD),
     2            LQTYPE(LFIELD), LSTYPE(LFIELD),
     3           (LEVELS (K,LFIELD), K=1, LMOUT)
  600       FORMAT (A1, 1X, A20, 5X, I4, 6X, I4, 6X, 4(5I1, 1X))
C
C              IF CHARFLAG IS AN ASTERISK (*), WE ARE FINISHED READING
C              IN THE DATA CARDS FOR THIS OUTPUT GRID.
C
            IF (CHARFLAG .EQ. '*') GO TO 1200
C
            NOUTVBLS = LFIELD
C
C              ECHO CHECK THE CARD JUST READ IN.
            IF (ISKIPPRT .EQ. 0) THEN
               WRITE (IOUTUPRT, 700)
     1                LFIELD, DESCRIPT(LFIELD),
     2                LQTYPE(LFIELD), LSTYPE(LFIELD),
     3               (LEVELS(K, LFIELD), K=1, LMOUT)
C
  700          FORMAT (' ', 15X, I4, 4X, A20, ',  LQTYPE=', I4,
     1                      ',  LSTYPE=', I4, ',  LEVELS=', 1X,
     2                      4(5I1, 1X))
C
            ENDIF
C
  800    CONTINUE
C
C              WE SHOULD NOT HAVE FALLEN THROUGH THE LOOP TO THIS POINT.
         WRITE (IOUTUPRT, 900) MAXOUTV, NOUTVBLS
  900    FORMAT ('0', 'IMPOSSIBLE PATH TAKEN AFTER STATEMENT ',
     1                '800 IN SUBROUTINE OUT2FILE.'/
     2           ' ', 'NOTE THAT MAXOUTV =', I10, ' AND ',
     3                'NOUTVBLS =', I10, '.'/
     4           ' ', 'EXECUTION CONTINUING.')
C
         GO TO 1200
C
 1000    CONTINUE
C              WE HAVE HIT THE INPUT END-OF-FILE.  THIS SHOULD NOT
C              HAVE HAPPENED.
         WRITE (IOUTUPRT, 1100)
 1100    FORMAT ('0', 'INPUT END-OF-FILE ENCOUNTERED ',
     1                'WHEN READING SPECIFICATIONS FOR OUTPUT ',
     2                'FIELDS.'/
     3           ' ', 'PROBABLE CAUSE IS ',
     4                'OMISSION OF * INDICATING THE END OF ',
     5                'SPECIFICATIONS FOR AN OUTPUT GRID.'/
     6           ' ', 'EXECUTION CONTINUING.')
C
 1200    CONTINUE
C              DONE WITH READING THE CARDS SPECIFYING THE OUTPUT
C              FIELDS FOR THIS OUTPUT GRID.
C
C              NOW, IF DESIRED, ACTUALLY POSTPROCESS EACH FIELD TO
C              BE OUTPUT FOR THIS OUTPUT GRID, IF ANY.
C
         IF ((NOUTVBLS .GT. 0) .AND. (ISKIPOUT .EQ. 0)) THEN
C
              INUNIT   = INUOUT
              IOUTUNIT = IOUTUOUT
C
              CALL OUTPUT (INUOUT)
C
         END IF
C
C              DONE POSTPROCESSING ALL FIELDS FOR THIS OUTPUT GRID,
C              IF REQUESTED TO DO SO.
C
C              RETURN TO TOP OF DO LOOP TO BEGIN POSTPROCESSING
C              NEXT OUTPUT GRID, IF ANY, FOR THIS FILE.
 1500 CONTINUE
C
C              WE SHOULD NOT HAVE FALLEN THROUGH THE LOOP TO THIS POINT.
      WRITE (IOUTUPRT, 1600)
 1600 FORMAT ('0', 'IMPOSSIBLE PATH TAKEN AFTER ',
     1             'STATEMENT 1500 IN SUBROUTINE OUT2FILE.' /
     2        ' ', 'EXECUTION CONTINUING.' /)
C
 2000 CONTINUE
C              DONE POSTPROCESSING ALL OUTPUT GRIDS FOR THIS
C              OUTPUT FILE.
C
C              REWIND THIS POSTPROCESSING DATA-CARD FILE.
      REWIND INUOUT
C
      RETURN
      END
