      SUBROUTINE OUTSIG2P(NDATA,NWORK,MB,LQQTYPE,GAMBOT,GAMTOP,ISM)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    OUTSIG2P    INTERPOLATE FROM SIGMA TO PRESSURE
C   PRGMMR: N PHILLIPS       ORG: W/NMC22    DATE: 83-12-14
C
C ABSTRACT: THE DESIRED DATA IS STORED IN THE NEEDED THREE-DIMEN.
C   NGM GRIDS IN  S( JSCR3NGM(NN,NDATA) ) WHERE NN=1,NUMGDREQ
C   IS THE SEQUENCE OF NEEDED NGM GRIDS( THE ACTUAL GRID NUMBER
C   BEING NG=NGRIDATA(NN).  FOR ANY PRESSURE SURFACE PLOUT(L)
C   THAT IS REQUESTED BY A DATA CARD, THIS ROUTINE
C   1) FOR EACH OF THE NUMGDREQ GRIDS INTERPOLATES THE VARIABLE
C   VERTICALLY FROM THE SIGMA LOCATIONS IN S(JSCR3NGM(NN,NDATA) )
C   TO THE PRESSURE SURFACE PLOUT(L),, USING THE GATHER INTEGERS
C   IN ARRAY IN(JADDK2L(NG,1)+(L-1)*LENGD(NG) ), AND THE INTERPOLATION
C   COEFFICIENTS IN S(JADDK2L(NG,2)+(L-1)*LENGD(NG) ).  THESE ARE
C   PUT INTO THAT GRID'S PLACE IN THE HORIZONTAL STAGING ARRAY FNGM.
C   2) CALLS OUTHORIZ TO HORIZONTALLY INTERPOLATE THIS PRESSURE SFC.
C   DATA FROM THE NGM GRID POINTS ('THE H-POINTS') TO THE OUTPUT GRID.
C   3) IF  ISM  IS ONE, THE OUTPUT ARRAY WILL BE SMOOTHED.
C   4) UPDATE THE LABEL INFORMATION AND SEND THE OUTPUT ARRAY TO
C   THE PACKER CODE FOR ULTIMATE TRANSMISSION TO THE FRONT END.
C   .
C   SIGMA DATA OF THE DESIRED VARIABLE MUST BE STORED IN THE
C   SUCCESSIVE THREE-DIMENSIONAL NGM GRIDS S( JSCR3NGM(NN,NDATA) ).
C
C PROGRAM HISTORY LOG:
C   83-12-14  N PHILLIPS
C   88-08-26  B SCHMIDT REVISED THE DOCBLOCK
C
C USAGE:  CALL OUTSIG2P(NDATA, NWORK, MB, LQQTYPE, GAMBOT, GAMTOP, ISM)
C   INPUT ARGUMENT LIST:
C     ISM      - =1, DO SMOOTHING.
C     NDATA    - WHICH OF THE SET OF NUMGDREQ THREE-
C                DIMENSIONAL NGM GRIDS CONTAINS THE DATA.
C     NWORK    - SCRATCH SPACE OF SIZE =MAX(IJMAX,IJOUT)
C     MB       - BITS(MB)=SCRATCH BIT ARRAY OF LENGTH IJOUT
C     LQQTYPE  - OFFICE NOTE 84 LABEL FOR THIS OUTPUT VBL.
C     GAMBOT,  - VALUES OF THE VERTICAL DERIVATIVE OF THE
C     GAMTOP     OUTPUT VBL. WITH RESPECT TO  -LOG(PRESS),
C                THAT WILL BE USED TO EXTRAPOLATE BELOW
C                OR ABOVE THE SIGMA LEVELS K=1 AND KM.
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - OUTHORIZ
C                  OUTNULAB
C                  OUTPACK
C                  OUTSMOO
C     LIBRARY:
C       COMMON   - COMBLANK
C                  COMCONST
C                  COMOUT
C
C REMARKS:
C   .
C   - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C   .
C   NAMES         MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C   -----         ----------------------------------        ---------
C   ALL OUTPUT FROM THIS ROUTINE OCCURS THROUGH CALLS TO
C   .   THE SUBROUTINES  OUTNULAB AND  OUTPACK
C   FOR OUTNULAB THE FOLLOWING O.N.84 LABAL INFO. IS PREPARED
C   .
C   S1VALUE        VALUE OF THE PRESSURE SURFACE(MBS)COMMON
C   .
C   LABQTYPE       O.N. 84 VALUE (TABLE 1) FOR THIS VARIABLE COMMON
C   .
C   LABSSUB1       =8 FOR OUTPUT ON PRESSURE SURFACE. COMMON
C   .
C   FOR THE CALL TO OUTPACK THE INFORMATION IS AS FOLLOWS
C   .
C   S(MSOUT2)      OUTPUT ARRAY OF VBL LABQTYPE ON PRESS.   ARGUMENT
C   .              SURFACE
C   .
C   IJOUT          SIZE OF OUTPUT GRID                      ARGUMENT
C   .
C   S(MSOUT1)      SCRATCH SPACE OF SIZE IJOUT FOR PACKER   ARGUMENT
C   .
C   DESCRIPT(NCARD) 20 CHARACTER STRING DESCRIBING VBL.     ARGUMENT
C   .
C   LABEL84        ARRAY IN COMOUT CONTAINING LABEL INFO.   ARGUMENT
C   .
C   IOUTUNIT       DISK UNIT NUMBER FOR PACKER TO WRITE ON. ARGUMENT
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C...TRANSLATED BY FPP 3.00Z36 11/09/90  14:43:54  
C...SWITCHES: OPTON=I47,OPTOFF=VAE0
C
C          INTERPOLATE FROM SIGMA TO PRESSURE ON NGM
C          GRIDS, INTERPOLATE THESE HORIZONTALLY
C          TO THE OUTPUT GRID, AND SEND DESIRED
C          PRESSURE LEVELS TO THE PACKER CODE.
C          SMOOTHING IS AN OPTION(ISM)
C
C
C     COMMON BLOCK /COMCONST/ CONTAINS GRID-RELATED PARAMETERS,
C          AND A FEW OTHER COMMON CONSTANTS.
      COMMON /COMCONST/ IJMAX, KM, LVLTOT, IJRAD, LOFCLDS(2,4),
     1                  ICALLRAD, IPHYSPL, NGRDUSE, NH,
     2                  NTIME, ITIME, NSTEPS,
     3                  IMG(INGRDUSE), JMG(INGRDUSE),
     4                  IAG(INGRDUS1), JAG(INGRDUS1),
     5                  IBG(INGRDUS1), JBG(INGRDUS1),
     6                  IADDRG(INIADDRS, INGRDUSE),
     7                  NPTSFH(2, INGRDUSE),
     8                  KUMULUS, LGRIDPPT, KLIFT1, KLIFT2, IBUCKET,
     9                  XPOLEH(INGRDUSE), YPOLEH(INGRDUSE), RADIUS,
     1                  DELSIG(IKM), PR(IKM), PRESS(IKM),
     2                  SIGINT(IKMP1),
     3                  DTOVDX, ANGVEL,
     4                  SIGMACC, SIGMAGSP, SIGMADHQ, CRITCONV,
     5                  SATDEL, RHFACTOR, QBOUND,
     6                  ANEM, BLKDR, CHARN, CONAUST, DDORF, PKATO,
     7                  SCALEHT, SIGDOT, DLAMNGM
C
      COMMON            SCR     (IIJMAX,  INSCR),
     1                  SCRGEOG (IIJMAX,  INSCRGEO),
     2                  SCR3    (IIJKMAX, INSCR3),
     3                  FILLER  (INFILLER),
     4                  VBL     (INVBL),
     5                  BITGRDH (IIJMAX, 2, INGRDUSE),
     6                  BITGRDU (IIJMAX, 2, INGRDUSE),
     7                  BITGRDV (IIJMAX, 2, INGRDUSE),
     8                  BITSEA  (IIJMAX, INGRDUSE),
     9                  BITSNO  (IIJMAX, INGRDUSE),
     1                  BITWVL  (IIJMAX, INGRDUSE)
C
      LOGICAL BITGRDH, BITGRDU, BITGRDV, BITSEA, BITSNO, BITWVL
C
C     COMMON BLOCK /COMOUT/ CONTAINS OUTPUT CODE PARAMETERS
C               AND ARRAYS.
C
      CHARACTER*20 DESCRIPT
C
      COMMON /COMOUT/
C
C          THE FOLLOWING ARE STORED BY OUT2FILE FROM DATA CARDS.
C          (THE LAST 6 WILL BE OVERWRITTEN BY  OUTPUT  IF KGTYPE
C          IS 5 OR 26 (I.E. AN LFM GRID DEFINED IN TABLE 7 OF
C          OFFICE NOTE 84.))
C
     1     INUNIT, IOUTUNIT, IOUTGRIB,
     2     KGTYPE, IMOUT, JMOUT, DNPEQOUT, DLAMOUT, XIPOUT, YJPOUT,
C
C          THE FOLLOWING COME FROM  MAIN  READING THE DATA CARDS.
     2   LQTYPE(IMAXOUTV),   LSTYPE(IMAXOUTV),
     3   DESCRIPT(IMAXOUTV), LEVELS(ILMOUT,IMAXOUTV),  NOUTVBLS,
C
C          THE FOLLOWING ARE DERIVED BY   OUTPUT  .
     4       IJOUT, LMOUT, NFILLER, NS2, NS3NGM, NS3OUT,
     5       COSOUT,   SINOUT,    DXOUT,    ZSIGLYR(IKM),
C
C           ARRAYS  PLOUT  AND  HTFD  ARE SET BY DATA STATEMENTS IN
C           THE SR  OUTPUT  .  'PADDER' FILLS OUT TO FULL WORD BNDRY.
     6      PLOUT(ILMOUT),   HTFD( 6),  PADDER,
C
C          THE FOLLOWING ADDRESSES AND ADDRESS ARRAYS ARE SET UP
C          BY   OUTPREP  WHEN IT IS CALLED BY   OUTPUT  .
     7  J1(INGRDUSE),          J2(INGRDUSE),   JADDK2L(INGRDUSE,2),
     8  JADDFNGM(INGRDUSE), JHREC(INGRDUSE),   JHKAP(INGRDUSE),
     9  JSCR2(12),    JSCR3NGM(INGRDUSE,12),   JSCR3OUT(12),
     A  LENGD(INGRDUSE), LTYPE(ILMOUT,INGRDUSE), MB1, MB2, MINT,
     B   MSOUT1,  MSOUT2,  MZGNDOUT,  MNGM3,  MHOUT, MZFR, MZTRO,
     C      MSZGND(INGRDUSE), NGRIDATA(INGRDUSE),
C
C            NUMGDREQ, THE NUMBER OF NGM GRIDS NEEDED TO COVER THE
C            OUTPUT GRID, AND ZLOUT, ARE SET BY  OUTPREP  .
     D                NUMGDREQ,    ZLOUT(ILMOUT),
C
C          OUTPREP ALSO ASSIGNS THE FOLLOWING DESCRIPTORS TO BE
C          USED BY  OUTHORIZ  FOR HORIZONTAL INTERPOLATION.
     E    AHORD,BHORD,CHORD,DHORD, ILLD,IULD,  SCRHORD,  OUTHORD,
C
C
C          THE FOLLOWING ARRAY CONTAINS THE INTEGERS NEEDED TO
C          FORM THE LABEL THAT GOES WITH EACH OUTPUT ARRAY.
     F               LABEL84( 27 ),
C
C          THE FOLLOWING SYMBOLS REPRESENT TEMPORARY VALUES OF
C          INTEGERS TO GO INTO  LABEL84 WHEN THE OUTPUT ARRAY
C          HAS BEEN PREPARED FOR PACKING.
C
     G  LABQTYPE, LABSSUB1, LABTMARK,  S1VALUE,  S2VALUE,
     H  LABMLDIF, LABSSUB2, LABFSUB2, LABKGRID, LABNUMPT
CER
      INTEGER IDGRBE(25)
C
      COMMON/ADDRS/MIILL,MIIUL,MSA,MSB,MSC,MSD
C
      LOGICAL BITD,BITS
C            THIS ROUTINE ASSUMES SURFACE TYPE IS PRESSURE
      INTEGER I,I1X
      DATA LSSTYPE/008/
C
C          STAGING AREA FOR DATA TO BE HORIZONTALLY INTERPOLATED.
      DIMENSION FNGM(INSUMG)
C          REFERENCE SCRATCH ARRAYS TO REDEFINE SPACE IN /COMSCR/.
      DIMENSION S(INSUMG),IN(INSUMG),BITS(INSUMG)
      EQUIVALENCE ( SCR(1,1), S(1), IN(1), BITS(1), FNGM(1) )
C
C
C
C          LOOK FOR A DATA CARD FOR THE VARIABLE TYPE  LQQTYPE ,
C          THAT IS ALSO WANTED ON PRESSURE SURFACES.
      DO 10 N=1,NOUTVBLS
      NCARD=N
      IF( LQTYPE(N) .NE. LQQTYPE )  GO TO 10
C          RIGHT VARIABLE,,IS IT WANTED ON PRESSURE SURFACES??
      IF( LSTYPE(N) .EQ. LSSTYPE ) GO TO 20
C
   10 CONTINUE
C          NO DATA CARD
C
      RETURN
C
   20 CONTINUE
C
C          LOOK FOR THE PRESSURE LEVELS (L) THAT ARE WANTED.
      DO 100 L=1,LMOUT
C
      IF ( LEVELS(L,NCARD) .EQ. 0 ) GO TO 100
C          LEVEL L IS WANTED AS OUTPUT
C          PERFORM THE SIGMA TO PRESSURE INTERPOLATION FOR ALL
C          THE NGM GRIDS THAT ARE NEEDED, STORING THE RESULT FOR
C          EACH OF THEM IN ITS PROPER PLACE IN ARRAY   FNGM  .
C
C
C           S( JSCR3NGM( 1, NDATA ) ) CONTAINS PT (I,J,K)=(1,1,1)
C           OF SIGMA DATA FOR THE FIRST NEEDED NGM GRID.
C
        DO 40 NN=1,NUMGDREQ
        NG=NGRIDATA(NN)
C
C
C          NEED GRID NG DATA INTERPOLATED TO PRESSURE SURFACE L.
        JJ1=J1(NG)
        JJ2=J2(NG)
        LEN=LENGD(NG)
        IM=IMG(NG)
        JM=JMG(NG)
        IJ=IM*JM
C          THE RELATIVE ADDRESS OF (I,J,K)=(1,JJ1,1) IN NGM GRID NG.
        J1ADD=(JJ1-1)*IM + 1
C          THE ADDRESS, MINUS ONE, OF GRID  NG  IN ARRAY   S   .
        ISIG=JSCR3NGM( NN, NDATA ) -1
C          THE ADDRESS, AND DESCRIPTOR, OF THE GATHER INTEGERS IN
C          THE ARRAY  IN  .
        IADG=JADDK2L(NG,1) +(L-1)*LEN
C          THE ADDRESS, AND DESCRIPTOR, OF THE INTERPOLATION COEFFS.
C          IN THE ARRAY   S   .
        IADC=JADDK2L(NG,2) + (L-1)*LEN
C          THE ADDRESS, AND DESCRIPTOR, OF THE J1ADD POINT FOR GRID
C          NG, IN THE ARRAY  FNGM  .
        IADF=JADDFNGM(NG) +J1ADD -1
C          THE ADDRESS, AND DESCRIPTOR, OF THE AVAILABLE SCRATCH
C          ARRAY IN  S   .
        IADSCR=JSCR2(NWORK)
C
C          FIRST ASSUME THAT THE OUTPUT PRESSURE SURFACE LIES
C          COMPLETELY BETWEEN SIGMA LAYERS K=1 AND K=KM.
C
C          GATHER THE VALUES BELOW THE OUTPUT PRESSURE SURFACE.
C
C*****  Code Expanded From Routine:  RDHGATHR
      DO 77054 I = 1, LEN
         S(I+IADSCR-1) = S(IN(I+IADG-1)+ISIG)
77054 CONTINUE
CMIC$ DO ALL VECTOR SHARED(LEN, IADG, IJ, ISIG, IADF, IN, S, FNGM)
CMIC$1    PRIVATE(I1X)
      DO 77055 I1X = 1, LEN
         FNGM(I1X+IADF-1) = S(IN(I1X+IADG-1)+IJ+ISIG)
77055 CONTINUE
C*****  End of Code Expanded From Routine:  RDHGATHR
C
CMIC$ DO ALL VECTOR SHARED(LEN, IADF, IADSCR, IADC, FNGM, S) PRIVATE(
CMIC$1   IQ2W6E)
      DO 88890 IQ2W6E=1,LEN
         FNGM(IADF+IQ2W6E-1)=FNGM(IADF+IQ2W6E-1)-S(IADSCR+IQ2W6E-1)
         FNGM(IADF+IQ2W6E-1)=FNGM(IADF+IQ2W6E-1)*S(IADC+IQ2W6E-1)
         FNGM(IADF+IQ2W6E-1)=FNGM(IADF+IQ2W6E-1)+S(IADSCR+IQ2W6E-1)
88890 CONTINUE
C
C          DO WE HAVE POINTS OUTSIDE K=1,KM,,,,,
        IF( LTYPE(L,NG) .EQ. 0 ) GO TO 40
C
C
C          SOME POINTS ARE OUTIDE THIS RANGE.
C          (THEY HAVE A ZERO VALUE FOR THEIR GATHER INTEGER IN  ID .)
C
C          GET BIT VECTOR=1 WHERE THIS OCCURS.
CMIC$ DO ALL VECTOR SHARED(LEN, IADG, MB, IN, BITS) PRIVATE(IQ2W6E)
      DO 88920 IQ2W6E=1,LEN
         BITS(MB+IQ2W6E-1)=IN(IADG+IQ2W6E-1).EQ.0
88920 CONTINUE
C          GET THE ADDRESS IN  S  OF (I,J)=(1,JJ1) FOR K=1 OR KM
C          AND SELECT THE PROPER ASSIGNED VERTICAL DERIVATIVE.
        JAD=ISIG + J1ADD
        GAM=GAMBOT
        IF( LTYPE(L,NG) .LT. 0 ) GO TO 25
        JAD=JAD+(KM-1)*IJ
        GAM=GAMTOP
   25   CONTINUE
C
CMIC$ DO ALL VECTOR SHARED(LEN, MB, JAD, GAM, IADC, IADF, BITS, S, FNGM)
CMIC$1    PRIVATE(IQ2W6E)
      DO 88930 IQ2W6E=1,LEN
       IF ( BITS(MB+IQ2W6E-1)) THEN
         FNGM(IADF+IQ2W6E-1)=S(JAD+IQ2W6E-1)+GAM*S(IADC+IQ2W6E-1)
       END IF
88930 CONTINUE
C
C          END OF THE NG LOOP
   40   CONTINUE
C
C          ARRAY FNGM NOW CONTAINS THE DATA AT PRESSURE SURFACE L
C          FROM ALL NGM GRIDS THAT ARE REQUIRED FOR THE HORIZONTAL
C          INTERPOLATION TO THE OUTPUT GRID.
C             DO THIS INTERPOLATION
C
        CALL OUTHORIZ(IN(MIILL),IN(MIIUL),S(MSA),S(MSB),S(MSC),
     *                S(MSD),S(MSOUT1),S(MSOUT2))
C
C          SUBROUTINE OUTHORIZ HAS PUT OUTPUT GRID DATA IN S(MSOUT2)
C
C            SHOULD THIS DATA BE SMOOTHED??????
      IF( ISM .EQ. 1 ) CALL OUTSMOO(MSOUT2)
C
C          SUBROUTINE OUTPACK CAN USE S(MSOUT1) FOR SCRATCH.
C
C          NEED THE PRESSURE SURFACE VALUE IN MBS.
      SVALU =1000.E0*PLOUT(L)
C          ENSURE A WHOLE NUMBER
      S1VALUE=ANINT(SVALU)
C
      LABQTYPE=LQQTYPE
      LABSSUB1=LSSTYPE
C          UPDATE THE ARRAY  LABEL84  .
        CALL OUTNULAB
C
C
C  SET UP PDS INFO
C
      CALL LAB2PDS(LABEL84,IDGRBE)
      IDGRBE(7) =   0
      IF(LABEL84(1).EQ.16) IDGRBE(8) = 11
      IF(LABEL84(1).EQ.17) IDGRBE(8) = 17
      IF(LABEL84(1).EQ.19) IDGRBE(8) = 13
      IF(LABEL84(1).EQ.40) IDGRBE(8) = 39
      IF(LABEL84(1).EQ.42) IDGRBE(8) = 40
      IF(LABEL84(1).EQ.88) IDGRBE(8) = 52
      IF(LABEL84(1).EQ.95) IDGRBE(8) = 51
      IDGRBE(9) =   100
      IDGRBE(10) =  0
      IARG = LABEL84(6)
      IDGRBE(11) =  LABEL84(5) * 10.**IARG
      IDGRBE(18) =  LABEL84(3)
      IDGRBE(19) =  0
      IDGRBE(20) =  0

C
c       CALL OUTPACK(S(MSOUT2),IJOUT,S(MSOUT1),DESCRIPT(NCARD),
c    1    LABEL84, IOUTUNIT )
        CALL GRIBITN (S(MSOUT2),IJOUT,S(MSOUT1),DESCRIPT(NCARD),
     1    IDGRBE,IOUTGRIB,LABEL84 )
C
C          THIS IS THE END OF THE L (OUTPUT LEVEL) LOOP.
  100 CONTINUE
C
C
  200 RETURN
      END
