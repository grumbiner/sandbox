      SUBROUTINE RESTARTR(IOUTURST, IOUTUPRT, SPECS, VBL,
     1                    BITSEA, BITSNO, BITWVL)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM:    RESTARTR    READS RESTART FILE
C   PRGMMR: J TUCCILLO       ORG: W/NMC4     DATE: 90-06-19
C
C ABSTRACT:    READ RESTART FILE WITH A MAXIMUM RECORD LENGTH OF
C              INUMREC ELEMENTS
C
C PROGRAM HISTORY LOG:
C   90-06-19  J TUCCILLO
C   98-09-14  HANN-MING JUANG for Y2K
C   98-11-19  ERIC ROGERS REPLACED ON85 LABEL IN FIRST RECORD 
C             OF RESTART FILE WITH A BINARY NCEP DATE RECORD
C             ALSO HARDWIRED ICRAY=1 ALWAYS
C
C USAGE:  CALL RESTARTR(IOUTURST, IOUTUPRT, SPECS, VBL,
C                         BITSEA, BITSNO, BITWVL)
C   INPUT ARGUMENTS:
C     IOUTURST  -  UNIT NUMBER OF RESTART FILE
C     IOUTUPRT  -  UNIT NUMBER OF OUTPUT FILE
C     SPECS     -  SPECIFICATION FILE
C     VBL       -  CONTAINS NGM MODEL FIELDS
C     BITSEA    -  CONTAINS UNFROZEN WATER LOGICAL MAP
C     BITSNO    -  CONTAINS SNOW COVER LOGICAL MAP
C     BITWVL    -  CONTAINS WATER VS. LAND LOGICAL MAP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      INCLUDE 'parmodel'
C
      DIMENSION VBL    ( INVBL ),
     *          SPECS  ( INSPECS ),
     *          BITSEA ( IIJMAX * INGRDUSE ),
     *          BITSNO ( IIJMAX * INGRDUSE ),
     *          BITWVL ( IIJMAX * INGRDUSE )
C
      LOGICAL BITSEA, BITSNO, BITWVL
C
      DIMENSION INCDAT(8)
C
      COMMON/GOLDBERG/ ICRAY
C
C=======================================================================
C
CHMHJ --------------------- READ AND CHECK FILE ORIGIN ONLY ---
      READ (IOUTURST, ERR=600, END=700) INCDAT
      ICRAY = 1
      PRINT 501
501   FORMAT(' *** THIS RESTART FILE IS FROM THE CRAY')
C--------------------------------- HMHJ
C
      ILEFT = MOD ( INSPECS, INUMREC )
      ILOOP = INSPECS / INUMREC
      IST   = 1
      ISP   = INUMREC
      DO 1 II = 1, ILOOP
         READ ( IOUTURST, ERR=600, END=700 ) (SPECS(I),I=IST,ISP)
         IST = ISP + 1
         ISP = ISP + INUMREC
1     CONTINUE
      IF ( ILEFT .GT. 0 ) THEN
         ISP = INSPECS
         READ ( IOUTURST, ERR=600, END=700 ) (SPECS(I),I=IST,ISP)
      END IF
C
      ILEFT = MOD ( INVBL, INUMREC )
      ILOOP = INVBL / INUMREC
      IST   =     1
      ISP   = INUMREC
      DO 2 II = 1, ILOOP
        READ (IOUTURST, ERR=600, END=700) (VBL(I), I=IST, ISP)
        IST = ISP + 1
        ISP = ISP + INUMREC
2     CONTINUE
      IF( ILEFT .NE. 0) THEN
        ISP = INVBL
        READ (IOUTURST, ERR=600, END=700) (VBL(I), I=IST, ISP)
      END IF
C
C     SAME VALUES FOR ALL LOGICAL MASKS
C
      ILEN = IIJMAX * INGRDUSE
      ILEFT = MOD ( ILEN, INUMREC )
      ILOOP = ILEN / INUMREC
C
C
      IST   =     1
      ISP   = INUMREC
      DO 3 II = 1, ILOOP
        READ (IOUTURST, ERR=600, END=700) (BITSEA(I), I=IST, ISP)
        IST = ISP + 1
        ISP = ISP + INUMREC
3     CONTINUE
      IF( ILEFT .NE. 0) THEN
        ISP = ILEN
        READ (IOUTURST, ERR=600, END=700) (BITSEA(I), I=IST, ISP)
      END IF
C
      IST   =     1
      ISP   = INUMREC
      DO 4 II = 1, ILOOP
        READ (IOUTURST, ERR=600, END=700) (BITSNO(I), I=IST, ISP)
        IST = ISP + 1
        ISP = ISP + INUMREC
4     CONTINUE
      IF( ILEFT .NE. 0) THEN
        ISP = ILEN
        READ (IOUTURST, ERR=600, END=700) (BITSNO(I), I=IST, ISP)
      END IF
C
      IST   =     1
      ISP   = INUMREC
      DO 5 II = 1, ILOOP
        READ (IOUTURST, ERR=600, END=700) (BITWVL(I), I=IST, ISP)
        IST = ISP + 1
        ISP = ISP + INUMREC
5     CONTINUE
      IF( ILEFT .NE. 0) THEN
        ISP = ILEN
        READ (IOUTURST, ERR=600, END=700) (BITWVL(I), I=IST, ISP)
      END IF
C
      RETURN
C
C*****READ ERROR
C
  600     WRITE (IOUTUPRT, 650)
  650     FORMAT ('0', 'ERROR WHILE READING RESTART FILE '/
     1            '0', 11X, 'EXECUTION TERMINATING.' /////)
C
      CALL EXIT(16)
C
C*****END OF FILE
C
  700     WRITE (IOUTUPRT, 800)
  800     FORMAT ('0', 'INPUT END-OF-FILE ENCOUNTERED PREMATURELY ',
     1                 'WHEN READING RESTART FILE '/
     2            '0', 11X, 'EXECUTION TERMINATING.' /////)
C
      CALL EXIT(16)
      END
