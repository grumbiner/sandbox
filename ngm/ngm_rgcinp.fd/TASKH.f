      SUBROUTINE TASKH ( INUALB , INUZ0 , INUMA , INUTSD )
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKH       INTERPOLATES SFC CHARACTERISTICS TO GRIDS
C   PRGMMR: TUCCILLO         ORG: W/NMC22    DATE: 86-10-21
C
C ABSTRACT: INTERPOLATES SURFACE CHARACTERISTICS TO THE GRID
C
C PROGRAM HISTORY LOG:
C   86-10-21  JIM TUCCILLO
C   88-08-03  BRIAN SCHMIDT ADDED THE DOCBLOCK
C   90-01-05  MIKE PECNIC IMPROVED SFC MOISTURE NEAR COASTLINES
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP
C
C   INPUT ARGUMENT LIST:
C     INUALB   - UNIT NUMBER FOR FILE OF 1 X 1 ALBEDO
C     INUZ0    - UNIT NUMBER FOR FILE OF R40 G.G. Z0
C     INUMA    - UNIT NUMBER FOR FILE OF 1 X 1 AVAILABLE MOISTURE
C     INUTSD   - UNIT NUMBER FOR FILE OF 1 X 1 TERRAIN STANDARD DEV.
C
C   INPUT FILES:
C     INUALB   - ALBEDO FILE
C     INUZ0    - Z0 FILE
C     INUMA    - AVAILABLE MOISTURE FILE
C     INUTSD   - TERRAIN STANDARD DEVIATION FILE
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C                  GLATS
C                  GRDSET
C                  INTERP
C                  STATS
C     LIBRARY:
C       COMMON   - CONSTN
C                  DUMMY2
C                  BLANK (CONTAINS VBL ARRAY)
C
C REMARKS: THIS PROGRAM IS FOR REAL INPUT AND OUTPUT
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C     THIS SUBROUTINE READS ALBEDO, Z0, MA, AND TERRAIN SD
C     AND INTERPOLATES THESE VALUES TO THE NGM GRID(S).
C                                            J. TUCCILLO (12 DEC 1985)
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      CHARACTER*1  LABEL85(32)
      CHARACTER*1  CHAR01 ( 8)
      CHARACTER*35 ERRMSG(4)
C
      COMMON           VBL( INVBL )
C
      COMMON /DUMMY2 / ANAL( ILONFP1 ,  INLAT ,  ILEVS )
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
C     SPECIFY THE SIZES OF THE LAT/LON FIXED FIELDS FOR
C     SURFACE ROUGHNESS AND ALBEDO (ASSUME ONE-POINT WRAP
C        AROUND IN LONGITUDE).
C
      PARAMETER (IMAXZ0 = 129,
     *           JMAXZ0 = 102,
     *           IMAXALB = 361,
     *           JMAXALB = 180)
C
      REAL WGTCS(JMAXZ0/2),RCS2(JMAXZ0/2),COLRAD(JMAXZ0),
     *     WGT(JMAXZ0/2),SLAT(JMAXZ0),WLAT(JMAXZ0)
      DIMENSION BUFLAB85(8)
      DIMENSION SPECIF(10), ISPECIF(10)
      DIMENSION ALB(IMAXALB, JMAXALB),Z0(IMAXZ0, JMAXZ0),
     *          AMA(IMAXALB, JMAXALB),TSD(IMAXALB,JMAXALB)
      DIMENSION ALONGZ0(IMAXZ0)
      DIMENSION ALATZ0 (JMAXZ0)
      DIMENSION ALONGALB(IMAXALB)
      DIMENSION ALATALB (JMAXALB)
C
      EQUIVALENCE (BUFLAB85, LABEL85)
      EQUIVALENCE (ANAL(1,1,1), ALB(1,1))
      EQUIVALENCE (ITEMP, CHAR01(1))
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
C
C     ENSURE THAT THE SIZE OF ARRAY ALB IS LESS THAN THE SIZE OF
C     ARRAY ANAL.
      LONFP10 =  ILONFP1
      NLAT0   =  INLAT
      LEVS0   =  ILEVS
      IF ((LONFP10 * NLAT0 * LEVS0) .LT. (IMAXALB * JMAXALB)) THEN
         WRITE (IOUTUPRT, 50)
   50    FORMAT ('0', 'THE SIZE OF ARRAY ANAL IN SUBROUTINE TASKH ',
     1                'IS SMALLER THAN THAT OF ARRAY ALB.'/
     2           ' ', 'EXECUTION TERMINATING.'//)
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
         ERRMSG(1) = 'IN SUBROUTINE TASKH    NEAR STATEME'
         ERRMSG(2) = 'NT NUMBER 50:                      '
         ERRMSG(3) = 'THE SIZE OF ARRAY ANAL IS SMALLER T'
         ERRMSG(4) = 'HAN THAT OF ARRAY ALB.             '
C
         CALL ERRSTOP (IOUTUPRT, ERRMSG)
      END IF
C
C
      IHOUR0  = NINT(SPECS(2))
      IDAY0   = NINT(SPECS(3))
      IMONTH0 = NINT(SPECS(4))
      IYEAR0  = NINT(SPECS(5))
C
C     READ ALBEDO FIELD
C
      IMREAD = (IMONTH0-1)/3 + 1
      IRECORD = 0
      DO 666 KMONTH=1,IMREAD
            IRECORD = IRECORD + 1
            READ (INUALB, ERR=2010, END=2050) ALB
            PRINT 7739 , IRECORD
7739        FORMAT(' ***** JUST READ ALBEDO FOR SEASON ',I6)
666   CONTINUE
C
C     READ Z0 FIELD
C
      IRECORD = 1
      READ (INUZ0, ERR=3010, END=3050) Z0
      PRINT 5739
5739  FORMAT(' ***** JUST READ Z0 FIELD     ')
C
C     READ MOISTURE AVAIBALITY
C
      IRECORD = 1
      READ (INUMA, ERR=4010, END=4050) AMA
      PRINT 7785
7785  FORMAT(' ***** JUST READ MOIST AVAILABILITY FIELD ')
C
C     READ TERRAIN STANDARD DEVIATION
C
      IRECORD = 1
      READ (INUTSD, ERR=5010, END=5050) TSD
      PRINT 7786
7786  FORMAT(' ***** JUST READ TERRAIN STANDARD DEVIATION ')
C
C     ERROR CHECK ALBEDO
C     SCALE SO THAT IT RANGES FROM 0. - 1.
C
      DO 200  J=1,JMAXALB
         DO 200  I=1,IMAXALB
            ALB(I,J)=ALB(I,J)*0.01E0
            IF(ALB(I,J).GT.0.8E0) ALB(I,J)=0.8E0
            IF(ALB(I,J).LT.0.06E0) ALB(I,J)=0.06E0
  200 CONTINUE
      print *,' done with loop 200'
C
C     REVERSE ORDER OF Z0 FIELD
C     Z0 , FROM GLOBAL GROUP, STARTS AT NORTH POLE
C     JEH'S INTERPOLATION ASSUMES THE FIELD STARTS AT THE SOUTH POLE
C
      JLIM = JMAXZ0 / 2
      DO 6666 J=1,JLIM
         DO 6666 I=1,IMAXZ0
            ATEMP = Z0(I,J)
            Z0(I,J) = Z0(I , JMAXZ0 + 1 - J)
            Z0(I , JMAXZ0 + 1 - J ) = ATEMP
6666  CONTINUE
      print *,' done with loop 6666'
C
C     CONVERT Z0 TO METERS FROM CENTIMETERS
C
      DO 250  J = 1, JMAXZ0
      DO 250  I = 1, IMAXZ0
      Z0(I,J) = Z0(I,J) * 0.01E0
  250 CONTINUE
      print *,' done with loop 250'
C
C     INTERPOLATE THE Z0 ANALYSIS FROM THE R40 GAUSSIAN
C     GRID TO THE NGM POLAR STEREOGRAPHIC GRIDS
C
      KMAX = 1
      ISYM = 0
      FACTOR = 1.0E0
C
C     SPECIFY THE LOCATIONS OF THE GRID IN LATITUDE AND
C     LONGITUDE.
C
      DO 7753 I=1,IMAXALB
         ALONGALB(I)=0.5E0 +  REAL(I-1)
7753  CONTINUE
      ALONGALB(IMAXALB)=ALONGALB(1)
C
      DO 7754 J=1,JMAXALB
         ALATALB(J)=-89.5E0 +  REAL(J-1)
7754  CONTINUE
C
C
      DO 1100 I=1,IMAXZ0
         ALONGZ0(I)= 0.E0+( REAL(I-1) / REAL (IMAXZ0-1) ) * 360.E0
 1100 CONTINUE
      ALONGZ0(IMAXZ0) = 0.E0
C
      JLIM = JMAXZ0/2
      print *,' enter glats'
      CALL SPLAT(4,JMAXZ0,SLAT,WLAT)
c     CALL GLATS (JMAXZ0,COLRAD,WGT,WGTCS,RCS2)
      print *,' exit glats'
      JINC = 0
      DO 7777 J=JMAXZ0,1,-1
              JINC = JINC + 1
              COLRAD(J) = ASIN(SLAT(J))
              ALATZ0(JINC) = COLRAD(J)*180.E0/3.14159E0
              print *,j,jinc,colrad(j),alatz0(jinc)
7777  CONTINUE
C
      print *,'start interp '
      CALL STATS (ALB,1,IMAXALB,IMAXALB,1,JMAXALB,JMAXALB,1,KMAX,KMAX,
     1            'INPUT ALBEDO ANAL ')
C
      CALL INTERP(ALB, IMAXALB, JMAXALB, KMAX, ALONGALB, ALATALB,
     1            ISYM, INDEXALB, FACTOR)
C
      CALL STATS (AMA,1,IMAXALB,IMAXALB,1,JMAXALB,JMAXALB,1,KMAX,KMAX,
     1            'INPUT MOIST AVAIL ')
C
C     CALL INTERP(AMA, IMAXALB, JMAXALB, KMAX, ALONGALB, ALATALB,
C    1            ISYM, INDEXMA, FACTOR)
C
      CALL STATS (TSD,1,IMAXALB,IMAXALB,1,JMAXALB,JMAXALB,1,KMAX,KMAX,
     1            'INPUT TER STD DEV ')
C
      CALL INTERP(TSD, IMAXALB, JMAXALB, KMAX, ALONGALB, ALATALB,
     1            ISYM, INDEXTSD, FACTOR)
C
      CALL STATS (Z0, 1,IMAXZ0,IMAXZ0, 1,JMAXZ0,JMAXZ0, 1,KMAX,KMAX,
     1            'INPUT Z0   ANAL ')
C
      CALL INTERP(Z0, IMAXZ0, JMAXZ0, KMAX, ALONGZ0, ALATZ0,
     1            ISYM, INDEXZ0, FACTOR)
C
C     ASSIGN ALBEDO AND Z0 ANALYSIS CHARACTERISTICS TO SPECS ARRAY
C
      SPECS(301) = JMAXALB
      SPECS(302) = ALATALB(1)
      SPECS(303) = 1.E0
      SPECS(304) = ALATALB(JMAXALB)
      SPECS(305) = IMAXALB
      SPECS(306) = ALONGALB(1)
      SPECS(307) = 1.E0
      SPECS(308) = ALONGALB(IMAXALB)
C
C     RESOLVE WATER VS. LAND INCONSISTENCIES
C
      DO 4400 NG=1,NGRDUSE
         CALL GRDSET (NG, INDEXH,
     *                DUMMY, IM, JM, XPOL, YPOL, CMU, DLAM)
         IJ = IM * JM
         MADDALB = IADDRG(INDEXALB, NG)
         MADDZ0  = IADDRG(INDEXZ0,  NG)
         MADDMA  = IADDRG(INDEXMA,  NG)
         MADDCG  = IADDRG(INDEXCG,  NG)
       DO 300  L=1,IJ
         VBL(MADDCG+L-1) = 65000.E0
         IF(BITSEA(L,NG)) THEN
           VBL(MADDALB+L-1) = 0.06E0
         ELSE
           IF(VBL(MADDALB+L-1).LT.0.15E0) VBL(MADDALB+L-1)=0.15E0
           IF(VBL(MADDZ0 +L-1).LT.0.01E0) VBL(MADDZ0 +L-1)=0.01E0
         ENDIF
  300  CONTINUE
C
C   NOW DO THE MOISTURE AVAILABILITY INTERPOLATION
C
      DO 4300 J=1,JM
         YNGM = REAL(J) - YPOL
C
         DO 4299 I=1,IM
            XNGM = REAL(I) - XPOL
            MADDMA  = IADDRG(INDEXMA, NG)  -1 -IM +I +IM*J
C
      IJPLACE = (J-1)*IM + I
      IF (BITWVL(IJPLACE,NG)) THEN
         VBL(MADDMA) = 1.0E0
      ELSE
C
C     DETERMINE THE LATITUDE AND LONGITUDE OF THE NGM GRID POINT.
            IRANGE = 1
            IPRT = 0
C     THE LONGITUDE WILL RANGE FROM 0 AT THE GREENWICH MERIDIAN
C     EASTWARD TO 360 DEGREES FOLLOWING THE CALL TO SUBROUTINE
C     CORNER WITH IRANGE=1.
            CALL CORNER (XNGM, YNGM, CMU, DLAM, IRANGE, IPRT,
     1                   XLONG, YLAT)
C
C     ENSURE LATITUDE IS WITHIN RANGE.
            IF (ABS(YLAT) .LE. 90.E0) GO TO 3320
            WRITE (IOUTUPRT, 3310) YLAT
 3310       FORMAT ('0', 'LATITUDE OUT OF RANGE IN SUBROUTINE TASKH. ',
     1                   'YLAT=', F8.2, '.  JOB TERMINATED.')
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
            ERRMSG(1) = 'IN SUBROUTINE TASKH    NEAR STATEME'
            ERRMSG(2) = 'NT NUMBER 3310:                    '
            ERRMSG(3) = 'THE LATITUDE IS OUT OF RANGE.      '
            ERRMSG(4) = '                                   '
C
            CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
 3320       CONTINUE
C     SET UP EQUATORIAL BOUNDARY CONDITION.
C     ALL FIELDS IN THIS SUBROUTINE ARE ASSUMED TO BE EVEN
C     WITH RESPECT TO EQUATORIAL SYMMETRY.
      YYLAT = ABS(YLAT)
C
C    NOW DETERMINE THE LOCATION OF THE NGM POINT WITH RESPECT TO
C    THE VARIOUS DATA FIELDS
C
C     DETERMINE THE LOCATION OF THE NGM GRID POINT WITHIN THE
C     1-DEGREE-BY-1-DEGREE HORIZONTAL MOISTURE AVAILABILITY FIELD.
C
C
      CALL FIND (YYLAT,ALATALB,JMAXALB,LAT0)
      LONG0 = INT(XLONG - 0.5E0) + 1
C
C
      XRATIO = XLONG - ALONGALB(LONG0)
      YRATIO = YYLAT - ALATALB(LAT0)
C
      II = LONG0
      JJ = LAT0
      IIP1= II + 1
      JJP1= JJ + 1
C
      AREA11 = (1.0E0 - XRATIO) * (1.0E0 - YRATIO)
      AREA21 = XRATIO * (1.0E0 - YRATIO)
      AREA12 = (1.0E0 - XRATIO) * YRATIO
      AREA22 = XRATIO * YRATIO
C
      KOUNT = 0
C
      IF(AMA(II, JJ) .LT. 1.E0) THEN
         KOUNT = KOUNT + 1
         AREA = AREA11
         IPOINT = II
         JPOINT = JJ
      END IF
C
      IF(AMA(II, JJP1) .LT. 1.E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA12 .GT. AREA) THEN
            AREA = AREA12
            IPOINT = II
            JPOINT = JJP1
         END IF
      END IF
C
      IF(AMA(IIP1, JJ) .LT. 1.E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA21 .GT. AREA) THEN
            AREA = AREA21
            IPOINT = IIP1
            JPOINT = JJ
         END IF
      END IF
C
      IF(AMA(IIP1 , JJP1) .LT. 1.E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA22 .GT. AREA) THEN
            AREA = AREA22
            IPOINT = IIP1
            JPOINT = JJP1
         END IF
      END IF
C
C     DETERMINE MA CONSIDERING THE NUMBER OF POINTS SURROUNDING THE
C     NGM GRID POINT WITH MA LESS THAN 1
C
      IF(KOUNT .GT. 0) GO TO 3461
C
C     NONE OF THE SURROUNDING POINTS OF THE 1-D X 1-D FIELD OF MOISTURE
C     AVAILABILITY ARE LESS THAN 1.
      VBL(MADDMA) = 0.15E0
C
      WRITE (IOUTUPRT, 3450) LONG0, LAT0, AMA(LONG0,LAT0), II, JJ,
     1            AMA(II,JJ),     AMA(II,JJP1),
     2            AMA(IIP1,JJP1), AMA(IIP1,JJ),
     3            I, J, NG
 3450 FORMAT (' ', 'ISLAND?', 2X, 2I4, F5.1, 2I4, 4F7.1, 3I5)
      GO TO 4001
C
 3461 CONTINUE
C     IF 1, 2, OR 3 OF THE SURROUNDING POINTS HAVE MOISTURE AVAIL.
C     LESS THAN 1, THE VALUE FOR THE NEAREST
C     POINT OF THE MOISTURE AVAILABILITY FIELD FOUND
C     SATISFYING THIS CONSTRAINT IS USED.
C
      IF(KOUNT .EQ. 4) GO TO 3471
C
      VBL(MADDMA)  = AMA(IPOINT, JPOINT)
C
      GO TO 4001
C
 3471 CONTINUE
C     ALL 4 POINTS SURROUNDING THE NGM POINT HAVE
C     SATISFACTORY MOISTURE AVAILABILITIES.
      VBL(MADDMA) = (1.E0 - XRATIO) * (1.E0 - YRATIO) * AMA(II,JJ)
     1                + XRATIO * (1.E0 -YRATIO) * AMA(IIP1,JJ)
     2                + (1.E0 - XRATIO) * YRATIO * AMA(II,JJP1)
     3                + XRATIO * YRATIO * AMA(IIP1,JJP1)
      GO TO 4001
C
 4001    CONTINUE
        END IF
 4299  CONTINUE
 4300  CONTINUE
 4400 CONTINUE
C
C
      GOTO 66666
C***********************************************************************
C***********************************************************************
C
C
 2010 CONTINUE
C     ERROR OCCURRED WHEN INPUTING THE ALBEDO
C     ANALYSIS.
      WRITE (IOUTUPRT, 2020) INUALB, IRECORD
 2020 FORMAT ('1', 'ERROR OCCURRED WHEN READING IN THE ALBEDO ',
     1             'ANALYSIS ON UNIT', I3, ' FOR RECORD', I2,
     2             '.' //)
C
      GO TO 2100
C
 2050 CONTINUE
C     UNEXPECTED END OF FILE HIT
C     WHEN INPUTING THE ALBEDO ANALYSIS.
      WRITE (IOUTUPRT, 2060) INUALB, IRECORD
 2060 FORMAT ('1', 'UNEXPECTED END OF FILE HIT WHEN READING ',
     1             'IN THE ALBEDO ANALYSIS ',
     2             'ON UNIT', I3, ' FOR RECORD', I2,
     3             '.' //)
C
C
 2100 CONTINUE
C     THERE WAS A PROBLEM WITH THE ALBEDO
      GOTO 66666
C
C
 3010 CONTINUE
C     ERROR OCCURRED WHEN INPUTING THE Z0 FIELD
C     ANALYSIS.
      WRITE (IOUTUPRT, 3020) INUZ0, IRECORD
 3020 FORMAT ('1', 'ERROR OCCURRED WHEN READING IN THE Z0 ',
     1             'ANALYSIS ON UNIT', I3, ' FOR RECORD', I2,
     2             '.' //)
C
      GO TO 3100
C
 3050 CONTINUE
C     UNEXPECTED END OF FILE HIT
C     WHEN INPUTING THE Z0 ANALYSIS.
      WRITE (IOUTUPRT, 3060) INUZ0, IRECORD
 3060 FORMAT ('1', 'UNEXPECTED END OF FILE HIT WHEN READING ',
     1             'IN THE Z0 ANALYSIS ',
     2             'ON UNIT', I3, ' FOR RECORD', I2,
     3             '.' //)
C
C
 3100 CONTINUE
C     THERE WAS A PROBLEM WITH THE Z0
      GOTO 66666
C
C
 4010 CONTINUE
C     ERROR OCCURRED WHEN INPUTING THE MA FIELD
C     ANALYSIS.
      WRITE (IOUTUPRT, 4020) INUMA, IRECORD
 4020 FORMAT ('1', 'ERROR OCCURRED WHEN READING IN THE MA ',
     1             'ANALYSIS ON UNIT', I3, ' FOR RECORD', I2,
     2             '.' //)
C
      GO TO 4100
C
 4050 CONTINUE
C     UNEXPECTED END OF FILE HIT
C     WHEN INPUTING THE MA ANALYSIS.
      WRITE (IOUTUPRT, 4060) INUMA, IRECORD
 4060 FORMAT ('1', 'UNEXPECTED END OF FILE HIT WHEN READING ',
     1             'IN THE MA ANALYSIS ',
     2             'ON UNIT', I3, ' FOR RECORD', I2,
     3             '.' //)
C
C
 4100 CONTINUE
C     THERE WAS A PROBLEM WITH THE MOISTURE AVAILABLITY
      GOTO 66666
C
C
 5010 CONTINUE
C     ERROR OCCURRED WHEN INPUTING THE TSD ANALYSIS
      WRITE (IOUTUPRT, 5020) INUTSD, IRECORD
 5020 FORMAT ('1', 'ERROR OCCURRED WHEN READING IN THE TSD ',
     1             'ANALYSIS ON UNIT', I3, ' FOR RECORD', I2,
     2             '.' //)
C
      GO TO 5100
 5050 CONTINUE
C     UNEXPECTED END OF FILE HIT
C     WHEN INPUTING THE TSD ANALYSIS.
      WRITE (IOUTUPRT, 5060) INUTSD, IRECORD
 5060 FORMAT ('1', 'UNEXPECTED END OF FILE HIT WHEN READING ',
     1             'IN THE TSD ANALYSIS ',
     2             'ON UNIT', I3, ' FOR RECORD', I2,
     3             '.' //)
C
C
 5100 CONTINUE
C     THERE WAS A PROBLEM WITH THE TSD FIELD
C
66666 RETURN
C
      END
