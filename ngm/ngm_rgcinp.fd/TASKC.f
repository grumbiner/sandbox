      SUBROUTINE TASKC (INSNOICE,INSST1DG,INDXST,INMSKNGM,INMSKIMS)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKC       INTERPOLATES SNOW/ICE AND SST TO NGM GRID
C
C ABSTRACT: INTERPOLATES HORIZONTALLY SST, SKIN TEMPERATURE, 
C   SNOW/SEA-ICE, AND 1-DEG LAT/LON LAND/SEA MASK (TO SPECIFY THE
C   LAND/SEA MASK - BITWVL), THE SNOW/ICE MASK (BITSNO), AND THE
C   OPEN WATER SURFACE HEAT FLUX MASK (BITSEA) AT NGM GRID POINTS
C
C PROGRAM HISTORY LOG:
C
C   85-03-27  JIM HOKE
C   87-12-27  MIKE PECNICK ADDED HIGH RES SNOW COVER
C   88-08-03  BRIAN SCHMIDT ADDED DOCBLOCK
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP
C   92-11-23  KEN MITCHELL ADDED USAF SNOW COVER, USAF LAND MASK,
C                          CAC/SAB LAND MASK, AND NEW GREAT LAKES MASK
C   97-03-07  KEN MITCHELL SWITCHED FROM NAS TO CRAY INPUT FILES OF
C                           NESDIS AND USAF SNOW/ICE AND CPC DAILY GLOBAL
C                           1-DEG SST.  REPLACED 2.0 DEG SST INTERPOLATION
C                           WITH 1-DEG SST INTERPOLATION
C   97-03-20  ERIC ROGERS FIXED BUG IN INTERPOLATION OF 1-DEG SST FIELD
C                          NEAR GREENWICH MERIDIAN
C   98-09-09  HANN-MING JUANG  FIX THE UNDEFINED VARIABLE AND CONVERT Y2K
C   98-11-24  YING LIN REPLACED USAF DAILY SNOW/ICE AND NESDIS/SAB WEEKLY 
C             SNOW/ICE NESDIS/IMS DAILY ICE/SNOW COVER
C
C USAGE:    CALL TASKC (INSNOICE, INSST1DG, INDXST, INMSKNGM, INMSKIMS)
C
C   INPUT ARGUMENT LIST:
C     INSNOICE - UNIT NUMBER OF FILE CONTAINING NESDIS/IMS SNOW/ICE
C     INSST1DG - UNIT NUMBER OF GRIB FILE CONTAINING DAILY GLOBAL SST
C     INDXST   - UNIT NUMBER OF GRIB INDEX FILE FOR INSST1DG
C     INMSKNGM - UNIT NUMBER OF 1 DEG LAT/LON N.H. NGM LAND/SEA MASK
C     INMSKIMS - UNIT NUMBER OF IMS 1024 X 1024 N.H. LAND/SEA MASK
C
C   INPUT FILES:
C     INSNOICE - NESDIS/IMS DAILY 1/16 BEDIENT N.H. SNOW/SEA-ICE COVER
C     INSST1DG - CPC DAILY 1-DEG GLOBAL SST
C     INDXST   - GRIB INDEX FILE FOR INSST1DG
C     INMSKNGM - 1 DEG LAT/LON  361 X 91 N.H. NGM LAND/SEA MASK
C     INMSKIMS - NESDIS/IMS 1024 X 1024 POLAR STEREO N.H. LAND/SEA MASK
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - CORNER
C                  ERRSTOP
C                  GRDSET
C                  GRIBST
C                  SNO16GET
C                  PRINTBIT
C                  PRINTIMS
C                  STATS
C     LIBRARY:
C
C       COMMON   - CONSTN
C                  BLANK (CONTAINS ARRAY VBL)
C                  DATE (TIME OF INITIAL CONDITION, PASSED ON FROM MAIN.F)
C       W3LIB    - IW3JDN
C                  W3FB05
C
c Remarks: 
c   NESDIS/IMS 1/16 bedient (23-km) snow/ice coverage is bin'd into
c   NGM grid boxes.
c    
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77/90
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
      INCLUDE 'parmodel'
C
      PARAMETER  (H90=90.0,H360=360.0,D5=5.E-1,D00=0.0,H1=1.0)
C
      CHARACTER*35 ERRMSG(4)
C
      LOGICAL  LKMSK,LWVL,LDO,ICESNOW
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
      COMMON /YYMMDD/ ICENYEAR, IMONTH, IDAY, IHOUR
C  The above denote the time of the run cycle.  (To be compared to the
C  date stamp in the snow file to see if the snow file is fresh enough).
C
      COMMON          VBL( INVBL )
C
C                                   N. HEM, 1-DEG LAND MASK
      DIMENSION    HLWC(361,91)
C                                   GLOBAL  1-DEG CPC SST ANAL
      DIMENSION    SSTH(361,180)
C                                   N. MEM. 1/16-BEDIENT NESDIS/IMS SNOW/ICE
      REAL         SCVH    (1024,1024)
      INTEGER      MSKSCVH (1024,1024)
C
C  NLAND - number of IMS land points gribbed into a bin around an
C          NGM grid point
C  NSEA -  number of IMS  sea points gribbed into a bin around an
C          NGM grid point
C  NSNO -  number of IMS snow points in each bin
C  NICE -  number of IMS  ice points in each bin
C
      INTEGER      NLAND(IIJMAX,INGRDUSE), NSEA(IIJMAX,INGRDUSE),
     &             NSNO (IIJMAX,INGRDUSE), NICE(IIJMAX,INGRDUSE)
      DIMENSION    SCPLT  (IIJMAX,INGRDUSE)
C
      DIMENSION    IMS    (INGRDUSE),  JMS   (INGRDUSE),
     1             XPOLS  (INGRDUSE),  YPOLS (INGRDUSE),
     2             CMUS   (INGRDUSE),  DLAMS (INGRDUSE)
C
C**************************  BEGIN EXECUTION ***********************
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
      NGRDUSE = INGRDUSE
C
C  SET NGPRT TO LARGER THAN NGRDUSE TO TURN OFF MUCH DEBUG PRINT
      NGPRT    = NGRDUSE
C
C     DEGRAD IS THE CONVERSION FACTOR FROM DEGREES TO RADIANS
C
      DEGRAD = 3.141592654E0 / 180.E0
C
C     EVEN SYMMETRY ABOUT EQUATOR IS SPECIFIED FOR THE FIELDS.
      ISYM = 2
C
C  SPECIFY PARAMETERS OF THE 1024 X 1024 NMC 1/16-BEDIENT GRID.
C  SOME PARAMETERS GIVEN FOR DOCUMENTATION PURP0SES, BUT NOT USED.
C  THE 1024 X 1024 IS SUBSET OF 1025 X 1025 1/16TH BEDIENT POLAR STEREO 
C  GRID TRUE AT 60 N AND CENTERED ON THE N. POLE, BUT WITHOUT LAST
C  ROW AND COLUMN (WHICH ARE OFF THE WORLD).  I-COLUMN AND J-ROW
C  INDEXING FOLLOWS THE NMC CONVENTION OF LEFT-RIGHT AND BOTTOM-UP.
C  HENCE POINT (1,1) IS IN LOWER LEFT CORNER AND POLE IS (513,512).
C
C  LOCATION OF POLE IN NMC CONVENTION
      XPNMC = 513.
      YPNMC = 512.
C  GRID MESH LENGTH AT 60N IN KM
      XMESHL  = 23.8125
C     N -- THE NUMBER OF GRID INTERVALS FROM THE POLE TO THE EQUATOR:
      ENNMC = 16.0E0 * 31.2043316E0
C  THE LONGITUDE ROTATION ANGLE OF THE GRID
C  (WITH RESPECT TO NGM ROTATION CONVENTION)
      ALNMC = +10.E0
C  THE ORIENTATION WEST LONGITUDE OF THE GRID
      ORIENT = 80.0E0
C  INITIALIZE THE LOGICAL FLAG FOR THE LAKE MASK:
      LKMSK   = .TRUE.
C
C  IF UNIT NO. INMSKIMS IS NEGATIVE, SKIP GREAT LAKES MASK MOD.
C.... IF (INMSKIMS .LT. 0)  LKMSK = .FALSE.
      INMSKIMS = IABS (INMSKIMS)
C
C  PRINT STATUS OF ABOVE FLAGS
      WRITE(IOUTUPRT,2211)  LKMSK
 2211 FORMAT(//1H ,5X,'  TASKC WILL MODIFY NGM GREAT LAKES MASK = ',L2)
C
C  INPUT THE 1 DEG X 1 DEG NGM LAND-WATER MASK,
C                                          (SEA=0.,LAND=1.)
      READ(INMSKNGM) HLWC
C
C============= READ IN THE DAILY GLOBAL 1-DEG CPC SST FIELD ===========
C
      CALL BAOPEN(INSST1DG,'fort.24',IRETO)
      CALL GRIBST(INSST1DG,INDXST,SSTH,IERR)
C
      IF (IERR.NE.0) THEN  
      WRITE (IOUTUPRT, 4558) INSST1DG
 4558 FORMAT ('0', 'ERROR OCCURRED WHEN READING IN SST        ',
     1             'FROM UNIT', I3, ' IN GRIB ' /
     2        ' ', 'EXECUTION TERMINATING.')
C
      STOP 222
      ENDIF
C
C  CALCULATE AND PRINT STATS FOR THE SST FIELD
            CALL STATS (SSTH,  1,361,361, 1,180,180,   1,1,1,
     1            'INPT SEA SFC TMP')
C
C========= READ NESDIS/IMS N.H. SNOW/ICE =====================
C
C  (MSKSCVH IS PASSED IN AS WORK ARRAY, LATER READ-IN AS LAND MASK)
C  
c 
      CALL SNO16GET(SCVH,IYY,IMM,IDD,INSNOICE,IOUTUPRT)
C
      NDAY=IW3JDN(IYY,IMM,IDD)
      WRITE (IOUTUPRT, 1) INSNOICE, IYY, IMM, IDD, NDAY 
    1 FORMAT(1H ,' NESDIS/IMS SNOW/ICE READ IN VIA UNIT NO.=',I3/
     2       1H ,' FOR VALID DATE OF Y-M-D =',I4,'-',I2,'-',I2/
     3       1H ,' WHICH IS JULIAN DAY =', I10) 
C
C   READ THE NESDIS-IMS LAND-SEA MASK
C                                          (SEA=0,LAND=1)
      DO J = 1, 1024
         READ(INMSKIMS,'(80I1)') (MSKSCVH(I,J),I=1,1024)
      ENDDO
C
      CALL PRINTIMS (SCVH, MSKSCVH)
C  CALCULATE AND PRINT STATS FOR THE NESDIS SNOW/ICE FIELD
      CALL STATS (SCVH,  1,1024,1024,   1,1024,1024,   1,1,1,
     1            'INPT IMS SNO/ICE')
C
      NGMDAY=IW3JDN(ICENYEAR,IMONTH,IDAY)
      WRITE(IOUTUPRT,2) ICENYEAR, IMONTH, IDAY, NGMDAY 
    2 FORMAT(1H ,' INITIAL NGM INITIAL CONDITION IS FOR DATE OF'/
     1       1H ,' Y-M-D =',I4,'-',I2,'-',I2/
     2       1H ,' WHICH IS JULIAN DAY =', I10) 
C
      IF (NGMDAY .GT. NDAY+4) THEN
         WRITE(IOUTUPRT,2644) NDAY,NGMDAY
 2644    FORMAT(/1H ,'******  WARNING   ****** WARNING ******'/
     1           1H ,'NESDIS/IMS SNOW IS MORE THAN 4 DAYS OLD'/
     2           1H ,43X,'   NESDIS SNOW ANAL JULDAY=',I7/
     3           1H ,43X,'           NGM PREP JULDAY=',I7)
      ENDIF
C
C
C---------- INPUT AND CHECK OF INPUT IMS FIELDS IS COMPLETE ------
C
C  ....................  BEGIN PROCESSING .........................
C
C------------ NEXT PROCESS IMS SNOW/ICE -------
C
C           ..... PROCESSING STEPS ..........
C  STEP 1: BIN IMS POINTS TO CLOSEST NGM POINT.
C          TO DO SO, LOOP OVER 1024 X 1024 IMS N.H. GRID AND
C          FOR EACH IMS POINT, FIND OUT FROM THE IMS MASK WHETHER 
C          IT IS A LAND OR SEA POINT; DETERMINE CLOSEST NGM POINT 
C          AND ACCUMULATE (BIN) INTO ARRAY NLAND OR NSEA ON NGM GRID
C       
C          BIN THE IMS SNOW/ICE POINTS INTO ARRAY NSNO/NICE ON NGM GRID
C
C  STEP 2: LASTLY, IN A LATER LOOP OVER ALL NGM LAND POINTS,
C          USE ABOVE COUNTS EMBODIED IN NGM ARRAYS NLAND, NSEA,
C          NSNO AND NICE TO SET NGM SNOW/NO-SNOW, ICE/NO-ICE
C
C ..............BEGIN IMS STEP 1...............................
C
C  INITIALIZE ACCUMULATION ARRAY ON NGM GRIDS
C
      NLAND = 0
      NSEA  = 0
      NSNO  = 0
      NICE  = 0
C
      DO 2055  NG=1,NGRDUSE
        CALL GRDSET (NG, INDEXH, DUMMY,
     1     IMS(NG),JMS(NG),XPOLS(NG),YPOLS(NG),CMUS(NG),DLAMS(NG))
C
        WRITE(IOUTUPRT,6789) IMS(NG),JMS(NG),XPOLS(NG),YPOLS(NG),
     1                                      CMUS(NG),DLAMS(NG)
 6789   FORMAT(' ',2X,'IM,JM,XPOL,YPOL,CMU,DLAM'/
     1                 ' ',2X,2I5,4F12.4/)
C
 2055 CONTINUE
C
C .....................BEGIN NESDIS/IMS STEP 1....................
C
      DO 2800  J=1,1024
        XJ= FLOAT(J) - YPNMC
        DO 2700  I=1,1024
          XI = FLOAT(I) - XPNMC
C
C  IMS MASK HAS TWO VALUES:  0=SEA, 1=LAND
C  IMS SNOW/ICE HAS TWO VALUES:  0=NOTHING, 1=SNOW OR ICE (USE THE MASK
C                                    TO DECIDE WHETHER IT'S SNOW OR ICE) 
C
C  DETERMINE LAT/LON OF IMS GRID POINT.
C  CONVERT WEST LONG FROM W3 CALL TO EAST LONG
C
          CALL W3FB05(XI,XJ,XMESHL,ORIENT,ALAT,ALONG)
          ALONG = 360.E0 - ALONG
C
C  FOR EACH NGM GRID, DETERMINE NGM POINT CLOSEST TO GIVEN IMS POINT
C
          RM   = COS(ALAT*DEGRAD)/(1.E0+SIN(ALAT*DEGRAD))
C
          DO 2600  NG=1,NGRDUSE
C
            RMA  =  RM / CMUS(NG)
            RADA = (ALONG-DLAMS(NG))*DEGRAD
            XA   = XPOLS(NG) + RMA * COS(RADA)
            YA   = YPOLS(NG) + RMA * SIN(RADA)
            IA   = NINT(XA)
            JA   = NINT(YA)
C  NEXT SET FLAG VALUES FOR "OUT OF DOMAIN" CASE
C  THESE FLAG VALUES ONLY NEEDED FOR LATER DEBUG PRINT
            IJA  = 999999
            NSCHK= 999999
C
C  IS THE CURRENT IMS POINT IS OFF NGM GRID?
            IF((IA.LT.1).OR.(IA.GT.IMS(NG))) GO TO 2599
            IF((JA.LT.1).OR.(JA.GT.JMS(NG))) GO TO 2599
C
C  COMPUTE LINEAR ADDRESS IN NGM GRID
            IJA  = (JA-1) * IMS(NG) + IA
C  UP COUNT OF 1/16-MESH LAND/COAST POINTS NEAR NGM POINT
            IF (MSKSCVH(I,J).EQ.0) THEN
               NSEA(IJA,NG) = NSEA(IJA,NG) + 1
               IF (SCVH(I,J).GT.0.5) NICE(IJA,NG) = NICE(IJA,NG) + 1
            ELSE
               NLAND(IJA,NG) = NLAND(IJA,NG) + 1
               IF (SCVH(I,J).GT.0.5) NSNO(IJA,NG) = NSNO(IJA,NG) + 1
            ENDIF
C
 2599     CONTINUE
C
 2600     CONTINUE
 2700   CONTINUE
 2800 CONTINUE
C
C--------------- END STEP 1 OF IMS SNOW/ICE LOGIC -------------
C
C  NOW BEGIN MAJOR LOOP OVER ALL NGM GRIDS AND POINTS
C     PROCESS EACH GRID POINT OF EACH NGM GRID.
C
C*******************************************************************
C
      DO 4400 NG=1,NGRDUSE
ctest
c     write(98,*) 'I, J, LON, LAT for NGM grid no. ', NG
ctest
C
C  DETERMINE WHETHER TO CORRECT LAND MASK AROUND GREAT LAKES
C
        LWVL = .FALSE.
        IF((NGRDUSE.EQ.2).AND.(NG.EQ.2).AND.(LKMSK)) LWVL=.TRUE.
        WRITE(IOUTUPRT,*) 'NGRDUSE=', NGRDUSE, '  NG=',NG,
     1     ' LKMSK=', LKMSK,' LWVL=', LWVL
C
C     ESTABLISH IMS SNOW DIAG PRINT HEADING FOR EACH NGM GRID
        WRITE(IOUTUPRT,7542)
 7542   FORMAT(/'  ','  NG  IN  JN       YLAT   XLON ',
     1           '  NLAND NSEA   NSNO  NICE'/)
C
C     AS CONTROLLED BY INDEXH, SPECIFY NGM FIELD AT H POINTS
C
        CALL GRDSET(NG, INDEXH, DUMMY, IM, JM, XPOL, YPOL, CMU, DLAM)
C
        IJ = IM * JM
C
        DO 4300 J=1,JM
          YNGM = REAL(J) - YPOL
C
C  DETERMINE IF J IN RANGE FOR GREAT LAKES LAND MASK MOD
C
        LDO = .FALSE.
        IF((J.GT.54).AND.(J.LT.64).AND.(LWVL)) LDO=.TRUE.
C
          DO 4299 I=1,IM
            XNGM = REAL(I) - XPOL
            MADDST  = IADDRG(INDEXST, NG)  -1 -IM +I +IM*J
C
C--------------- DETERMINE LAT/LON OF NGM GRID POINT -------------
C
            IRANGE = 1
            IPRT = 0
C     THE LONGITUDE WILL RANGE FROM 0 AT THE GREENWICH MERIDIAN
C     EASTWARD TO 360 DEGREES FOLLOWING THE CALL TO SUBROUTINE
C     CORNER WITH IRANGE=1.
            CALL CORNER (XNGM, YNGM, CMU, DLAM, IRANGE, IPRT,
     1                   XLONG, YLAT)
ctest
c     write(98,99) i, j, xlong, ylat
 99   format(2(2x,i3),4x,2(f7.2,2x))
ctest
C
C     ENSURE LATITUDE IS WITHIN RANGE.
            IF (ABS(YLAT) .LE. 90.E0) GO TO 3320
            WRITE (IOUTUPRT, 3310) YLAT
 3310       FORMAT ('0', 'LATITUDE OUT OF RANGE IN SUBROUTINE TASKC. ',
     1                   'YLAT=', F8.2, '.  JOB TERMINATED.')
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
            ERRMSG(1) = 'IN SUBROUTINE TASKC    NEAR STATEME'
            ERRMSG(2) = 'NT NUMBER 3310:                    '
            ERRMSG(3) = 'THE LATITUDE IS OUT OF RANGE.      '
            ERRMSG(4) = '                                   '
C
            CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
 3320       CONTINUE
C     SET UP EQUATORIAL BOUNDARY CONDITION.
C     ALL FIELDS IN THIS SUBROUTINE ARE ASSUMED TO BE EVEN
C     WITH RESPECT TO EQUATORIAL SYMMETRY.
            YYLAT = ABS(YLAT)
            IF (ISYM .EQ. 2) GO TO 3340
            WRITE (IOUTUPRT, 3330) ISYM
 3330 FORMAT ('0', 'ONLY EVEN SYMMETRY ALLOWED IN SUBROUTINE TASKC. ',
     1             'ISYM=', I8, '.  JOB TERMINATED.')
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
            ERRMSG(1) = 'IN SUBROUTINE TASKC    NEAR STATEME'
            ERRMSG(2) = 'NT NUMBER 3330:                    '
            ERRMSG(3) = 'A FIELD THAT DID NOT HAVE EVEN SYMM'
            ERRMSG(4) = 'ETRY WAS SPECIFIED.                '
C
            CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
 3340       CONTINUE
C
C------- DETERMINE LOCATION OF NGM POINT IN VARIOUS INPUT GRIDS -------
C
C     DETERMINE THE LOCATION OF THE NGM GRID POINT WITHIN THE
C     1-DEGREE-BY-1-DEGREE N.HEMIS. 361 X 91 LAND MASK FIELD.
C
            LAT0  = INT(YYLAT) + 1
            LONG0 = INT(XLONG) + 1
C
C     DETERMINE THE LOCATION OF THE NGM GRID POINT WITHIN THE
C     1-DEGREE-BY-1-DEGREE GLOBAL HI RES SST FIELD.
C
C.... LATHR0  = INT(YYLAT+90.0) + 1
C.... LONGHR0 = INT(XLONG)      + 1
C
            LATHR0  = INT(YYLAT+H90)
            DIF = (YYLAT+H90)-LATHR0
            IF (DIF.GT.D5) LATHR0 = MIN (LATHR0+1 , 179)
C
            LONGHR0 = INT(XLONG)
            DIF = XLONG-LONGHR0
            IF (DIF.GT.D5) LONGHR0=LONGHR0+1
            IF (LONGHR0.EQ.0) LONGHR0=360
C
C**************** SET NGM LAND/WATER MASK ARRAY *******************
C
C     IF HLWC1 < 0.5 WE ARE OVER WATER.  THEN SET SO THAT
C     BITWVL IS '1' IF WE ARE OVER WATER AND '0' IF LAND
C
            XRATIO = XLONG + 1.0E0 - REAL(LONG0)
            YRATIO = YYLAT + 1.0E0 - REAL(LAT0 )
CER
            IF(XRATIO.LT.0.) XRATIO=360.+XRATIO
CER
            HLWC1 = (1.E0 - XRATIO) * (1.E0 - YRATIO) * HLWC(LONG0,LAT0)
     1            + XRATIO * (1.E0 -YRATIO) * HLWC(LONG0+1,LAT0)
     2            + (1.E0 - XRATIO) * YRATIO * HLWC(LONG0,LAT0+1)
     3            + XRATIO * YRATIO * HLWC(LONG0+1,LAT0+1)
C
            IF ( HLWC1 .LT. 0.5E0) THEN
              BITWVL( (J-1) * IM + I , NG ) = .TRUE.
            ELSE
              BITWVL( (J-1) * IM + I , NG ) = .FALSE.
            END IF
C
      IF (LDO)  THEN
C
C     BITWVL( ( 59-1) * IM +  99, NG) = .FALSE.
C     BITWVL( ( 59-1) * IM + 102, NG) = .FALSE.
C     BITWVL( ( 60-1) * IM +  90, NG) = .FALSE.
C     BITWVL( ( 61-1) * IM +  96, NG) = .FALSE.
C     BITWVL( ( 63-1) * IM +  90, NG) = .FALSE.
C
      BITWVL( 8625, NG) = .FALSE.
      BITWVL( 8628, NG) = .FALSE.
      BITWVL( 8763, NG) = .FALSE.
      BITWVL( 8916, NG) = .FALSE.
      BITWVL( 9204, NG) = .FALSE.
C
C     BITWVL( ( 55-1) * IM +  94, NG) = .TRUE.
C     BITWVL( ( 57-1) * IM +  99, NG) = .TRUE.
C     BITWVL( ( 57-1) * IM + 100, NG) = .TRUE.
C     BITWVL( ( 59-1) * IM +  93, NG) = .TRUE.
C     BITWVL( ( 59-1) * IM + 100, NG) = .TRUE.
C     BITWVL( ( 60-1) * IM + 101, NG) = .TRUE.
C     BITWVL( ( 60-1) * IM + 102, NG) = .TRUE.
C     BITWVL( ( 61-1) * IM +  97, NG) = .TRUE.
C
      BITWVL( 8032, NG) = .TRUE.
      BITWVL( 8331, NG) = .TRUE.
      BITWVL( 8332, NG) = .TRUE.
      BITWVL( 8619, NG) = .TRUE.
      BITWVL( 8626, NG) = .TRUE.
      BITWVL( 8774, NG) = .TRUE.
      BITWVL( 8775, NG) = .TRUE.
      BITWVL( 8917, NG) = .TRUE.
C
      HLWC1 = 1.0E0
      IF ( BITWVL( (J-1) * IM + I, NG) )  HLWC1 = 0.0E0
C
      ENDIF
C
C*************** DETERMINE SNOW/ICE NGM POINTS *********************
C*********  AND PROVISIONAL SKIN TEMP OVER SNOW/ICE   **************
C
C  IF LATITUDE OF NGM POINT IS NEG (S.H.), IMPOSE NO SNOW/ICE
C
      IF(YLAT.LE.0.E0) GO TO 3400
C
C-------------- BEGIN STEP 2 OF IMS SNOW/ICE LOGIC ---------------
C
C  FOR EACH NGM POINT, FIND OUT WHETHER IT IS A LAND POINT OR A SEA
C  POINT (ALL NON-LAND POINTS ARE CALLED SEA POINTS -  BE IT SEA OR
C  LAKE OR WHATEVER), CHECK THE BINS TO SEE HOW MANY POINTS ARE OF 
C  LIKE-KIND.  IF THERE ARE NO LIKE-KIND POINTS WITHIN THE BIN AT ALL,
C  PRINT OUT A RED-FLAG MESSAGE (AMONG THESE NGM POINTS, IT WOULD BE
C  DESIGNATED AS SNOW/ICE POINTS IF AT LEAST HALF OF THE IMS POINTS IN
C  ITS BIN ARE SNOW/ICE COVERED).  
C  
C  THEN IF AMONG THE LIKE-KIND IMS POINTS, AT LEAST HALF OF THEM ARE
C  COVERED WITH ICE (SEA) OR SNOW (LAND), THEN THE NGM POINT WOULD BE
C  ICE OR SNOW COVERED.
C
C  LOGICAL VARIABLE BITWVL(IIJMAX,INGRDUSE) IS THE NGM LAND/SEA MASK.
C     SEA=.TRUE., LAND=.FALSE.
C
      IJA = (J-1)*IM + I
C  Assume that this point has no ice or snow, unless proven otherwise:
      ICESNOW = .FALSE.
C
      IF ( BITWVL(IJA,NG) ) THEN
C
C       This is an NGM sea point.  Check to see if there are at least
C       one IMS sea points in the bin.  If not, then if at least half 
C       of the IMS land points are covered with snow, designate this 
C       as an NGM snow/ice point.
C
        IF (NSEA(IJA,NG) .LT. 1) THEN
          WRITE(IOUTUPRT,*) 'AT NG=',NG, ' I=',I,' J=',J, 
     &       ' LAT=', YLAT, ' LON=', XLONG, 
     &       ' NO IMS SEA POINT IN BIN FOR THE NGM SEA POINT!'
          IF (2.*NSNO(IJA,NG).GE. NLAND(IJA,NG)) ICESNOW= .TRUE.
          GOTO 3360
        ENDIF
C
C       If at least half of the IMS sea points in the bin have ice, 
C       then this NGM point is ice covered:
        IF (2.*NICE(IJA,NG) .GE. NSEA(IJA,NG)) ICESNOW= .TRUE.
      ELSE
C
C       This is an NGM land point.  Check to see if there are enough
C       IMS land points in the bin.  If not, then if at least half 
C       of the IMS sea points are covered with ice, designate this 
C       as an NGM snow/ice point.
C
        IF (NLAND(IJA,NG) .LT. 1) THEN
          WRITE(IOUTUPRT,*) 'AT NG=',NG, ' I=',I,' J=',J, 
     &       ' LAT=', YLAT, ' LON=', XLONG, 
     &       ' NO IMS LAND POINT IN BIN FOR THE NGM LAND POINT!'
          IF (2.*NICE(IJA,NG).GE. NSEA(IJA,NG)) ICESNOW= .TRUE.
          GOTO 3360
        ENDIF
C
C       If at least half of the IMS land points in the bin have snow, 
c       then this NGM point is snow covered:
        IF (2.*NSNO(IJA,NG) .GE. NLAND(IJA,NG)) ICESNOW= .TRUE.
      ENDIF
C
C     DEBUG PRINT BELOW
      IF((NG.EQ.NGPRT).AND.
     1 ((J.EQ.55).OR.(J.EQ.60).OR.(J.EQ.65).OR.(J.EQ.72).OR.
     2  (J.EQ.75).OR.(J.EQ.85).OR.(J.EQ.90).OR.(J.EQ.95)))
     3 WRITE(IOUTUPRT,7543) NG,I,J,YLAT,XLONG,NLAND(IJA,NG),
     4          NSEA(IJA,NG),NSNO(IJA,NG),NICE(IJA,NG)
 7543 FORMAT('  ',3I4,3X,2F8.2,3X,I3,2X,I3,4X,I3,2X,I3)
C
 3360 CONTINUE
C
C  ------------ END STEP 2 OF IMS SNOW/ICE LOGIC ------------------
C
      IF(.NOT. ICESNOW) GO TO 3400
C
C     THE NGM POINT IS OVER ICE/SNOW.
C
      BITSEA( (J-1) * IM + I, NG) = .FALSE.
      BITSNO( (J-1) * IM + I, NG) = .TRUE.
C
C------------ DETERMINE SFC (SKIN) TEMP OVER ICE OR SNOW -------------
C
C     THE SURFACE TEMPERATURE IS INTERPOLATED FROM THE SST ANALYSIS
C     (NOTE: SST ANAL IS DEFINED OVER ENTIRE HEMISPHERE, EVEN OVER LAND,
C     FORMERLY THE SURFACE TEMP FOR ICE/SNOW WAS ZERO DEGREES KELVIN.)
C     LATER, BEFORE FIRST TIME STEP IN NGM, THESE SURFACE TEMPS OVER
C     ICE/SNOW ARE OVERWRITTEN WITH PHYSICAL SKIN TEMPS FROM ENERGY BAL.
C     PROVISIONAL TEMPS HERE ARE FOR GRAPHICAL OUTPUT CONVENIENCE.
C
      XRATIO =  XLONG       + D5  - REAL(LONGHR0)
CER
      IF(XRATIO.LT.0.) XRATIO=360.+XRATIO
CER
      YRATIO = (YYLAT+H90)  + D5  - REAL(LATHR0 )
C
      VBL(MADDST) =
     1        (1.E0 - XRATIO) * (1.E0 - YRATIO) * SSTH(LONGHR0,LATHR0)
     2       + XRATIO * (1.E0 -YRATIO) * SSTH(LONGHR0+1,LATHR0)
     3       + (1.E0 - XRATIO) * YRATIO * SSTH(LONGHR0,LATHR0+1)
     4       + XRATIO * YRATIO * SSTH(LONGHR0+1,LATHR0+1)
C
      GO TO 4299
C
C***************** BEGIN NON SNOW/ICE POINTS  ***********************
C*************  INTERPOLATE SFC (SKIN) TEMPERATURE  *****************
C
C                1 - OVER WATER USE OBSERVED SSTS
C                2 - OVER LAND USE EXTRAPOLATED SSTS
C                    (LAND SFC TMPS ARE PROVISIONAL FOR GRAPHICS)
C
 3400 CONTINUE
C
C     THE NGM POINT IS NOT OVER ICE/SNOW.
C
      BITSNO( (J-1) * IM + I, NG) = .FALSE.
C
C ------------- BEGIN HI-RES SFC TEMP INTERPOLATION ----------------
C
      XRATIO =  XLONG      + D5 - REAL(LONGHR0)
      YRATIO = (YYLAT+H90) + D5 - REAL(LATHR0 )
CER
      IF(XRATIO.LT.0.) XRATIO=360.+XRATIO
CER
C
      II = LONGHR0
      JJ = LATHR0
      IIP1= II + 1
      JJP1= JJ + 1
      if(xratio.lt.0.) then
       print *,'xratio<0 at ',ii,jj,xratio,xlong,longhr0
       xratio=360.+xratio
      end if
C
      IF(HLWC1 .GE. 0.5E0) GO TO 3501
C
C     THE POINT IS ASSUMED TO BE OVER WATER IF THE VALUE OF HLWC
C     IS LESS THAN 0.5 WHEN INTERPOLATED FROM THE 1-DEGREE-BY-1-DEGREE
C     HLWC FIELD TO THE NGM GRID.
C     THE VALUE FOR HLWC ON THE 1-DEGREE-BY-1-DEGREE GRID IS
C     0 OVER WATER AND 1 OVER LAND.
C
C............... HI-RES SFC TEMP INTERP OVER ICE-FREE WATER.............
C
      AREA11 = (1.0E0 - XRATIO) * (1.0E0 - YRATIO)
      AREA21 = XRATIO * (1.0E0 - YRATIO)
      AREA12 = (1.0E0 - XRATIO) * YRATIO
      AREA22 = XRATIO * YRATIO
C
C     BEFORE THE WATER TEMPERATURE CAN BE SPECIFIED FOR THIS
C     NON-ICE POINT OVER WATER, THE SURFACE TEMPERATURE OF SURROUNDING
C     POINTS MUST BE CHECKED.
C     ON THE  GRID, ALL WATER TEMPERATURES ARE ASSUMED TO
C     BE GREATER THAN 271.2 DEGREES KELVIN.
C      (NOTE: BECAUSE OF SALT CONTENT, SEA-WATER IN ARCTIC
C       LATS DOES NOT FREEZE UNTIL -1.8 C NOMINALLY)
C
      KOUNT = 0
C
      IF(SSTH(II, JJ) .GE. 271.2E0) THEN
         KOUNT = KOUNT + 1
         AREA = AREA11
         IPOINT = II
         JPOINT = JJ
      END IF
C
      IF(SSTH(II, JJP1) .GE. 271.2E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA12 .GT. AREA) THEN
            AREA = AREA12
            IPOINT = II
            JPOINT = JJP1
         END IF
      END IF
C
      IF(SSTH(IIP1, JJ) .GE. 271.2E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA21 .GT. AREA) THEN
            AREA = AREA21
            IPOINT = IIP1
            JPOINT = JJ
         END IF
      END IF
C
      IF(SSTH(IIP1 , JJP1) .GE. 271.2E0) THEN
         KOUNT = KOUNT + 1
         IF (AREA22 .GT. AREA) THEN
            AREA = AREA22
            IPOINT = IIP1
            JPOINT = JJP1
         END IF
      END IF
C
C     DETERMINE SST CONSIDERING THE NUMBER OF POINTS SURROUNDING THE
C     NGM GRID POINT WITH TEMPERATURE GREATER THAN OR EQUAL
C     TO 271.2 DEGREES KELVIN.
C     FORMERLY THE SURFACE TEMPERATURE WAS ALSO DETERMINED IN THIS
C     MANNER.
C
      IF(KOUNT .GT. 0) GO TO 3461
C
C     NONE OF THE SURROUNDING POINTS OF THE 2-D X 2-D FIELD OF SURFACE
C     TEMPERATURE ARE GREATER THAN OR EQUAL TO 271.2 DEGREES KELVIN.
C     THE POINTS MAY BE LAKES.
      BITSEA( (J-1) * IM + I, NG) = .FALSE.
      VBL(MADDST) =
     1      (1.E0 - XRATIO) * (1.E0 - YRATIO) * SSTH(II,JJ)
     2      + XRATIO * (1.E0 -YRATIO) * SSTH(IIP1,JJ)
     3      + (1.E0 - XRATIO) * YRATIO * SSTH(II,JJP1)
     4      + XRATIO * YRATIO * SSTH(IIP1,JJP1)
C
      WRITE (IOUTUPRT, 3450) LONG0, LAT0, HLWC(LONG0,LAT0), 
     1            SSTH(II,JJ),     SSTH(II,JJP1),
     2            SSTH(IIP1,JJP1), SSTH(IIP1,JJ),
     3            I, J, NG
 3450 FORMAT (' ', 'LAKES?', 2X, 2I4, F5.1, 4F7.1, 3I5)
      GO TO 4001
C
 3461 CONTINUE
C     IF 1, 2, OR 3 OF THE SURROUNDING POINTS HAVE SURFACE TEMPERATURES
C     GREATER THAN OR EQUAL TO 271.2 DEGREES KELVIN, THE VALUE
C     FOR THE NEAREST POINT OF THE SURFACE TEMPERATURE FIELD FOUND
C     SATISFYING THIS CONSTRAINT IS USED.
C
      IF(KOUNT .EQ. 4) GO TO 3471
C
      BITSEA( (J-1) * IM + I, NG) = .TRUE.
      VBL(MADDST)  = SSTH(IPOINT, JPOINT)
C
      GO TO 4001
C
 3471 CONTINUE
C     ALL 4 POINTS SURROUNDING THE NON-ICE WATER POINT HAVE
C     SATISFACTORY SURFACE TEMPERATURES.
      BITSEA( (J-1) * IM + I, NG) = .TRUE.
      VBL(MADDST) = (1.E0 - XRATIO) * (1.E0 - YRATIO) * SSTH(II,JJ)
     1                + XRATIO * (1.E0 -YRATIO) * SSTH(IIP1,JJ)
     2                + (1.E0 - XRATIO) * YRATIO * SSTH(II,JJP1)
     3                + XRATIO * YRATIO * SSTH(IIP1,JJP1)
      GO TO 4001
C
 3501 CONTINUE
C
C.............. HI-RES SFC TEMP INTERP FOR SNOW-FREE LAND ...........
C
      BITSEA( (J-1) * IM + I, NG) = .FALSE.
      VBL(MADDST) = (1.E0 - XRATIO) * (1.E0 - YRATIO) * SSTH(II,JJ)
     1                + XRATIO * (1.E0 -YRATIO) * SSTH(IIP1,JJ)
     2                + (1.E0 - XRATIO) * YRATIO * SSTH(II,JJP1)
     3                + XRATIO * YRATIO * SSTH(IIP1,JJP1)
C
 4001 CONTINUE
C
 4299 CONTINUE
 4300 CONTINUE
C
c      write(98,*)'SNOW/ICE for grid no. ', NG, ' im=',im,' jm=',jm
c      do 4395 j = 1, jm
c      do 4395 i = 1, im
c        write(98,4396) i, j, bitsno((j-1)*im+i,ng)
c4395  continue
c4396  format(i3,1x,i3,5x,l1)
C
C******************** END MAJOR NGM GRID POINT LOOP ****************
C
      IF (NG .EQ. NGRDUSE) THEN
      CALL PRINTBIT (BITWVL(1,NG),IM,JM,NG,'BITWVL                 ')
      CALL PRINTBIT (BITSNO(1,NG),IM,JM,NG,'BITSNO                 ')
      CALL PRINTBIT (BITSEA(1,NG),IM,JM,NG,'BITSEA                 ')
      ENDIF
C
C.....   SET-UP SNOW/ICE PLOT FLAGS OVER NGM GRIDS
C.....   SKIP OVER IN OPERATIONS
      IF (NG .EQ. NGRDUSE+1) THEN
        DO 4390 J=1,JM
        DO 4390 I=1,IM
          IF (BITWVL((J-1)*IM+I,NG)) THEN
           SCPLT((J-1)*IM+I,NG) = 1.0
            IF (BITSNO((J-1)*IM+I,NG))  SCPLT((J-1)*IM+I,NG) = 2.0
          ELSE
           SCPLT((J-1)*IM+I,NG) = 3.0
            IF (BITSNO((J-1)*IM+I,NG))  SCPLT((J-1)*IM+I,NG) = 4.0
          ENDIF
 4390   CONTINUE
C
C      WRITE OUT LAND-SEA MASK AND SNOW/ICE FLAGS FOR PLOTTING
C
        IPU = 60 + NG
        IF (NG.NE.NGRDUSE) THEN
          WRITE(IPU) ((SCPLT((J-1)*IM+I,NG), I=1,IM), J=1,JM)
        ELSE
C         WRITE OUT GRID TYPE 105, I.E., 83 X 83 SUBSET OF GRID 104
          WRITE(IPU) ((SCPLT((J-1)*IM+I,NG), I=36,118), J=22,104)
        ENDIF
      ENDIF
C
 4400 CONTINUE
C
      RETURN
      END
