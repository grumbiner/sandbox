      SUBROUTINE TASKF
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKF       CALCULATES THE MASS-WEIGHTED U,V WINDS
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: CALCULATES THE MASS-WEIGHTED U AND V WIND COMPONENTS
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL TASKF
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - GRDSET
C     LIBRARY:
C       COMMON   - CONSTN
C                  BLANK (CONTAINS THE VBL ARRAY)
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON          VBL( INVBL )
C
C
C     FIRST THE V COMPONENT IS PROCESSED.
C
      DO 1100 NG=1,NGRDUSE
      CALL GRDSET (NG, INDEXHV, DUMMY, IM, JM, DUMMY, DUMMY,DUMMY,DUMMY)
      IJ     = IM * JM
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      MADDHV = IADDRG(INDEXHV, NG) -1 -IM -IJ
      MADDH  = IADDRG(INDEXH , NG) -1 -IM
C
      DO 1000 J=1,JM
C
      DO 900 K=1,KM
C
      DO 800 I=1,IM-1
      VBL(MADDHV+I+IM*J+IJ*K)
     1     = 0.5E0 * (VBL(MADDH+I+IM*J) + VBL(MADDH+I+1+IM*J))
     2     * VBL(MADDHV+I+IM*J+IJ*K)
  800 CONTINUE
C
C     THE VALUE OF HV AT I=IM IS DEFINED HERE WITH A REASONABLE
C     VALUE EVEN THOUGH IT WILL BE OVERWRITTEN AT THE START
C     OF THE FORECAST.
      VBL(MADDHV+IM+IM*J+IJ*K)
     1     = VBL(MADDH+IM+IM*J) * VBL(MADDHV+IM+IM*J+IJ*K)
C
  900 CONTINUE
C
 1000 CONTINUE
C
 1100 CONTINUE
C
C
C     NOW THE U COMPONENT IS PROCESSED.
C
      DO 2100 NG=1,NGRDUSE
      CALL GRDSET (NG, INDEXHU, DUMMY, IM, JM, DUMMY, DUMMY,DUMMY,DUMMY)
C
C     SPECIFY THE POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ     = IM * JM
      MADDHU = IADDRG(INDEXHU, NG) -1 -IM -IJ
      MADDH  = IADDRG(INDEXH , NG) -1 -IM
C
C
      DO 2000 J=1,JM
C
C     IF THIS IS THE LAST ROW WE ASSUME THAT
C     THE SURFACE PRESSURE OF THE NONEXISTENT NEXT ROW IS THE
C     SAME AS THE CURRENT ROW.
C     THE VALUE OF HU AT J=JM IS DEFINED ALTHOUGH IT WILL BE
C     OVERWRITTEN AT THE START OF THE FORECAST.
C
      DO 1900 K=1,KM
C
      DO 1800 I=1,IM
         IF (J .LT. JM) THEN
            VBL(MADDHU+I+IM*J+IJ*K)
     1          = 0.5E0 * (VBL(MADDH+I+IM*J) + VBL(MADDH+I+IM+IM*J))
     2          * VBL(MADDHU+I+IM*J+IJ*K)
         ELSE
            VBL(MADDHU+I+IM*J+IJ*K)
     1          = VBL(MADDH+I+IM*J)
     2          * VBL(MADDHU+I+IM*J+IJ*K)
         END IF
 1800 CONTINUE
C
 1900 CONTINUE
C
 2000 CONTINUE
C
 2100 CONTINUE
C
      RETURN
C
      END
