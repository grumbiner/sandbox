      SUBROUTINE EXMOIS( P , Q , PB , QB )
C
      IMPLICIT REAL ( A - H, O - Z )
C
C  SET PRESSURE LVL (BARS) IN LOWER STRATOSPHERE ABOVE WHICH
C  SPECIFIC HUMIDITY WILL BE ASSUMED CONSTANT AND SET THIS
C  CONSTANT, WHICH IS ADOPTED FROM THE STANDARD ATMOSPHERE
C  PROFILES GIVEN ON PAGES 1-5 THRU 1-11 IN
C  NASA REPORT X-613-86-10 BY HARSHVARDHAN
C
      DATA   PLIMS , QLIMS / 0.06E0 , 4.0E-6 /
C
C  NOW PERFORM POWER LAW EXTRAPOLATION (I.E. LOG-Q LINEAR IN LOG-P)
C  OF SPECIFIC HUMIDITY UPWARD FROM UPPER TROPOSPHERE TO LOWER
C  STRATOSPHERE.  EXTRAPOLATE TO AN ASYMPTOTIC LIMITING VALUE OF
C  Q=QLIMS ABOVE P=PLIMS.  CHOICE OF POWERE LAW EXTRAPOLATION
C  FOLLOWS RECOMMENDATION IN AFGL TECH REPORT 85/0090 BY MITCHELL.
C
C
      IF ( P .LE. PLIMS )  THEN
         Q = QLIMS
C
      ELSE
         BD = PB/PLIMS
         BN = QB/QLIMS
         BB = LOG(BN)/LOG(BD)
         AA = QLIMS/PLIMS**BB
C
         Q  = AA * P**BB
C
      ENDIF
C
      RETURN
      END
