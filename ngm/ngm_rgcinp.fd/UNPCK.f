C
      SUBROUTINE UNPCK(PACK,REAL8,LABEL)
CDIR$ INTEGER=64
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: UNPCK          UNPACK RECORD INTO CRAY 64 BIT F.P.
C   AUTHOR: JONES,R.E.       ORG: W342       DATE: 89-10-17
C
C ABSTRACT: UNPACKS A RECORD IN OFFICE NOTE 84 FORMAT AND CONVERT THE
C   PACKED DATA TO CRAY REAL 64 BIT FLOATING POINT NUMBERS. THE
C   OFFICE NOTE 84 DATA IS BIT FOR BIT THE SAME ON THE NAS-9050 AND
C   THE CRAY.
C
C PROGRAM HISTORY LOG:
C   89-10-20  R.E.JONES
C   90-02-02  R.E.JONES  CHANGE TO CRAY FUNCTION FOR INTEGER*2, F.P.
C   90-10-11  R.E.JONES  SPECIAL VERSION OF W3AI01 TO UNPACK RECORDS
C                        PACKED BY BIG VERSION OF W3AI00. WILL DO
C                        OLD AND NEW VERSION.
C   91-03-19  R.E.JONES  MAKE SPECIAL VERSION OF W3AI01 TO UNPACK
C                        BIG RECORDS THE OPERATIONAL VERSION.
C   92-11-22  K.MITCHELL EXPAND SIZE OF ITEMP ARRAY IN ORDER TO
C                        PROPERLY HANDLE FIELD SIZE OF J=512 X 512
C                        CHANGE NAME OF SUBROUTINE FROM W3AI01 TO
C                        UNPCK TO AVOID LOADER LIBRARY CONFLICTS
C
C USAGE:  CALL UNPCK (PACK, REAL8, LABEL)
C
C   INPUT VARIABLES:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ------ --------- -----------------------------------------------
C     PACK   ARG LIST  INTEGER ARRAY WITH DATA IN OFFICE NOTE 84
C                      FORMAT TO BE UNPACKED. ON THE CRAY THIS
C                      ARRAY IS HALF THE SIZE IT IS ON THE NAS-9050.
C                      ON THE NAS-9050 A 65*65 FIELD PACKS INTO
C                      A 2125 32 BIT INTEGER ARRAY. ON THE CRAY IT
C                      WILL BE A 1013 WORD INTEGER ARRAY. A LFM
C                      ON THE NAS IS 1205 WORDS, ON THE CRAY 603.
C
C   OUTPUT VARIABLES:
C     NAMES  INTERFACE DESCRIPTION OF VARIABLES AND TYPES
C     ------ --------- -----------------------------------------------
C     REAL8  ARG LIST  REAL ARRAY OF N WORDS. WHERE N IS GIVEN IN
C                      WORD 6 OF PACK. WORD 6 OF PACK MUST
C                      CONTAIN CENTER AND SCALING VALUES.
C     LABEL  ARG LIST  SIX WORD INTEGER LABEL COPIED FROM PACK, 12
C                      OFFICE NOTE 84 32 BIT ID'S.
C
C   SUBPROGRAMS CALLED:
C     NAMES                                                   LIBRARY
C     ------------------------------------------------------- --------
C     USICTC USSCTC                                           SYSTEM
C
C   REMARKS: LABEL AND PACK MAY BE EQUIVALENCED, OR PACK AND REAL4 MAY
C     BE EQUIVALENCED, BUT NOT ALL THREE.
C
C ATTRIBUTES:
C   LANGUAGE: CRAY CFT77 FORTRAN
C   MACHINE:  CRAY Y-MP8/832
C
C$$$
C
       REAL     REAL8(*)
C
       INTEGER  ITEMP(275000)
       INTEGER  LABEL(6)
       INTEGER  PACK(*)
       INTEGER  MASK16
       INTEGER  MASK32
C
       SAVE
C
       DATA  MASK16/X'000000000000FFFF'/
       DATA  MASK32/X'00000000FFFFFFFF'/
C
C      MOVE OFFICE NOTE 84 12 32 BIT ID'S INTO LABEL
C
       DO 10 I = 1,6
         LABEL(I) = PACK(I)
 10    CONTINUE
C
C GET WORD COUNT, AVERAGE VALUE, SCALING FACTOR, J, A , N.
C
         J   = IAND(LABEL(4),MASK16)
         IF (J.EQ.0) THEN
           J   = IAND(LABEL(6),MASK32)
           IF (J.EQ.0) THEN
             PRINT *,' UNPCK ERROR, NUMBER OF WORDS IN GRID IS 0'
             RETURN
           ENDIF
         ENDIF
C
C        CONVERT IBM 32 BIT MEAN VALUE TO CRAY 64 BIT F.P. NUMBER
C
         CALL USSCTC(LABEL(5),5,A,1)
C
C        GET SCALING VALUE N, CAN BE NEGATIVE (INTEGER*2 TWO'S COMPL.)
C
         CALL USICTC(LABEL(6),3,N,1,2)
C
         TWON = 2.0 ** (N - 15)
C
C UNPACK, CONVERT TO REAL 64 BIT FLOATING POINT DATA
C
         CALL USICTC(PACK(7),1,ITEMP,J,2)
C
         DO 20 I = 1,J
           REAL8(I) = FLOAT(ITEMP(I)) * TWON + A
  20     CONTINUE
C
         RETURN
       END
