C
      SUBROUTINE SIGCON (DELSIG, KM, POVH, PIOVHK)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SIGCON      COMPUTE THE LAYER PRESSUER AND EXNER FCN
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: CALCULATES THE LAYER PRESSURE NORMALIZED
C   BY SURFACE PRESSURE AND THE LAYER EXNER FUNCTION AS
C   NORMALIZED BY SURFACE PRESSURE TO THE KAPPA POWER,
C   GIVEN THE THICKNESSES OF THE SIGMA LAYERS AND THE
C   NUMBER OF LAYERS.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL SIGCON (DELSIG, KM, POVH, PIOVHK)
C   INPUT ARGUMENT LIST:
C     DELSIG   - THICKNESSES OF SIGMA LAYERS
C     KM       - NUMBER OF SIGMA LAYERS
C
C   OUTPUT ARGUMENT LIST:
C     POVH     - LAYER PRESSURE NORMALIZED BY SURFACE PRESSURE
C     PIOVHK   - LAYER EXNER FUNCTION NORMALIZED BY SURFACE
C                PRESSURE TO THE KAPPA POWER
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      DIMENSION DELSIG( IKM )
      DIMENSION PIOVHK( IKM ), POVH( IKM )
      DIMENSION SIGHAT( IKMP1 ), SIGEXP( IKMP1 )
C
C
      CKAPPA = 287.05E0 / 1005.E0
      ONEOVK = 1.0E0 / CKAPPA
      ONEPK  = 1.0E0 + CKAPPA
C
      SIGHAT(1) = 1.0E0
      SIGEXP(1) = 1.0E0
C
      DO 100 K=2,KM
         SIGHAT(K) = SIGHAT(K-1) - DELSIG(K-1)
         SIGEXP(K) = REAL (REAL(SIGHAT(K)) ** REAL(ONEPK))
  100 CONTINUE
C
      SIGHAT(KM+1) = 0.E0
      SIGEXP(KM+1) = 0.E0
C
      DO 200 K=1,KM
         PIOVHK(K) = (SIGEXP(K) - SIGEXP(K+1))
     1              /(ONEPK * DELSIG(K))
         POVH(K) = REAL (REAL(PIOVHK(K)) ** REAL(ONEOVK))
  200 CONTINUE
C
      RETURN
C
      END
