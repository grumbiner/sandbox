      SUBROUTINE LOLA (SPECS, LATG2, INULOLA)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    LOLA        PUTS VERT LAT STRIPS & CREATES LAT ARRAY
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: PERFORMS THE FOLLOWING PROCESSES
C   1)  INPUTS FIELDS IN VERTICAL LATITUDINAL STRIPS
C   .   ON A LATITUDE-LONGITUDE GRID FROM A
C   .   SEQUENTIAL FILE,
C   2)  PLACES THE STRIPS IN A LARGE ARRAY IN THE CENTRAL MEMORY,
C   3)  FILLS IN SYMMETRIC FIELDS SOUTH OF THE EQUATOR FOR AS
C   .   FAR SOUTH AS THE NGM WILL EVER REASONABLY REACH
C   .   (FOR NH = 10, THIS IS 9.94 S.),
C   4)  CREATES AN ARRAY GIVING THE LATITUDE OF EACH LATITUDINAL
C   .   STRIP.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-02  BRIAN SCHMIDT ADDED DOCBLOCKS
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP
C   98-11-25  ERIC ROGERS CHANGED READ OF ON85 LABEL AT BEGINNING
C             OF SELA RECORD WITH READ OF BINARY NCEP DATE RECORD
C
C USAGE:    CALL LOLA (SPECS, LATG2, INULOLA)
C   INPUT ARGUMENT LIST:
C     SPECS    - ARRAY CONTAINING LATITUDE INFORMATION
C     LATG2    - NORTHERNMOST LATITUDE OF INPUT FIELD
C     INULOLA  - UNIT NUMBER OF SEQUENTIAL DATA SET TO BE READ IN
C
C   OUTPUT ARGUMENT LIST:
C     NONE
C
C   INPUT FILES:
C     INULOLA  - FILE CONTAINING VERTICAL LATITUDINAL STRIPS
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     LIBRARY:
C       COMMON   - CONSTS
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      DIMENSION INPDAT(8)
      REAL         DUM1, DUM2( IKMP1 ), DUM3( IKM )
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      DIMENSION SPECS( INSPECS )
      DIMENSION IDUM (4)
C
      NLATSH = NLAT - LATG2 - 1
C
C              FIRST, READ IN THE OFFICE NOTE 85 LABEL.
      READ (INULOLA) INPDAT
C
C     PRINT THE DATE WORD LABEL.
      IYEAR     = INPDAT(1)
C
      IMONTH    = INPDAT(2)
C
      IDAY      = INPDAT(3)
C
      IHOUR     = INPDAT(5)
C 
      WRITE (6, 100) INULOLA, 
     1               IYEAR, IMONTH, IDAY, IHOUR
C
  100 FORMAT ('0', 'THE NCEP DATE RECORD FOR THE EXTRA-FINE-',
     1             'RESOLUTION LOLA GRID WAS READ ',
     2             'FROM THE DISK WITH UNIT NUMBER ', I2, '.'/
     3             ' THE VALID DATE IS :' /
     5             'Y/M/D/H = ', I4, ' / ', I2, ' / ', I2,
     6             ' / ', I2)
C
C     NOW READ IN SELA'S FILE LABEL, BUT IGNORE IT.
      READ (INULOLA) DUM1, IDUM, DUM2, DUM3
C
C     THE LATITUDINAL STRIPS CAN NOW BE PROCESSED.
C
C     FIRST, THE N.H. LATITUDINAL STRIPS ARE READ IN AND PLACED
C     IN A LARGE ARRAY BY LATITUDINAL STRIP.
C
      LBLOCK = NLATSH
C
      DO 500 LAT=1, LATG2
         LBLOCK = LBLOCK + 1
         READ (INULOLA) (STRIPARA(I, LBLOCK), I=1, NSIGSTP)
         SLAT(LBLOCK) = SPECS(500 + LAT)
C
  500 CONTINUE
C
C
C     THE POLAR LATITUDE IS GIVEN THE SAME VALUES AS THE
C     NORTHERNMOST GAUSSIAN LATITUDE.  THE POLAR VALUES WILL BE
C     MORE CORRECTLY SPECIFIED LATER IN THE PROGRAM.
      SLAT(LBLOCK+1) = 90.
      DO 510  I=1,NSIGSTP
      STRIPARA(I,LBLOCK+1) = STRIPARA(I,LBLOCK)
  510 CONTINUE
C
C
C     FOR LATITUDES SOUTH OF THE EQUATOR,  ALL FIELDS DERIVED
C     FROM THE SPECTRAL COEFFICIENT FILE ARE SET EQUAL TO THEIR
C     NORTHERN HEMISPHERIC COUNTERPARTS (EVEN SYMMETRY), EXCEPT
C     FOR THE MERIDIONAL WIND FIELD (ODD SYMMETRY).
C
      IVMAX = IT - 1
C
      DO 600 LAT=1,NLATSH
C
         LBLKN = NLATSH + LAT
         LBLKS = NLATSH - LAT + 1
         SLAT(LBLKS) = -SLAT(LBLKN)
C
C     EVEN SYMMETRY IS IMPOSED ON ALL FIELDS (EXCEPT FOR
C     ODD SYMMETRY ON THE MERIDIONAL VELOCITY FIELD).
         DO 525 I=1, NSIGSTP
            STRIPARA(I, LBLKS) = STRIPARA(I, LBLKN)
  525    CONTINUE
C
         DO 550 I=IV,IVMAX
            STRIPARA(I, LBLKS) = - STRIPARA(I, LBLKN)
  550    CONTINUE
C
  600 CONTINUE
C
C
C     PRINT THE U AND V WIND COMPONENTS IN LAYERS 13 AND 14
C     OVER THE UNITED STATES.
C
      LAT1  = 20
      LAT2  = 60
      LONG1 = 205
      LONG2 = 264
      LEV1  = 12
      LEV2  = 14
C
c     DO 800 LEV = LEV1, LEV2
C
c     WRITE (6, 725) LEV
c 725 FORMAT ('0', 'U AT LEVEL', I3, ':'/)
C
c     DO 800 LAT = LAT2, LAT1, -1
C
c        WRITE (6, 730)  LAT
c 730    FORMAT (' ', 'LAT=', I3, ':')
C
c        WRITE (6, 750)
c    1               (STRIPARA(IU-1+ (LEV-1)*LONF + I, LAT+NLATSH),
c    2                I=LONG1,LONG2)
c 750    FORMAT (' ', 8X, 20F6.1)
C
C
C
c 800 CONTINUE
C
c     DO 1000 LEV = LEV1, LEV2
C
c     WRITE (6, 925) LEV
c 925 FORMAT ('0', 'V AT LEVEL', I3, ':'/)
C
c     DO 1000 LAT = LAT2, LAT1, -1
C
c        WRITE (6, 730)  LAT
C
c        WRITE (6, 950)
c    1               (STRIPARA(IV-1+ (LEV-1)*LONF + I, LAT+NLATSH),
c    2                I=LONG1,LONG2)
c 950    FORMAT (' ', 8X, 20F6.1)
C
c1000 CONTINUE
C
      RETURN
C
      END
