      SUBROUTINE FIND (X, A, IDIM, M0)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    FIND        LOCATES RELATIVE POSITION OF A VALUE
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: LOCATES THE RELATIVE POSITION OF THE VALUE
C   X WITH RESPECT TO THE ARRAY A OF DIMENSION IDIM.
C   THE QUANTITY M0 IS THE FIRST-GUESS LOCATION AND ULTIMATE
C   LOCATION.  THE ULTIMATE VALUE OF M0 IS SUCH THAT X IS
C   .GE. A(M0) AND X IS .LT. A(M0 + 1).
C   THE ARRAY A IS ASSUMED TO BE ORDERED BY INCREASING VALUE.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-02  BRIAN SCHMIDT ADDED DOCBLOCK
C
C USAGE:    CALL FIND (X, A, IDIM, M0)
C   INPUT ARGUMENT LIST:
C     X        - VALUE WHOSE LOCATION IN THE ARRAY IS TO BE FOUND
C     A        - ARRAY IN WHICH THE RELATIVE POSITION OF X IS TO
C                BE FOUND
C     IDIM     - FIRST DIMENSION OF THE ARRAY A
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     M0       - LOCATION OF THE VALUE X IN THE ARRAY A
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
      CHARACTER*35 ERRMSG(4)
C
      DIMENSION A(IDIM)
C
C     SPECIFY THE PRINTER UNIT NUMBER.
      IOUTUPRT = 6
C
C     SPECIFY COUNTERS FOR MAXIMUM AMOUNT OF PRINTING.
      DATA KOUNTMIN /0/
      DATA KOUNTMAX /0/
C
C     ENSURE THAT M0 IS A REASONABLE VALUE.
      M0 = MIN(M0, IDIM)
      M0 = MAX(M0, 1)
C
C     CHECK WHETHER X IS WITHIN RANGE.
      IF (X - A(IDIM)) 200, 50, 100
C
   50 M0 = IDIM - 1
      GO TO 500
C
  100 CONTINUE
C     X IS ABOVE RANGE OF A.  IT IS SET EQUAL TO A(IDIM).
      M0 = IDIM - 1
      X = A(IDIM)
      KOUNTMAX = KOUNTMAX + 1
C
      IF (KOUNTMAX .LE. 10) THEN
         WRITE (IOUTUPRT, 150) X, A(IDIM)
  150    FORMAT(' ', 'IN SUBROUTINE FIND, X=', 1PE13.5,
     1        ' IS LARGER THAN A(IDIM)=', 1PE13.5, '  .  IT IS SET ',
     2        'EQUAL TO A(IDIM).')
C
      ELSE IF (KOUNTMAX .EQ. 11) THEN
         WRITE (IOUTUPRT, 175)
  175    FORMAT (' ', 'IN SUBROUTINE FIND, THERE WERE MORE THAN ',
     1                '10 CASES WHERE X WAS LARGER THAN A(IDIM).',
     2                '     ********************'/)
      END IF
C
      GO TO 500
C
  200 IF (X - A(1)) 250, 225, 290
C
  225 M0 = 1
      GO TO 500
C
  250 CONTINUE
C     X IS LESS THAN THE RANGE OF A.  IT IS SET TO A(1).
      M0 = 1
      X = A(1)
      KOUNTMIN = KOUNTMIN + 1
      IF (KOUNTMIN .LE. 10) THEN
         WRITE (IOUTUPRT, 275) X, A(1)
  275    FORMAT(' ', 'IN SUBROUTINE FIND, X=', 1PE13.5,
     1        ' IS SMALLER THAN A(1)=', 1PE13.5, '  .  IT IS SET ',
     2        'EQUAL TO A(1).')
C
      ELSE IF (KOUNTMIN .EQ. 11) THEN
         WRITE (IOUTUPRT, 285)
  285    FORMAT (' ', 'IN SUBROUTINE FIND, THERE WERE MORE THAN ',
     1                '10 CASES WHERE X WAS SMALLER THAN A(1).',
     2                '     ********************')
      END IF
C
      GO TO 500
C
  290 CONTINUE
C
C     X IS WITHIN THE RANGE OF A.
      IF (X .LT. A(M0)) GO TO 350
C
C     SEARCH BY STEPPING UPWARD THROUGH A.
      MP1 = M0 + 1
      DO 300 M=MP1,IDIM
      IF (X .GE. A(M)) GO TO 300
      M0 = M - 1
      GO TO 500
  300 CONTINUE
      WRITE (IOUTUPRT, 325) X, M0
  325 FORMAT('0', 'ERROR IN ARRAY SEARCH IN SUBROUTINE FIND.  X, M0 =',
     1            1PE13.5, I5, ' .  JOB TERMINATED.')
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE FIND     NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 325:                     '
      ERRMSG(3) = 'THERE IS AN ERROR IN THE SEARCHING '
      ERRMSG(4) = 'PROCESS THROUGH THE ARRAY.         '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
C     SEARCH BY STEPPING DOWNWARD THROUGH A.
  350 CONTINUE
      MM1 = M0 - 1
      DO 400 M=1,MM1
      MM = MM1 - M + 1
      IF(X .LT. A(MM)) GO TO 400
      M0 = MM
      GO TO 500
  400 CONTINUE
C
      WRITE (IOUTUPRT, 325) X, M0
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE FIND     NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 400:                     '
      ERRMSG(3) = 'THERE IS AN ERROR IN THE SEARCHING '
      ERRMSG(4) = 'PROCESS THROUGH THE ARRAY.         '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
  500 CONTINUE
C
      RETURN
C
      END
