      SUBROUTINE GLATS(KHAF,COLRAD,WGT,WGTCS,RCS2)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GLATS       COMPUTES COLAT OF R40 G.G. (FROM SELA)
C   PRGMMR: TUCCILLO         ORG: W/NMC22    DATE: 86-10-21
C
C ABSTRACT: COMPUTES COLATITUDE OF R40 G.G.
C
C PROGRAM HISTORY LOG:
C   86-10-21  JIM TUCCILLO
C   88-08-02  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL GLATS ( KHAF, COLRAD, WGT, WGTCS, RCS2 )
C   INPUT ARGUMENT LIST:
C     KHAF     - NUMBER OF GAUSSIAN LATITUDES PER HEMISPHERE
C
C   OUTPUT ARGUMENT LIST:
C     COLRAD   - COLATITUDE IN RADIANS
C     WGT      - NOT USED
C     WGTCS    - NOT USED
C     RCS2     - NOT USED
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - POLY
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      DIMENSION  COLRAD(KHAF) ,WGT(KHAF), WGTCS(KHAF), RCS2(KHAF)
C
      EPS=1.E-12
      SI=1.
      K2=2*KHAF
      RK2=K2
      SCALE=2.  /(RK2*RK2)
      K1=K2-1
      PI=ATAN(SI)*4.
      DRADZ=PI/360.
      RAD=0.
      DO 1000 K=1,KHAF
      ITER=0
      DRAD=DRADZ
1     CALL POLY(K2,RAD,P2)
2     P1 =P2
      ITER=ITER+1
      RAD=RAD+DRAD
      CALL POLY(K2,RAD,P2)
      IF ( SIGN(SI,P1) .EQ. SIGN(SI,P2) )  GO TO 2
      IF(DRAD.LT.EPS)GO TO 3
      RAD=RAD-DRAD
      DRAD=DRAD*0.25
      GO TO 1
3     CONTINUE
      COLRAD(K)=RAD
      PHI=RAD   *180.  /PI
      CALL POLY(K1,RAD,P1)
      X=  COS(RAD)
      W=SCALE*(1.  -X*X)/(P1*P1)
      WGT(K)=    W
      SN=  SIN(RAD)
      W=W/(SN*SN)
      WGTCS(K)=    W
      RC=1./(SN*SN)
      RCS2(K)=   RC
      CALL POLY(K2,RAD,P1)
      PRPHI =    PHI
      PRCOL =    COLRAD(K)
1000  CONTINUE
      PRINT 100,KHAF
100   FORMAT(1H ,'END GLATS FOR ',I3)
      RETURN
      END
