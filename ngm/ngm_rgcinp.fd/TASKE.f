      SUBROUTINE TASKE
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKE       PERFORMS MULTIPLE PROCESSES ON NGM FIELDS
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: PERFORMS THE FOLLOWING PROCESSES:
C   1)  TO CALCULATE THE SATURATION SPECIFIC HUMIDITY AT THE
C   .   WATER SURFACE,
C   2)  ENSURE THAT THE SPECIFIC HUMIDITY IS LARGER THAN
C   .   SOME SMALL VALUE,
C   3)  EXTRAPOLATE MOISTURE UPWARD FROM THE LAST MOISTURE
C       ANALYSIS LAYER (SUBROUTINE EXMOIS).
C   4)  TO CALCULATE POTENTIAL TEMPERATURE FROM VIRTUAL
C   .   TEMPERATURE, SPECIFIC HUMIDITY, AND SURFACE PRESSURE,
C   5)  TO PERFORM THE DRY CONVECTIVE ADJUSTMENT, AND
C   6)  TO CALCULATE THE MASS-WEIGHTED SPECIFIC HUMIDITY AND
C   .   POTENTIAL TEMPERATURE.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED THE DOCBLOCK
C   98-09-10  HANN-MING JUANG FIXED THE UNDEFINED VARIABLES FOR F90
C
C USAGE:    CALL TASKE
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - CORNER
C                  EXMOIS
C                  GRDSET
C     LIBRARY:
C       COMMON   - CONSTN
C                  COMMON1
C                  BLANK (CONTAINS ARRAY VBL)
C       W3LIB    -
C       W4LIB    - DELETE THE CORRESPONDING LINE OR LINES
C       GRAPHICS - IF LIBRARY IS UNNEEDED
C
C REMARKS: IF THE SEA SURFACE TEMPERATURE IS LESS THAN OR EQUAL TO
C   272 K, THE SATURATION SPECIFIC HUMIDITY AT THE WATER
C   SURFACE IS SET TO ZERO TO PREVENT EVAPORATION.
C   THE DRY CONVECTIVE ADJUSTMENT CONSERVES POTENTIAL ENERGY
C   INSTEAD OF MEAN POTENTIAL TEMPERATURE. THE ADJUSTMENT
C   DOES NOT CHANGE THE RELATIVE HUMIDITY.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      IMPLICIT REAL (A-H,O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON           VBL( INVBL )
C
      COMMON /COMMON1/ VIRTTEMP( IIJMAX ,  INGRDUSE )
C
      DIMENSION P( IKM ), PI( IKM ), THOLD( IKM ), THNEW( IKM ), RH( IKM
     1 )
C
      DIMENSION RHMAX ( IKM ,  INGRDUSE )
      DIMENSION IMAXRH( IKM ,  INGRDUSE )
      DIMENSION JMAXRH( IKM ,  INGRDUSE )
      DIMENSION NUMRH ( IKM ,  INGRDUSE )
C
      DATA KEX1 /0/
C
      RGASD  = 287.05E0
      RGASV  = 461.5E0
      CSUBP  = 1005.E0
      CKAPPA = RGASD  / CSUBP
      EPS    = RGASD /  RGASV
      FACTOR = (1.0E0 - EPS) / EPS
C
C     SPECIFY THE MINIMUM VALUE FOR SPECIFIC HUMIDITY.
      QMIN = 4.0E-6
C
CC....KB =  ILEVH  - 1
      KB =  ILEVH
C
C  SET COUNTERS AND CONSTANTS USED IN DIAGNOSTIC PRINT
C
      KX1 =0
      KX2 =0
      KX3 =0
      KX4 =0
      HMAX  = -1.E-8
      TMAX  = -1.E-8
      QSLOW =  1.E8
      QLOW  =  1.E8
C
C
C     IN THE CLAUSIUS-CLAPYRON EQUATION, THE CONSTANTS FOR LIQUID
C     WATER, AND NOT ICE, ARE ALWAYS USED.
      AL =  5.00650E0
      BL = 19.83923E0
      AI =  0.09859E0
      BI = 22.48869E0
C
      A = AL
      B = BL
      APB = A + B
C
C     INITIALIZE THE ARRAYS USED TO RECORD THE MAGNITUDE
C     OF THE LARGEST RELATIVE HUMIDITIES AND THE NUMBER OF POINTS
C     WITH RELATIVE HUMIDITIES GREATER THAN 100 PERCENT.
      DO 25 NG = 1, NGRDUSE
      DO 25 K  = 1, KM
         RHMAX(K, NG) = -1.0E+10
         NUMRH(K, NG) =  0
   25 CONTINUE
C
C     PROCESS EACH GRID POINT OF EACH NGM GRID.
      DO 1100 NG=1,NGRDUSE
C
C     ZERO THE COUNTERS OF THE NUMBER OF OCCURRENCES OF
C     NEGATIVE SPECIFIC HUMIDITY.
      KOUNT1 = 0
      KOUNT2 = 0
C
C     NUMPRT IS A COUNTER FOR THE DRY CONVECTIVE ADJUSTMENT.
      NUMPRT = 0
C
      CALL GRDSET (NG, INDEXHTH, DUMMY, IM, JM, XPOL, YPOL, CMU, DLAM)
C
C     SPECIFY POINTERS FOR VARIOUS VARIABLES IN ARRAY VBL.
      IJ = IM * JM
      MADDHU  = IADDRG(INDEXHU , NG) -1 -IM -IJ
      MADDHV  = IADDRG(INDEXHV , NG) -1 -IM -IJ
      MADDHTH = IADDRG(INDEXHTH, NG) -1 -IM -IJ
      MADDHQ  = IADDRG(INDEXHQ,  NG) -1 -IM -IJ
      MADDH   = IADDRG(INDEXH,   NG) -1 -IM
      MADDST  = IADDRG(INDEXST,  NG) -1 -IM
      MADDSQ  = IADDRG(INDEXSQ,  NG) -1 -IM
C
      KPRINT=0
C
      DO 1000 J=1,JM
C
      DO 900 I=1,IM
C
C=======================================================
C
C  CHK WHETHER TO TURN ON THE DIAGNOSTIC PRINT OF UPPER LVL
C  MOISTURE EXTRAPOLATION
C
c     IF((MOD(I,45).EQ.0).AND.(MOD(J,45).EQ.0)) KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.54).AND.(J.EQ.4))  KPRINT=1
c     IF((NG.EQ.1).AND.(I.EQ.70).AND.(J.EQ.22))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.58).AND.(J.EQ. 3))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.60).AND.(J.EQ.14))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.54).AND.(J.EQ. 9))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.37).AND.(J.EQ.38))  KPRINT=1
c     IF((NG.EQ.2).AND.(I.EQ.27).AND.(J.EQ.45))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.28).AND.(J.EQ.44))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.72).AND.(J.EQ. 1))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.57).AND.(J.EQ.60))  KPRINT=1
c     IF((NG.EQ.3).AND.(I.EQ.37).AND.(J.EQ.76))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.56).AND.(J.EQ. 5))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.43).AND.(J.EQ.43))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ. 5).AND.(J.EQ.40))  KPRINT=1
C     IF((NG.EQ.1).AND.(I.EQ.33).AND.(J.EQ.29))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.52).AND.(J.EQ.67))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.50).AND.(J.EQ.21))  KPRINT=1
C     IF((NG.EQ.2).AND.(I.EQ.32).AND.(J.EQ.40))  KPRINT=1
c     IF((NG.EQ.3).AND.(I.EQ.69).AND.(J.EQ.88))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.67).AND.(J.EQ.21))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.46).AND.(J.EQ.64))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.15).AND.(J.EQ.38))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ. 8).AND.(J.EQ.26))  KPRINT=1
C     IF((NG.EQ.3).AND.(I.EQ.77).AND.(J.EQ.13))  KPRINT=1
C
C======================================================
C
C     SURFACE PRESSURE IS RETRIEVED AND EXPONENTIATED.
      H = VBL(MADDH + I + IM*J)
      HK = REAL(REAL(H) ** REAL(CKAPPA))
C
      IF ( H .GT. 1.1E0) KX3=KX3+1
C.....IF ( H .GT. 1.1E0) KPRINT=1
      IF ( H .GT. HMAX)  HMAX=H
C
      IF ( KPRINT .EQ. 1) THEN
      PRINT 14,  NG, KB, TMAX
   14 FORMAT('  '//'  ',4X,'NG = ',I1,3X,'KB = ',I2,
     * 3X,'TMAX = ',F6.1,'  I,J,K3-HLRG,K4-NEGQS,K1-QESS,K2-BQMIN,',
     * 'HMX,H,T,QSLO,QSFRS,QSAT,QLO,QFRS,Q,K ='/)
c    * 'HMX,H,HTH,T,QSLO,QSFRS,QSAT,QH,QLO,QFRS,Q,K ='/)
      ENDIF
C
C     THE SATURATION SPECIFIC HUMIDITY AT THE WATER SURFACE
C     IS CALCULATED FROM THE SST AND THE SURFACE PRESSURE.
C*****THE FOLLOWING STARRED LINES ALLOW THE USE OF SST VALUES
C*****OVER LAND OR ICE.
C*****VBL(MADDSQ + I + IM*J) = 0.0E0
      SST = VBL(MADDST + I +IM*J)
C*****IF (SST .LE. 272.E0) GO TO 100
      EWTEMP = 0.00611E0 * REAL( REAL(273.16E0/SST) ** REAL(A) )
     1     * EXP(APB * (1.0E0 - 273.16E0/SST))
      VBL(MADDSQ + I + IM*J) = 0.622E0 * EWTEMP
     1                          / (H - 0.378E0 * EWTEMP)
C*100 CONTINUE
C
C
C     THETA IS CALCULATED AND THE DRY ADIABATIC ADJUSTMENT PERFORMED.
C
      DO 200 K=1,KM
C
C     FIRST THE LAYER PRESSURES (IN BARS) AND LAYER EXNER FUNCTIONS
C     (PI) ARE CALCULATED.
      P(K) = POVH(K)   * H
      PI(K)= PIOVHK(K) * HK
C
C  TRACK OCCURRENCES OF LOW SPECIFIC HUMIDITY
C
      Q     = VBL(MADDHQ+I+IM*J+IJ*K)
      QFRST = Q
      IF ( Q .LT. QLOW) QLOW=Q
      IF ( Q .LT. QMIN) KX2=KX2+1
C
C     BOUND SPECIFIC HUMIDITY AWAY FROM ZERO.
      VBL(MADDHQ+I+IM*J+IJ*K) = MAX(QMIN, VBL(MADDHQ+I+IM*J+IJ*K))
C
C     THE TEMPERATURE IS COMPUTED FROM THE VIRTUAL
C     TEMPERATURE AND SPECIFIC HUMIDITY.
      T = VBL(MADDHTH+I+IM*J+IJ*K)
     1     / (1.0E0 + FACTOR * VBL(MADDHQ+I+IM*J+IJ*K))
C
C     THETA IS CALCULATED FROM TEMPERATURE AND THE EXNER FUNCTION.
      THOLD(K) = T / PI(K)
      THNEW(K) = THOLD(K)
C
C     CALCULATE SATURATION SPECIFIC HUMIDITY FROM
C     TEMPERATURE, AND PRESSURE (IN BARS).
      ESAT = 0.00611E0 * REAL ( REAL(273.16E0/T) ** REAL(A) )
     1       * EXP(APB * (1.0E0 - 273.16E0/T))
      QSAT = 0.622E0 * ESAT / (P(K) - 0.378E0 * ESAT)
C
      QSFRST = QSAT
      IF ( QSAT .LT. QSLOW) QSLOW=QSAT
      IF ( QSAT .LT. 0.E0 ) THEN
         QSAT = QMIN/0.70E0
         KX4 = KX4 + 1
      ENDIF
C
      Q = VBL(MADDHQ+I+IM*J+IJ*K)
C
C  EXTRAPOLATE MOISTURE (Q) UPWARD INTO THOSE LAYERS WITH NO
C  HUMIDITY ANALYSIS
C
      IF ( K .GT. KB )  THEN
C
         QB = VBL(MADDHQ+I+IM*J+IJ*KB)
C
         CALL EXMOIS( P(K) , Q , P(KB) , QB )
C
C  CONSTRAIN EXTRAPOLATED VALUES TO LESS THAN 70 PCT SATURATION
C
         QSAT7 = 0.70E0 * QSAT
         IF ( Q .GT. QSAT7)  THEN
            KEX1 = KEX1 + 1
            Q = MAX(QSAT7,QMIN)
         ENDIF
C
      ENDIF
C
C  CALCULATE RELATIVE HUMIDITY
C
      RH(K) = Q / QSAT
C
C  CHK WHETHER TO PRINT DIAGNOSTIC PROFILES
C
      IF ( KPRINT .EQ. 1)  THEN
c        PRINT 15, I,J,KX3,KX4,KX1,KX2,
c    *      HMAX,H,HTH,T,QSLOW,QSFRST,QSAT,QH,QLOW,QFRST,Q,K
c  15    FORMAT(1X,I3,1X,I2,4(1X,I5),2(1X,F5.3),
c    *   1X,F6.1,1X,F5.1,7(1X,E9.2),1X,I2)
         PRINT 15, I,J,KX3,KX4,KX1,KX2,
     *      HMAX,H,T,QSLOW,QSFRST,QSAT,QLOW,QFRST,Q,K
   15    FORMAT(1X,I3,1X,I2,4(1X,I5),2(1X,F5.3),
     *   1x,F5.1,6(1X,E9.2),1X,I2)
      ENDIF
C
  200 CONTINUE
C
C  RESET PRINT FLAG
C
      KPRINT=0
C
C     WE CAN NOW PERFORM THE DRY ADIABATIC ADJUSTMENT.
      LTOP = 0
C
  225 CONTINUE
C     RETURN POINT IF ANY ADJUSTMENT WAS NECESSARY.  THE ENTIRE
C     SOUNDING MUST BE CHECKED BECAUSE THE LAYER BELOW THE ADJUSTED
C     LAYERS MAY NOW BE UNSTABLE.
      DO 400 K=2,KM
      IF(THNEW(K) .GE. THNEW(K-1)) GO TO 400
C
C     ADJUSTMENT IS NECESSARY.  SET THE INITIAL LAYER LIMITS.
      LBOT = K - 1
      LTOP = K
C
C     RETURN POINT IF MORE THAN 2 ADJACENT LAYERS ARE SUPERADIABATIC.
  250 CONTINUE
C     COMPUTE THE ADJUSTED THETA FOR LAYERS LBOT TO LTOP.
      THNUM = 0.0E0
      THDEN = 0.0E0
      DO 300 KK=LBOT,LTOP
      THNUM = THNUM + DELSIG(KK) * PI(KK) * THNEW(KK)
      THDEN = THDEN + DELSIG(KK) * PI(KK)
  300 CONTINUE
C
      THADJ = THNUM / THDEN
C
      DO 350 KK=LBOT,LTOP
      THNEW(KK) = THADJ
  350 CONTINUE
C
C     IF THIS SUPERADIABATIC REGION HAS REACHED TO THE TOP OF THE
C     MODEL, CHECK THE ENTIRE SOUNDING AGAIN.
      IF (LTOP .GE. KM) GO TO 225
C
C     IF THE NEXT LAYER IS STABLE, CHECK THE ENTIRE SOUNDING AGAIN.
      IF (THNEW(LTOP+1) .GE. THNEW(LTOP) ) GO TO 225
C
C     NEXT LAYER IS UNSTABLE, WE MUST READJUST ALL ADJACENT UNSTABLE
C     LAYERS TOGETHER.
      LTOP = LTOP + 1
      GO TO 250
  400 CONTINUE
C
  500 CONTINUE
C     DRY CONVECTIIVE ADJUSTMENT IS COMPLETE FOR THIS POINT.
C     PRINT OUT INFORMATION CONCERNING IT, IF DESIRED.
      IF (LTOP .EQ. 0) GO TO 750
C     SOME DRY CONVECTIVE ADJUSTMENT HAD TO BE DONE FOR THIS POINT.
C     DETERMINE THE LATITUDE AND LONGITUDE OF THE POINT.
      IRANGE = 2
      IPRINT = 0
      CALL CORNER (REAL(I)-XPOL, REAL(J)-YPOL, CMU, DLAM, IRANGE,
     1             IPRINT, XLONG, YLAT)
C
C     ONLY THE FIRST 10 NORTHERN HEMISPHERIC POINTS
C     WITH LARGE DRY CONVECTIVE ADJUSTMENTS WILL BE PRINTED.
      IF (YLAT .LT. 0.0E0) GO TO 750
      NUMPRT = NUMPRT + 1
      IF (NUMPRT .GT. 10) GO TO 750
C
C     A LARGE CHANGE IN THETA IS PRINTED AFTER BEING STORED IN THOLD.
      DELTHMAX = -9999.E0
C
      DO 600 K=1,KM
         THOLD(K) = THNEW(K) - THOLD(K)
         DELTHMAX = MAX ( DELTHMAX, ABS(THOLD(K) ) )
  600 CONTINUE
C
      IF (DELTHMAX .GT. 5.0E0) THEN
         PRINT 700, I, J, XLONG, YLAT, (THOLD(K), K=1,KM)
  700    FORMAT('0', 'I, J, LONGITUDE, LATITUDE=', 2I4, 2F10.3/
     1          ' ', 16F8.3)
      END IF
C
  750 CONTINUE
C
      DO 800 K=1,KM
C
C     CALCULATE TEMPERATURE FROM THE ADJUSTED THETA
C     AND THE EXNER FUNCTION (PI).
      T = PI(K) * THNEW(K)
C
C     CALCULATE THE ADJUSTED SPECIFIC HUMIDITY FROM RELATIVE
C     HUMIDITY, ADJUSTED TEMPERATURE, AND PRESSURE.  SPECIFIC
C     HUMIDITY IS BOUND AWAY FROM ZERO AND THE PRESSURE-WEIGHTED
C     SPECIFIC HUMIDITY AND TEMPERATURE ARE CALCULATED.
C
      ESAT = 0.00611E0 * REAL( REAL(273.16E0/T) ** REAL(A) )
     1       * EXP(APB * (1.0E0 - 273.16E0/T))
      QSAT = 0.622E0 * ESAT / (P(K) - 0.378E0 * ESAT)
      Q = RH(K) * QSAT
C
C     NEGATIVE VALUES OF SPECIFIC HUMIDITY ARE PRINTED.
      IF (Q .GE. 0.0E0) GO TO 775
      KOUNT2 = KOUNT2 + 1
      IF (KOUNT2 .GE. 1) GO TO 775
      PRINT 126, KOUNT2, NG, I, J, K, VBL(MADDHU+I+IM*J+IJ*K),
     1     VBL(MADDHV+I+IM*J+IJ*K), VBL(MADDHTH+I+IM*J+IJ*K), Q
  126 FORMAT (' ', 'Q IS NEGATIVE.  KOUNT,NG,I,J,K,U,V,T,Q=',
     1             I6, 4I5, 4(1PE16.7))
C
  775 CONTINUE
C     BOUND SPECIFIC HUMIDITY AWAY FROM ZERO AND MULTIPLY BY
C     SURFACE PRESSURE.  THE BOUNDING MAY BE NECESSARY
C     IF SOME DRY CONVECTIVE ADJUSTMENT WAS DONE.
      Q = MAX (QMIN, Q)
      VBL(MADDHQ+I+IM*J+IJ*K) = H * Q
C
      VBL(MADDHTH+I+IM*J+IJ*K) = THNEW(K) * H
C
C     CALCULATE VIRTUAL TEMPERATURE FOR THE BOTTOM LAYER AND SAVE
C     IT FOR USE IN SUBROUTINE TASKG, WHICH SETS UP THE SURFACE
C     PRESSURE TENDENCY METHOD.
      IF (K .EQ. 1) VIRTTEMP(I+(J-1)*IM, NG) = T * (1.0E0 + FACTOR * Q)
C
C     DETERMINE THE MAGNITUDES AND LOCATIONS OF THE POINTS WITH
C     THE LARGEST RELATIVE HUMIDITIES, AND THE NUMBER OF POINTS WITH
C     RELATIVE HUMIDITIES GREATER THAN 100 PERCENT.
      RH(K) = Q / QSAT
C
      IF (RH(K) .GT. 1.0E0) THEN
         NUMRH(K, NG) = NUMRH(K, NG) + 1
      END IF
C
      IF (RH(K) .GT. RHMAX(K, NG)) THEN
         RHMAX (K, NG) = RH(K)
         IMAXRH(K, NG) = I
         JMAXRH(K, NG) = J
      END IF
C
  800 CONTINUE
C
C     FOOT OF THE I LOOP.
  900 CONTINUE
C
C     FOOT OF THE J LOOP.
 1000 CONTINUE
C
      PRINT 1050, NG, NUMPRT
 1050 FORMAT ('0', '***** THE NUMBER OF GRID POINTS WITH DRY ',
     1             'CONVECTIVE ADJUSTMENT IN THE NORTHERN HEMISPHERE ',
     2             'ON GRID', I2, ' IS', I7, '. *****')
C
C     SUMMARY OF NUMBER OF POINTS WITH NEGATIVE SPECIFIC HUMIDITY.
      PRINT 1060, NG, KOUNT1
 1060 FORMAT ('0', '***** THE NUMBER OF OCCURRENCES OF NEGATIVE ',
     1             'SPECIFIC HUMIDITIES ON GRID',I2,
     2             ' BEFORE DRY CONVECTIVE ',
     3             'ADJUSTMENT IS', I5, '.')
C
      PRINT 1070, NG, KOUNT2
 1070 FORMAT ('0', '***** THE NUMBER OF OCCURRENCES OF NEGATIVE ',
     1             'SPECIFIC HUMIDITIES ON GRID',I2,
     2             ' AFTER  DRY CONVECTIVE ',
     3             'ADJUSTMENT IS', I5, '.'/)
C
C     FOOT OF THE NGRDUSE LOOP.
 1100 CONTINUE
C
C
C     PRINT THE MAGNITUDES AND LOCATIONS OF THE LARGEST RELATIVE
C     HUMIDITIES, AND THE NUMBER OF POINTS WITH RELATIVE HUMIDITIES
C     GREATER THAN 100 PERCENT.
      WRITE (6, 1200)
 1200 FORMAT (/// '0', 'STATISTICS ON THE INITIAL RELATIVE ',
     1                 'HUMIDITIES:'/
     2            '0', 8X, 'LAYER', 7X, 'MAX RH (PERCENT)',
     3                 7X, 'I POINT OF MAX',
     4                 7X, 'J POINT OF MAX',
     5                 7X, 'NO. OF POINTS WITH RH GREATER THAN ',
     6                     '100 PERCENT')
C
      DO 1600 NG = 1, NGRDUSE
C
         WRITE (6, 1300) NG
 1300    FORMAT ('0', 'GRID', I2, ':')
C
         DO 1500 K = KM, 1, -1
C
            WRITE (6, 1400) K, RHMAX(K, NG), IMAXRH(K, NG),
     1                      JMAXRH(K, NG), NUMRH(K, NG)
 1400       FORMAT (' ', I11, 2PF20.2, I20, I21, I37)
C
 1500    CONTINUE
C
 1600 CONTINUE
C
C
      WRITE (6, 1700)
 1700 FORMAT ('0')
C
C
      RETURN
C
      END
