      SUBROUTINE HORINT(FIELD, IMAX, JMAX, KMAX, ALONG, ALAT, ISYM,
     1                  XLONG, YLAT, COLUMN)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    HORINT      INTERPOLATES TO A VERTICAL COLUMN
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: INTERPOLATES HORIZONTALLY TO A LATITUDE-
C   LONGITUDE POINT, GIVEN A FIELD ON A LONGITUDE-LATITUDE GRID.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-02  BRIAN SCHMIDT ADDED DOCBLOCKS
C
C USAGE:    CALL HORINT(FIELD, IMAX, JMAX, KMAX, ALONG, ALAT, ISYM,
C                       XLONG, YLAT, COLUMN)
C   INPUT ARGUMENT LIST:
C     FIELD    - INPUT 3D ARRAY ON A LONGITUDE-LATITUDE GRID
C     IMAX     - FIRST DIMENSION OF ARRAY 'FIELD'
C     JMAX     - SECOND DIMENSION OF ARRAY 'FIELD'
C     KMAX     - THIRD DIMENSION OF ARRAY 'FIELD'
C     ALONG    - ARRAY OF LONGITUDES DESCRIBING THE ARRAY 'FIELD'
C     ALAT     - ARRAY OF LATITUDES DESCRIBING THE ARRAY 'FIELD'
C     ISYM     - EQUATORIAL SYMMETRY VARIABLE
C     XLONG    - LONGITUDE TO WHICH THE ARRAY 'FIELD' IS INTERPOLATED
C     YLAT     - LATITUDE TO WHICH THE ARRAY 'FIELD' IS INTERPOLATED
C
C   OUTPUT ARGUMENT LIST:
C     COLUMN   - ARRAY CONTAINING VALUES OF ARRAY 'FIELD' INTERPOLATED
C                TO XLONG,YLAT FOR KMAX LEVELS IN THE VERTICAL
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT CONCERNING ERROR MESSAGES
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - FIND
C                  ERRSTOP
C
C REMARKS: THE SUBROUTINE EXPECTS THAT AT LEAST THE FIRST LONGITUDE
C   IS DUPLICATED.  IF ONLY THE FIRST LONGITUDE IS DUPLICATED,
C   ONLY BILINEAR INTERPOLATION IS POSSIBLE.
C   ISYM INDICATES THE EQUATORIAL SYMMETRY OF THE FIELD.
C   EQUATORIAL SYMMETRY IS IMPOSED ON ALL SOUTHERN HEMISPHERIC
C   POINTS.  IT IS ASSUMED THAT EQUATORIAL BLENDING OF NORTHERN
C   AND SOUTHERN HEMISPHERIC POINTS, IF IT MUST BE DONE, HAS ALREADY
C   BEEN DONE.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
      CHARACTER*35 ERRMSG(4)
C
      DIMENSION FIELD(IMAX, JMAX, KMAX)
      DIMENSION ALONG(IMAX), ALAT(JMAX)
      DIMENSION COLUMN(KMAX)
C
      DATA LAT0/1/
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
C     ENSURE THAT THE LONGITUDE IS WITHIN RANGE.
C     THE LONGITUDE WILL RANGE FROM 0. AT THE GREENWICH MERIDIAN
C     EASTWARD TO 360. DEGREES FOLLOWING THIS SECTION.
      XXLONG = MOD(XLONG, 360.E+0)
      IF (XXLONG .LT. 0.0E+0) XXLONG = XXLONG + 360.0E+0
C
C     ENSURE LATITUDE IS WITHIN RANGE.
      IF (ABS(YLAT) .GT. 90.E+0) THEN
         WRITE (IOUTUPRT, 100) YLAT
  100    FORMAT ('0', 'LATITUDE OUT OF RANGE IN SUBROUTINE HORINT. ',
     1                'YLAT=', F8.2, '.  JOB TERMINATED.')
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
         ERRMSG(1) = 'IN SUBROUTINE HORINT   NEAR STATEME'
         ERRMSG(2) = 'NT NUMBER 100:                     '
         ERRMSG(3) = 'THE LATITUDE IS OUT OF RANGE.      '
         ERRMSG(4) = '                                   '
C
         CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
      END IF
C
C     THE EQUATORIAL BOUNDARY CONDITIONS ARE SPECIFIED, IF NECESSARY.
      YYLAT = YLAT
      FUNC = +1.E+0
C
C     IF THE POINT IS IN THE NORTHERN HEMISPHERE, THE SYMMETRY CONDITION
C     IS IRRELEVANT.
      IF(YLAT .GE. 0.0E+0) GO TO 500
C
C     IF THE POINT IS IN THE SOUTHERN HEMISPHERE AND ISYM EQUALS 0,
C     NO SYMMETRY CONDITION IS APPLIED IN SUBROUTINE HORINT.  THE FIELD
C     PROBABLY HAS ALREADY BEEN SET UP SYMMETRICALLY.
      IF(ISYM .EQ. 0)  GO TO 500
C
      IF(ISYM .NE.  2) GO TO 300
C     EVEN SYMMETRY AND THE POINT IS SOUTH OF THE EQUATOR, SO THE VALUES
C     FOR THE NORTHERN HEMISPHERE ARE USED IN THE INTERPOLATION.
      YYLAT = - YYLAT
      GO TO 500
C
  300 CONTINUE
      IF(ISYM .NE. 1) GO TO 400
C     ODD SYMMETRY AND THE POINT IS SOUTH OF THE EQUATOR, SO THE
C     NEGATIVE OF THE VALUES FOR THE NORTHERN HEMISPHERE ARE USED
C     IN THE INTERPOLATION.
      YYLAT = - YYLAT
      FUNC = -1.0E+0
      GO TO 500
C
  400 CONTINUE
C     AN IMPROPER SYMMETRY CONDITION WAS SPECIFIED.
      WRITE (IOUTUPRT, 450) ISYM
  450 FORMAT('0', '***** IMPROPER SYMMETRY CONDITION REQUESTED ',
     1            'FOR SUBROUTINE HORINT.  ISYM=', I6,
     2            '.  EXECUTION TERMINATED.')
C
C              TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE HORINT   NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 450:                     '
      ERRMSG(3) = 'AN IMPROPER SYMMETRY CONDITION WAS '
      ERRMSG(4) = 'SPECIFIED.                         '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
  500 CONTINUE
C     DETERMINE THE LOCATION OF THE INTERPOLATION POINT WITH
C     RESPECT TO THE LATITUDE-LONGITUDE GRID.  THE LATITUDE AND
C     LONGITUDE OF THE LATITUDE-LONGITUDE GRID POINT WITH SLIGHTLY
C     SMALLER LATITUDE AND LONGITUDE ARE CALCULATED.
C
C     FIRST THE LATITUDE.
      CALL FIND (YYLAT, ALAT, JMAX, LAT0)
C
C     NOW THE LONGITUDE.  THE LONGITUDES ARE ASSUMED TO BE EVENLY
C     SPACED, WITH THE FIRST LONGITUDE BEING AT ALONG(1) AND PROCEEDING
C     EASTWARD. THE SPECIAL CASE OF XXLONG < ALONG(1) IS HANDLED OK.
      DLONG = ALONG(2) - ALONG(1)
      LONG0 = INT((XXLONG-ALONG(1))/DLONG) + 1
      IF ( XXLONG .LT. ALONG(1) ) THEN
           LONG0 = IMAX - 1
           XXLONG = XXLONG + 360.E0
      END IF
C
C     THE WEIGHTING FACTORS FOR THE INTERPOLATION ARE CALCULATED.
      XRATIO = (XXLONG - ALONG(LONG0)) / DLONG
      YRATIO = (YYLAT - ALAT(LAT0)) / (ALAT(LAT0 + 1) - ALAT(LAT0))
      A1 = (1.E+0 - XRATIO) * (1.E+0 - YRATIO)
      A2 = XRATIO * (1. - YRATIO)
      A3 = (1.E+0 - XRATIO) * YRATIO
      A4 = XRATIO * YRATIO
C
C     THE BILINEAR INTERPOLATION IS PERFORMED.
      DO 1000 K=1,KMAX
C
      COLUMN(K) = (A1 * FIELD(LONG0,LAT0,K)
     1            +A2 * FIELD(LONG0+1,LAT0,K)
     2            +A3 * FIELD(LONG0,LAT0+1,K)
     3            +A4 * FIELD(LONG0+1,LAT0+1,K)) * FUNC
C
 1000 CONTINUE
C
      RETURN
C
      END
