C
C
      SUBROUTINE TASKA
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKA       ROTATE WINDS TO NGM COORDINATE SYSTEM
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: ROTATES THE WIND COMPONENTS IN A LATITUDE-LONGITUDE
C   COORDINATE SYSTEM TO THOSE IN THE NGM COORDINATE SYSTEM.  IT
C   IS ASSUMED THAT THE EQUATORIAL SYMMETRY CONDITION HAS ALREADY
C   BEEN APPLIED.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL TASKA
C
C   SUBPROGRAMS CALLED:
C     LIBRARY:
C       COMMON   - CONSTS
C                  CONSTN
C
C REMARKS: NOTE THAT WIND COMPONENTS AT THE NORTH POLE ARE ROTATED,
C   EVEN THOUGH THEY HAVE YET TO BE CORRECTLY DETERMINED.
C   THE WIND COMPONENTS AT THE NORTH POLE STILL EQUAL THE
C   VALUES AT THE NORTHERNMOST GAUSSIAN LATITUDE.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON /DUMMY2/ ANAL( ILONFP1 ,  INLAT ,  ILEVS )
C
      DIMENSION SINE( ILONF ), COSINE( ILONF )
C
      EQUIVALENCE (ANAL(1,1,1),SINE(1)),(ANAL( ILONFP1 ,1,1),COSINE(1))
C
C
C     COMPUTE THE SINE AND COSINE OF THE ROTATION ANGLE FOR
C     EACH LONGITUDE.
      DEGRAD = 3.141592654E0 / 180.E0
      DLONG = 360.E0 / REAL(LONF)
C
      DO 100 I=1,LONF
         ROTANG = DEGRAD * (DLONG * REAL(I-1) - DLAMNGM)
         SINE(I) =   SIN(ROTANG)
         COSINE(I) = COS(ROTANG)
  100 CONTINUE
C
C     PROCESS EACH LATITUDINAL STRIP OF THE NORTHERN HEMISPHERE.
C     SOUTHERN HEMISPHERIC STRIPS ARE NOT NEEDED IN THIS
C     PROGRAM BECAUSE OF THE EQUATORIAL SYMMETRY CONDITION.
      LBLOCK = 0
C
      DO 300 LAT=1,NLAT
         LBLOCK = LBLOCK + 1
C
         LOCU = IU - 1
         LOCV = IV - 1
C
         DO 200 K=1,LEVS
         DO 200 I=1,LONF
            LOCU = LOCU + 1
            LOCV = LOCV + 1
            A =                      -STRIPARA(LOCV, LBLOCK) *COSINE(I)
     1                               -STRIPARA(LOCU, LBLOCK) *  SINE(I)
            STRIPARA(LOCV, LBLOCK) = -STRIPARA(LOCV, LBLOCK) * SINE(I)
     1                               +STRIPARA(LOCU, LBLOCK) *COSINE(I)
            STRIPARA(LOCU, LBLOCK) =  A
  200    CONTINUE
C
  300 CONTINUE
C
      RETURN
C
      END
