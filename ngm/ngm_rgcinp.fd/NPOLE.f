      SUBROUTINE NPOLE
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    NPOLE       EXTRAPOLATES FIELDS TO NORTH POLE STRIP
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: EXTRAPOLATES THE FIELDS (IN LATITUDINAL
C   STRIPS CONTAINING DATA ON A LATITUDE-LONGITUDE GRID
C   ON SIGMA SURFACES) TO THE NORTH POLE LATITUDINAL
C   STRIP.  THE MEAN VALUES (IN THE LONGITUDINAL DIRECTION) FOR
C   THE PENULTIMATE AND ANTEPENULTIMATE LATITUDINAL
C   STRIPS ARE EXTRAPOLATED TO THE NORTH POLE AS A LINEAR
C   FUNCTION OF DISTANCE ON A POLAR STEREOGRAPHIC
C   PROJECTION.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-02  BRIAN SCHMIDT ADDED THE DOCBLOCK
C
C USAGE:    CALL NPOLE
C   INPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C     LIBRARY:
C       COMMON   - CONSTS
C
C REMARKS: NOTE THAT THE DATA AT THIS POINT IN THE PROGRAM,
C   HOWEVER, IS STILL ON THE LATITUDE-LONGITUDE GRID.
C   NOTE ALSO THAT THE WIND COMPONENTS HAVE ALREADY BEEN
C   ROTATED TO THE FRAMEWORK OF THE NGM POLAR STEREOGRAPHIC
C   GRID.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      CHARACTER*35 ERRMSG(4)
C
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C
      DEGRAD = 3.141592654E+0 /180.E+0
C
C
C     THE NUMBER OF 1-LEVEL SEGMENTS (EACH OF LENGTH LONF)
C     CONSTITUTING A LATITUDINAL STRIP IS CALCULATED.
      NSEG = NSIGSTP / LONF
      IF (NSEG * LONF .EQ. NSIGSTP) GO TO 30
      WRITE (IOUTUPRT, 20) LONF, NSIGSTP
   20 FORMAT ('0', '***** NSIGSTP IS NOT A MULTIPLE OF LONF IN ',
     1             'NPOLE. *****'/
     2        ' ', 'LONF=', I8, ',  NSIGSTP=', I8,
     3             '.  EXECUTION TERMINATED.')
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE NPOLE    NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 20:                      '
      ERRMSG(3) = 'NSIGSTP IS NOT A MULTIPLE OF LONF. '
      ERRMSG(4) = '                                   '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
C
   30 CONTINUE
C     FOR EACH SEGMENT, THE LONGITUDINAL MEANS FOR THE
C     PENULTIMATE AND ANTEPENULTIMATE LATITUDES ARE CALCULATED
C     AND THE VALUES EXTRAPOLATED TO THE POLE.
C     NOTE THAT TO REDUCE THE POSSIBILITY OF TRUNCATION ERROR,
C     A REPRESENTATIVE VALUE IS TEMPORARILY SUBTRACTED FROM
C     THE FIELD OF EACH SEGMENT IN THE CALCULATION OF THE MEAN.
C
C     FIRST, THE CONSTANTS OF THE EXTRAPOLATION ARE SPECIFIED.
      SINE1 = SIN (DEGRAD * SLAT(NLAT - 1) )
      SINE2 = SIN (DEGRAD * SLAT(NLAT - 2) )
      RHO1  = SQRT((1.0E+0 - SINE1) / (1.0E+0 + SINE1))
      RHO2  = SQRT((1.0E+0 - SINE2) / (1.0E+0 + SINE2))
      FACT1 =  RHO2 / (RHO2 - RHO1)
      FACT2 = -RHO1 / (RHO2 - RHO1)
C
      WRITE (IOUTUPRT, 50) FACT2, FACT1
   50 FORMAT ('0', 'EXTRAPOLATION TO GET POLE VALUES IN ',
     1             'SUBROUTINE NPOLE.'/
     2        ' ', 'WEIGHTS FOR ANTEPENULTIMATE AND PENULTIMATE ',
     3             'ROWS ARE', 2(1PE15.6))
C
C
      DO 300 M=1,NSEG
C
      ISTART = (M-1) * LONF + 1
      ISTOP  = M * LONF
      SUM1   = 0.0E+0
      SUM2   = 0.0E+0
      REF1   = STRIPARA(ISTART, NLAT-1)
      REF2   = STRIPARA(ISTART, NLAT-2)
C
      DO 100 I=ISTART,ISTOP
      SUM1 = SUM1 + (STRIPARA(I, NLAT-1) - REF1)
      SUM2 = SUM2 + (STRIPARA(I, NLAT-2) - REF2)
  100 CONTINUE
C
C     THE LONGITUDINAL MEANS ARE CALCULATED.
      AMEAN1 = (SUM1 / REAL(LONF)) + REF1
      AMEAN2 = (SUM2 / REAL(LONF)) + REF2
C
C     THE VALUE AT THE POLE CAN NOW BE CALCULATED BY LINEAR
C     EXTRAPOLATION.
C
      PVALUE = FACT1 * AMEAN1 + FACT2 * AMEAN2
C
C     WRITE (IOUTUPRT, 150) M, AMEAN2, AMEAN1, PVALUE
C 150 FORMAT (' ', 'FOR SEGMENT=', I3, ' ANTEPENULTIMATE MEAN=',
C    1             1PE15.6, ',  PENULTIMATE MEAN=', 1PE15.6,
C    2             ', SO THAT POLE VALUE=', 1PE15.6)
C
C     THE POLE VALUE IS ASSIGNED TO EVERY LONGITUDE OF THE
C     POLE LATITUDINAL STRIP.
      DO 200 I=ISTART,ISTOP
      STRIPARA(I, NLAT) = PVALUE
  200 CONTINUE
C
  300 CONTINUE
C
      RETURN
      END
