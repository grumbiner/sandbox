C
C
C
      SUBROUTINE TASKD
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKD       INTERPOLATES QUANTITIES TO NGM GRID PTS
C   PRGMMR: JIM HOKE         ORG: W/NMC/22    DATE: 85-03-27
C
C ABSTRACT: INTERPOLATES HORIZONTALLY THE SURFACE PRESSURE,
C   U AND V WIND COMPONENTS, VIRTUAL TEMPERATURE, SPECIFIC
C   HUMIDITY, AND TERRAIN HEIGHT TO THE NGM GRID POINTS.
C   THE INTERPOLATION IS DONE ON SIGMA SURFACES.
C   THE INPUT FIELDS ARE IN ARRAY STRIPARA, WHICH CONTAINS
C   LATITUDE-LONGITUDE GRIDDED FIELDS ON LATITUDINAL STRIPS ON
C   SIGMA SURFACES.
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED DOCBLOCK
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP
C
C USAGE:    CALL TASKD
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - CULL
C                  GRDSET
C                  INTERP
C                  GRDSET
C     LIBRARY:
C       COMMON   - CONSTS
C                  CONSTN
C                  DUMMY2
C                  BLANK (CONTAINS ARRAY VBL)
C
C REMARKS: THE FIELDS ARE ASSUMED TO HAVE HAD THE EQUATORIAL
C   SYMMETRY CONDITION ALREADY ALLIED TO THEM, SO THAT
C   NO SYMMETRY CONDITION IS NEEDED IN THIS SUBROUTINE FOR
C   THE HORIZONTAL INTERPOLATION.
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      COMMON /CONSTS/ IH, IQ, IT, IU, IV, IZ,
     1                NLAT, LEVH, LEVS, LONF, NSIGSTP,
     2                SLAT( INLAT ), STRIPARA( INSIGSTP ,  INLAT )
C
C       *****   END OF INCLUDED COMMON BLK CONSTS   *****
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON          VBL( INVBL )
      COMMON /DUMMY2/ ANAL( ILONFP1 ,  INLAT ,  ILEVS )
C
      DIMENSION  ALONG( ILONFP1 )
C
C
      IMAX   = LONF + 1
      JMAX   = NLAT
      ISYM   = 0
      DLONG  = 360.E0 / REAL(LONF)
C
C     FIRST, SPECIFY THE LOCATIONS OF THE INPUT GRID LONGITUDE.  THE
C     GRID LATITUDES ARE ALREADY AVAILABLE IN SLAT.
      DO 100 I=1,IMAX
         ALONG(I) = REAL(I-1) * DLONG
  100 CONTINUE
C
C     THE SURFACE PRESSURE IS INTERPOLATED FIRST.
C     THE UNITS ARE CONVERTED FROM CENTIBARS TO BARS.
      KMAX    = 1
      FACTOR  = 0.01E0
C
      CALL CULL (IH, ANAL, IMAX, JMAX, KMAX)
C
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,KMAX,KMAX,
     1            'INPUT SFC PRESS ')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT, ISYM,
     1             INDEXH, FACTOR)
C
C
C     THE U WIND COMPONENT IS INTERPOLATED NEXT.
      KMAX = LEVS
      FACTOR = 1.0E0
C
      CALL CULL (IU, ANAL, IMAX, JMAX, KMAX)
C
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,KMAX,KMAX,
     1            'INPUT U FIELD   ')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT, ISYM,
     1             INDEXHU, FACTOR)
C
C
C     THE V COMPONENT IS NOW INTERPOLATED.
      KMAX = LEVS
      FACTOR = 1.0E0
C
      CALL CULL (IV, ANAL, IMAX, JMAX, KMAX)
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,KMAX,KMAX,
     1            'INPUT V FIELD   ')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT, ISYM,
     1             INDEXHV, FACTOR)
C
C
C     NOW THE VIRTUAL TEMPERATURE.
      KMAX = LEVS
      FACTOR = 1.0E0
C
      CALL CULL (IT, ANAL, IMAX, JMAX, KMAX)
C
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,KMAX,KMAX,
     1            'INPUT VIRT TEMP ')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT, ISYM,
     1             INDEXHTH, FACTOR)
C
C
C     NOW THE SPECIFIC HUMIDITY.
C     NOTE THAT THE SPECIFIC HUMIDITY FOR LAYERS LEVH+1 THROUGH LEVS
C     IS SET TO ZERO. LATER IN SUBROUTINE TASKE, SPECIFIC HUMIDITY
C     IS EXTRAPOLATED UPWARD THRU THESE LAYERS VIA CALL EXMOIS.
C
      KMAX = LEVH
      FACTOR = 1.0E0
C
      CALL CULL (IQ, ANAL, IMAX, JMAX, KMAX)
C
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,KMAX,KMAX,
     1            'INPUT SPEC HUMID')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT, ISYM,
     1             INDEXHQ, FACTOR)
C
      IF (LEVH .LT. LEVS) THEN
C
         DO 300 NG=1, NGRDUSE
            CALL GRDSET (NG, INDEXHQ,
     1                   MADDRS, IM, JM, DUMMY, DUMMY, DUMMY, DUMMY)
            IJ = IM * JM
C
            JADDRS = MADDRS + (LEVH - 1) * IJ
            DO 200 K=LEVH+1, LEVS
               JADDRS = JADDRS + IJ
                 DO 150  L=1,IJ
                   VBL(JADDRS+L-1) = 0.0E0
  150            CONTINUE
  200       CONTINUE
C
  300    CONTINUE
C
      END IF
C
C
C     FINALLY THE TERRAIN HEIGHT.
C     THE TERRAIN HEIGHTS ARE MULTIPLIED BY G/CP BEFORE
C     PLACEMENT INTO ARRAY VBL.
      KMAX = 1
      FACTOR = 9.8E0 / 1005.E0
C
      CALL CULL (IZ, ANAL, IMAX, JMAX, KMAX)
C
      CALL STATS (ANAL, 1,IMAX,IMAX, 1,JMAX,JMAX, 1,1,KMAX,
     1            'INPUT GROUND HT ')
C
      CALL INTERP (ANAL, IMAX, JMAX, KMAX, ALONG, SLAT,
     1            ISYM, INDEXPSI, FACTOR)
C
      RETURN
C
      END
