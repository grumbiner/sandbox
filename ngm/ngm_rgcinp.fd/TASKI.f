      SUBROUTINE TASKI (INSSOILT)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TASKI       INITIALIZES SOIL TEMPERATURE ON ALL GRIDS
C   PRGMMR: KEN MITCHELL     ORG: W/NMC22    DATE: 97-03-07
C
C ABSTRACT: INITIALIZES NGM SUBSURFACE SOIL TEMPERATURE BY APPLYING
C           A SPECIFIED TIME LAG IN DAYS TO A MONTHLY CLIMATOLOGY
C           OF GLOBAL SURFACE AIR TEMPERATURE.  TWO MONTHLY MEAN
C           SURFACE AIR TEMPERATURE FIELDS ARE INPUT AND INTERPOLATED
C           IN TIME ON THE INPUT GLOBAL LAT/LON GRID.  THE TEMPORAL
C           INTERPOLATION ALGORITHM FOLLOWS CLOSELY A SIMILAR MRF
C           ALGORITHM FOR INTERPOLATION OF MONTHLY CLOUD CLIMATOLOGY.
C           LASTLY AN INTERPOLATION IN SPACE TO THE NGM GRIDS IS DONE.
C           SOURCE OF CLIMATOLOGY WAS NCAR VIA AFGL AND OREGON STATE U.
C
C PROGRAM HISTORY LOG:
C   89-02-03  KEN MITCHELL ORIGINAL
C   91-06-25  KEN MITCHELL ADJUSTED OFFSET FOR THE NEW 2-GRID CONFIG
C   97-03-07  KEN MITCHELL DELETED USE OF NAS 30-DAY ARCHIVE FILE OF
C                SIGMA LVL-1 RDAS TEMPERATURE IN FAVOR OF DEFAULT
C                NCAR MONTHLY SFC AIR TEMP CLIMO
C
C   INPUT ARGUMENT LIST:
C     INSSOILT - UNIT NUMBER FOR FILE OF SFC AIR TEMPERATURE CLIMO
C
C   INPUT FILES:
C     INSSOILT - MONTHLY CLIMATOLOGY OF GLOBAL SURFACE AIR TEMPERATURE
C                ORGANIZED ACCORDING TO ONE RECORD PER MONTH STARTING
C                WITH JANUARY AND ENDING WITH DECEMBER
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C                  INTERP
C                  STATS
C     LIBRARY:
C       COMMON   - CONSTN
C                  DUMMY2
C                  BLANK (CONTAINS VBL ARRAY)
C
C REMARKS: THIS PROGRAM IS FOR REAL INPUT AND OUTPUT
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN77
C   MACHINE:  CRAY
C
C$$$
C
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      CHARACTER*4  LTAB(2)
      CHARACTER*16 LMN(12)
      CHARACTER*35 ERRMSG(4)
C
      COMMON           VBL( INVBL )
C
      COMMON /DUMMY2 / ANAL( ILONFP1 ,  INLAT ,  ILEVS )
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
C     SPECIFY THE EXPECTED SIZE OF THE INPUT SFC AIR TEMPERATURE
C     CLIMATOLOGY (5 DEG LONG FROM 0 TO 360 WITH WRAP AROUND AND
C     4 DEG LAT FROM -90 TO 90) AND THE ASSUMED SOIL TEMPERATURE
C     TIME CONSTANT (LAG) IN DAYS FOR THE NOMINAL 10 CM DEPTH OF
C     THE NGM SOIL SLAB.
C
      PARAMETER (IMAX = 73,
     *           JMAX = 46,
     *            LAG = 10)
C
C
      DIMENSION  TSS(IMAX,JMAX),TSS1(IMAX,JMAX)
     *                         ,TSS2(IMAX,JMAX)
      DIMENSION  ALONGTSS(IMAX)
      DIMENSION  ALATTSS (JMAX)
C
      DIMENSION  DAYHF(13),RMDAY(12)
C
      EQUIVALENCE  (ANAL(1,1,1),  TSS(1,1))
      EQUIVALENCE  (ANAL(1,1,2), TSS1(1,1))
      EQUIVALENCE  (ANAL(1,1,3), TSS2(1,1))
C
      DATA  LTAB/'TSS1','TSS2'/
C
      DATA LMN/'SOIL TEMP MNTH01','SOIL TEMP MNTH02','SOIL TEMP MNTH03',
     *         'SOIL TEMP MNTH04','SOIL TEMP MNTH05','SOIL TEMP MNTH06',
     *         'SOIL TEMP MNTH07','SOIL TEMP MNTH08','SOIL TEMP MNTH09',
     *         'SOIL TEMP MNTH10','SOIL TEMP MNTH11','SOIL TEMP MNTH12'/
C
C  SPECIFY JULIAN DAY OF THE MIDDLE OF EACH MONTH.  REPEAT JANUARY
C  AS A 13TH MONTH TO SIMPLIFY INTERPOLATION IN TIME.  ALSO SPECIFY
C  ACCUMULATED JULIAN DAYS FOR THE START OF EACH MONTH.
C
      DATA  DAYHF/ 15.5,  45.0,  74.5, 105.0, 135.5, 166.0,
     *            196.5, 227.5, 258.0, 288.5, 319.0, 349.5, 380.5/
C
      DATA  RMDAY/  0.0,  31.0,  59.0,  90.0, 120.0, 151.0,
     *            181.0, 212.0, 243.0, 273.0, 304.0, 334.0/
C
C
C     SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOPRT = 6
C
C  CONSTRAIN LAG TO WITHIN 0 TO 60 DAYS.  ALGORITHM WILL CORRECTLY
C  HANDLE LAG UP TO 349 DAYS, BUT WE CHOOSE TO FURTHER RESTRICT LAG
C  HERE FROM PHYSICAL CONSIDERATIONS.
C
      IF ((LAG.LT.0).OR.(LAG.GT.60)) THEN
C
         WRITE(IOPRT,40) LAG
         ERRMSG(1) = 'IN SUBROUTINE TASKI    NEAR STATEME'
         ERRMSG(2) = 'NT NUMBER 25:                      '
         ERRMSG(3) = 'THE SPECIFIED TIME LAG IN DAYS FOR '
         ERRMSG(4) = 'SOIL TEMPERATURE IS OUT OF RANGE.  '
         CALL ERRSTOP (IOPRT,ERRMSG)
      END IF
C
C     ENSURE THAT DIMENSIONS OF ARRAY ANAL ARE APPROPRIATE FOR
C     EQUIVALENCE TO ARRAYS TSS, TSS1, TSS2
C
      LONFP10 =  ILONFP1
      NLAT0   =  INLAT
      LEVS0   =  ILEVS
      LANLSZ  = LONFP10*NLAT0
      IJSZTSS = IMAX*JMAX
C
      IF ((LEVS0.LT.3).OR.(LANLSZ.LT.IJSZTSS)) THEN
         WRITE (IOPRT, 50)
         ERRMSG(1) = 'IN SUBROUTINE TASKI    NEAR STATEME'
         ERRMSG(2) = 'NT NUMBER 50:                      '
         ERRMSG(3) = 'THE SIZE OF ARRAY ANAL IS SMALLER T'
         ERRMSG(4) = 'HAN NECESSARY FOR EQUIVALENCE USED '
         CALL ERRSTOP (IOPRT,ERRMSG)
      END IF
C
C  SPECIFY THE BASETIME OF INPUT FOR THIS FORECAST.  THEN
C  COMPUTE RJDAY: THE NO. OF DAYS (FRACTIONAL) TRANSPIRED SINCE
C  00Z JAN 1ST OF CURRENT YEAR.  (NOTE THIS "JULIAN DAY" DEFINITION
C  IS PURPOSELY ONE DAY LESS THAN USUAL DEFINITION.)  LASTLY APPLY
C  SPECIFIED TIME LAG IN DAYS AND COMPUTE JULIAN DAY (RJDLAG) OF THE
C  TIME "LAG DAYS" EARLIER THAN CURRENT ANALYSIS TIME.
C
      IH = NINT(SPECS(2))
      ID = NINT(SPECS(3))
      IM = NINT(SPECS(4))
      IY = NINT(SPECS(5))
C
      RJDAY = RMDAY(IM) + REAL(ID-1) + REAL(IH)/24.0E0
C
      RJDLAG = RJDAY - REAL(LAG)
      IF (RJDLAG.LT.DAYHF(1))     RJDLAG = RJDLAG + 365.E0
C
C  NOTE FROM ABOVE THAT RJDAY RANGES FROM 0. TO 364.5.  WHILE
C  RJDLAG RANGES FROM 15.5 TO 380.5.  THESE LATTER RJDLAG
C  LOWER AND UPPER LIMITS REFLECT THE FACT THAT JANUARY IS
C  TREATED BOTH AS 1ST AND "13TH" MONTH FOR EASIER INTERPOLATION.
C
C  NOW PRINT KEY PARAMETERS AT THIS POINT
C
      WRITE(IOPRT,60)     IY,IM,ID,IH,RJDAY,LAG,RJDLAG
C
C  NEXT FIND THE TWO CONSECUTIVE MONTHS WHOSE MID-MONTH
C  JULIAN DAYS BRACKET THE VALUE OF RJDLAG.  IF BRACKETING
C  MONTHS NOT FOUND, PRINT ERROR AND STOP.
C
           MON1=0
      DO 200  MON=1,12
        IF ((RJDLAG.GE.DAYHF(MON)).AND.(RJDLAG.LT.DAYHF(MON+1))) THEN
           MON1 = MON
           MON2 = MON+1
        END IF
  200 CONTINUE
C
      IF (MON1.EQ.0) THEN
         WRITE(IOPRT,65)
         ERRMSG(1) = ' ERROR IN SUBROUTINE TASKI:        '
         ERRMSG(2) = ' THE VALUEOF LAGGED JULDAY        '
         ERRMSG(3) = ' IS OUT OF RANGE                   '
         ERRMSG(4) = ' EXECUTION TERMINATING ABNORMALLY  '
         CALL ERRSTOP (IOPRT,ERRMSG)
      END IF
C
C  COMPUTE I/O RECORDS OF BRACKETING MONTHS IN CLIMATOLOGY FILE.
C  RECORD MREC1 WILL DENOTE EARLIER MONTH TO BE READ INTO TSS1.
C  RECORD MREC2 WILL DENOTE LATER MONTH TO BE READ INTO TSS2.
C
      IF(MON2.LE.12) THEN
         MREC1 = MON1
         MREC2 = MON2
         WRITE(IOPRT,70)     MON1,MON2,MREC1,MREC2,INSSOILT
         DO 300  KREC=1,MREC1
         READ(INSSOILT,ERR=2010,END=2050) TSS1
         CALL STATS(TSS1,1,IMAX-1,IMAX,1,JMAX,JMAX,1,1,1,LMN(KREC))
  300    CONTINUE
         READ(INSSOILT,ERR=2010,END=2050) TSS2
         CALL STATS(TSS2,1,IMAX-1,IMAX,1,JMAX,JMAX,1,1,1,LMN(MREC2))
C
      ELSE
C
         MREC1 = 12
         MREC2 = 1
         WRITE(IOPRT,70)     MON1,MON2,MREC1,MREC2,INSSOILT
         READ(INSSOILT,ERR=2010,END=2050) TSS2
         CALL STATS(TSS2,1,IMAX-1,IMAX,1,JMAX,JMAX,1,1,1,LMN(MREC2))
         DO 400  KREC=2,MREC1
         READ(INSSOILT,ERR=2010,END=2050) TSS1
         CALL STATS(TSS1,1,IMAX-1,IMAX,1,JMAX,JMAX,1,1,1,LMN(KREC))
  400    CONTINUE
C
      END IF
C
C  PROCEED WITH LINEAR INTERPOLATION IN TIME
C
      WGHT1 = (DAYHF(MON2)-RJDLAG)/(DAYHF(MON2)-DAYHF(MON1))
      WGHT2 = (RJDLAG-DAYHF(MON1))/(DAYHF(MON2)-DAYHF(MON1))
C
      DO 425  J=1,JMAX
      DO 425  I=1,IMAX
      TSS(I,J) = WGHT1*TSS1(I,J) + WGHT2*TSS2(I,J)
  425 CONTINUE
C
C        COMPUTE/PRINT SUMMARY STATS OF SOIL TEMPERATURES AFTER
C        TEMPORAL INTERPOLATION BUT STILL ON ORIGINAL INPUT GRID
C
      CALL STATS (TSS,1,IMAX-1,IMAX,1,JMAX,JMAX,1,1,1,
     *            'INPUT SOIL T ANAL ')
C
C     PREPARE PARAMETERS TO INTERPOLATE TO THE NGM GRIDS
C
      KMAX=1
      ISYM = 0
      FACTOR = 1.0E0
C
C     SPECIFY LATITUDES AND LONGITUDES OF INPUT GRID
C
      DO 450 I=1,IMAX
         ALONGTSS(I)=0.0E0 +  REAL(I-1)*5.0E0
  450 CONTINUE
      ALONGTSS(IMAX)=ALONGTSS(1)
C
      DO 475 J=1,JMAX
         ALATTSS(J)=-90.0E0 +  REAL(J-1)*4.0E0
  475 CONTINUE
C
C        INTERPOLATE SOIL TEMPERATURES HORIZONTALLY IN SPACE TO
C        ALL NGM GRIDS AND STORE IN VBL ARRAY LOCATIONS FOR SOIL TEMP
C
      CALL INTERP(TSS, IMAX, JMAX, KMAX, ALONGTSS, ALATTSS,
     *            ISYM, INDEXTSS, FACTOR)
C
C..................  OLD NAS APPROACH (PART A) .....................
C
C  NOW ON THE DOMAIN OF THE 36-DAY ARCHIVE C-GRID, READ-IN
C  THE TIME-AVERAGED SIGMA-ONE ANAL OF TEMP AND OVERLAY CLIMO
C
C  REPLACE INSSOILT UNIT NO. WITH INSSOILC IN ORDER TO
C  UTILIZE THE SAME I/O ERROR MESSAGES
C
C....INSSOILT=INSSOILC
C
C  SET DIMENSIONS OF C-GRID AS RETAINED IN 36-DAY ARCHIVE
C  (STILL NEED IN POST-NAS ERA FOR LATER DIAGNOSTIC PRINT)
C
      IDIM=83
      JDIM=83
C
C  SPECIFY NO. OF FORMATTED VALUES IN EACH RECORD OF
C  FORMATTED INPUT FILE INSSOILC
C
      NFV =18
C
C  DETERMINE NO. OF FORMATTED INPUT RECORDS PER ROW OF
C   83x83 C-GRID IN THE ARCHIVE
C
      LZF = IDIM/NFV
      IF((IDIM-LZF*NFV).NE.0)  LZF=LZF+1
C
C  SET I,J OFFSETS BETWEEN ARCHIVE C-GRID AND RAFS OPS C-GRID
C  for the old 113 x 91 (PRIOR TO AUG 91 JIF):
C...... JOFF=4
C...... IOFF=18
C  for the new B-grid (POST AUG 91, BUT BEFORE NAS RETIREMENT)
C...... JOFF=4 + 14
C...... IOFF=18 + 16
C
C................ END OLD NAS APPROACH (PART A) ................
C
C  RETRIEVE VBL ADDRESS/DIMENSIONS OF OPS C-GRID SUBSOIL-T
C
C...old B-grid...CALL GRDSET(3,INDEXTSS,MADDRS,IM,JM,DUM,DUM,DUM,DUM)
C
C  for the new B-grid
C
      CALL GRDSET(2,INDEXTSS,MADDRS,IM,JM,DUM,DUM,DUM,DUM)
      MADDTSS = MADDRS - 1 - IM
C
C  PRINT-OUT climatological SUBSOIL-T IN VBL before read
C
      kLZF = IM/NFV
      IF((IM-kLZF*NFV).NE.0)  kLZF=kLZF+1
      DO 98630  L=1,kLZF
      IS = (L-1)*NFV + 1
      IE = L*NFV
      IF(IE.GT.IM)  IE=IM
      WRITE(6,79)  (I, I=IE,IS)
  79  FORMAT(9X,19I6//)
      DO 98625  J=JM,1,-1
      KB = MADDTSS + IM*J + IS
      KE = MADDTSS + IM*J + IE
      WRITE(6,80)  J, ((VBL(K)-273.2E0), K=KB,KE)
  80  FORMAT(I7,3X,18F6.1)
98625 CONTINUE
98630 CONTINUE
C
C................ OLD NAS APPROACH (PART B) .....................
C
C.......    READ ARCHIVE C-GRID SUBSOIL-T INTO PROPER VBL LOCATIONS
C
C.... JB =    1 + JOFF
C.... JE = JDIM + JOFF
C.... WRITE(6,75) MADDRS,MADDTSS,IM,JM,IOFF,JOFF,IDIM,JDIM,
C....*            JB,JE,NFV,LZF
C.75  FORMAT('0',3X,2I10,10I5/)
C
C FIRST READ ID RECORD OF INPUT C-GRID SOIL-T FILE
C
C.... READ(INSSOILC,76,ERR=2010,END=2050) IYRE,IMNE,IDYE,IHRE,NDAYK
C.76  FORMAT(8X,5I2)
C.... WRITE(6,78) IYRE,IMNE,IDYE,IHRE,NDAYK
C.78  FORMAT('0',3X,5I5/)
C
C NEXT READ DATA FROM INPUT C-GRID SOIL-T FILE
C
C.... DO 620  J=JB,JE
C.... DO 610  L=1,LZF
C.... IS = ((L-1)*NFV+1) + IOFF
C.... IE = L*NFV
C.... IF(IE.GT.IDIM)  IE=IDIM
C.... IE = IE + IOFF
C.... KB = MADDTSS + IM*J + IS
C.... KE = MADDTSS + IM*J + IE
C.... READ(INSSOILC,77,ERR=2010,END=2050) (VBL(K), K=KB,KE)
C..77 FORMAT(18F6.1)
C.610 CONTINUE
C.620 CONTINUE
C
C  PRINT-OUT OPS C-GRID SUBSOIL-T IN VBL
C
C.... LZF = IM/NFV
C.... IF((IM-LZF*NFV).NE.0)  LZF=LZF+1
C.... DO 630  L=1,LZF
C.... IS = (L-1)*NFV + 1
C.... IE = L*NFV
C.... IF(IE.GT.IM)  IE=IM
C.... WRITE(6,79)  (I, I=IE,IS)
C.... DO 625  J=JM,1,-1
C.... KB = MADDTSS + IM*J + IS
C.... KE = MADDTSS + IM*J + IE
C.... WRITE(6,80)  J, ((VBL(K)-273.2E0), K=KB,KE)
C.625 CONTINUE
C.630 CONTINUE
C
C ...............  END OLD NAS APPROACH (PART B) ................
C
C       FINISH OF NORMAL PROCESSING REACHED SO JUMP TO RETURN
C
      GO TO 666
C
C***********************************************************************
C*****************  HANDLE READ ERRORS BELOW  **************************
C
C
 2010 CONTINUE
C     ERROR OCCURRED WHEN INPUTING THE SURFACE TEMP
C     MONTHLY CLIMO.
      WRITE (IOPRT, 2020) INSSOILT
 2020 FORMAT ('1', 'ERROR OCCURRED WHEN READING IN THE SFC TMP',
     1             'CLIMATOLOGY ON UNIT',I3//)
C
      GO TO 2100
C
 2050 CONTINUE
C     UNEXPECTED END OF FILE HIT
C     WHEN INPUTING THE SFC TEMP MONTHLY CLIMO
      WRITE (IOPRT, 2060) INSSOILT
 2060 FORMAT ('1', 'UNEXPECTED END OF FILE HIT WHEN READING ',
     1             'IN THE SFC TEMP MONTHLY CLIMO',
     2             'ON UNIT', I3//)
C
 2100 CONTINUE
C
C***********************************************************
C***********   RETURN TO MAIN PROGRAM   ********************
C
  666 RETURN
C
C************************************************************
C***********  MOST FORMAT STATEMENTS FOLLOW **************
C
   40 FORMAT('0', ' THE SPECIFIED SOIL TEMPERATURE TIME LAG =',I7,
     1            ' IS OUT OF RANGE'/' ', ' EXECUTION TERMINATING'/)
   50 FORMAT('0', ' THE SIZE OF ARRAY ANAL IN SUBROUTINE TASKI ',
     1            ' IS TOO SMALL FOR APPLIED EQUIVALENCE'/
     2       ' ', ' EXECUTION TERMINATING.'//)
   60 FORMAT('0', ' TASKI PARAMETERS: BASETIME OF INPUT IS  YEAR = ',I4,
     1           '  MONTH = ',I3,'   DAY = ',I3,'  HOUR = ',I3/' ',
     2         19X,'BASETIME JULDAY = ',F7.2,'   LAG = ',I4,
     3             '  LAGGED JULDAY = ',F7.2)
   65 FORMAT('0','  THE VALUE OF LAGGED JULDAY IN SUBROUTINE TASKI',
     1            ' IS OUT OF RANGE.  EXECUTION IS TERMINATING')
   70 FORMAT('0', ' MON1 = ',I3,'   MON2 = ',I3,'   MREC1 = ',I3,
     1         '   MREC2 = ',I3,'   FROM I/O UNIT = ',I3)
C
      END
