C
C
      SUBROUTINE PRINTBIT (BITS, IDIM, JDIM, NG, LABEL)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    PRINTBIT    PRINTS THE LOGICAL ARRAY CALLED BITS
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: PRINTS THE LOGICAL ARRAY CALLED BITS
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-03  BRIAN SCHMIDT ADDED DOCBLOCK
C   90-07-01  KEN MITCHELL CONVERTED TO CRAY Y-MP
C   92-07-14  KEN MITCHELL MODIFIED TO PRINT ENTIRE GRID
C
C USAGE:    CALL PRINTBIT (BITS, IDIM, JDIM, NG, LABEL)
C   INPUT ARGUMENT LIST:
C     BITS     - LOGICAL ARRAY TO BE PRINTED
C     IDIM     - FIRST DIMENSION OF ARRAY BITS
C     JDIM     - SECOND DIMENSION OF ARRAY BITS
C     NG       - GRID NUMBER FOR LOGICAL ARRAY TO BE PRINTED
C     LABEL    - LABEL DESCRIBING GRID NG
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C REMARKS: THE LABEL CAN HAVE UP TO 24 CHARACTERS
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77
C   MACHINE:  CRAY Y-MP
C
C$$$
C
      LOGICAL BITS
C
      DIMENSION BITS(IDIM, JDIM)
      DIMENSION LABEL(3)
      DIMENSION ILABEL1(IDIM),ILABEL2(IDIM),ILABEL3(IDIM)
C
C     ITOT IS NO. OF "PAGES" PRINTED PER FIELD
C      (MAX OF 74 COLUMNS OF VALUES PRINTED PER PAGE)
C
      ITOT = (IDIM - 1)/74 + 1
C
      DO 100 I=1,IDIM
      ILABEL1(I) = I / 100
      ILABEL2(I) = ( I - ILABEL1(I)*100 ) / 10
      ILABEL3(I) = MOD (I, 10)
  100 CONTINUE
C
      PRINT 200, NG, (LABEL(I), I=1,3)
  200 FORMAT (///,'LOGICAL MASK ARRAY FOR GRID',I2,' -- ',3A8)
C
C     PRINT THE NUMBER OF ELEMENTS AND THE NUMBER OF TRUE ELEMENTS
C
      IJ     = IDIM * JDIM
      NUMONES= 0
      DO 225  I=1,IDIM
      DO 225  J=1,JDIM
      IF(BITS(I,J)) NUMONES=NUMONES+1
  225 CONTINUE
      PRINT 250, IJ, NUMONES
  250 FORMAT (//,'THERE ARE', I6, ' ELEMENTS DISPLAYED, OF WHICH',
     1             I6, ' ARE TRUE.')
C
      DO 800  K=1,ITOT
C
      IB = 1 + (K-1)*74
      IE = IB + 74 - 1
      IF (IE .GT. IDIM) IE=IDIM
C
      PRINT 290, K,ITOT,IB,IE
  290 FORMAT(//,1X,'PAGE=',I2,' OF',I2,' (COLS ',I3,' TO ',I3,')')
C
      PRINT 300, (ILABEL1(I), I=IB,IE)
  300 FORMAT(/, 3X, 'I= ', 74I1)
C
      PRINT 400, (ILABEL2(I), I=IB,IE)
  400 FORMAT(  6X,  74I1)
C
      PRINT 500, (ILABEL3(I), I=IB,IE)
  500 FORMAT(   6X,  74I1)
C
      PRINT 550
  550 FORMAT (/)
C
      DO 700 J=1,JDIM
         JJ = JDIM + 1 - J
C
         PRINT 600, JJ, (BITS(I, JJ), I=IB,IE)
  600    FORMAT ( 'J=', I3, 1X, 74L1)
C
  700 CONTINUE
C
  800 CONTINUE
C
      PRINT 550
C
      RETURN
      END
