      SUBROUTINE INTERP (FIELD, IMAX, JMAX, KMAX, ALONG, ALAT,
     1                   ISYM, INDEX, FACTOR)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    INTERP      INTERPOLATES TO ALL NGM GRIDS
C   PRGMMR: JIM HOKE         ORG: W/NMC22    DATE: 85-03-27
C
C ABSTRACT: INTERPOLATES A FIELD ON A LATITUDE-LONGITUDE GRID TO
C   ALL THE NGM GRIDS, AND THEN STORES THE RESULT INTO ARRAY VBL
C
C PROGRAM HISTORY LOG:
C   85-03-27  JIM HOKE
C   88-08-02  BRIAN SCHMIDT ADDED DOCBLOCKS
C   89-02-03  KEN MITCHELL ADDED SOIL TEMPERATURE INDEX CHECK
C
C USAGE:    CALL INTERP(FIELD, IMAX, JMAX, KMAX, ALONG, ALAT,
C                       ISYM, INDEX, FACTOR)
C   INPUT ARGUMENT LIST:
C     FIELD    - 3D ARRAY
C     IMAX     - FIRST DIMENSION OF ARRAY FIELD
C     JMAX     - SECOND DIMENSION OF ARRAY FIELD
C     KMAX     - THIRD DIMENSION OF ARRAY FIELD
C     ALONG    - ARRAY OF LONGITUDES DESCRIBING THE ARRAY FIELD
C     ALAT     - ARRAY OF LATITUDES DESCRIBING THE ARRAY FIELD
C     ISYM     - EQUATORIAL SYMMETRY VARIABLE
C     INDEX    - ADDRESS REFERENCE FOR STORING THE ARRAY FIELD
C                IN THE ARRAY VBL.
C     FACTOR   - SCALING PARAMETER
C
C   OUTPUT ARGUMENT LIST:
C     NONE
C
C   OUTPUT FILES:
C     FT06F001 - FOR PRINTOUT
C
C   SUBPROGRAMS CALLED:
C     UNIQUE:    - ERRSTOP
C                  GRDSET
C                  CORNER
C                  HORINT
C     LIBRARY:
C       COMMON   - CONSTN
C                  BLANK (CONTAINS ARRAY VBL)
C
C ATTRIBUTES:
C   LANGUAGE: CYBER FORTRAN 200
C   MACHINE:  CYBER 205
C
C$$$
C
      IMPLICIT REAL (A-H, O-Z)
C
C
      INCLUDE 'parmodel'
C
C
      LOGICAL  BITSEA, BITSNO, BITWVL
C
      COMMON /CONSTN/ IMG( INGRDUSE ), JMG( INGRDUSE ),
     1                IAG( INGRDUS1 ), JAG( INGRDUS1 ),
     2                IBG( INGRDUS1 ), JBG( INGRDUS1 ),
     3                IADDRG( INIADDRS ,  INGRDUSE ),
     4                INDEXHU,  INDEXHV,  INDEXHTH, INDEXHQ,
     5                INDEXH,   INDEXST,  INDEXSQ,  INDEXALB,
     6                INDEXSLP, INDEXPSI, INDEXCD,  INDEXCC,  INDEXGSP,
     7                INDEXRAD, INDEXDLW, INDEXDSW, INDEXCPR, INDEXMA,
     8                INDEXSEC, INDEXZ0, INDEXTSS,  INDEXCG,  INDEXDHQ,
     9                INDEXTSD, KM, NH, NGRDUSE,
     1                SPECS( INSPECS ), DELSIG( IKM ),
     2                POVH( IKM ), PIOVHK( IKM ),
     3                CMUU  ( INGRDUSE ),
     4                XPOLEH( INGRDUSE ), YPOLEH( INGRDUSE ),
     5                XPOLEU( INGRDUSE ), YPOLEU( INGRDUSE ),
     6                XPOLEV( INGRDUSE ), YPOLEV( INGRDUSE ),
     7                DLAMNGM,
     8                BITSEA( IIJMAX ,  INGRDUSE ),
     9                BITSNO( IIJMAX ,  INGRDUSE ),
     1                BITWVL( IIJMAX ,  INGRDUSE )
C
C       ****   END OF INCLUDED COMMON BLOCK CONSTN   ***
C
C
      COMMON VBL( INVBL )
C
      CHARACTER*35 ERRMSG(4)
C
      DIMENSION COLUMN( ILEVS )
      DIMENSION FIELD(IMAX,JMAX,KMAX), ALONG(IMAX), ALAT(JMAX)
C
C
C
C              SPECIFY THE UNIT NUMBER OF THE PRINTER.
      IOUTUPRT = 6
C     WE SPECIFY THAT THE LONGITUDE RETURNED BY SUBROUTINE
C     CORNER IS TO RANGE FROM 0 TO 360 DEGREES AND THAT
C     THAT SUBROUTINE IS NOT TO PRINT OUT THE LATITUDES
C     AND LONGITUDES IT CALCULATES.
      IRANGE = 1
      IPRT = 0
C
C     THE DUMMY VALUES ARE SPECIFIED FOR USE AT POINTS FARTHER
C     SOUTH THAN THE NGM WILL EVER FORECAST.
C     THE DUMMY VALUES ARE THE VALUES BEFORE THE SCALING
C     PARAMETER (FACTOR) IS APPLIED.
      IF( (INDEX .NE. INDEXHU) .AND. (INDEX .NE. INDEXHV) .AND.
     1    (INDEX .NE. INDEXHQ) .AND. (INDEX. NE. INDEXPSI) .AND.
     2    (INDEX .NE. INDEXCD) ) GO TO 5
      DUMMY = 0.0E+0
      GO TO 30
C
    5 CONTINUE
      IF((INDEX .NE. INDEXHTH) .AND. (INDEX .NE. INDEXTSS)) GO TO 10
C     INDEXHTH REPRESENTS VIRTUAL TEMPERATURE AT THIS POINT.
C     INDEXTSS REPRESENTS SOIL TEMPERATURE
      DUMMY = 250.0E+0
      GO TO 30
C
   10 CONTINUE
      IF (INDEX .NE. INDEXH) GO TO 12
      DUMMY = 100.0E+0
      GO TO 30
C
   12 CONTINUE
      IF (INDEX .NE. INDEXSLP) GO TO 13
      DUMMY = 1000.0E+0
      GO TO 30
C
   13 CONTINUE
      IF (INDEX .NE. INDEXALB) GO TO 14
      DUMMY = 0.06E+0
      GO TO 30
C
   14 CONTINUE
      IF (INDEX .NE. INDEXZ0) GO TO 15
      DUMMY = 0.001E+0
      GO TO 30
C
   15 CONTINUE
      IF (INDEX .NE. INDEXMA) GO TO 16
      DUMMY = 0.10E+0
      GO TO 30
C
   16 CONTINUE
      IF (INDEX .NE. INDEXTSD) GO TO 17
      DUMMY = 0.E+0
      GO TO 30
C
   17 CONTINUE
C     THE SUBROUTINE IS BEING USED FOR INTERPOLATION OF A FIELD
C     FOR WHICH IT WAS NOT ORIGINALLY INTENDED.  A DUMMY VALUE
C     HAS NOT BEEN SPECIFIED IN THE CODE FOR THIS VARIABLE FOR
C     POINTS FARTHER SOUTH THAN THE NGM WILL EVER FORECAST.
C     SPECIFICATION OF DUMMY VALUES FOR SURFACE TEMPERATURE,
C     SURFACE MOISTURE, AND ALBEDO MAY CREATE SOME CONFUSION
C     WITH SPECIAL VALUES USED FOR THESE FIELDS TO INDICATE
C     SUCH THINGS AS LAND POINTS.
      WRITE (IOUTUPRT, 20) INDEX
   20 FORMAT ('0', '***** A VARIABLE TYPE WAS SPECIFIED FOR WHICH ',
     1             'SUBROUTINE INTERP WAS NOT PREPARED.'/
     2        ' ', 'INDEX=', I5, '  EXECUTION TERMINATED.')
C
C     TERMINATE THE CONTROLLEE WITH A RETURN CODE OF 8.
      ERRMSG(1) = 'IN SUBROUTINE INTERP   NEAR STATEME'
      ERRMSG(2) = 'NT NUMBER 20:                      '
      ERRMSG(3) = 'A VARIABLE TYPE WAS SPECIFIED FOR W'
      ERRMSG(4) = 'HICH SUB INTERP WAS NOT PREPARED.  '
C
      CALL ERRSTOP (IOUTUPRT, ERRMSG)
C
C
   30 CONTINUE
      DO 400 NG=1,NGRDUSE
      CALL GRDSET(NG, INDEX, MADDRS, IM, JM, XPOL, YPOL, CMU, DLAM)
C
      IJ     = IM * JM
      JADDRS = MADDRS - 1 - IM - IJ
C
      DO 300 J=1,JM
      Y = REAL(J) - YPOL
C
      DO 200 I=1,IM
      X = REAL(I) - XPOL
C
C     DETERMINE THE LATITUDE/LONGITUDE OF THE NGM GRID POINT.
      CALL CORNER(X, Y, CMU, DLAM, IRANGE, IPRT, XLONG, YLAT)
C
C     1) IF THE POINT IS FARTHER SOUTH THAN THE SOUTHERNMOST
C     LATITUDINAL STRIP, WHICH (FOR FIELDS ON THE LATITUDE-
C     LONGITUDE GRID IN LATITUDINAL STRIPS)
C     SHOULD HAVE BEEN CHOSEN (THROUGH
C     THE USE OF PARAMETER NLATSH) TO BE SOUTH OF THE
C     SOUTHERNMOST NGM FORECAST POINT FOR THE GIVEN NH AND
C     LATITUDINAL STRIPS (NOTE THAT THE DRAG COEFFICIENT
C     IS ON ANOTHER LOLA GRID THAT ONLY REACHS TO THE EQUATOR), AND
C     2) IF NORTHERN HEMISPHERIC VALUES ARE NOT TO BE USED TO
C     SPECIFY THE VALUE, THEN
C     A DUMMY VALUE IS ASSIGNED TO THE PARTICULAR VARIABLE.
      IF ((YLAT .GE. ALAT(1)) .OR. (ISYM .EQ. 1)
     1                        .OR. (ISYM .EQ. 2) ) GO TO 40
      DO 35 K=1,KMAX
      COLUMN(K) = DUMMY
   35 CONTINUE
      GO TO 45
C
   40 CONTINUE
C     HORIZONTALLY INTERPOLATE THE FIELD TO THE NGM GRID POINT.
      CALL HORINT(FIELD, IMAX, JMAX, KMAX, ALONG, ALAT, ISYM,
     1            XLONG, YLAT, COLUMN)
C
   45 CONTINUE
C     STORE THE INTERPOLATED VALUES IN ARRAY VBL.
      DO 100 K=1,KMAX
      VBL(JADDRS + I + IM*J + IJ*K ) = COLUMN(K) * FACTOR
  100 CONTINUE
C
  200 CONTINUE
C
  300 CONTINUE
C
  400 CONTINUE
C
      RETURN
C
      END
