      SUBROUTINE COF2GRD (LUN1,NC,KMAX,IWIND,JROMB,
     1      JCAP,AGRID,AGRID2)
C
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    COF2GRD     CONVERT ONE RECORD OF SIGMA COEFF FILE
C                            TO LAT/LON GRID
C   PRGMMR: ROGERS           ORG: W/NP22     DATE: 99-01-28
C
C ABSTRACT: CONVERT SIGMA COEFFICIENT RECORD TO GRID SPACE USING
C           SPLIB ROUTINES. THESE ROUTINES WILL RETURN A GLOBAL
C           LAT/LON GRID WHOSE RESOLUTION IS DETERMINED BY THE 
C           NUMBER OF GRID POINTS. THEN, THE RELEVENT SUBSET FOR
C           WHICH WE HAVE HIGH-RES OROGRAPHY IS EXTRACTED (DIMENSION
C           OF BOTH THE EXTRACTED GRID AND GLOBAL GRID SET IN 
C           parmanl FILE)
C
C PROGRAM HISTORY LOG:
C   99-01-28  ROGERS
C
C USAGE:    CALL COF2GRD(LUN1,NC,KMAX,IWIND,JROMB, 
C               JCAP,AGRID,AGRID2)
C
C   INPUT ARGUMENT LIST:
C     LUN1     - FORTRAN UNIT FOR SIGMA FILE
C     NC       - LENGTH OF SIGMA RECORD = RES+1*RES+2
C     KMAX     - NUMBER OF SIGMA LEVELS IN GLOBAL MODEL
C     IWIND    - SWITCH WHICH SHOULD BE SET=1 IF WE WANT TO
C                PROCESS DIVERGENCE/VORTICITY COEFFICIENTS FOR
C                LAT/LON WINDS 
C     JROMB    - SPECTRAL DOMAIN SHAPE (0 FOR TRIANGULAR, 
C                1 FOR RHOMBOIDAL)
C     JCAP     - SPECTRAL TRUNCATION
C
C   OUTPUT FILES:
C     AGRID    - EXTRACTED LAT/LON GRID
C     AGRID2   - SECONDARY EXTRACTED LAT/LON GRID; USED ONLY IF U AND
C                V WIND COMPONENTS MUST BE RETURNED 
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN-90
C   MACHINE: CRAY C-90
C
C$$$
      INCLUDE "parmanl"
      COMMON /GRID/ POLA,NORTH,ALONVT,POLEI,POLEJ,XMESHL,SIGERR
C
C     XMESHL = EAST-WEST GRID INCREMENT
C     POLEI  = SOUTH-NORTH GRID INCREMENT
C     POLEJ  = WESTERN BOUNDARY OF LAT/LON GRID
C     ALONVT = SOUTHERN BOUNDARY OF LAT/LON GRID
C
      REAL AGRID(IMAX,JMAX),AGRID2(IMAX,JMAX)
      REAL GRIDGB(IMGLO,JMGLO),DWORK(NC),ZWORK(NC),HWORK(NC)
      REAL GRIDGB1(IMGLO,JMGLO)
      INTEGER IWIND
C
C   COMPUTE INDICES FOR GRID EXTRACTION
C
      ISTART = (360.0 - POLEJ) / XMESHL + 1
      IEND = ISTART + IMAX - 1
      JSTART = ((ALONVT + 90.0) / POLEI) + 1
      JEND = JSTART + JMAX - 1 
      print *,istart,iend,jstart,jend
C
      IF(IWIND .EQ. 0) THEN
        READ(LUN1) HWORK
C
C   CONVERT COEFFICIENTS TO GRID SPACE HERE, ENSURING THAT THE
C   RETURNED LAT/LON GRID STARTS AT THE SOUTH POLE
C
        print *,JROMB,JCAP,IMGLO,JMGLO
        CALL SPTRAN(JROMB,JCAP,0,IMGLO,JMGLO,1,0,0,-IMGLO,IMGLO,
     1        0,0,0,0,1,HWORK,GRIDGB(1,JMGLO),GRIDGB(1,1),1)
        print *,'after sptran',JROMB,JCAP,IMGLO,JMGLO
C     
C   EXTRACT WORKING GRID FROM GLOBAL GRID
C
        DO J = 1, JMAX
          DO I = 1, IMAX
            AGRID(I,J) = GRIDGB(ISTART+I-1,JSTART+J-1)
          ENDDO
        ENDDO
C
      ELSE
C
        READ(LUN1) DWORK
        READ(LUN1) ZWORK
C
C  GRIDGB = U-COMPONENT
C  GRIDGB1 = V-COMPONENT
C
        CALL SPTRANV(JROMB,JCAP,0,IMGLO,JMGLO,1,0,0,-IMGLO,IMGLO,
     1        0,0,0,0,1,DWORK,ZWORK,GRIDGB(1,JMGLO),GRIDGB(1,1),
     2        GRIDGB1(1,JMGLO),GRIDGB1(1,1),1)
C     
C   EXTRACT WORKING GRID FROM GLOBAL GRID
C
        DO J = 1, JMAX
          DO I = 1, IMAX
            AGRID(I,J) = GRIDGB(ISTART+I-1,JSTART+J-1)
            AGRID2(I,J) = GRIDGB1(ISTART+I-1,JSTART+J-1)
          ENDDO
        ENDDO
C
      ENDIF
C
      RETURN
      END
