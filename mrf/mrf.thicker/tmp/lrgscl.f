 
 
 
CFPP$ NOCONCUR R
CFPP$ EXPAND(FPVS)
C-----------------------------------------------------------------------
      SUBROUTINE LRGSCL(IM,IX,KM,DT,PS,T,Q,SL,DEL,SLK,RN,LAT)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    LRGSCL      CALCULATE GRID-SCALE PRECIPITATION
C   PRGMMR: HUA-LU PAN       ORG: W/NMC23    DATE: 94-04-15
C
C ABSTRACT: CALCULATES GRID-SCALE CONDENSATION FOR ONE LEAP-FROG
C   TIMESTEP, PRODUCES RAIN, AND ADJUSTS TEMPERATURE AND SPECIFIC
C   HUMIDITY BY WET-BULB PROCESS.  EVAPORATION OF PART OR ALL OF THE
C   RAIN MAY OCCUR AS IT TRAVERSES UNSATURATED LAYERS ON THE WAY DOWN.
C   FOR CONDITIONALLY UNSTABLE LAYERS, A CONVECTIVE ADJUSTMENT PROCEDURE
C   IS APPLIED TO ADJUST TO A UNIFORM THETA-E.
C
C PROGRAM HISTORY LOG:
C   94-04-15  HUA-LU PAN
C
C USAGE:    CALL LRGSCL(IM,IM2,KM,DT,PS,T,Q,SL,DEL,SLK,RAIN,LAT)
C
C   INPUT ARGUMENT LIST:
C     IM       - INTEGER NUMBER OF POINTS
C     IM2      - REAL FIRST DIMENSION OF T AND Q
C     KM       - INTEGER NUMBER OF LEVELS
C     DT       - REAL TIME STEP IN SECONDS
C     PS       - REAL (IM) SURFACE PRESSURE IN KILOPASCALS (CB)
C     T        - REAL (IM2,KM) CURRENT TEMPERATURE IN KELVIN
C     Q        - REAL (IM2,KM) CURRENT SPECIFIC HUMIDITY IN KG/KG
C     SL       - REAL (KM) SIGMA VALUES
C     DEL      - REAL (KM) SIGMA LAYER THICKNESS
C     SLK      - REAL (KM) SIGMA VALUES ** KAPPA
C     LAT      - INTEGER LATITUDE NUMBER
C
C   OUTPUT ARGUMENT LIST:
C     Q        - REAL (IM2,KM) ADJUSTED SPECIFIC HUMIDITY IN KG/KG
C     T        - REAL (IM2,KM) ADJUSTED TEMPERATURE IN KELVIN
C     RN       - REAL (IM) LARGE-SCALE RAIN IN METERS
C
C SUBPROGRAMS CALLED:
C   FPVS     - FUNCTION TO COMPUTE SATURATION VAPOR PRESSURE
C   FPKAP    - FUNCTION TO COMPUTE P RAISED TO THE FACTOR KAPPA
C   FTHE     - FUNCTION TO COMPUTE THETA-E
C   FTMA     - FUNCTION TO COMPUTE TEMPERATURE AND MOISTURE ALONG A
C              MOIST ADIABAT
C
C REMARKS: THE PRECIPITATION REACHING THE GROUND SHOULD BE HALVED
C   BEFORE IT IS USED TO INCREMENT GESHEM, THE RUNNING TOTAL.  THIS
C   PREVENTS DOUBLE-COUNTING OF GESHEM, WHICH IS INCREMENTED EVERY
C   HALF LEAP-FROG TIMESTEP.
C          THE EVAPORATION RATE FOR FALLING PRECIP IS CALCULATED
C   ACCORDING TO A METHOD DEVISED BY E.KESSLER, IN WHICH A MEAN DROP
C   SURFACE AREA IS OBTAINED FROM THE RAINWATER CONTENT.
C          ICE IS NOT CONSIDERED.
C          FUNCTIONS FPVS,FPKAP,FTHE,FTMA ARE INLINED BY FPP.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77
C   MACHINE:  CRAY
C
C$$$
      DIMENSION DEL(KM),SL(KM),PS(IM),
     &          Q(IX,KM),T(IX,KM),RN(IM)
C  PHYSICAL PARAMETERS
      PARAMETER(G= 9.8000E+0 ,RD= 2.8705E+2 ,RV= 4.6150E+2 ,
     &          CP= 1.0046E+3 ,HVAP= 2.5000E+6 )
      PARAMETER(ELOCP=HVAP/CP,EL2ORC=HVAP*HVAP/(RV*CP),
     &          EPS=RD/RV,EPSM1=RD/RV-1.)
C-----------------------------------------------------------------------
C  CONDENSE RAIN TO WETBULB TEMPERATURE IF SUPERSATURATED
C  OR EVAPORATE RAIN USING KESSLER PARAMETERIZATION.
      DO I=1,IM
        RN(I)=0.
      ENDDO
      DO K=KM,1,-1
        DO I=1,IM
          DPOVG=DEL(K)/G*PS(I)
          ES=FPVS(T(I,K))
          QS=EPS*ES/(SL(K)*PS(I)+EPSM1*ES)
          QCOND=(Q(I,K)-QS)/(1.+EL2ORC*QS/T(I,K)**2)
          IF(QCOND.GT.0.) THEN
            Q(I,K)=Q(I,K)-QCOND
            T(I,K)=T(I,K)+QCOND*ELOCP
            RN(I)=RN(I)+QCOND*DPOVG
          ELSEIF(RN(I).GT.0.) THEN
            QEVAP=-QCOND*(1.-EXP(-0.32*SQRT(2.*DT*RN(I))))
            RNEVAP=MIN(QEVAP*DPOVG,RN(I))
            Q(I,K)=Q(I,K)+RNEVAP/DPOVG
            T(I,K)=T(I,K)-RNEVAP/DPOVG*ELOCP
            RN(I)=RN(I)-RNEVAP
          ENDIF
        ENDDO
      ENDDO
C-----------------------------------------------------------------------
      RETURN
      END
