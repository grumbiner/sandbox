      SUBROUTINE OMEGAS(NX,NXS,NZ,
     1      DPHI,DLAM,CG,UG,VG,DG,DEL,RCL,VVEL,PS,SL)
CFPP$ NOCONCUR R
C....   CODE LIFTED FROM POST (MCP1840) JUN 88--COMPUTES VVEL (CB/SEC)
C....    INPUT PS IN CB,OUTPUT VVEL IN CB/SEC
C....   DO LOOPS ALTERED FOR BETTER VECTORIZATION POSSIBILITIES..K.A.C.
      DIMENSION DPHI(NXS),DLAM(NXS),
     1 CG(NX,NZ),UG(NXS,NZ),VG(NXS,NZ),
     2 DG(NXS,NZ),DEL(NZ),PS(NXS),SL(NZ)
C...   VVEL CONTAINS OMEGA IN LAYERS ON RETURN FROM SUBROUTINE...
      DIMENSION VVEL(NX,NZ)
      DIMENSION DB(NX,NZ),CB(NX,NZ),DOT(NX,NZ+1)
      DO 1 K=1,NZ+1
        DO 1 LO=1,NX
          DOT(LO,K) = 0. E 0
    1 CONTINUE
C...  COMPUTE C=V(TRUE)*DEL(LN(PS)).DIVIDE BY COS FOR DEL COS FOR V
      DO 3 LO=1,NX
        DPHI(LO)=DPHI(LO)*RCL
        DLAM(LO)=DLAM(LO)*RCL
    3 CONTINUE
      DO 5 LE=1,NZ
        DO 4 LO=1,NX
          CG(LO,LE)=UG(LO,LE)*DLAM(LO)+VG(LO,LE)*DPHI(LO)
    4   CONTINUE
    5 CONTINUE
      DO 10 LO=1,NX
        DB(LO,1)=DEL(1)*DG(LO,1)
        CB(LO,1)=DEL(1)*CG(LO,1)
   10 CONTINUE
      DO 6 LE=1,NZ-1
        DO 6 LO=1,NX
          DB(LO,LE+1)=DB(LO,LE)+DEL(LE+1)*DG(LO,LE+1)
          CB(LO,LE+1)=CB(LO,LE)+DEL(LE+1)*CG(LO,LE+1)
    6 CONTINUE
C...    SIGMA DOT COMPUTED ONLY AT INTERIOR INTERFACES
      DO 7 K=1,NZ-1
        DO 7 LO=1,NX
          DOT(LO,K+1)=DOT(LO,K)+DEL(K)
     1               *(DB(LO,NZ)+CB(LO,NZ)-DG(LO,K)-CG(LO,K))
    7 CONTINUE
      DO 8 K=1,NZ
        DO 8 LO=1,NX
          VVEL(LO,K)=  SL(K)*(CG(LO,K)-CB(LO,NZ)-DB(LO,NZ))-
     1                0.5*(DOT(LO,K+1)+DOT(LO,K))
          VVEL(LO,K)=VVEL(LO,K)*PS(LO)
CCC       VVEL(LO,K)=VVEL(LO,K)*PS(LO)*10.
    8 CONTINUE
      RETURN
      END
