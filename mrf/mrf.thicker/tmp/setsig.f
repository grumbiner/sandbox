      SUBROUTINE SETSIG(CI, SI, DEL, SL, CL, RPI, NSG)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SETSIG      SETS UP MODEL SIGMA STRUCTURE.
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 88-04-05
C
C ABSTRACT: SETS UP MODEL SIGMA STRUCTURE BASED ON VERTICAL
C   SIGMA SPACING DEFINED IN THE SUBROUTINE.
C
C PROGRAM HISTORY LOG:
C   88-04-05  JOSEPH SELA
C
C USAGE:    CALL SETSIG (CI, SI, DEL, SL, CL, RPI)
C
C   OUTPUT ARGUMENT LIST:
C     CI       - ARRAY OF 1.0-SI AT EACH LEVEL.
C     SI       - ARRAY OF SIGMA VALUE AT EACH LEVEL.
C     DEL      - ARRAY OF SIGMA SPACING AT EACH LAYER.
C     SL       - ARRAY OF SIGMA AT MIDPOINT OF SIGMA LAYERS.
C     CL       - ARRAY OF 1.0-SL AT EACH LAYER MIDPOINT.
C     RPI      - ARRAY OF PI RATIOS NEEDED IN THERMODYNAMIC EQUATION.
C
C   OUTPUT FILES:
C     OUTPUT   - PRINTOUT FILE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      REAL RK,RK1,RKR
      DIMENSION CI( 29 ), SI( 29 ),
     1 DEL( 28 ), SL( 28 ), CL( 28 ), RPI( 27 )
      INTEGER IDATE(4)
C
      PRINT 98, NSG
98    FORMAT (1H0, 'BEGIN SETSIG - GETTING SIGS FROM UNIT',I4)
      REWIND NSG
      READ(NSG)
      READ(NSG) FHOUR,IDATE,SI,SL
      REWIND NSG
      DO 1 LI=1, 29
1     CI(LI) = 1. E 0 - SI(LI)
      DO 3 LE=1, 28
      CL(LE) = 1. E 0 - SL(LE)
      DEL(LE) = SI(LE) - SI(LE+1)
3     CONTINUE
C     COMPUTE PI RATIOS FOR TEMP. MATRIX.
      RK =  2.8705E+2 / 1.0046E+3
      DO 4 LE=1, 27
      BASE = SL(LE+1)/SL(LE)
4     RPI(LE) = BASE**RK
      DO 5 LE=1, 29
      PRINT 100, LE, CI(LE), SI(LE)
100   FORMAT (1H , 'LEVEL=', I2, 2X, 'CI=', F6.3, 2X, 'SI=', F6.3)
5     CONTINUE
      PRINT 97
97    FORMAT (1H0)
      DO 6 LE=1, 28
      PRINT 101, LE, CL(LE), SL(LE), DEL(LE)
101   FORMAT (1H , 'LAYER=', I2, 2X, 'CL=', F6.3, 2X, 'SL=', F6.3, 2X,
     1 'DEL=', F6.3)
6     CONTINUE
      PRINT 102, (RPI(LE), LE=1, 27 )
102   FORMAT (1H0, 'RPI=', (18(1X,F6.3)) )
      RETURN
      END
