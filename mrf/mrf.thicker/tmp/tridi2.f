CFPP$ NOCONCUR R
C-----------------------------------------------------------------------
      SUBROUTINE TRIDI2(L,N,CL,CM,CU,R1,R2,AU,A1,A2)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TRIDI2      SOLVES TRIDIAGONAL MATRIX PROBLEMS.
C   PRGMMR: IREDELL          ORG: W/NMC23    DATE: 91-05-07
C
C ABSTRACT: THIS ROUTINE SOLVES MULTIPLE TRIDIAGONAL MATRIX PROBLEMS
C   WITH 2 RIGHT-HAND-SIDE AND SOLUTION VECTORS FOR EVERY MATRIX.
C   THE SOLUTIONS ARE FOUND BY ELIMINATING OFF-DIAGONAL COEFFICIENTS,
C   MARCHING FIRST FOREWARD THEN BACKWARD ALONG THE MATRIX DIAGONAL.
C   THE COMPUTATIONS ARE VECTORIZED AROUND THE NUMBER OF MATRICES.
C   NO CHECKS ARE MADE FOR ZEROES ON THE DIAGONAL OR SINGULARITY.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL
C
C USAGE:    CALL TRIDI2(L,N,CL,CM,CU,R1,R2,AU,A1,A2)
C
C   INPUT ARGUMENT LIST:
C     L        - INTEGER NUMBER OF TRIDIAGONAL MATRICES
C     N        - INTEGER ORDER OF THE MATRICES
C     CL       - REAL (L,2:N) LOWER DIAGONAL MATRIX ELEMENTS
C     CM       - REAL (L,N) MAIN DIAGONAL MATRIX ELEMENTS
C     CU       - REAL (L,N-1) UPPER DIAGONAL MATRIX ELEMENTS
C                (MAY BE EQUIVALENT TO AU IF NO LONGER NEEDED)
C     R1       - REAL (L,N) 1ST RIGHT-HAND-SIDE VECTOR ELEMENTS
C                (MAY BE EQUIVALENT TO A1 IF NO LONGER NEEDED)
C     R2       - REAL (L,N) 2ND RIGHT-HAND-SIDE VECTOR ELEMENTS
C                (MAY BE EQUIVALENT TO A2 IF NO LONGER NEEDED)
C
C   OUTPUT ARGUMENT LIST:
C     AU       - REAL (L,N-1) WORK ARRAY
C     A1       - REAL (L,N) 1ST SOLUTION VECTOR ELEMENTS
C     A2       - REAL (L,N) 2ND SOLUTION VECTOR ELEMENTS
C
C REMARKS: THIS ROUTINE CAN BE EASILY MODIFIED TO SOLVE A DIFFERENT
C   NUMBER OF RIGHT-HAND-SIDES AND SOLUTIONS PER MATRIX BESIDES 2.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      DIMENSION CL(L,2:N),CM(L,N),CU(L,N-1),R1(L,N),R2(L,N),
     &          AU(L,N-1),A1(L,N),A2(L,N)
C-----------------------------------------------------------------------
      DO I=1,L
        FK=1./CM(I,1)
        AU(I,1)=FK*CU(I,1)
        A1(I,1)=FK*R1(I,1)
        A2(I,1)=FK*R2(I,1)
      ENDDO
      DO K=2,N-1
        DO I=1,L
          FK=1./(CM(I,K)-CL(I,K)*AU(I,K-1))
          AU(I,K)=FK*CU(I,K)
          A1(I,K)=FK*(R1(I,K)-CL(I,K)*A1(I,K-1))
          A2(I,K)=FK*(R2(I,K)-CL(I,K)*A2(I,K-1))
        ENDDO
      ENDDO
      DO I=1,L
        FK=1./(CM(I,N)-CL(I,N)*AU(I,N-1))
        A1(I,N)=FK*(R1(I,N)-CL(I,N)*A1(I,N-1))
        A2(I,N)=FK*(R2(I,N)-CL(I,N)*A2(I,N-1))
      ENDDO
      DO K=N-1,1,-1
        DO I=1,L
          A1(I,K)=A1(I,K)-AU(I,K)*A1(I,K+1)
          A2(I,K)=A2(I,K)-AU(I,K)*A2(I,K+1)
        ENDDO
      ENDDO
C-----------------------------------------------------------------------
      RETURN
      END
