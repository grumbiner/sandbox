      SUBROUTINE SOLMRF(JD,FJD,R,DLT,ALP,SLAG,N,ALAT,HANG,TAUDA,COSZ)
CFPP$ NOCONCUR R
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SOLMRF      ASTRONOMICAL(SOLAR) DATA - SW RADIATION
C   PRGMMR: KENNETH CAMPANA  ORG: W/NMC23    DATE: 89-07-07
C
C ABSTRACT: THIS IS A GFDL CODE ....
C   COMPUTES RADIUS VECTOR,DECLINATION AND RIGHT ASCENSION OF SUN,
C   EQUATION OF TIME, AND HOUR ANGLE OF SUN AT SUNSET FOR N EQUALLY
C   SPACED LATITUDES FOR GIVEN JULIAN DAY AND FRACTION..ALSO COMPUTES
C   DAYLIGHT LATITUDE MEAN COSINE SOLAR ZENITH ANGLE AND LATITUDINAL
C   AVERAGED FRACTIONAL DAYLIGHT.
C
C PROGRAM HISTORY LOG:
C   77-07-21  ROBERT WHITE,GFDL.
C   89-07-07  KENNETH CAMPANA-UPDATED-TO PASS ALAT THRU SUBROUTINE
C                             CALL RATHER THAN LABELED COMMON.
C
C USAGE:    CALL SOLMRF(JD,FJD,R,DLT,ALP,SLAG,N,ALAT,HANG,TAUDA,COSZ)
C   INPUT ARGUMENT LIST:
C     JD       - JULIAN DAY FOR CURRENT FCST HOUR.
C     FJD      - FRACTION OF THE JULIAN DAY.
C     N        - NUMBER OF GAUSSIAN LATIUTUDES + 2 (FOR THE POLES).
C     ALAT     - GAUSSIAN LATITUDES + 2 (POLES) IN RADIANS.
C   OUTPUT ARGUMENT LIST:
C     R        - RADIUS VECTOR OF THE SUN.
C     DLT      - DECLINATION OF THE SUN (RADIANS).
C     ALP      - RIGHT ASCENSION OF THE SUN.
C     SLAG     - EQUATION OF TIME (RADIANS).
C     HANG     - HOUR ANGLE OF SUN AT SUNSET (FOR N LATS).
C     TAUDA    - LATITUDINAL AVER. FRACTIONAL DAYLIGHT (FOR N LATS).
C     COSZ     - LATITUDINAL DAYLIGHT-MEAN COSINE SOLAR ZENITH ANGLE.
C
C   OUTPUT FILES:
C     OUTPUT   - PRINT FILE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      PARAMETER (PI= 3.141593E+0 ,TPI=2.0*PI,HPI=0.5*PI,RAD=180.0/PI)
C
C
C    *******************************************************************
C    *                            S O L M R F                          *
C... *  ORIGINAL GFDL CODE (EXCEPT FOR PASSING ALAT IN CALL LIST)--    *
C    *    I.E. COMPUTES GAUSSIAN LATITUDE MEAN COSINE SOLAR ZEN ANGLE  *
C    *  UPDATES BY HUALU PAN TO LIMIT ITERATIONS IN NEWTON METHOD AND  *
C    *  ALSO CCR REDUCED FROM(1.3E-7)--BOTH TO AVOID NONCONVERGENCE IN *
C    *  NMC S HALF PRECISION VERSION OF GFDL S CODE   ----  FALL 1988  *
C    *******************************************************************
C
C         STATEMENTS BLOCKED BY ROBERT K. WHITE.......21 JULY 1977
C
C
C.....SOLMRF COMPUTES RADIUS VECTOR, DECLINATION AND RIGHT ASCENSION OF
C.....SUN, EQUATION OF TIME, AND HOUR ANGLE OF SUN AT SUNSET FOR N
C.....EQUALLY SPACED LATITUDES GIVEN JULIAN DAY AND FRACTION.
C
                              D I M E N S I O N
     1   ALAT(N),  HANG(N),    COSZ(N),    TAUDA(N)
C
                                   D A T A
     1   CYEAR/365.25/,      CCR/1.3 E -6/
C
C.....TPP = DAYS BETWEEN EPOCH AND PERIHELION PASSAGE OF 1900
C.....SVT6 = DAYS BETWEEN PERIHELION PASSAGE AND MARCH EQUINOX OF 1900
C.....JDOR = JD OF EPOCH WHICH IS JANUARY 0, 1900 AT 12 HOURS UT
C
                                   D A T A
     1   TPP/1.55/,          SVT6/78.035/,       JDOR/2415020/
C
C    *******************************************************************
C
      DAT=FLOAT(JD-JDOR)-TPP+FJD
C    COMPUTES TIME IN JULIAN CENTURIES AFTER EPOCH
      T=FLOAT(JD-JDOR)/36525. E 0
C    COMPUTES LENGTH OF ANOMALISTIC AND TROPICAL YEARS (MINUS 365 DAYS)
      YEAR=.25964134 E 0+.304 E -5*T
      TYEAR=.24219879 E 0-.614 E -5*T
C    COMPUTES ORBIT ECCENTRICITY AND ANGLE OF EARTH'S INCLINATION FROM T
      EC=.01675104 E 0-(.418 E -4+.126 E -6*T)*T
      ANGIN=23.452294 E 0-(.0130125 E 0+.164 E -5*T)*T
      ADOR=JDOR
      JDOE=ADOR+(SVT6*CYEAR)/(YEAR-TYEAR)
C    DELEQN=UPDATED SVT6 FOR CURRENT DATE
      DELEQN=FLOAT(JDOE-JD)*(YEAR-TYEAR)/CYEAR
      YEAR=YEAR+365. E 0
      SNI=SIN(ANGIN/RAD)
      TINI=1. E 0/TAN(ANGIN/RAD)
      ER=SQRT((1. E 0+EC)/(1. E 0-EC))
      QQ=DELEQN*TPI/YEAR
C    DETERMINE TRUE ANOMALY AT EQUINOX
      E=1. E 0
      ITER = 0
 32   EP=E-(E-EC*SIN(E)-QQ)/(1. E 0-EC*COS(E))
      CD=ABS(E-EP)
      E=EP
      ITER = ITER + 1
      IF(ITER.GT.10) THEN
        WRITE(6,*) ' ITERATION COUNT FOR LOOP 32 =', ITER
        WRITE(6,*) ' E, EP, CD =', E, EP, CD
      ENDIF
      IF(ITER.GT.10) GOTO 1032
      IF(CD.GT.CCR) GO TO 32
 1032 CONTINUE
      HE=.5 E 0*E
      EQ=2. E 0*ATAN(ER*TAN(HE))
C    DATE=DAYS SINCE LAST PERIHELION PASSAGE
      DATE = MOD(DAT,YEAR)
C    SOLVE ORBIT EQUATIONS BY NEWTON'S METHOD
      EM=TPI*DATE/YEAR
      E=1. E 0
      ITER = 0
 31   EP=E-(E-EC*SIN(E)-EM)/(1. E 0-EC*COS(E))
      CR=ABS(E-EP)
      E=EP
      ITER = ITER + 1
      IF(ITER.GT.10) THEN
        WRITE(6,*) ' ITERATION COUNT FOR LOOP 31 =', ITER
      ENDIF
      IF(ITER.GT.10) GOTO 1031
      IF(CR.GT.CCR) GO TO 31
 1031 CONTINUE
      R=1. E 0-EC*COS(E)
      HE=.5 E 0*E
      W=2. E 0*ATAN(ER*TAN(HE))
      SIND=SNI*SIN(W-EQ)
      DLT=ASIN(SIND)
      ALP=ASIN(TAN(DLT)*TINI)
      TST=COS(W-EQ)
      IF(TST.LT.0. E 0) ALP=PI-ALP
      IF(ALP.LT.0. E 0) ALP=ALP+TPI
      SUN=TPI*(DATE-DELEQN)/YEAR
      IF(SUN.LT.0. E 0) SUN=SUN+TPI
      SLAG=SUN-ALP-.03255 E 0
C    COMPUTE HOUR ANGLE OF SUNSET AT ALL LATITUDES
      IF(DLT.EQ.0. E 0) THEN
        DO 10 I=1,N
        HANG(I)=HPI
        TAUDA(I)=0.5 E 0
        COSZ(I)= MAX (COS(ALAT(I)/HPI),0. E 0)
   10   CONTINUE
      ELSE
      DO 1 I=1,N
      SS=SIN(ALAT(I))*SIN(DLT)
      CC=COS(ALAT(I))*COS(DLT)
      AP=ABS(ALAT(I))
      EPS=ABS(AP-HPI)
      IF(EPS.GT.CCR) GO TO 14
      HANG(I)=HPI*ABS(AP/ALAT(I)+ABS(DLT)/DLT)
      GO TO 5
   14 AR=-SS/CC
      AC=ABS(AR)
      IF(AC+CCR.GT.1. E 0) GO TO 3
      IF(AC+CCR.LT.1. E 0) GO TO 4
  2   HANG(I)=(AC-AR)*HPI
      GO TO 5
  3   IF(AR.LT.0. E 0) GO TO 25
      HANG(I)=0.
      GO TO 5
 25   HANG(I)=PI
      GO TO 5
  4   HANG(I)=ACOS(AR)
  5   TAUDA(I)= MAX (HANG(I)/PI,0. E 0)
      IF(HANG(I).EQ.0. E 0) GO TO 100
      COSZ(I)= MAX ((SS+CC*SIN(HANG(I))/HANG(I)),0. E 0)
      GO TO 1
  100 COSZ(I)=0. E 0
    1 CONTINUE
      ENDIF
      RETURN
      END
