      SUBROUTINE CLDIN(IIN,JTWIDL,JIN,IOUT,
     1                 ILEFT,IRGHT,WGTLON,INSLAT,WGTLAT,
     2                 CV,CAMT,LEVS,
     3                 XX,WGT,SUM,NN,LTWIDL,LATRD1)
      DIMENSION CV(IIN,LEVS,JTWIDL)
      DIMENSION CAMT(IOUT,LEVS)
      DIMENSION ILEFT(IOUT),IRGHT(IOUT),WGTLON(IOUT)
      DIMENSION XX(IOUT,4),WGT(IOUT,4),SUM(IOUT,2)
      DIMENSION NN(IOUT)
C...    CLONE OF CINTP WITHOUT DOING CLD TOPS AND BASES.....
C        SIMPL LINEAR INTERPOLATION OF CLDAMT, UNLESS ONLY 1,2 OF THE
C         SURROUNDING PTS HAS CL. THEN,IF OUTPUT GRIDPT NOT CLOSE ENUF
C         DO NOT INTERPOLATE TO IT(PREVENTS SPREADING OF CLDS)..
C           FOR 1 PT CLOUD AMT -INTRP WGT GE (.7)**2 ...
C           FOR 2 PT CLOUD AMT -SUM OF INTRP WGT GE .45...
C              .45 USED RATHER THAN .5 TO GIVE BETTER RESULT FOR
C              DIAGONALLY OPPOSED PTS...
C---     NHSH = 1,-1 FOR NORTHERN,SOUTHERN HEMISPHERE
C         HERE INSTEAD OF AN EXTRAPOLATION,JUST DO A SIMPLE MEAN....
C
C---   POLAR REGIONS,NO EXTRAP (INSLAT < 0)
      IF (INSLAT.LT.0) GO TO 600
      INTH = MOD(LTWIDL + INSLAT - LATRD1 - 1,JTWIDL) + 1
      INTH1 = MOD(INTH,JTWIDL) + 1
      DO 1000 KEN=1,LEVS
       IF (INSLAT.EQ.JIN) GO TO 105
       DO 100 I=1,IOUT
C----   NORMALIZED DISTANCE FROM UPPER LAT TO GAUSSIAN LAT
        XX(I,1) = CV(ILEFT(I),KEN,INTH)
        XX(I,2) = CV(ILEFT(I),KEN,INTH1)
        XX(I,3) = CV(IRGHT(I),KEN,INTH)
        XX(I,4) = CV(IRGHT(I),KEN,INTH1)
        WGT(I,1) = (1. E  0-WGTLON(I))*(1. E  0-WGTLAT)
        WGT(I,2) = (1. E  0-WGTLON(I))*WGTLAT
        WGT(I,3) = WGTLON(I)*(1. E  0-WGTLAT)
        WGT(I,4) = WGTLON(I)*WGTLAT
  100  CONTINUE
      GO TO 130
  105  DO 110 I=1,IOUT
C----   NORMALIZED DISTANCE FROM UPPER LAT TO GAUSSIAN LAT
        XX(I,1) = CV(ILEFT(I),KEN,INTH)
        XX(I,3) = CV(IRGHT(I),KEN,INTH)
        WGT(I,1) = (1. E  0-WGTLON(I))*(1. E  0-WGTLAT)
        WGT(I,2) = (1. E  0-WGTLON(I))*WGTLAT
        WGT(I,3) = WGTLON(I)*(1. E  0-WGTLAT)
        WGT(I,4) = WGTLON(I)*WGTLAT
  110  CONTINUE
       IOUT2 = IOUT / 2
       DO 120 I=1,IOUT2
        XX(I,2) = CV(ILEFT(I+IOUT2),KEN,INTH)
        XX(I+IOUT2,2) = CV(ILEFT(I),KEN,INTH)
        XX(I,4) = CV(IRGHT(I+IOUT2),KEN,INTH)
        XX(I+IOUT2,4) = CV(IRGHT(I),KEN,INTH)
  120  CONTINUE
C---      NN WILL BE NUMBER OF SURROUNDING PTS WITH CLD (GT ZERO)
  130  DO 10 I=1,IOUT
        NN(I) = 0
   10  CONTINUE
       DO 12 J=1,2
        DO 12 I=1,IOUT
         SUM(I,J) = 0. E 0
   12  CONTINUE
        DO 150 KPT=1,4
          DO 14 I=1,IOUT
            IF (XX(I,KPT).GT.0. E 0) THEN
              NN(I) = NN(I) + 1
              SUM(I,1) = SUM(I,1) + WGT(I,KPT)
            ENDIF
   14     CONTINUE
          DO 15 I=1,IOUT
            SUM(I,2) = SUM(I,2) + WGT(I,KPT) * XX(I,KPT)
   15     CONTINUE
  150   CONTINUE
        DO 16 I=1,IOUT
          IF (NN(I).EQ.1.AND.SUM(I,1).GT.0.49 E 0) GO TO 17
          IF (NN(I).EQ.2.AND.SUM(I,1).GE.0.45 E 0) GO TO 17
          IF (NN(I).GE.3) GO TO 17
            CAMT(I,KEN) = 0. E 0
            GO TO 18
   17     CONTINUE
            CAMT(I,KEN) = SUM(I,2)
   18     CONTINUE
   16   CONTINUE
 1000  CONTINUE
      RETURN
C--- POLAR REGION-NO EXTRAPOLATION
  600 CONTINUE
      JA = IABS(INSLAT)
      DO 2000 KEN=1,LEVS
       DO 200 I=1,IOUT
C----    GET LEFT POINT ON NEAREST LATITUDE
        XX(I,1) = CV(ILEFT(I),KEN,JA)
        XX(I,2) = CV(IRGHT(I),KEN,JA)
        WGT(I,1) = 1. E  0-WGTLON(I)
        WGT(I,2) = WGTLON(I)
  200 CONTINUE
C---      NN WILL BE NUMBER OF SURROUNDING PTS WITH CLD (GT ZERO)
      DO 20 I=1,IOUT
        NN(I) = 0
   20 CONTINUE
      DO 22 J=1,2
       DO 22 I=1,IOUT
        SUM(I,J) = 0. E 0
   22 CONTINUE
        DO 202 KPT=1,2
          DO 24 I=1,IOUT
            IF (XX(I,KPT).GT.0. E 0) THEN
              NN(I) = NN(I) + 1
              SUM(I,1) = SUM(I,1) + WGT(I,KPT)
            ENDIF
   24     CONTINUE
          DO 25 I=1,IOUT
            SUM(I,2) = SUM(I,2) + WGT(I,KPT) * XX(I,KPT)
   25     CONTINUE
  202   CONTINUE
        DO 26 I=1,IOUT
          IF (NN(I).EQ.1.AND.SUM(I,1).GT.0.7 E 0) GO TO 27
          IF (NN(I).EQ.2) GO TO 27
            CAMT(I,KEN) = 0. E 0
            GO TO 28
   27     CONTINUE
            CAMT(I,KEN) = SUM(I,2)
   28     CONTINUE
   26   CONTINUE
 2000 CONTINUE
      RETURN
      END
