      SUBROUTINE GLOBLM(Y,GLOBL,RLAT,WGT,KD,LD,COLRAD,LTITL,FHOUR)
C...   COMPUTE AREA WEIGHTED MEANS - GLOBAL,HEMISPHERIC,ETCCCC
C..   INPUT:
C          Y      = LATITUDINAL MEANS - LD LATITUDES,KD LAYERS..
C          COLRAD = CO-LATIUDES IN RADIANS (N.H.)
C          LTITL  = PRINTING LABEL (CHARACTER*20)
C          FHOUR  = FORECAST HOUR
C..   OUTPUT:   PRINTED VALUES (FT06F001)
C            GLOBL(1,KD)=GLOBAL MEAN
C            GLOBL(2,KD)=NORTHERN HEMISPHERE MEAN
C            GLOBL(3,KD)=NORTHERN HEMISPHERE POLAR (90-60DEG) MEAN
C            GLOBL(4,KD)=NORTHERN HEMISPHERE MIDLAT(60-30DEG) MEAN
C            GLOBL(5,KD)=NORTHERN HEMISPHERE TROPIC(30-0 DEG) MEAN
C            GLOBL(6,KD)=SOUTHERN HEMISPHERE MEAN
C            GLOBL(7,KD)=SOUTHERN HEMISPHERE POLAR (90-60DEG) MEAN
C            GLOBL(8,KD)=SOUTHERN HEMISPHERE MIDLAT(60-30DEG) MEAN
C            GLOBL(9,KD)=SOUTHERN HEMISPHERE TROPIC(30-0 DEG) MEAN
      DIMENSION Y(KD,LD),COLRAD(LD)
      CHARACTER*20 LTITL
      DIMENSION GLOBL(9,KD),RLAT(LD),WGT(LD)
      DIMENSION GLOBX(9,100)
      COMMON /GLBSAV/ LATLON,WTSUM(5),J30,J60
      DATA LATLON/ 0 /
      JJ = LD / 2
      KK = KD
      IF (LATLON.GT.0) GO TO 30
C...   COMPUTE AREA WGTS AND LATITUDES JUST POLEWARD OF 60DEG (J60)
C         AND JUST POLEWARD OF 30DEG (J30)
      LATLON = 10
      RAD60 =  3.141593E+0  / 3. E 0
      RAD30 =  3.141593E+0  / 6. E 0
C...   LATITUDE IN RADIANS
      DO 5 J=1,JJ
        RLAT(J) =   3.141593E+0  / 2. E 0 - COLRAD(J)
    5 CONTINUE
      DO 10 J=1,JJ
        IF(RLAT(J).LT.RAD60) GO TO 15
        J60 = J
   10 CONTINUE
      GO TO 200
   15 DO 20 J=1,JJ
        IF(RLAT(J).LT.RAD30) GO TO 25
        J30 = J
   20 CONTINUE
      GO TO 205
   25 DO 26 N=1,5
        WTSUM(N) = 0.
   26 CONTINUE
      RLATS = 0.5 E 0 * (RLAT(1)+RLAT(2))
      WGT(1) = 1. - SIN(RLATS)
      RLATN = 0.5 E 0 * (RLAT(JJ)+RLAT(JJ-1))
      WGT(JJ) = SIN(RLATN)
      DO 27 LA=2,JJ-1
        RLATN = 0.5 E 0 * (RLAT(LA)+RLAT(LA-1))
        RLATS = 0.5 E 0 * (RLAT(LA)+RLAT(LA+1))
        WGT(LA) = SIN(RLATN) - SIN(RLATS)
   27 CONTINUE
      PRINT 9,JJ,J60,J30
    9 FORMAT(1H ,' JJ=',I4,'  J60=',I4,'  J30=',I4)
      DO 28 LA=1,JJ
C..    GLOBAL AREA
        WTSUM(1) = WTSUM(1) + 2. E 0 * WGT(LA)
C..    HEMISPHERIC AREA
        WTSUM(2) = WTSUM(2) + WGT(LA)
C..    LATITUDE STRIP AREAS
        IF (LA.GT.J60) GO TO 31
        WTSUM(3) = WTSUM(3) + WGT(LA)
        GO TO 28
   31   IF (LA.GT.J30) GO TO 32
        WTSUM(4) = WTSUM(4) + WGT(LA)
        GO TO 28
   32   WTSUM(5) = WTSUM(5) + WGT(LA)
   28 CONTINUE
C...
   30 CONTINUE
C...
      DO 35 K = 1,KK
        DO 35 J = 1,9
          GLOBL(J,K) = 0. E 0
   35 CONTINUE
      DO 75 LA=1,JJ
       DO 45 K=1,KK
        GLOBL(2,K) = GLOBL(2,K) + WGT(LA)*Y(K,LA)
        GLOBL(6,K) = GLOBL(6,K) + WGT(LA)*Y(K,LD+1-LA)
        GLOBL(1,K) = GLOBL(2,K) + GLOBL(6,K)
   45  CONTINUE
       IF (LA.GT.J60) GO TO 55
       DO 50 K=1,KK
        GLOBL(3,K) = GLOBL(3,K) + WGT(LA)*Y(K,LA)
        GLOBL(7,K) = GLOBL(7,K) + WGT(LA)*Y(K,LD+1-LA)
   50  CONTINUE
       GO TO 75
   55  IF (LA.GT.J30) GO TO 65
       DO 60 K=1,KK
        GLOBL(4,K) = GLOBL(4,K) + WGT(LA)*Y(K,LA)
        GLOBL(8,K) = GLOBL(8,K) + WGT(LA)*Y(K,LD+1-LA)
   60  CONTINUE
       GO TO 75
   65  DO 70 K=1,KK
        GLOBL(5,K) = GLOBL(5,K) + WGT(LA)*Y(K,LA)
        GLOBL(9,K) = GLOBL(9,K) + WGT(LA)*Y(K,LD+1-LA)
   70  CONTINUE
   75 CONTINUE
      DO 85 K=1,KK
        GLOBX(1,K) = GLOBL(1,K) / WTSUM(1)
        GLOBL(1,K) = GLOBL(1,K) / WTSUM(1)
        DO 80 I=2,5
          GLOBX(I,K) = GLOBL(I,K) / WTSUM(1)
          GLOBX(I+4,K) = GLOBL(I+4,K) / WTSUM(1)
          GLOBL(I,K) = GLOBL(I,K) / WTSUM(I)
          GLOBL(I+4,K) = GLOBL(I+4,K) / WTSUM(I)
   80   CONTINUE
   85 CONTINUE
      PRINT 92,LTITL,FHOUR
      PRINT 93
      PRINT 97
      PRINT 95
      DO 90 KEN=1,KK
        K = KK+1-KEN
        PRINT 100,K,(GLOBL(I,K),I=1,9)
   90 CONTINUE
      PRINT 98
      PRINT 95
      DO 11 KEN=1,KK
        K = KK+1-KEN
        PRINT 100,K,(GLOBX(I,K),I=1,9)
   11 CONTINUE
      RETURN
   92 FORMAT (1H ,10X,' AREA WEIGHTED MEANS',2X,A20,' FCSTHR=',F6.1)
   93 FORMAT (1H ,11X,' -------------------------------------')
   95 FORMAT (1H ,4X,'K',5X,'GLOBAL',4X,'N HEMI',4X,'NHPOLR',4X,
     1        'NHMIDL',4X,'NHTRPC',4X,'S HEMI',4X,
     2        'SHPOLR',4X,'SHMIDL',4X,'SHTRPC')
   97 FORMAT (1H ,15X,'====>  WEIGHTED RELATIVE TO SPECIFIED REGION')
   98 FORMAT (1H ,15X,'====>  WEIGHTED RELATIVE TO ENTIRE GLOBE')
  100 FORMAT (1H ,I5,9F10.3)
  200 PRINT 201
      STOP
  205 PRINT 206
      STOP
  201 FORMAT (1H ,' STOP -- CAN T FIND J60')
  206 FORMAT (1H ,' STOP -- CAN T FIND J30')
      END
