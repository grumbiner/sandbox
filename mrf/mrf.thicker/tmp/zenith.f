      SUBROUTINE ZENITH(FJD,DLT,SLAG,RLAT,RLON,HANG,DHR,NLNG,COSZ,FRAC)
CFPP$ NOCONCUR R
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    ZENITH      COMPUTE COSINE SOLAR ZENITH ANGLE
C   PRGMMR: KENNETH CAMPANA  ORG: W/NMC23    DATE: 89-07-07
C
C ABSTRACT: THIS CODE WRITTEN AT GFDL .....
C   COMPUTES EFFECTIVE MEAN (OVER SPECIFIED INTERVAL,DHR)
C   COSINE OF ZENITH ANGLE AND DAYLIGHT FRACTION
C   FROM LATITUDE,LONGITUDE,AND COMPUTED HOUR ANGLE OF SUNSET.
C   INPUT ARGUMENTS TO CIRCULAR FUNCTIONS ARE IN RADIANS.
C
C PROGRAM HISTORY LOG:
C   77-10-19  ROBERT WHITE,GFDL.
C   89-07-07  KENNETH CAMPANA-UPDATED-TO CALCULATE SUNSET HOUR ANGLE
C                                     HERE RATHER THAN IN SUBR-SOLAR-
C
C USAGE:    CALL ZENITH(FJD,DLT,SLAG,XLAT,XLON,HANG,DHR,NLNG,
C                       COSZ,FRAC)
C   INPUT ARGUMENT LIST:
C     FJD      - FRACTION OF THE JULIAN DAY.
C     DLT      - DECLINATION OF THE SUN (RADIANS).
C     SLAG     - EQUATION OF TIME (RADIANS).
C     XLAT     - LATITUDE (DEGREES) FOR EACH OF THE NLNG PTS.
C     XLON     - LONGITUDE (DEGREES) FOR EACH OF THE NLNG PTS.
C     DHR      - INTERVAL (HOURS) OVER WHICH TO AVERAGE COSZ,FRAC.
C     NLNG     - NUMBER OF POINTS IN LONGITUDINAL DIRECTION.
C   OUTPUT ARGUMENT LIST:
C     HANG     - HOUR ANGLE OF SUN AT SUNSET.
C     COSZ     - TEMPORAL MEAN COSINE SOLAR ZENITH ANGLE.
C     FRAC     - TEMPORAL AVERAGE FRACTIONAL DAYLIGHT.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      PARAMETER (PI= 3.141593E+0 ,TPI=2.0*PI,PID24=PI/24.0,HPI=0.5*PI)
C
C    *******************************************************************
C    *                           Z E N I T H                           *
C    *    STATEMENTS BLOCKED BY ROBERT K. WHITE.......19 OCT. 1977     *
C    *    INPUT LAT/LON DEG(XLAT,XLON),COMPUTE SUNSET HR ANGL KAC MAR89*
C    *******************************************************************
C
                               D I M E N S I O N
     1   COSZ(NLNG),           FRAC(NLNG)
      DIMENSION RLAT(NLNG),RLON(NLNG),HANG(NLNG)
C
                                 L O G I C A L
     1   RISE,               SET
C
                                 D A T A
     1   CCR/1.3 E -6/
C
C    *******************************************************************
C
C     ZENITH COMPUTES EFFECTIVE MEAN COSINE OF ZENITH ANGLE AND DAYLIGHT
C       FRACTION FROM LATITUDE AND COMPUTED HOUR ANGLE OF SUNSET(THE
C       LATTER IS OBTAINED FROM SUBROUTINE SOLAR)..
C---   INPUT ARGUMENTS TO CIRCULAR FUNCTIONS ARE IN RADIANS.
C
      CVPR=TPI/FLOAT(NLNG/2)
      GHA=FJD*TPI+SLAG
      ARG=DHR*PID24
      SINFAC=SIN(ARG)/ARG
      IF(DLT.EQ.0. E 0) THEN
      DO 60 I=1,NLNG
      CC=COS(RLAT(I))
      HANG(I) = HPI
      CONS=CC/HPI
CKAC  XLNG = CVPR * (FLOAT(I) - 1. E 0)
      HLOC=GHA+RLON(I)+ARG
C     LOCAL HOUR ANGLE SHIFTED BY HALF OF THE AVERAGING PERIOD
      HLOC = MOD(HLOC,TPI)
      IF(HLOC.GT.PI) HLOC=HLOC-TPI
      HLPAR=HLOC+ARG
      ARMHL=ARG-HLOC
      SET=HLPAR.GT.HPI
      RISE=ARMHL.GT.HPI
      IF(RISE.AND.SET) GO TO 57
      IF(HLPAR.GT.PI) GO TO 58
      IF(ARMHL.GT.PI) GO TO 59
      IF(SET) GO TO 53
      IF(RISE) GO TO 54
      FRAC(I)=1. E 0
      COSZ(I)=CC*COS(HLOC)*SINFAC
      GO TO 52
 53   DELSH=.5 E 0*(HPI+ARMHL)
      GO TO 55
 54   DELSH=.5 E 0*(HPI+HLPAR)
 55   IF(DELSH.LE.0. E 0) GO TO 56
      FRAC(I)=DELSH/ARG
      COSZ(I)=CC*COS(HPI-DELSH)*SIN(DELSH)/DELSH
      GO TO 52
 57   FRAC(I)=HPI/ARG
      COSZ(I)=CONS
      GO TO 52
 58   DELE=.5 E 0 * MAX (HLPAR+HPI-TPI,0. E 0)
      DELW=.5 E 0 * MAX (HPI+ARMHL,0. E 0)
      GO TO 70
 59   DELE=.5 E 0 * MAX (HPI+HLPAR,0. E 0)
      DELW=.5 E 0 * MAX (ARMHL+HPI-TPI,0. E 0)
 70   FRAC(I)=(DELE+DELW)/ARG
      IF(FRAC(I).EQ.0. E 0) GO TO 71
      COSZ(I)=CC*(COS(HPI-DELE)*SIN(DELE)+
     1            COS(HPI-DELW)*SIN(DELW))/(DELE+DELW)
      GO TO 52
 56   FRAC(I)=0. E 0
 71   COSZ(I)=0. E 0
 52   CONTINUE
      COSZ(I) =  MIN (1. E 0,COSZ(I))
      COSZ(I) =  MAX (0. E 0,COSZ(I))
      FRAC(I) =  MIN (1. E 0,FRAC(I))
 60   CONTINUE
      ELSE
      DO 20 I=1,NLNG
C...   COMPUTE HOUR ANGLE OF SUNSET FOR EACH POINT
      SS=SIN(RLAT(I))*SIN(DLT)
      CC=COS(RLAT(I))*COS(DLT)
      IF(DLT.EQ.0. E 0) GO TO 16
      AP=ABS(RLAT(I))
      EPS=ABS(AP-HPI)
      IF(EPS.GT.CCR) GO TO 14
      HANG(I)=HPI*ABS(AP/RLAT(I)+ABS(DLT)/DLT)
      GO TO 35
   14 AR=-SS/CC
      AC=ABS(AR)
      IF(AC+CCR.GT.1. E 0) GO TO 33
      IF(AC+CCR.LT.1. E 0) GO TO 34
   32 HANG(I)=(AC-AR)*HPI
      GO TO 35
   33 IF(AR.LT.0. E 0) GO TO 25
      HANG(I)=0.
      GO TO 35
 25   HANG(I)=PI
      GO TO 35
 16   HANG(I)=HPI
      GO TO 35
   34 HANG(I)=ACOS(AR)
   35 HA=HANG(I)
      IF(HA.GT.0. E 0) CONS=SS+CC*SIN(HA)/HA
CKAC  XLNG = CVPR * (FLOAT(I) - 1. E 0)
      HLOC=GHA+RLON(I)+ARG
C     LOCAL HOUR ANGLE SHIFTED BY HALF OF THE AVERAGING PERIOD
      HLOC = MOD(HLOC,TPI)
      IF(HLOC.GT.PI) HLOC=HLOC-TPI
      HLPAR=HLOC+ARG
      ARMHL=ARG-HLOC
      SET=HLPAR.GT.HA
      RISE=ARMHL.GT.HA
      IF(RISE.AND.SET) GO TO 7
      IF(HLPAR.GT.PI) GO TO 8
      IF(ARMHL.GT.PI) GO TO 9
      IF(SET) GO TO 3
      IF(RISE) GO TO 4
      FRAC(I)=1. E 0
      COSZ(I)=SS+CC*COS(HLOC)*SINFAC
      GO TO 2
  3   DELSH=.5 E 0*(HA+ARMHL)
      GO TO 5
  4   DELSH=.5 E 0*(HA+HLPAR)
  5   IF(DELSH.LE.0. E 0) GO TO 6
      FRAC(I)=DELSH/ARG
      COSZ(I)=SS+CC*COS(HA-DELSH)*SIN(DELSH)/DELSH
      GO TO 2
  7   IF(HA.LE.0. E 0) GO TO 6
      FRAC(I)=HA/ARG
      COSZ(I)=CONS
      GO TO 2
  8   DELE=.5 E 0 * MAX (HLPAR+HA-TPI,0. E 0)
      DELW=.5 E 0 * MAX (HA+ARMHL,0. E 0)
      GO TO 10
  9   DELE=.5 E 0 * MAX (HA+HLPAR,0. E 0)
      DELW=.5 E 0 * MAX (ARMHL+HA-TPI,0. E 0)
 10   FRAC(I)=(DELE+DELW)/ARG
      IF(FRAC(I).EQ.0. E 0) GO TO 11
      COSZ(I)=SS+CC*(COS(HA-DELE)*SIN(DELE)+
     1               COS(HA-DELW)*SIN(DELW))/(DELE+DELW)
      GO TO 2
  6   FRAC(I)=0. E 0
 11   COSZ(I)=0. E 0
  2   CONTINUE
      COSZ(I) =  MIN (1. E 0,COSZ(I))
      COSZ(I) =  MAX (0. E 0,COSZ(I))
      FRAC(I) =  MIN (1. E 0,FRAC(I))
   20 CONTINUE
      ENDIF
      RETURN
      END
