CFPP$ NOCONCUR R
      SUBROUTINE DCYC2(LON2,LEVS,SOLHR,SLAG,SINLAB,COSLAB,SDEC,CDEC,
     &                 XLON,CZMN,SFCDLW,SFCNSW,TF,TSEA,TSFLW,SWH,HLW,
     &                 DLWSFC,ULWSFC,SLRAD,TAU)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    DCYC2       ALTERS RADIATION FOR APPROX DIURNAL CYCLE
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 88-05-06
C           KENNETH CAMPANA
C
C ABSTRACT: A DIURNAL CYCLE APPROXIMATION IS APPLIED TO PREVIOUSLY
C   COMPUTED RADIATIVE FLUXES AND HEATING RATES. FIRST,THE CURRENT
C   LOCAL-TIME VALUE (FOR THIS PARTICULAR MODEL TIME STEP) OF THE
C   COSINE SOLAR ZENITH ANGLE (COSZ) IS COMPUTED FOR ALL GAUSSIAN GRID
C   POINTS. SHORTWAVE (SW) HEATING RATES WHICH WERE COMPUTED WITH
C   LATITUDINAL MEAN COSZ IN THE SEPARATE RADIATION CALCULATION
C   ARE WEIGHTED BY THE RATIO OF ACTUAL TO MEAN COSZ (SEE MRF MODEL
C   DOCUMENTATION,1988,CHAPTER 3,'RADIATIVE PROCESSES',AUTHORED BY
C   K. CAMPANA,..). SURFACE SW FLUXES ARE ALSO COSZ WEIGHTED. SURFACE
C   LONGWAVE (LW) FLUX FROM THE ATMOSPHERE IS ALTERED EACH TIMESTEP
C   TO ACCOUNT FOR DIURNAL CHANGES OF MODEL TEMPERATURE IN THE LOWER
C   ATMOSPHERE. LW HEATING RATES FROM THE SEPARATE RADIATION
C   COMPUTATION ARE UNTOUCHED.
C
C PROGRAM HISTORY LOG:
C   88-05-06  JOSEPH SELA
C
C USAGE:    CALL DCYC2(LAT,SSDEC,SOLHR,COLRAD,CZMN,SFCDLW,SFCNSW,
C                      TF,TOV,SLRAD,SWH,HLW,TAU)
C   INPUT ARGUMENT LIST:
C     LAT      - ROW NUMBER OF GAUSSIAN LATITUDE(N.H.).
C     SSDEC    - SINE OF THE SOLAR DECLINATION FOR TODAY'S DATE-
C                         PART OF THE OUTPUT FROM RADIATION CODES.
C     SOLHR    - TIME IN HOURS AFTER 00 HR GREENWICH.
C     COLRAD   - CO-LATITUDES OF GAUSSIAN GRID IN RADIANS(N.H.).
C     CZMN     - MEAN COSINE SOLAR ZENITH ANGLE FOR ALL GAUSSIAN LATS-
C                         PART OF THE OUTPUT FROM RADIATION CODES.
C     SFCDLW   - DOWNWARD LW FLUX AT EARTH SFC(FROM RADIATION CODE)
C                IN CAL CM-2 MIN-1.
C     SFCNSW   - NET SW FLUX AT EARTH SFC(FROM RADIATION CODE USING
C                CZMN) IN CAL CM-2 MIN-1.
C     TF       - CURRENT VALUE OF MODEL TEMPERATURES(W/O BASIC STATE)
C                IN DEG K.
C     TOV      - BASIC STATE TEMPERATURE FOR ALL MODEL LAYERS(DEG K).
C     SWH      - MODEL LYR SW HEATING RATES(FROM RADIATION CODE,USING
C                CZMN) - DEG/SEC.
C     HLW      - MODEL LYR LW HEATING RATES(FROM RADIATION CODE),
C                IN DEG/SEC.
C
C   OUTPUT ARGUMENT LIST:
C     SLRAD    - SURFACE NET RADIATIVE FLUX (EXCEPT LW UPWARD FLUX
C                FROM SFC,WHICH IS ADDED IN PROGTN) -
C                UNITS ARE CAL CM-2 MIN-1 .
C     TAU      - LAYER VALUES OF TEMPERATURE TENDENCY AFTER ADDING
C                ALTERED RADIATIVE HEATING RATES-UNITS ARE DEG/SEC.
C
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      DIMENSION XLON(LON2  ),CZMN(LON2),SFCDLW(LON2),SFCNSW(LON2),
     &          TF(LON2),TSEA(LON2),TSFLW(LON2),SINLAB(LON2),
     &          SWH(LON2,LEVS),HLW(LON2,LEVS),COSLAB(LON2),
     &          DLWSFC(LON2),ULWSFC(LON2),SLRAD(LON2),TAU(LON2,LEVS)
      PARAMETER (HSIGMA= 5.6730E-8 ,CNWATT=- 4.1855E+0 *1.E4/60.)
      DIMENSION XMU(LON2)
C-----------------------------------------------------------------------
C  COMPUTE COSINE OF SOLAR ZENITH ANGLE FOR BOTH HEMISPHERES.
      CNS= 3.141593E+0 *(SOLHR-12.)/12.+SLAG
      DO I=1,LON2
        SS=SINLAB(I)*SDEC
        CC=COSLAB(I)*CDEC
        CH=CC*COS(XLON(I)+CNS)
        XMU(I)=CH+SS
C       XMU(I)=(SINLAB(I)*SDEC)
C    1        +(COSLAB(I)*CDEC)*COS(XLON(I)+CNS)
      ENDDO
      DO I=1,LON2
C  NORMALIZE BY AVERAGE VALUE OVER RADIATION PERIOD FOR DAYTIME.
        IF(XMU(I).GT.0.01.AND.CZMN(I).GT.0.01) THEN
          XMU(I)=XMU(I)/CZMN(I)
        ELSE
          XMU(I)=0.
        ENDIF
C  ADJUST LONGWAVE FLUX AT SURFACE TO ACCOUNT FOR T CHANGES IN LAYER 1.
        SDLW=SFCDLW(I)*(TF(I)/TSFLW(I))**4
C  RETURN NET SURFACE RADIATIVE FLUX.
        SLRAD(I)=SFCNSW(I)*XMU(I)+SDLW
C  RETURN DOWNWARD AND UPWARD LONGWAVE FLUX AT GROUND, RESPECTIVELY.
        DLWSFC(I)=SDLW*CNWATT
        ULWSFC(I)=HSIGMA*TSEA(I)**4
      ENDDO
C  ADD RADIATIVE HEATING TO TEMPERATURE TENDENCY
      DO K=1,LEVS
        DO I=1,LON2
          TAU(I,K)=TAU(I,K)+SWH(I,K)*XMU(I)+HLW(I,K)
        ENDDO
      ENDDO
      RETURN
      END
