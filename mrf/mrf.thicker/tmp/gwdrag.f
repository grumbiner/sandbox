CFPP$ NOCONCUR R
      SUBROUTINE GWDRAG(IDIMT,IDIMT2,KDIM,
     .                  U1,V1,T1,PSTAR,VTJ,USQJ,KSM,KBJ,KBPS,
     .                  VELCO,BNVL2,ROLL,RO,TAUB,SI,DEL,SL,SIGK,RCL,
     .                  LAT,KDT,HPRIMX,AKWNMB,TENSIO)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GWDRAG      PERFORMS GRAVITY WAVE DRAG COMPUTATIONS.
C   PRGMMR: JORDAN C. ALPERT ORG: W/NMC23    DATE: 91-03-12
C
C ABSTRACT: PERFORMS GRAVITY WAVE DRAG COMPUTATIONS.
C
C PROGRAM HISTORY LOG:
C   87-06-03  JORDAN C. ALPERT  GWDRAG, GWDPS.
C   89-02-01  HANN-MING HENRY JUANG  CHANGE TO FORTRAN 77.
C   91-03-03  SELA-ROZWODOSKI  CRAY GUARD CODE AND CONSTANTS -
C                              GGWDRA, GGWDPS.
C
C USAGE:    CALL GWDRAG(U1,V1,T1,PSTAR,VTJ,USQJ,KSM,KBJ,KBPS,
C                       VELCO,BNVL2,ROLL,RO,TAUB,SI,DEL,SL,SIGK,RCL,
C                       LAT,KDT,HPRIMX,AKWNMB,TENSIO)
C   INPUT ARGUMENT LIST:
C     U1       - ZONAL      WIND COMPONENT *COS(LAT)  M/SEC AT T0-DT.
C     V1       - MERIDIONAL WIND COMPONENT *COS(LAT)  M/SEC AT T0-DT.
C     T1       - TEMPERATURE DEG K AT T0-DT.
C     PSTAR    - SURFACE PRESSURE (CB).
C     VTJ      - VIRTUAL TEMPERATURE.
C     USQJ     - RICHARDSON NUMBER.
C     KSM      - TOP    OF LOW LEVEL LAYER FOR GWDRAG SET TO 2.
C     KBJ      - BOTTOM OF LOW LEVEL LAYER FOR GWDRAG SET TO 1.
C     KBPS     - BOTTOM STARTING SIGMA LEVEL FOR P&S STRESS CALCULATION
C     SI(N)    - P/PSFC AT BASE OF LAYER N.
C     DEL(N)   - POSITIVE INCREMENT OF P/PSFC ACROSS LAYER N.
C     SL(N)    - P/PSFC AT MIDDLE OF LAYER N.
C     RCL      - RECIPROCAL OF SQUARE OF COS(LAT).
C     LAT      - LATITUDE  NUMBER.  USED ONLY AS DIAGNOSTIC.
C     KDT      - TIME STEP NUMBER.  USED ONLY AS DIAGNOSTIC.
C     HPRIMX   - TOPOGRAPHIC STANDARD DEVIATION  (M).
C
C   OUTPUT ARGUMENT LIST:
C     VELCO    - COMP OF WIND ALONG THE DIRECTION OF LOW LEVEL LAYER.
C     BNVL2    - BRUNT-VIASILA FREQ AS (N) AND ALSO (N**2).
C     ROLL     - LOW LEVEL RO.
C     RO       - DENSITY  MTS.
C     TAUB     - SURFACE STRESS.
C     SIGK     - SINGLE DIMENSION ARRAY OF LENGTH KDIM WHICH HOLDS
C              - THE CONSTANTS OF INVERSE SIGMA VALUES RAISED TO
C              - R/CP POWER FROM SUBROUTINE GGWDPS.
C     AKWNMB   - LENGTH SCALE.
C     TENSIO   - STRESS.
C
C REMARKS: LIST CAVEATS, OTHER HELPFUL HINTS OR INFORMATION
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN, CFT77.
C   MACHINE:  CRAY Y-MP.
C
C$$$
C-----GLAS MIGWD TO BE CALLED FROM MONNIN
C-----VERSION 5.0 (VERTICAL SMOOTHING IN LOW LEVEL)
C-----  THIS ROUTINE IS FROM J.C. JUSEM GLAS GRAVITY WAVE
C-----  PARAMETERIZATION.   IT RESIDES IN MONN3 EXTERNAL TO THE
C-----  VERTICAL DIFFUSION.  THE PRODCEDURE TO COMPUTE THE STRESS FROM
C-----  MOUNTAIN GRAVITY WAVES IS FROM PALMER, SHUTS AND SWINBANK:
C----- 'ALLEVIATION OF A SYSTEMATIC WESTERLY BIAS IN GENERAL
C-----  CIRCULATION AND NUMERICAL WEATHER PREDICTION MODELS THROUGH
C-----  AN OROGRAPHIC GRAVITY WAVE PARAMETERIZATION'.
C-----
C-----                <JORDAN C. ALPERT>                             *J*
C-----
C-----                      F R O M   M O N I N
C-----    U1,V1    THE WIND * COS(LAT)(M/S)
C-----    T1       THE TEMPERATURE (K)
C-----    HPRIME   TOPOGRAPHIC STANDARD DEVIATION  (M) AND VAR (M**2)(X)
C-----    PSTAR    SURFACE PRESSURE (CB)
C-----    VTJ      VIRTUAL TEMPERATURE
C-----    USQJ     RICHARDSON NUMBER
C-----    RCS      RECIPROCAL OF COSINE ( LATITUDE)
C-----    KBJ,KSM  BOTTOM & TOP OF LOW LEVEL LAYER FOR GWDRAG SET TO 1,2
C-----    KBPS     BOTTOM STARTING SIGMA LEVEL FOR P&S STRESS CALCULATIO
C-----    ULOW     THE SPEED IN THE LOW LEVEL LAYER
C-----
C-----                    F R O M    G W D R A G
C----- HCO,HSI   LOW LEVEL LAYER WIND ANGLE
C----- RO        DENSITY  MTS
C----- TENSIO    STRESS
C----- BNV2      BRUNT-VIASILA FREQ AS (N) AND ALSO (N**2)
C----- VELCO     COMP OF WIND ALONG THE DIRECTION OF LOW LEVEL LAYER
C----- ICRILV    BIT VECTOR CONTROL FOR STABLE, UNSTABE, OR CRIT LEVEL
C----- FRO2      LOCAL FROUDE   **2
C----- CRIF2     CRITICAL FROUDE   **2
C-----
C
CRAY       SAVE
C
C  G L A S   M I G W D  (GWDRAG)
C
                        D I M E N S I O N
     1  RO(IDIMT,KDIM),HPRIME(IDIMT)
     2, HPRIMX(IDIMT)
     3, TENSIO(IDIMT,KDIM)
     4, HCO(IDIMT),HSI(IDIMT),VELCO(IDIMT,KDIM-1)
     4, CRIF2(IDIMT),FRO2(IDIMT)
C
C  ABOVE IS STORAGE FOR GWDRAG AND BELOW IS STORAGE PASSED FROM MONN
C
       DIMENSION VTJ(IDIMT,KDIM),U1(IDIMT2,KDIM),V1(IDIMT2,KDIM)
       DIMENSION T1(IDIMT2,KDIM),USQJ(IDIMT,KDIM),BNVL2(IDIMT),
     7           SI(KDIM+1),DEL(KDIM),CL(KDIM),SL(KDIM),SIGK(KDIM),
     8           BNV2(IDIMT),ULOW(IDIMT),ROLL(IDIMT),TAUB(IDIMT)
      DIMENSION PSTAR(IDIMT),ZMEAN(KDIM,2)
C
      LOGICAL ICRILV(IDIMT)
      REAL AKAPPA
C
C-----ONLY DO CONSTANTS FIRST TIME THROUGH MONNIN
C
C
C-----GSFC TO NMC BRIDGE CONSTANTS
C
      NTOPM1=KDIM-1
C
        GRAV =  9.8000E+0
        GRAV2 = GRAV * GRAV
        AGRAV = 1./GRAV
        RGAS =  2.8705E+2
        GR2 = 2. * GRAV2 / RGAS
        AKAPPA = 2. / 7.
        FROCUT=0.85 * 0.85
C
        RCS=SQRT(RCL)
C
C-----THE VARIANCE OF TOPOGRAPHY (COMES IN AS STD DEV) ON A LAT PAIR
C
      DO 100 I= 1, IDIMT
      HPRIME(I) = HPRIMX(I) * HPRIMX(I)
 100  CONTINUE
C
C-----CONSTRAIN VARIANCE TO BE NOT GREATER THAN 160000 M**2
C     ONLY IF P&S LOW LEVEL STRESS IS USED - COMMENT FOR GFCL LOW LEV
C
      DO 200 I=1, IDIMT
      VALUE = 1.6 E 5
      HPRIME(I) = MIN( HPRIME(I), VALUE )
 200  CONTINUE
C
C-----INITIALIZE CRITICAL LEVEL CONTROL VECTOR BITS ALL TO ZERO
C
       DO 300 I=1, IDIMT
          ICRILV(I) = .FALSE.
 300   CONTINUE
C
C----BNVL2, THE LOW LEVEL BRUNT-VIASLA FREQUENCY IS NOT A FCT OF K
C----SQRT (N**2) NB: I USE BNVL2 FOR N**2 AND NOW N ITSELF
C
        DO 400 I=1,IDIMT
        VALUE = 0.0
        BNVL2(I) = MAX( BNVL2(I) , VALUE )
        BNVL2(I) = SQRT ( BNVL2(I) )
 400    CONTINUE
C
C-----SET INITIAL VALUES FOR STRESS
C
        DO 500 K=1,KDIM
        DO 500 I=1,IDIMT
          TENSIO(I,K) = 0.0
 500    CONTINUE
C
C------ LEVEL LOOP
C
C-----SET UP BOTTOM VALUES OF STRESS IF WE ARE NOT STARTING FROM
C-----FROM LEVEL 1 - NB (-) HERE BY CONVENTION
C
       DO 600 KLOW = 1, KBPS
       DO 600 I=1,IDIMT
          TENSIO(I,KLOW) = -1. * TAUB(I)
 600   CONTINUE
C
          DO 1100 K = KBPS, NTOPM1
C
          DO 1110 I=1, IDIMT
          FRO2(I) = 0.0
CRAY 1110     CONTINUE
C
C-----CALCULATE SQUARED BRUNT VAISALA FREQUENCY AT LEVEL K
C---- SIGKM IS RECIP( SIGMA** KAPPA) AT TOP OF LAYER
C---- SIGKIN IS RECIP( SIGMA** KAPPA) AT BOTTOM OF LAYER
C
          SIGKM = SIGK(K+1)
          SIGKIN = SIGK(K)
C
C--- N**2 AS FUNCTION OF K - BRANCH ON LOW LEVEL <KSM - TV IS USED (VTJ)
C OR JUST ALLOW THIS TYPE OF OPERATION ON USQJ  (RI)
C
CRAY         DO 1120 I=1,IDIMT
            BNV2(I) = GR2 * (SL(K+1) + SL(K))
     .    * (VTJ(I,K+1) * SIGKM - VTJ(I,K) * SIGKIN)
     .    /  (  (VTJ(I,K+1) * SIGKM
     .                 + VTJ(I,K) * SIGKIN)
     .       * (SL(K) - SL(K+1))
     .                 * (T1(I,K+1) + T1(I,K)) )
C
C-----UNSTABLE LAYER IF RI < 0 - - USING RI IN PLACE OF N**2
C
        ICRILV(I) = ICRILV(I) .OR. ( USQJ(I,K)
     1                    .LE. 0.0 )
C
       ICRILV(I) = ICRILV(I) .OR. ( USQJ(I,K)
     1                                  .LT. 0.25 )
CRAY 1120   CONTINUE
C
C-----COMPUTE CRITICAL FROUDE    ---> MEANS STABLE (ICRILV=0)
C
CRAY         DO 1150 I=1, IDIMT
          IF    ( .NOT. ICRILV(I) )  THEN
                CRIF2(I) = 1 - .25 / USQJ(I,K)
                CRIF2(I) = CRIF2(I) * CRIF2(I)
          ELSE
          CRIF2(I)=0.0
          ENDIF
CRAY 1150    CONTINUE
C
C-----UNSTABLE LAYER IF UPPER AIR VEL COMP ALONG SURF VEL <=0 (CRIT LAY)
C---- AT (U-C)=0. CRIT LAYER EXISTS AND BIT VECTOR SHOULD BE SET (.LE.)
C
CRAY        DO 1160 I=1,IDIMT
       ICRILV(I) = ICRILV(I) .OR. VELCO(I,K)
     1                                  .LE. 0.0
CRAY 1160  CONTINUE
C
C-----SQRT (N**2)  NB: I USE BNV2 FOR N**2 AND NOW N  ITSELF
C
CRAY        DO 1170 I=1, IDIMT
         VALUE = 0.
         BNV2(I) = MAX( BNV2(I) , VALUE )
         BNV2(I) = SQRT ( BNV2(I) )
CRAY 1170   CONTINUE
 1110   CONTINUE
C
C-----COMPUTING STRESS AT SURFACE  AND 1 LEVEL UP & LIMIT MAX VALUE
C
C----USING TAUB AT KBJ LEVEL
C
       IF(K .EQ. KBJ)  THEN
C
        DO 1180 I=1, IDIMT
         CRIF2(I) = MIN( CRIF2(I) , FROCUT )
CRAY 1180   CONTINUE
CRAY        DO 1190 I=1, IDIMT
               IF    ( .NOT. ICRILV(I) )  THEN
C
C-----GFDL LOW LEVEL SURFACE STRESS IS NEGITIVE WITH RESPECT TO GLAS
C-----BECASUE GLAS SUBTRACTS TENDENCY WHILE MONIN ADDS  WE CHANGE TAUB
C-----TO -TAUB (NB BUT CHANGE BACK FOR MONIN TENDENCY
C
                 TENSIO(I,K) = -1. * TAUB(I)
C
C---FR**2 AT SURFACE ONLY
C
               FRO2(I) = BNVL2(I) * BNVL2(I) *
     1        HPRIME(I) / (VELCO(I,K) * VELCO(I,K))
C
               ENDIF
CRAY 1190   CONTINUE
 1180   CONTINUE
C
       ELSE
C
C-----IN GLAS VERSION THERE IS A CALCULATION AT THE BOUNDARY LAYER
C-----WHICH IS NOT A "PART" OF THE MODEL STD LEVELS AND A CALC AT THE
C-----FIRST MODEL LAYER.  THE VALUE FOR TENSIO AT THE BNDY LAYER IS SET
C-----TO THE VALUE AT THE FIRST MODEL LAYER.  IN NMC THE BNDY LAYER CAN
C-----BE IGNORED ALL TOGETHER BECAUSE THE STRESS (TENSIO) IS ON SI'S,
C-----INTERFACES WHILE THE DEACCERATION IS ON LAYERS SL'S.
C
C-----COMPUTE THE LOCAL FROUDE   FOR THE STABLE CASE - MAKE SURE
C-----THAT BY CHANCE THE PROJECTION OF THE LOCAL WIND (U1,V1)
C-----SHOULD NOT BE SMALLER THEN 1.M/S, SINCE IT IS CUBED IN THE
C-----DENOMINATOR
C-----
C
          DO 1195 I=1,IDIMT
            IF   ( .NOT.  ICRILV(I) ) THEN
                 FRO2(I) = BNV2(I) / ( (AKWNMB * 0.5) *
     1          ( RO(I,K) + RO(I,K+1) ) *
     2      VELCO(I,K) * VELCO(I,K) * VELCO(I,K) ) *
     3          TENSIO(I,K)
            ENDIF
 1195     CONTINUE
C
       ENDIF
C
C-----COMPUTE STRESS AT LEVEL IN QUESTION FOR STABLE CASE
C
         DO 1210 I=1,IDIMT
            IF( .NOT.ICRILV(I) .AND. FRO2(I) .GT. CRIF2(I) ) THEN
C
C------FRO2 CHANGED TO > FROM .GE.
C
                  TENSIO(I,K+1) = TENSIO(I,K) * CRIF2(I) / FRO2(I)
            ENDIF
C
C----- CONSTANT STRESS IF CRIT FROUDE   NOT MET (.LE. 6/1)
C
            IF( .NOT. ICRILV(I) .AND. FRO2(I) .LE. CRIF2(I) )  THEN
                  TENSIO(I,K+1) = TENSIO(I,K)
             ENDIF
 1210    CONTINUE
C
C-----ALL DONE - PASS BACK STRESS PROFILE AND VERTICALLY DIFF
C
 1100 CONTINUE
C
      RETURN
      END
