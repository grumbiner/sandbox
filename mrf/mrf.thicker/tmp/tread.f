      SUBROUTINE TREAD(N,FHOUR,IDATE,GZ,Q,TE,DI,ZE,RQ,SL,SI,Z00)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    TREAD       READS SIGMA LEVEL SPECTRAL COEFFICIENTS.
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 88-04-27
C
C ABSTRACT: READS A COMPLETE SET OF SIGMA LEVEL SPECTRAL COEFFICIENTS
C   AT A SINGLE TIME TO BE USED TO START THE MODEL FORECAST.
C   THE SUBROUTINE COMPARES SI AND SL (THE MODEL'S VERTICAL
C   STRUCTURE) COMPUTED IN SETSIG WITH THE SI AND SL OF THE
C   INPUT COEFFICIENTS IN ORDER TO MAKE SURE THE COEFFICIENTS
C   WERE GENERATED UNDER THE SAME VERTICAL STRUCTURE.
C
C PROGRAM HISTORY LOG:
C   88-04-27  JOSEPH SELA
C
C USAGE:    CALL TREAD (N,FHOUR,IDATE,GZ,Q,TE,DI,ZE,RQ,SL,SI,Z00)
C   INPUT ARGUMENT LIST:
C     N        - FORTRAN UNIT NUMBER FOR FILE CONTAINING THE
C                COMPLETE SET OF SIGMA LEVEL COEFFICIENTS.
C     SL       - SIGMA LAYERS     COMPUTED IN SETSIG.
C     SI       - SIGMA INTERFACES COMPUTED IN SETSIG.
C
C   OUTPUT ARGUMENT LIST:
C     FHOUR    - FORECAST HOUR OF THE SET OF COEFFICIENTS READ
C                FROM UNIT N.
C     IDATE    - IDATE(1)=INITIAL HOUR (GMT) OF FORECAST FROM
C                         WHICH COEFFICIENTS WERE MADE.
C                IDATE(2)=MONTH (1-12).
C                IDATE(3)=DAY OF THE MONTH.
C                IDATE(4)=YEAR OF THE CENTURY.
C     GZ       - LAPLACIAN OF TOPOGRAPHY.
C                GZ IS MULTIPLIED BY THE CONSTANT SNNP1 ARRAY.
C                GZ IS THEN MULTIPLIED BY THE GRAVITY CONSTANT AND
C                DIVIDED BY THE SQUARE OF THE RADIUS OF THE EARTH.
C     Q        - LN(PSFC)          COEFFICIENTS.
C     TE       - TEMPERATURE       COEFFICIENTS.
C     DI       - DIVERGENCE        COEFFICIENTS.
C     ZE       - VORTICITY         COEFFICIENTS.
C     RQ       - SPECIFIC HUMIDITY COEFFICIENTS.
C     Z00      - MEAN TOPOGRAPHY.
C                Z00 IS SET EQUAL TO GZ(1) AFTER GZ IS READ.
C
C   INPUT FILES:
C     UNIT N   - COMPLETE SET OF SIGMA LEVEL SPECTRAL COEFFICIENTS.
C
C   OUTPUT FILES:
C     OUTPUT   - PRINT FILE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      COMMON/COMBIT/NDEX( 4032 ),SNNP1( 4032 )
      COMMON/COMBIT/LAB(4),IFIN,ICEN,IGEN,ICEN2,IENST,IENSI,RUNID,USRID
      CHARACTER*8 LAB
      DIMENSION
     1 GZ( 4033 ),
     2  Q( 4033 ),
     3 TE( 4033 , 28 ),DI( 4033 , 28 ),ZE( 4033 , 28 ),
     4 RQ( 4033 , 28 )
      DIMENSION IDATE(4),SI( 29 ),SL( 28 )
      DIMENSION          XI( 29 ),XL( 28 )
      DIMENSION DUMMY(201- 29 - 28 )
      DIMENSION ENSEMBLE(2),DUMMY2(21)
C
C  ADDITION BY MK 4/5/90
C
      SAVE IFP
      DATA IFP/0/
C
      IF(IFP.EQ.0) THEN
        CALL CMPIND
        IFP=1
      ENDIF
C
C  END ADDITION
C
C     SPECTRAL DATA FILE FORMAT
C.....LAB
C.....HOUR,IDATE(4),SI( 29 ),SL( 28 )
C.....ZLN Q TE DI ZE
      READ(N)LAB
C     PRINT 3000,LAB,N
3000  FORMAT(1H0,'TREAD LAB  ',4A10,' N=',I3)
      READ(N)FHOUR,IDATE,(XI(K),K=1, 29 ),(XL(K),K=1, 28 )
     &       ,DUMMY,WAVES,XLAYERS,TRUN,ORDER,REALFORM,GENCODE
C    &       ,RLOND,RLATD,RLONP,RLATP,RLONR,RLATR,TRACERS
C    &       ,SUBCEN,ENSEMBLE,PPID,SLID,VCID,VMID,VTID,RUNID,USRID
C    &       ,DUMMY2
      IF(IGEN.EQ.0) IGEN=GENCODE
      PRINT *,'TREAD UNIT,FHOUR,IDATE=',N,FHOUR,IDATE
      READ(N)(GZ(I),I=1, 4032 )
C
      Z00=GZ(1)
      DO 300 J=1, 4032
      GZ(J)=GZ(J)*SNNP1(J)
300   CONTINUE
      GA2= 9.8000E+0 /( 6.3712E+6 * 6.3712E+6 )
      DO 350 J=1, 4032
      GZ(J)=GZ(J)*GA2
350   CONTINUE
      CALL BARTRI(GZ,GZ,GZBAR)
      PRINT 99,GZBAR
99    FORMAT(1H ,'LAP(GZ)=',E12.3)
C
      READ(N)(Q(I),I=1, 4032 )
      DO 400 K=1, 28
      READ(N)(TE(I,K),I=1, 4032 )
400   CONTINUE
      DO 401 K=1, 28
      READ(N)(DI(I,K),I=1, 4032 )
      READ(N)(ZE(I,K),I=1, 4032 )
401   CONTINUE
      DO 200 K=1, 28
      READ(N)(RQ(I,K),I=1, 4032 )
200   CONTINUE
C
      DO 3 K=1, 28
      XL(K)=XL(K)-SL(K)
3     CONTINUE
      PRINT 100,(XL(K),K=1, 28 )
      DO 4 K=1, 29
      XI(K)=XI(K)-SI(K)
4     CONTINUE
      PRINT 100,(XI(K),K=1, 29 )
100   FORMAT(1H0, 12   (E9.3))
      PRINT 101,N,FHOUR,IDATE,Z00
101   FORMAT (1H0, 'IF ABOVE TWO ROWS NOT ZERO,INCONSISTENCY IN SIG.DEF'
     1,'ON N=',I2,2X,F6.1,2X,4(I4),'Z00=',E12.4)
      RETURN
      END
