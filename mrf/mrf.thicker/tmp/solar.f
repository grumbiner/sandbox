      SUBROUTINE SOLAR(JD,FJD,R,DLT,ALP,SLAG,SDEC,CDEC)
C>YH  SUBROUTINE SOLAR(JD,FJD,R,DLT,ALP,SLAG)
CFPP$ NOCONCUR R
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    SOLAR       ASTRONOMICAL(SOLAR) DATA - SW RADIATION
C   PRGMMR: KENNETH CAMPANA  ORG: W/NMC23    DATE: 89-07-07
C
C ABSTRACT: THIS CODE WRITTEN AT GFDL ....
C   COMPUTES RADIUS VECTOR,DECLINATION AND RIGHT ASCENSION OF SUN,
C   AND EQUATION OF TIME. SAME AS SUBROUTINE -SOLMRF-, BUT
C   WITH HOUR ANGLE,FRACTIONAL DAYLIGHT,AND
C   MEAN ZENITH ANGLE CALCULATIONS REMOVED (SUBROUTINE -ZENITH-
C   CALCULATES THESE FOR EACH POINT RATHER THAN EACH LATITUDE).
C   --THIS CODE IS TO BE USED FOR OTHER FCST MODELS OR FOR THE MRF
C   --MODEL IF 'INSTANTANEOUS' SW CALCULATIONS DESIRED.
C
C PROGRAM HISTORY LOG:
C   77-07-21  ROBERT WHITE,GFDL.
C   89-07-07  KENNETH CAMPANA-MOVED THE HOUR ANGLE CALCULATIONS TO
C                              SUBROUTINE -ZENITH-
C
C USAGE:    CALL SOLMRF(JD,FJD,R,DLT,ALP,SLAG)
C   INPUT ARGUMENT LIST:
C     JD       - JULIAN DAY FOR CURRENT FCST HOUR.
C     FJD      - FRACTION OF THE JULIAN DAY.
C   OUTPUT ARGUMENT LIST:
C     R        - RADIUS VECTOR OF THE SUN.
C     DLT      - DECLINATION OF THE SUN (RADIANS).
C     ALP      - RIGHT ASCENSION OF THE SUN.
C     SLAG     - EQUATION OF TIME (RADIANS).
C
C   OUTPUT FILES:
C     OUTPUT   - PRINT FILE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
      PARAMETER (PI= 3.141593E+0 ,TPI=2.0*PI,HPI=0.5*PI,RAD=180.0/PI)
C
C
C    *******************************************************************
C    *                            S O L A R                            *
C... *  PATTERNED AFTER ORIGINAL GFDL CODE---                          *
C... *     BUT NO CALCULATION OF LATITUDE MEAN COS SOLAR ZENITH ANGLE..*
C... *     ZENITH ANGLE CALCULATIONS DONE IN SUBR ZENITH IN THIS CASE..*
C... *  HR ANGLE,MEAN COSZ,AND MEAN TAUDA CALC REMOVED--K.A.C. MAR 89  *
C    *  UPDATES BY HUALU PAN TO LIMIT ITERATIONS IN NEWTON METHOD AND  *
C    *  ALSO CCR REDUCED FROM(1.3E-7)--BOTH TO AVOID NONCONVERGENCE IN *
C    *  NMC S HALF PRECISION VERSION OF GFDL S CODE   ----  FALL 1988  *
C    *******************************************************************
C
C.....SOLAR COMPUTES RADIUS VECTOR, DECLINATION AND RIGHT ASCENSION OF
C.....SUN, EQUATION OF TIME
C
                                   D A T A
     1   CYEAR/365.25/,      CCR/1.3 E -6/
C
C.....TPP = DAYS BETWEEN EPOCH AND PERIHELION PASSAGE OF 1900
C.....SVT6 = DAYS BETWEEN PERIHELION PASSAGE AND MARCH EQUINOX OF 1900
C.....JDOR = JD OF EPOCH WHICH IS JANUARY 0, 1900 AT 12 HOURS UT
C
                                   D A T A
     1   TPP/1.55/,          SVT6/78.035/,       JDOR/2415020/
C
C    *******************************************************************
C
      DAT=FLOAT(JD-JDOR)-TPP+FJD
C    COMPUTES TIME IN JULIAN CENTURIES AFTER EPOCH
      T=FLOAT(JD-JDOR)/36525. E 0
C    COMPUTES LENGTH OF ANOMALISTIC AND TROPICAL YEARS (MINUS 365 DAYS)
      YEAR=.25964134 E 0+.304 E -5*T
      TYEAR=.24219879 E 0-.614 E -5*T
C    COMPUTES ORBIT ECCENTRICITY AND ANGLE OF EARTH'S INCLINATION FROM T
      EC=.01675104 E 0-(.418 E -4+.126 E -6*T)*T
      ANGIN=23.452294 E 0-(.0130125 E 0+.164 E -5*T)*T
      ADOR=JDOR
      JDOE=ADOR+(SVT6*CYEAR)/(YEAR-TYEAR)
C    DELEQN=UPDATED SVT6 FOR CURRENT DATE
      DELEQN=FLOAT(JDOE-JD)*(YEAR-TYEAR)/CYEAR
      YEAR=YEAR+365. E 0
      SNI=SIN(ANGIN/RAD)
      TINI=1. E 0/TAN(ANGIN/RAD)
      ER=SQRT((1. E 0+EC)/(1. E 0-EC))
      QQ=DELEQN*TPI/YEAR
C    DETERMINE TRUE ANOMALY AT EQUINOX
      E=1. E 0
      ITER = 0
 32   EP=E-(E-EC*SIN(E)-QQ)/(1. E 0-EC*COS(E))
      CD=ABS(E-EP)
      E=EP
      ITER = ITER + 1
      IF(ITER.GT.10) THEN
        WRITE(6,*) ' ITERATION COUNT FOR LOOP 32 =', ITER
        WRITE(6,*) ' E, EP, CD =', E, EP, CD
      ENDIF
      IF(ITER.GT.10) GOTO 1032
      IF(CD.GT.CCR) GO TO 32
 1032 CONTINUE
      HE=.5 E 0*E
      EQ=2. E 0*ATAN(ER*TAN(HE))
C    DATE=DAYS SINCE LAST PERIHELION PASSAGE
      DATE = MOD(DAT,YEAR)
C    SOLVE ORBIT EQUATIONS BY NEWTON'S METHOD
      EM=TPI*DATE/YEAR
      E=1. E 0
      ITER = 0
 31   EP=E-(E-EC*SIN(E)-EM)/(1. E 0-EC*COS(E))
      CR=ABS(E-EP)
      E=EP
      ITER = ITER + 1
      IF(ITER.GT.10) THEN
        WRITE(6,*) ' ITERATION COUNT FOR LOOP 31 =', ITER
      ENDIF
      IF(ITER.GT.10) GOTO 1031
      IF(CR.GT.CCR) GO TO 31
 1031 CONTINUE
      R=1. E 0-EC*COS(E)
      HE=.5 E 0*E
      W=2. E 0*ATAN(ER*TAN(HE))
C>YH  SIND=SNI*SIN(W-EQ)
C>YH  DLT=ASIN(SIND)
      SDEC=SNI*SIN(W-EQ)
      CDEC=SQRT(1. E 0 - SDEC*SDEC)
      DLT=ASIN(SDEC)
      ALP=ASIN(TAN(DLT)*TINI)
      TST=COS(W-EQ)
      IF(TST.LT.0. E 0) ALP=PI-ALP
      IF(ALP.LT.0. E 0) ALP=ALP+TPI
      SUN=TPI*(DATE-DELEQN)/YEAR
      IF(SUN.LT.0. E 0) SUN=SUN+TPI
      SLAG=SUN-ALP-.03255 E 0
      RETURN
      END
