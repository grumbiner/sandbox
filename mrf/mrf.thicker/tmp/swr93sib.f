      SUBROUTINE SWR93SIB(FSWC,HSWC,UFSWC,DFSWC,FSWL,HSWL,UFSWL,DFSWL,
     1                    PRESS,COSZRO,TAUDAR,RH2O,RRCO2,SSOLAR,QO3,
CFE932                    NCLDS,KTOPSW,KBTMSW,CIRAB,CIRRF,CUVRF,CAMT,
     2                    NCLDS,KTOP,KBTM,CAMT,CRR,CTT,
     A                    ALVB,ALNB,ALVD,ALND,GDFVB,GDFNB,GDFVD,GDFND)
CFPP$ NOCONCUR R
C===>    *********************************************************
C --- SWR91SIB --- MODIFIED FROM SWR89-BAND12....YUTAI HOU
C --- SWR93SIB --- MODIFIED FROM SWR91SIB AS NOTED BELOW..
C        -INPUTS 12 BANDS CLD REFLECTANCE AND TRANSMITANCE
C        -CRR,CTT TO REPLACE CIRAB,CIRRF,CUVRF...YUTAI HOU ..FEB 93
C
C     -SW- RADIATION CODE............................
C        INPUTS:PRESS,COSZRO,TAUDAR,RH2O,RRCO2,SSOLAR,QO3,NCLDS,
CDE93           KTOPSW,KBTMSW,CIRAB,CIRRF,CUVRF,CAMT,
C               KTOP,KBTM,CAMT,CRR,CTT,
C               ALVB,ALVD,ALNB,ALND;
C        OUTPUT:FSWC,HSWC,UFSWC,DFSWC,FSWL,HSWL,UFSWL,DFSWL,
C               GDFVB,GDFVD,GDFNB,GDFND.
C --- SWR91SIB --- MODIFIED BY K. CAMPANA..06 MAR 92
C         INCLUDE HPCON,PARMC CHANGED TO HCON,RDPARM
C         6 Q..... VARIABLE NAMES RESTORED TO ORIGINAL 7,8 CHAR
C         CHANGE O3DIFF,DIFFCC TO O3DIFCTR,DIFFCTR
C --- SWR91SIB --- MODIFIED BY Y. HOU         FEB 93
C         INPUTS 12 BANDS CLD REFLECTTANCE AND TRANSMITTANCE
C         CRR,CTT TO REPLACE CIRAB,CIRRF,CUVRF
C --- SWR93SIB --- MODIFIED BY Y. HOU         DEC 93
C         INPUT VARIABLES KTOP, KBTM
C ---          --- MODIFIED BY Y. HOU         FEB 94
C         TO INCORPORATE B KATZ IMPROVED CODING
C
C===>    *********************************************************
      COMMON/PHYCON/AMOLWT,CSUBP,DIFFCTR,G,GRAVDR,O3DIFCTR,P0,
     *            P0XZP2,P0XZP8,P0X2,RADCON,RGAS,RGASSP,SECPDA
      COMMON/PHYCON/RATCO2MW,RATH2OMW
      COMMON/PHYCON/RADCON1
      COMMON/PHYCON/GINV,P0INV,GP0INV
      COMMON/HCON/HUNDRED,HNINETY,SIXTY,FIFTY,TEN,EIGHT,FIVE,
     *            FOUR,THREE,TWO,ONE,HAF,QUARTR,ZERO
      COMMON/HCON/H83E26,H71E26,H1E15,H1E13,H1E11,H1E8,H4E5,
     *            H165E5,H5725E4,H488E4,H1E4,H24E3,H20788E3,
     *            H2075E3,H1224E3,H5E2,H3082E2,H3E2,H2945E2,
     *            H23E2,H15E2,H35E1,H3P6,H181E1,H18E1,H2P9,H2P8,
     *            H2P5,H1P8,H1P4387,H1P4,H1P25892,HP8,HP518,
     *            HP369,HP1
      COMMON/HCON/H44871M2,H559M3,H1M3,H987M4,H285M4,H1M4,
     *            H6938M5,H394M5,H37412M5,H1439M5,H128M5,H1M5,
     *            H7M6,H4999M6,H25452M6,H1M6,H391M7,H1174M7,
     *            H8725M8,H327M8,H257M8,H1M8,H23M10,H14M10,
     *            H11M10,H1M10,H83M11,H82M11,H8M11,H77M11,
     *            H72M11,H53M11,H48M11,H44M11,H42M11,H37M11,
     *            H35M11,H32M11,H3M11,H28M11,H24M11,H23M11,
     *            H2M11,H18M11,H15M11,H14M11,H114M11,H11M11,
     *            H1M11,H96M12,H93M12,H77M12,H74M12,H65M12,
     *            H62M12,H6M12,H45M12,H44M12,H4M12,H38M12,
     *            H37M12,H3M12,H29M12,H28M12,H24M12,H21M12,
     *            H16M12,H14M12,H12M12,H8M13,H46M13,H36M13,
     *            H135M13,H12M13,H1M13,H3M14,H15M14,H14M14,
     *            H1M17,H1M18,H1M19,H1M20,H1M21,H1M22,H1M23,
     *            H1M24,H26M30,H14M30,H25M31,H21M31,H12M31,
     *            H9M32,H55M32,H45M32,H4M33,H62M34,H1M60
      COMMON/HCON/HMP575,HM13EZ,HM19EZ,HM1E1,HM181E1,HM1E2
      COMMON/HCON/H1E6,H2E6,H1M2,HMP66667,HM6666M2,HP166666,
     *            H41666M2,HMP5,HM2M2,H29316E2,H1226E1,H3116E1,
     *            H9P94,HP6,H625M2,HP228,HP60241,HM1797E1,
     *            H8121E1,H2E2,HM1EZ,H26E2,H44194M2,H1P41819
      COMMON/HCON/HP219,HP144,HP816,H69766E5,H235M3,HP26,
     *            H129M2,H75826M4,H1P082,HP805,H1386E2,
     *            H658M2,H1036E2,H2118M2,H42M2,H323M4,
     *            H67390E2,HP3795,HP5048,H102M5,H451M6
      COMMON/HCON/H16E1,HM161E1,H161E1,H3M3,H101M16,
     *            HM1597E1,H25E2,HP118666,H15M5,H3P5,H18E3,
     *            H6P08108,HMP805,HP602409,HP526315,
     *            H28571M2,H1M16
      COMMON/HCON/H3M4
      COMMON/HCON/HM8E1
      COMMON/HCON/H28E1
C     PARAMETER SETTINGS FOR THE LONGWAVE AND SHORTWAVE RADIATION CODE:
C          IMAX   =  NO. POINTS ALONG THE LAT. CIRCLE USED IN CALCS.
C          L      =  NO. VERTICAL LEVELS (ALSO LAYERS) IN MODEL
C***NOTE: THE USER NORMALLY WILL MODIFY ONLY THE IMAX AND L PARAMETERS
C          NBLW   =  NO. FREQ. BANDS FOR APPROX COMPUTATIONS. SEE
C                      BANDTA FOR DEFINITION
C          NBLX   =  NO. FREQ BANDS FOR APPROX CTS COMPUTATIONS
C          NBLY   =  NO. FREQ. BANDS FOR EXACT CTS COMPUTATIONS. SEE
C                      BDCOMB FOR DEFINITION
C          INLTE  =  NO. LEVELS USED FOR NLTE CALCS.
C          NNLTE  =  INDEX NO. OF FREQ. BAND IN NLTE CALCS.
C          NB,KO2 ARE SHORTWAVE PARAMETERS; OTHER QUANTITIES ARE DERIVED
C                    FROM THE ABOVE PARAMETERS.
      PARAMETER (L= 28 )
      PARAMETER (IMAX= 64 )
      PARAMETER (IMBX=IMAX+1)
      PARAMETER (NBLW=163,NBLX=47,NBLY=15)
      PARAMETER (NBLM=NBLY-1)
      PARAMETER (LP1=L+1,LP2=L+2,LP3=L+3)
      PARAMETER (LM1=L-1,LM2=L-2,LM3=L-3)
      PARAMETER (LL=2*L,LLP1=LL+1,LLP2=LL+2,LLP3=LL+3)
      PARAMETER (LLM1=LL-1,LLM2=LL-2,LLM3=LL-3)
      PARAMETER (LP1M=LP1*LP1,LP1M1=LP1M-1)
      PARAMETER (LP1V=LP1*(1+2*L/2))
      PARAMETER (LP121=LP1*NBLY)
      PARAMETER (LL3P=3*L+2)
      PARAMETER (NB=12)
      PARAMETER (INLTE=3,INLTEP=INLTE+1,NNLTE=56)
      PARAMETER (NB1=NB-1)
      PARAMETER (KO2=12)
      PARAMETER (KO21=KO2+1,KO2M=KO2-1)
C     PARAMETER SETTINGS FOR THE LONGWAVE AND SHORTWAVE RADIATION CODE:
C          IMAX   =  NO. POINTS ALONG THE LAT. CIRCLE USED IN CALCS.
C          L      =  NO. VERTICAL LEVELS (ALSO LAYERS) IN MODEL
C***NOTE: THE USER NORMALLY WILL MODIFY ONLY THE IMAX AND L PARAMETERS
C          NB IS A SHORTWAVE PARAMETER; OTHER QUANTITIES ARE DERIVED
C                    FROM THE ABOVE PARAMETERS.
C --- VARIABLES AS IN ARGUMENT LIST
                       D I M E N S I O N
CFE931  FSWC  (IMBX,LP1),  HSWC  (IMBX,LP1)
CFE932, FSWL  (IMBX,LP1),  HSWL  (IMBX,LP1)
     1  FSWC  (IMBX,LP1),  HSWC  (IMBX,LP1),   CRR   (IMBX,NB,LP1)
     2, FSWL  (IMBX,LP1),  HSWL  (IMBX,LP1),   CTT   (IMBX,NB,LP1)
     3, UFSWC (IMBX,LP1),  DFSWC (IMBX,LP1)
     4, UFSWL (IMBX,LP1),  DFSWL (IMBX,LP1)
     5, PRESS (IMBX,LP1),  RH2O  (IMBX,L),     QO3   (IMBX,L)
     6, CAMT  (IMBX,LP1),  KTOP  (IMBX,LP1),   KBTM  (IMBX,LP1)
CFE937, CIRAB (IMBX,LP1),  CIRRF (IMBX,LP1),   CUVRF (IMBX,LP1)
     8, COSZRO(IMAX),      TAUDAR(IMAX),       NCLDS (IMAX)
     A, ALVB  (IMAX),  ALNB  (IMAX),  ALVD  (IMAX),  ALND  (IMAX)
     B, GDFVB (IMAX),  GDFNB (IMAX),  GDFVD (IMAX),  GDFND (IMAX)
C --- LOCAL VARIABLES
                       D I M E N S I O N
     1  PP    (IMBX,LP1),  DP    (IMBX,LP1),   PR2   (IMBX,LP1)
     3, DU    (IMBX,LP1),  DUCO2 (IMBX,LP1),   DUO3  (IMBX,LP1)
     4, FF    (IMBX,LP1),  FFCO2 (IMBX,LP1),   FFO3  (IMBX,LP1)
     5, RRAY  (IMAX),      DFNTOP(IMBX,NB),    SECZ  (IMAX)
     6, REFL  (IMAX),      TEMP1 (IMAX),       REFL2 (IMAX)
     7, TEMPF (IMAX),      TEMPG (IMAX)
     A, CCMAX (IMAX),      XAMT  (IMBX,LP1),   KBTMSW(IMBX,LP1)
                       D I M E N S I O N
     1                     UD    (IMBX,LP1),   UR    (IMBX,LP1)
     2, UCO2  (IMBX,LLP2), UDCO2 (IMBX,LP1),   URCO2 (IMBX,LP1)
     3, UO3   (IMBX,LLP2), UDO3  (IMBX,LP1),   URO3  (IMBX,LP1)
     4, TCO2  (IMBX,LLP2), TDCO2 (IMBX,LP1),   TUCO2 (IMBX,LP1)
     5, TO3   (IMBX,LLP2), TDO3  (IMBX,LP1),   TUO3  (IMBX,LP1)
                       D I M E N S I O N
     1  DFN   (IMBX,LP1),  UFN   (IMBX,LP1),   CR    (IMBX,LP1)
     2, TTD   (IMBX,LP1),  TTU   (IMBX,LP1),   CT    (IMBX,LP1)
     3, PPTOP (IMBX,LP1),  DPCLD (IMBX,LP1)
                       D I M E N S I O N
     1  MNKBTM(LP1)     ,  MNKTOP(LP1)     ,   MXKBTM(LP1)
     2, MXKTOP(LP1)
                       L O G I C A L
     1  LSAVE(IMAX),LSAVE0(IMBX,LP1),LSAVE1(IMBX,LP1,LP1)
     2, LSAVE2(IMBX,LP1,LP1)
C --- EQUIVALENCED LOCAL VARIABLES
                       D I M E N S I O N
     1  TTUB1 (IMBX,LP1),  TUCL1 (IMBX,LP1)
     2, TTDB1 (IMBX,LP1),  TDCL1 (IMBX,LP1),   TDCL2 (IMBX,LP1)
     3, UFNTRN(IMBX,LP1),  UFNCLU(IMBX,LP1),   TCLU  (IMBX,LP1)
     4, DFNTRN(IMBX,LP1),  DFNCLU(IMBX,LP1),   TCLD  (IMBX,LP1)
     5, ALFA  (IMBX,LP1),  ALFAU (IMBX,LP1)
                     E Q U I V A L E N C E
     1  (UDO3 , UO3 (1,1), DFNCLU),  (URO3 , UO3 (1,LP2), UFNCLU)
     2, (UDCO2, UCO2(1,1), TCLD  ),  (URCO2, UCO2(1,LP2), TCLU  )
     3, (TDO3 , TO3 (1,1), DFNTRN),  (TUO3 , TO3 (1,LP2), UFNTRN)
     4, (TDCO2, TCO2(1,1)        ),  (TUCO2, TCO2(1,LP2)        )
     5, (FF   , ALFA ),   (FFCO2 , ALFAU ),   (FFO3  , TTDB1 )
     6, (DU   , TTUB1),   (DUCO2 , TUCL1 ),   (DUO3  , TDCL1 )
     7, (PR2  , TDCL2)
C
C---COMMON FOR LOCAL DATA VARIABLES---
      COMMON /SWRSAV/ ABCFF(NB),PWTS(NB),CFCO2,CFO3,REFLO3,RRAYAV
C                           D A T A
C    1  ABCFF / 2*4.0E-5, 0.002, 0.035, 0.377, 1.95, 9.40, 44.6,
C    1          190.0,    989.0, 2706.0, 39011.0 /
C    2, PWTS  / 0.5000, 0.121416, 0.0698, 0.1558, 0.0631, 0.0362,
C    2          0.0243, 0.0158, 0.0087, 0.001467, 0.002342, 0.001075 /
C    3, CFCO2, CFO3, REFLO3, RRAYAV / 508.96, 466.64, 1.9, 0.144 /
C    1  ABCFF / 2*4.0E-5, .002, .035, .377, 1.95, 9.40, 44.6, 190. /
C    2, PWTS  /.5000,.1470,.698,.1443,.0584,.0335,.0225,.0158,.0087/
C    3, CFCO2, CFO3, REFLO3, RRAYAV / 508.96, 466.64, 1.9, 0.144 /
C
C     CALCULATE SECANT OF ZENITH ANGLE (SECZ),FLUX PRESSURES(PP),
C     LAYER WIDTH (DP) AND PRESSURE SCALING FACTOR (PR2).
      DO 100 I=1,IMAX
        SECZ(I) = H35E1/SQRT(H1224E3*COSZRO(I)*COSZRO(I)+ONE)
        PP(I,1)   = ZERO
        PP(I,LP1) = PRESS(I,LP1)
        TEMP1(I)  = ONE/PRESS(I,LP1)
100   CONTINUE
      DO 110 K=2,L
      DO 110 I=1,IMAX
        PP(I,K) = HAF*(PRESS(I,K)+PRESS(I,K-1))
110   CONTINUE
      DO 120 K=1,L
      DO 120 I=1,IMAX
        DP (I,K) = PP(I,K+1)-PP(I,K)
        PR2(I,K) = HAF*(PP(I,K)+PP(I,K+1))
120   CONTINUE
      DO 130 K=1,L
      DO 130 I=1,IMAX
        PR2(I,K) = PR2(I,K)*TEMP1(I)
130   CONTINUE
C     CALCULATE ENTERING FLUX AT THE TOP FOR EACH BAND(IN CGS UNITS)
      DO 140 N=1,NB
      DO 140 IP=1,IMAX
        DFNTOP(IP,N) = SSOLAR*H69766E5*COSZRO(IP)*TAUDAR(IP)*PWTS(N)
140   CONTINUE
C     EXECUTE THE LACIS-HANSEN REFLECTIVITY PARAMETERIZATION
C     FOR THE VISIBLE BAND
      DO 150 I=1,IMAX
        RRAY(I) = HP219/(ONE+HP816*COSZRO(I))
        REFL(I) = RRAY(I) + (ONE-RRAY(I))*(ONE-RRAYAV)*ALVB(I)/
     1            (ONE-ALVD(I)*RRAYAV)
150   CONTINUE
      DO 155 I=1,IMAX
        RRAY(I) = 0.104/(ONE+4.8*COSZRO(I))
        REFL2(I)= RRAY(I) + (ONE-RRAY(I))*(ONE-0.093)*ALVB(I)/
     1            (ONE-ALVD(I)*0.093)
155   CONTINUE
C     CALCULATE PRESSURE-WEIGHTED OPTICAL PATHS FOR EACH LAYER
C     IN UNITS OF CM-ATM. PRESSURE WEIGHTING IS USING PR2.
C     DU= VALUE FOR H2O;DUCO2 FOR CO2;DUO3 FOR O3.
      DO 160 K=1,L
      DO 160 I=1,IMAX
        DU   (I,K) = GINV*RH2O(I,K)*DP(I,K)*PR2(I,K)
        DUCO2(I,K) = (RRCO2*GINV*CFCO2)*DP(I,K)*PR2(I,K)
        DUO3 (I,K) = (GINV*CFO3)*QO3(I,K)*DP(I,K)
160   CONTINUE
C...............  CALCULATE CLEAR SKY SW FLUX
C     OBTAIN THE OPTICAL PATH FROM THE TOP OF THE ATMOSPHERE TO THE
C     FLUX PRESSURE. ANGULAR FACTORS ARE NOW INCLUDED. UD=DOWNWARD
C     PATH FOR H2O,WIGTH UR THE UPWARD PATH FOR H2O. CORRESPONDING
C     QUANTITIES FOR CO2,O3 ARE UDCO2/URCO2 AND UDO3/URO3.
      DO 200 IP=1,IMAX
        UD   (IP,1) = ZERO
        UDCO2(IP,1) = ZERO
        UDO3 (IP,1) = ZERO
200   CONTINUE
      DO 210 K=2,LP1
      DO 210 I=1,IMAX
        UD   (I,K) = UD   (I,K-1)+DU   (I,K-1)*SECZ(I)
        UDCO2(I,K) = UDCO2(I,K-1)+DUCO2(I,K-1)*SECZ(I)
        UDO3 (I,K) = UDO3 (I,K-1)+DUO3 (I,K-1)*SECZ(I)
210   CONTINUE
      DO 220 IP=1,IMAX
        UR   (IP,LP1) = UD   (IP,LP1)
        URCO2(IP,LP1) = UDCO2(IP,LP1)
        URO3 (IP,LP1) = UDO3 (IP,LP1)
220   CONTINUE
      DO 230 K=L,1,-1
      DO 230 IP=1,IMAX
        UR   (IP,K) = UR   (IP,K+1)+DU   (IP,K)*DIFFCTR
        URCO2(IP,K) = URCO2(IP,K+1)+DUCO2(IP,K)*DIFFCTR
        URO3 (IP,K) = URO3 (IP,K+1)+DUO3 (IP,K)*O3DIFCTR
230   CONTINUE
C     CALCULATE CO2 ABSORPTIONS . THEY WILL BE USED IN NEAR INFRARED
C     BANDS.SINCE THE ABSORPTION AMOUNT IS GIVEN (IN THE FORMULA USED
C     BELOW, DERIVED FROM SASAMORI) IN TERMS OF THE TOTAL SOLAR FLUX,
C     AND THE ABSORPTION IS ONLY INCLUDED IN THE NEAR IR (50 PERCENT
C     OF THE SOLAR SPECTRUM), THE ABSORPTIONS ARE MULTIPLIED BY 2.
C       SINCE CODE ACTUALLY REQUIRES TRANSMISSIONS, THESE ARE THE
C     VALUES ACTUALLY STORED IN TCO2.
      DO 240 K=2,LL+1
      DO 240 I=1,IMAX
        TCO2(I,K) = ONE-TWO*(H235M3*EXP(HP26*LOG(UCO2(I,K)+H129M2))
     1                       -H75826M4)
240   CONTINUE
C     NOW CALCULATE OZONE ABSORPTIONS. THESE WILL BE USED IN
C     THE VISIBLE BAND.JUST AS IN THE CO2 CASE, SINCE THIS BAND IS
C     50 PERCENT OF THE SOLAR SPECTRUM,THE ABSORPTIONS ARE MULTIPLIED
C     BY 2. THE TRANSMISSIONS ARE STORED IN TO3.
      HTEMP = H1036E2*H1036E2*H1036E2
      DO 250 K=2,LL+1
      DO 250 I=1,IMAX
        TO3(I,K) = ONE - TWO*UO3(I,K)*
     1             (H1P082*EXP(HMP805*LOG(ONE+H1386E2*UO3(I,K)))+
     2             H658M2/(ONE+HTEMP*UO3(I,K)*UO3(I,K)*UO3(I,K))+
     3             H2118M2/(ONE+UO3(I,K)*(H42M2+H323M4*UO3(I,K))))
250   CONTINUE
C   START FREQUENCY LOOP (ON N) HERE
C
C--- BAND 1 (VISIBLE) INCLUDES O3 AND H2O ABSORPTION
      DO 260 K=1,L
      DO 260 I=1,IMAX
        TTD(I,K+1) = EXP(HM1EZ*MIN(FIFTY,ABCFF(1)*UD(I,K+1)))
        TTU(I,K) = EXP(HM1EZ*MIN(FIFTY,ABCFF(1)*UR(I,K)))
        DFN(I,K+1) = TTD(I,K+1)*TDO3(I,K+1)
        UFN(I,K) = TTU(I,K)*TUO3(I,K)
260   CONTINUE
      DO 270 I=1,IMAX
        DFN(I,1)   = ONE
        UFN(I,LP1) = DFN(I,LP1)
270   CONTINUE
C     SCALE VISIBLE BAND FLUXES BY SOLAR FLUX AT THE TOP OF THE
C     ATMOSPHERE (DFNTOP(I,1))
C     DFSW/UFSW WILL BE THE FLUXES, SUMMED OVER ALL BANDS
      DO 280  K=1,LP1
      DO 280  I=1,IMAX
        DFSWL(I,K) =         DFN(I,K)*DFNTOP(I,1)
        UFSWL(I,K) = REFL(I)*UFN(I,K)*DFNTOP(I,1)
280   CONTINUE
      DO 285 I=1,IMAX
        GDFVB(I) = DFSWL(I,LP1)*EXP(-0.15746*SECZ(I))
        GDFVD(I) = ((ONE-REFL2(I))*DFSWL(I,LP1) -
     1              (ONE-ALVB(I)) *GDFVB(I)) / (ONE-ALVD(I))
        GDFNB(I) = ZERO
        GDFND(I) = ZERO
285   CONTINUE
C---NOW OBTAIN FLUXES FOR THE NEAR IR BANDS. THE METHODS ARE THE SAME
C   AS FOR THE VISIBLE BAND, EXCEPT THAT THE REFLECTION AND
C   TRANSMISSION COEFFICIENTS (OBTAINED BELOW) ARE DIFFERENT, AS
C   RAYLEIGH SCATTERING NEED NOT BE CONSIDERED.
      DO 350 N=2,NB
        IF (N.EQ.2) THEN
C   THE WATER VAPOR TRANSMISSION FUNCTION FOR BAND 2 IS EQUAL TO
C   THAT OF BAND 1 (SAVED AS TTD,TTU)
C--- BAND 2-9 (NEAR-IR) INCLUDES O3, CO2 AND H2O ABSORPTION
          DO 290 K=1,L
          DO 290 I=1,IMAX
            DFN(I,K+1) = TTD(I,K+1)*TDCO2(I,K+1)
            UFN(I,K) = TTU(I,K)*TUCO2(I,K)
290       CONTINUE
        ELSE
C   CALCULATE WATER VAPOR TRANSMISSION FUNCTIONS FOR NEAR INFRARED
C   BANDS. INCLUDE CO2 TRANSMISSION (TDCO2/TUCO2), WHICH
C   IS THE SAME FOR ALL INFRARED BANDS.
          DO 300 K=1,L
          DO 300 I=1,IMAX
            DFN(I,K+1)=
     &           EXP(HM1EZ*MIN(FIFTY,ABCFF(N)*UD(I,K+1)))*TDCO2(I,K+1)
            UFN(I,K)=EXP(HM1EZ*MIN(FIFTY,ABCFF(N)*UR(I,K)))*TUCO2(I,K)
300       CONTINUE
        ENDIF
C---AT THIS POINT,INCLUDE DFN(1),UFN(LP1), NOTING THAT DFN(1)=1 FOR
C   ALL BANDS, AND THAT UFN(LP1)=DFN(LP1) FOR ALL BANDS.
        DO 310 I=1,IMAX
          DFN(I,1)   = ONE
          UFN(I,LP1) = DFN(I,LP1)
310     CONTINUE
C     SCALE THE PREVIOUSLY COMPUTED FLUXES BY THE FLUX AT THE TOP
C     AND SUM OVER BANDS
        DO 320 K=1,LP1
        DO 320 I=1,IMAX
          DFSWL(I,K) = DFSWL(I,K) +         DFN(I,K)*DFNTOP(I,N)
          UFSWL(I,K) = UFSWL(I,K) + ALNB(I)*UFN(I,K)*DFNTOP(I,N)
320     CONTINUE
        DO 330 I=1,IMAX
          GDFNB(I) = GDFNB(I) + DFN(I,LP1)*DFNTOP(I,N)
330     CONTINUE
350   CONTINUE
      DO 360 K=1,LP1
      DO 360 I=1,IMAX
        FSWL(I,K) = UFSWL(I,K)-DFSWL(I,K)
360   CONTINUE
      DO 370 K=1,L
      DO 370 I=1,IMAX
        HSWL(I,K) = RADCON*(FSWL(I,K+1)-FSWL(I,K))/DP(I,K)
370   CONTINUE
C
C---END OF FREQUENCY LOOP (OVER N)
C.................CALCULATE CLOUDY SKY SW FLUX
      KCLDS=NCLDS(1)
      MAXKTP=KTOP(1,NCLDS(1)+1)
CDE93 MAXKTP=KTOPSW(1,NCLDS(1)+1)
      DO 400 I=2,IMAX
        KCLDS=MAX(NCLDS(I),KCLDS)
        MAXKTP=MAX(KTOP(I,NCLDS(I)+1),MAXKTP)
CDE93   MAXKTP=MAX(KTOPSW(I,NCLDS(I)+1),MAXKTP)
400   CONTINUE
      IF (KCLDS .EQ. 0) THEN
        DO 410 K=1,LP1
        DO 410 I=1,IMAX
          DFSWC(I,K) = DFSWL(I,K)
          UFSWC(I,K) = UFSWL(I,K)
          FSWC (I,K) = FSWL (I,K)
410     CONTINUE
        DO 420 K=1,L
        DO 420 I=1,IMAX
          HSWC(I,K) = HSWL(I,K)
420     CONTINUE
        RETURN
      END IF
      DO 430 K=1,LP1
      DO 430 I=1,IMAX
        XAMT(I,K) = CAMT(I,K)
        KBTMSW(I,K) = KBTM(I,K)
430   CONTINUE
      DO 440 K=2,KCLDS+1
      DO 440 I=1,IMAX
        IF (CAMT(I,K).GT.ZERO) KBTMSW(I,K) = KBTMSW(I,K) + 1
440   CONTINUE
      DO 445 I=1,IMAX
        IF(NCLDS(I).LE.0) THEN
          CCMAX(I) = ZERO
        ELSE
          CCMAX(I) = ONE
        ENDIF
445   CONTINUE
      DO 450 K=1,KCLDS
        DO 450 I=1,IMAX
          IF(K.LE.NCLDS(I)) THEN
            CCMAX(I) = CCMAX(I) * (ONE - CAMT(I,K+1))
          ENDIF
450   CONTINUE
      DO 455 I=1,IMAX
        IF(NCLDS(I).GT.0) CCMAX(I) = ONE - CCMAX(I)
455   CONTINUE
      DO 460 K=1,KCLDS
        DO 460 I=1,IMAX
          IF(K.LE.NCLDS(I) .AND. CCMAX(I).GT.ZERO) THEN
            XAMT(I,K+1) = CAMT(I,K+1)/CCMAX(I)
          ENDIF
460   CONTINUE
C     DO 470 I=1,IMAX
C       NNCLDS   = NCLDS(I)
C       CCMAX(I) = ZERO
C       IF (NNCLDS .LE. 0) GO TO 470
C       DO 450 K=1,NNCLDS
C         CCMAX(I) = CCMAX(I) * (ONE - CAMT(I,K+1))
C450    CONTINUE
C       CCMAX(I) = ONE - CCMAX(I)
C       IF (CCMAX(I) .GT. ZERO) THEN
C         DO 460 K=1,NNCLDS
C           XAMT(I,K+1) = CAMT(I,K+1)/CCMAX(I)
C460      CONTINUE
C       END IF
C470  CONTINUE
      DO 480 K=1,LP1
      DO 480 I=1,IMAX
        FF   (I,K) = DIFFCTR
        FFCO2(I,K) = DIFFCTR
        FFO3 (I,K) = O3DIFCTR
480   CONTINUE
      DO 490 K=1,MAXKTP
        DO 490 IP=1,IMAX
CDE93     IF(K.LE.KTOPSW(IP,NCLDS(IP)+1)) THEN
          IF(K.LE.KTOP(IP,NCLDS(IP)+1)) THEN
            FF   (IP,K) = SECZ(IP)
            FFCO2(IP,K) = SECZ(IP)
            FFO3 (IP,K) = SECZ(IP)
          ENDIF
490   CONTINUE
C     DO 490 IP=1,IMAX
CDE93   JTOP = KTOPSW(IP,NCLDS(IP)+1)
C       JTOP = KTOP(IP,NCLDS(IP)+1)
C     DO 490 K=1,JTOP
C       FF   (IP,K) = SECZ(IP)
C       FFCO2(IP,K) = SECZ(IP)
C       FFO3 (IP,K) = SECZ(IP)
C490  CONTINUE
      DO 500 I=1,IMAX
        RRAY(I) = HP219/(ONE+HP816*COSZRO(I))
        REFL(I) = RRAY(I) + (ONE-RRAY(I))*(ONE-RRAYAV)*ALVD(I)/
     1            (ONE-ALVD(I)*RRAYAV)
500   CONTINUE
      DO 510 IP=1,IMAX
        UD   (IP,1) = ZERO
        UDCO2(IP,1) = ZERO
        UDO3 (IP,1) = ZERO
510   CONTINUE
      DO 520 K=2,LP1
      DO 520 I=1,IMAX
        UD   (I,K) = UD   (I,K-1)+DU   (I,K-1)*FF   (I,K)
        UDCO2(I,K) = UDCO2(I,K-1)+DUCO2(I,K-1)*FFCO2(I,K)
        UDO3 (I,K) = UDO3 (I,K-1)+DUO3 (I,K-1)*FFO3 (I,K)
520   CONTINUE
      DO 530 IP=1,IMAX
        UR   (IP,LP1) = UD   (IP,LP1)
        URCO2(IP,LP1) = UDCO2(IP,LP1)
        URO3 (IP,LP1) = UDO3 (IP,LP1)
530   CONTINUE
      DO 540 K=L,1,-1
      DO 540 IP=1,IMAX
        UR   (IP,K) = UR   (IP,K+1)+DU   (IP,K)*DIFFCTR
        URCO2(IP,K) = URCO2(IP,K+1)+DUCO2(IP,K)*DIFFCTR
        URO3 (IP,K) = URO3 (IP,K+1)+DUO3 (IP,K)*O3DIFCTR
540   CONTINUE
      DO 550 K=2,LL+1
      DO 550 I=1,IMAX
        TCO2(I,K) = ONE - TWO*(H235M3 * EXP(HP26*LOG(UCO2(I,K)+H129M2))
     1                        -H75826M4)
550   CONTINUE
      DO 560 K=2,LL+1
      DO 560 I=1,IMAX
        TO3(I,K) = ONE - TWO*UO3(I,K)*
     1             (H1P082*EXP(HMP805*LOG(ONE+H1386E2*UO3(I,K)))+
     2             H658M2/(ONE+HTEMP*UO3(I,K)*UO3(I,K)*UO3(I,K))+
     3             H2118M2/(ONE+UO3(I,K)*(H42M2+H323M4*UO3(I,K))))
560   CONTINUE
C********************************************************************
C---THE FIRST CLOUD IS THE GROUND; ITS PROPERTIES ARE GIVEN
C   BY REFL (THE TRANSMISSION (0) IS IRRELEVANT FOR NOW!).
C********************************************************************
      DO 570 I=1,IMAX
        CR(I,1) = REFL(I)
570   CONTINUE
C***OBTAIN CLOUD REFLECTION AND TRANSMISSION COEFFICIENTS FOR
C   REMAINING CLOUDS (IF ANY) IN THE VISIBLE BAND
C---THE MAXIMUM NO OF CLOUDS IN THE ROW (KCLDS) IS USED. THIS CREATES
C   EXTRA WORK (MAY BE REMOVED IN A SUBSEQUENT UPDATE).
      DO 580 KK=2,KCLDS+1
      DO 580 I=1,IMAX
CFE93   CR(I,KK) = CUVRF(I,KK)*XAMT(I,KK)
CFE93   CT(I,KK) = ONE-CR(I,KK)
        CR(I,KK) = CRR(I,1,KK)*XAMT(I,KK)
        CT(I,KK) = ONE - (ONE-CTT(I,1,KK))*XAMT(I,KK)
580   CONTINUE
C---OBTAIN THE PRESSURE AT THE TOP,BOTTOM AND THE THICKNESS OF
C   "THICK" CLOUDS (THOSE AT LEAST 2 LAYERS THICK). THIS IS USED
C   LATER IS OBTAINING FLUXES INSIDE THE THICK CLOUDS, FOR ALL
C   FREQUENCY BANDS.
      DO 590 KK=1,KCLDS
      DO 590 I=1,IMAX
CDE93   IF ((KBTMSW(I,KK+1)-1).GT.KTOPSW(I,KK+1)) THEN
        IF ((KBTM(I,KK+1)).GT.KTOP(I,KK+1)) THEN
CDE93      PPTOP(I,KK)=PP(I,KTOPSW(I,KK+1))
           PPTOP(I,KK)=PP(I,KTOP(I,KK+1))
           DPCLD(I,KK)=ONE/(PPTOP(I,KK)-PP(I,KBTMSW(I,KK+1)))
        ENDIF
590   CONTINUE
      DO 600 K=1,L
      DO 600 I=1,IMAX
        TTDB1(I,K+1) = EXP(HM1EZ*MIN(FIFTY,ABCFF(1)*UD(I,K+1)))
        TTUB1(I,K) = EXP(HM1EZ*MIN(FIFTY,ABCFF(1)*UR(I,K)))
        TTD  (I,K+1) = TTDB1(I,K+1)*TDO3(I,K+1)
        TTU  (I,K) = TTUB1(I,K)*TUO3(I,K)
600   CONTINUE
      DO 610 I=1,IMAX
        TTD(I,1)   = ONE
        TTU(I,LP1) = TTD(I,LP1)
610   CONTINUE
C***FOR EXECUTION OF THE CLOUD LOOP, IT IS NECESSARY TO SEPARATE OUT
C   TRANSMISSION FCTNS AT THE TOP AND BOTTOM OF THE CLOUDS, FOR
C   EACH BAND N. THE REQUIRED QUANTITIES ARE:
C      TTD(I,KTOPSW(I,K),N)  K RUNS FROM 1 TO NCLDS(I)+1:
C      TTU(I,KTOPSW(I,K),N)  K RUNS FROM 1 TO NCLDS(I)+1:
C      TTD(I,KBTMSW(I,K),N)  K RUNS FROM 1 TO NCLDS(I)+1:
C      AND INVERSES OF THE FIRST TWO. THE ABOVE QUANTITIES ARE
C      STORED IN TDCL1,TUCL1,TDCL2, AND DFNTRN,UFNTRN, RESPECTIVELY,
C      AS THEY HAVE MULTIPLE USE IN THE PGM.
C---FOR FIRST CLOUD LAYER (GROUND) TDCL1,TUCL1 ARE KNOWN:
      DO 620 I=1,IMAX
        TDCL1 (I,1) = TTD(I,LP1)
        TUCL1 (I,1) = TTU(I,LP1)
        TDCL2 (I,1) = TDCL1(I,1)
        DFNTRN(I,1) = ONE/TDCL1(I,1)
        UFNTRN(I,1) = DFNTRN(I,1)
620   CONTINUE
      DO 630 KK=2,KCLDS+1
      DO 630 I=1,IMAX
CDE93   TDCL1(I,KK) = TTD(I,KTOPSW(I,KK))
CDE93   TUCL1(I,KK) = TTU(I,KTOPSW(I,KK))
        TDCL1(I,KK) = TTD(I,KTOP(I,KK))
        TUCL1(I,KK) = TTU(I,KTOP(I,KK))
        TDCL2(I,KK) = TTD(I,KBTMSW(I,KK))
630   CONTINUE
C---COMPUTE INVERSES
      DO 640 K=1,KCLDS
      DO 640 I=1,IMAX
        DFNTRN(I,K+1) = ONE/TDCL1(I,K+1)
        UFNTRN(I,K+1) = ONE/TUCL1(I,K+1)
640   CONTINUE
C---COMPUTE THE TRANSMISSIVITY FROM THE TOP OF CLOUD (K+1) TO THE
C   TOP OF CLOUD (K). THE CLOUD TRANSMISSION (CT) IS INCLUDED. THIS
C   QUANTITY IS CALLED TCLU (INDEX K). ALSO, OBTAIN THE TRANSMISSIVITY
C   FROM THE BOTTOM OF CLOUD (K+1) TO THE TOP OF CLOUD (K)(A PATH
C   ENTIRELY OUTSIDE CLOUDS). THIS QUANTITY IS CALLED TCLD (INDEX K).
      DO 650 K=1,KCLDS
      DO 650 I=1,IMAX
        TCLU(I,K) = TDCL1(I,K)*DFNTRN(I,K+1)*CT(I,K+1)
        TCLD(I,K) = TDCL1(I,K)/TDCL2(I,K+1)
650   CONTINUE
C***THE FOLLOWING IS THE RECURSION RELATION FOR ALFA: THE REFLECTION
C   COEFFICIENT FOR A SYSTEM INCLUDING THE CLOUD IN QUESTION AND THE
C   FLUX COMING OUT OF THE CLOUD SYSTEM INCLUDING ALL CLOUDS BELOW
C   THE CLOUD IN QUESTION.
C---ALFAU IS ALFA WITHOUT THE REFLECTION OF THE CLOUD IN QUESTION
      DO 660 I=1,IMAX
        ALFA (I,1)=CR(I,1)
        ALFAU(I,1)=ZERO
660   CONTINUE
C---AGAIN,EXCESSIVE CALCULATIONS-MAY BE CHANGED LATER!
      DO 670 KK=2,KCLDS+1
      DO 670 I=1,IMAX
        ALFAU(I,KK)= TCLU(I,KK-1)*TCLU(I,KK-1)*ALFA(I,KK-1)/
     1        (ONE - TCLD(I,KK-1)*TCLD(I,KK-1)*ALFA(I,KK-1)*CR(I,KK))
        ALFA (I,KK)= ALFAU(I,KK)+CR(I,KK)
670   CONTINUE
C     CALCULATE UFN AT CLOUD TOPS AND DFN AT CLOUD BOTTOMS
C---NOTE THAT UFNCLU(I,KCLDS+1) GIVES THE UPWARD FLUX AT THE TOP
C   OF THE HIGHEST REAL CLOUD (IF NCLDS(I)=KCLDS). IT GIVES THE FLUX
C   AT THE TOP OF THE ATMOSPHERE IF NCLDS(I) < KCLDS. IN THE FIRST
C   CASE, TDCL1 EQUALS THE TRANSMISSION FCTN TO THE TOP OF THE
C   HIGHEST CLOUD, AS WE WANT. IN THE SECOND CASE, TDCL1=1, SO UFNCLU
C   EQUALS ALFA. THIS IS ALSO CORRECT.
CKZ   DO 680 I=1,IMAX
      DO 680 KK=KCLDS,1,-1
      DO 680 I=1,IMAX
        IF(KK.GE.NCLDS(I)) THEN
          UFNCLU(I,KK+1) = ALFA(I,KK+1)*TDCL1(I,KK+1)
          DFNCLU(I,KK+1) = TDCL1(I,KK+1)
        END IF
680   CONTINUE
C---THIS CALCULATION IS THE REVERSE OF THE RECURSION RELATION USED
C  ABOVE
CKZ   DO 690 KK=KCLDS,1,-1
      DO 690 KK=KCLDS,2,-1
      DO 690 I=1,IMAX
        IF(KK.LE.NCLDS(I)) THEN
          UFNCLU(I,KK) = UFNCLU(I,KK+1)*ALFAU(I,KK+1)/(ALFA(I,KK+1)*
     1                   TCLU(I,KK))
          DFNCLU(I,KK) = UFNCLU(I,KK)/ALFA(I,KK)
        END IF
690   CONTINUE
CKZ
      DO 695 I=1,IMAX
        UFNCLU(I,1) = UFNCLU(I,2)*ALFAU(I,2)/(ALFA(I,2)*TCLU(I,1))
        DFNCLU(I,1) = UFNCLU(I,1)/ALFA(I,1)
695   CONTINUE
      DO 700 K=1,KCLDS+1
      DO 700 I=1,IMAX
        UFNTRN(I,K) = UFNCLU(I,K)*UFNTRN(I,K)
        DFNTRN(I,K) = DFNCLU(I,K)*DFNTRN(I,K)
700   CONTINUE
C---CASE OF KK=1( FROM THE GROUND TO THE BOTTOM OF THE LOWEST CLOUD)
      MNKBOT = KBTMSW(1,2)
      DO 710 I=2,IMAX
        MNKBOT = MIN(KBTMSW(I,2),MNKBOT)
710   CONTINUE
      DO 720 K=MNKBOT,LP1
        DO 720 I=1,IMAX
          LSAVE0(I,K) = K.GE.KBTMSW(I,2)
          IF(LSAVE0(I,K)) THEN
            UFN(I,K) = UFNTRN(I,1)*TTU(I,K)
            DFN(I,K) = DFNTRN(I,1)*TTD(I,K)
          ENDIF
720   CONTINUE
C     DO 720 I=1,IMAX
C       J2=KBTMSW(I,2)
C       DO 710 K=J2,LP1
C         UFN(I,K) = UFNTRN(I,1)*TTU(I,K)
C         DFN(I,K) = DFNTRN(I,1)*TTD(I,K)
C710    CONTINUE
C720  CONTINUE
C---REMAINING LEVELS (IF ANY!)
      DO 760 KK=2,KCLDS+1
CDE93   MXKTOP(KK) = KTOPSW(1,KK)
        MXKTOP(KK) = KTOP(1,KK)
        MNKBTM(KK+1) = KBTMSW(1,KK+1)
CDE93   MNKTOP(KK) = KTOPSW(1,KK)
        MNKTOP(KK) = KTOP(1,KK)
        MXKBTM(KK) = KBTMSW(1,KK)
        DO 725 I=2,IMAX
CDE93     MXKTOP(KK) = MAX(KTOPSW(I,KK),MXKTOP(KK))
          MXKTOP(KK) = MAX(KTOP(I,KK),MXKTOP(KK))
          MNKBTM(KK+1) = MIN(KBTMSW(I,KK+1),MNKBTM(KK+1))
CDE93     MNKTOP(KK) = MIN(KTOPSW(I,KK),MNKTOP(KK))
          MNKTOP(KK) = MIN(KTOP(I,KK),MNKTOP(KK))
          MXKBTM(KK) = MAX(KBTMSW(I,KK),MXKBTM(KK))
725     CONTINUE
        DO 730 I=1,IMAX
CDE93     LSAVE(I) = KTOPSW(I,KK).GT.1
          LSAVE(I) = KTOP(I,KK).GT.1
730     CONTINUE
        DO 740 K=MNKBTM(KK+1),MXKTOP(KK)
          DO 740 I=1,IMAX
            LSAVE1(I,K,KK) = K.GE.KBTMSW(I,KK+1) .AND.
     1                       K.LE.KTOP(I,KK) .AND. LSAVE(I)
CDE931                       K.LE.KTOPSW(I,KK) .AND. LSAVE(I)
            IF(LSAVE1(I,K,KK)) THEN
              UFN(I,K) = UFNTRN(I,KK)*TTU(I,K)
              DFN(I,K) = DFNTRN(I,KK)*TTD(I,K)
            ENDIF
740     CONTINUE
C---FOR THE THICK CLOUDS, THE FLUX DIVERGENCE THROUGH THE CLOUD
C   LAYER IS ASSUMED TO BE CONSTANT. THE FLUX DERIVATIVE IS GIVEN BY
C   TEMPF (FOR THE UPWARD FLUX) AND TEMPG (FOR THE DOWNWARD FLUX).
        DO 745 I=1,IMAX
          IF ((KBTM(I,KK)).GT.KTOP(I,KK)) THEN
            TEMPF(I) = (UFNCLU(I,KK)-UFN(I,KBTMSW(I,KK)))*DPCLD(I,KK-1)
            TEMPG(I) = (DFNCLU(I,KK)-DFN(I,KBTMSW(I,KK)))*DPCLD(I,KK-1)
          ENDIF
745     CONTINUE
        DO 750 K=MNKTOP(KK)+1,MXKBTM(KK)-1
          DO 750 I=1,IMAX
CDE93       LSAVE2(I,K,KK) = K.GT.KTOPSW(I,KK) .AND.
            LSAVE2(I,K,KK) = K.GT.KTOP(I,KK) .AND.
     1                       K.LT.KBTMSW(I,KK) .AND. LSAVE(I)
            IF(LSAVE2(I,K,KK)) THEN
              UFN(I,K) = UFNCLU(I,KK)+TEMPF(I)*(PP(I,K)-PPTOP(I,KK-1))
              DFN(I,K) = DFNCLU(I,KK)+TEMPG(I)*(PP(I,K)-PPTOP(I,KK-1))
            ENDIF
750     CONTINUE
760   CONTINUE
C     DO 760 KK=2,KCLDS+1
C     DO 755 I=1,IMAX
CDE93   J1=KTOPSW(I,KK)
C       J1=KTOP(I,KK)
C       J2=KBTMSW(I,KK+1)
C       IF (J1.EQ.1) GO TO 755
C       DO 730 K=J2,J1
C         UFN(I,K) = UFNTRN(I,KK)*TTU(I,K)
C         DFN(I,K) = DFNTRN(I,KK)*TTD(I,K)
C730    CONTINUE
C---FOR THE THICK CLOUDS, THE FLUX DIVERGENCE THROUGH THE CLOUD
C   LAYER IS ASSUMED TO BE CONSTANT. THE FLUX DERIVATIVE IS GIVEN BY
C   TEMPF (FOR THE UPWARD FLUX) AND TEMPG (FOR THE DOWNWARD FLUX).
C       J3=KBTMSW(I,KK)
C       IF ((J3-J1).GT.1) THEN
C         TEMPF = (UFNCLU(I,KK)-UFN(I,J3))*DPCLD(I,KK-1)
C         TEMPG = (DFNCLU(I,KK)-DFN(I,J3))*DPCLD(I,KK-1)
C         DO 740 K=J1+1,J3-1
C           UFN(I,K) = UFNCLU(I,KK)+TEMPF*(PP(I,K)-PPTOP(I,KK-1))
C           DFN(I,K) = DFNCLU(I,KK)+TEMPG*(PP(I,K)-PPTOP(I,KK-1))
C740      CONTINUE
C       ENDIF
C755  CONTINUE
C760  CONTINUE
      DO 770 K=1,LP1
      DO 770 I=1,IMAX
        DFSWC(I,K) = DFN(I,K)*DFNTOP(I,1)
        UFSWC(I,K) = UFN(I,K)*DFNTOP(I,1)
770   CONTINUE
      DO 780 I=1,IMAX
        TEMP1(I) = ONE - CCMAX(I)
        GDFVB(I) = TEMP1(I)*GDFVB(I)
        GDFNB(I) = TEMP1(I)*GDFNB(I)
        GDFVD(I) = TEMP1(I)*GDFVD(I) + CCMAX(I)*DFSWC(I,LP1)
780   CONTINUE
C---NOW OBTAIN FLUXES FOR THE NEAR IR BANDS. THE METHODS ARE THE SAME
C   AS FOR THE VISIBLE BAND, EXCEPT THAT THE REFLECTION AND
C   TRANSMISSION COEFFICIENTS ARE DIFFERENT, AS
C   RAYLEIGH SCATTERING NEED NOT BE CONSIDERED.
CFE93 DO 790 I=1,IMAX*(KCLDS+1)
CFE93   CR(I,1) = CIRRF(I,1)*XAMT(I,1)
CFE93   CT(I,1) = ONE-XAMT(I,1)*(CIRRF(I,1)+CIRAB(I,1))
C90   CONTINUE
C
      DO 1000 N=2,NB
CFE93
        DO 790 K=1,KCLDS+1
        DO 790 I=1,IMAX
          CR(I,K) = CRR(I,N,K)*XAMT(I,K)
          CT(I,K) = ONE - (ONE-CTT(I,N,K))*XAMT(I,K)
790     CONTINUE
CFE93
        IF (N.EQ.2) THEN
C   THE WATER VAPOR TRANSMISSION FUNCTION FOR BAND 2 IS EQUAL TO
C   THAT OF BAND 1 (SAVED AS TTDB1,TTUB1)
          DO 800 K=1,L
          DO 800 I=1,IMAX
            TTD(I,K+1) = TTDB1(I,K+1)*TDCO2(I,K+1)
            TTU(I,K) = TTUB1(I,K)*TUCO2(I,K)
800       CONTINUE
        ELSE
          DO 810 K=1,L
          DO 810 I=1,IMAX
            TTD(I,K+1) = EXP(HM1EZ*MIN(FIFTY,ABCFF(N)*UD(I,K+1)))
     1               * TDCO2(I,K+1)
            TTU(I,K) = EXP(HM1EZ*MIN(FIFTY,ABCFF(N)*UR(I,K)))
     1               * TUCO2(I,K)
810       CONTINUE
        ENDIF
C---AT THIS POINT,INCLUDE TTD(1),TTU(LP1), NOTING THAT TTD(1)=1 FOR
C   ALL BANDS, AND THAT TTU(LP1)=TTD(LP1) FOR ALL BANDS.
        DO 820 I=1,IMAX
          TTU(I,LP1) = TTD(I,LP1)
          TTD(I,1)   = ONE
820     CONTINUE
C***FOR EXECUTION OF THE CLOUD LOOP, IT IS NECESSARY TO SEPARATE OUT
C   TRANSMISSION FCTNS AT THE TOP AND BOTTOM OF THE CLOUDS, FOR
C   EACH BAND N. THE REQUIRED QUANTITIES ARE:
C      TTD(I,KTOPSW(I,K),N)  K RUNS FROM 1 TO NCLDS(I)+1:
C      TTD(I,KBTMSW(I,K),N)  K RUNS FROM 2 TO NCLDS(I)+1:
C      TTU(I,KTOPSW(I,K),N)  K RUNS FROM 1 TO NCLDS(I)+1:
C      AND INVERSES OF THE ABOVE. THE ABOVE QUANTITIES ARE STORED
C      IN TDCL1,TDCL2,TUCL1,AND DFNTRN,UFNTRN,RESPECTIVELY, AS
C      THEY HAVE MULTIPLE USE IN THE PGM.
C---FOR FIRST CLOUD LAYER (GROUND) TDCL1,TUCL1 ARE KNOWN:
        DO 830 I=1,IMAX
          TDCL1 (I,1) = TTD(I,LP1)
          TUCL1 (I,1) = TTU(I,LP1)
          TDCL2 (I,1) = TDCL1(I,1)
          DFNTRN(I,1) = ONE/TDCL1(I,1)
          UFNTRN(I,1) = DFNTRN(I,1)
830     CONTINUE
        DO 840 KK=2,KCLDS+1
        DO 840 I=1,IMAX
CDE93     TDCL1(I,KK) = TTD(I,KTOPSW(I,KK))
CDE93     TUCL1(I,KK) = TTU(I,KTOPSW(I,KK))
          TDCL1(I,KK) = TTD(I,KTOP(I,KK))
          TUCL1(I,KK) = TTU(I,KTOP(I,KK))
          TDCL2(I,KK) = TTD(I,KBTMSW(I,KK))
840     CONTINUE
        DO 850 K=1,KCLDS
        DO 850 I=1,IMAX
          DFNTRN(I,K+1) = ONE/TDCL1(I,K+1)
          UFNTRN(I,K+1) = ONE/TUCL1(I,K+1)
850     CONTINUE
        DO 860 K=1,KCLDS
        DO 860 I=1,IMAX
          TCLU(I,K) = TDCL1(I,K)*DFNTRN(I,K+1)*CT(I,K+1)
          TCLD(I,K) = TDCL1(I,K)/TDCL2(I,K+1)
860     CONTINUE
C***THE FOLLOWING IS THE RECURSION RELATION FOR ALFA: THE REFLECTION
C   COEFFICIENT FOR A SYSTEM INCLUDING THE CLOUD IN QUESTION AND THE
C   FLUX COMING OUT OF THE CLOUD SYSTEM INCLUDING ALL CLOUDS BELOW
C   THE CLOUD IN QUESTION.
        DO 870 I=1,IMAX
          ALFA (I,1) = CR(I,1)
          ALFAU(I,1) = ZERO
870     CONTINUE
C---AGAIN,EXCESSIVE CALCULATIONS-MAY BE CHANGED LATER!
        DO 880 KK=2,KCLDS+1
        DO 880 I=1,IMAX
          ALFAU(I,KK) = TCLU(I,KK-1)*TCLU(I,KK-1)*ALFA(I,KK-1)/(ONE -
     1             TCLD(I,KK-1)*TCLD(I,KK-1)*ALFA(I,KK-1)*CR(I,KK))
          ALFA (I,KK) = ALFAU(I,KK)+CR(I,KK)
880     CONTINUE
C     CALCULATE UFN AT CLOUD TOPS AND DFN AT CLOUD BOTTOMS
C---NOTE THAT UFNCLU(I,KCLDS+1) GIVES THE UPWARD FLUX AT THE TOP
C   OF THE HIGHEST REAL CLOUD (IF NCLDS(I)=KCLDS). IT GIVES THE FLUX
C   AT THE TOP OF THE ATMOSPHERE IF NCLDS(I) < KCLDS. IT THE FIRST
C   CASE, TDCL1 EQUALS THE TRANSMISSION FCTN TO THE TOP OF THE
C   HIGHEST CLOUD, AS WE WANT. IN THE SECOND CASE, TDCL1=1, SO UFNCLU
C   EQUALS ALFA. THIS IS ALSO CORRECT.
CKZ     DO 890 I=1,IMAX
        DO 890 KK=KCLDS,1,-1
        DO 890 I=1,IMAX
          IF(KK.GE.NCLDS(I)) THEN
            UFNCLU(I,KK+1) = ALFA(I,KK+1)*TDCL1(I,KK+1)
            DFNCLU(I,KK+1) = TDCL1(I,KK+1)
          END IF
890     CONTINUE
CKZ     DO 900 KK=KCLDS,1,-1
        DO 900 KK=KCLDS,2,-1
        DO 900 I=1,IMAX
          IF(KK.LE.NCLDS(I)) THEN
            UFNCLU(I,KK) = UFNCLU(I,KK+1)*ALFAU(I,KK+1)/(ALFA(I,KK+1)*
     1                     TCLU(I,KK))
            DFNCLU(I,KK) = UFNCLU(I,KK)/ALFA(I,KK)
          END IF
900     CONTINUE
CKZ
        DO 905 I=1,IMAX
          UFNCLU(I,1) = UFNCLU(I,2)*ALFAU(I,2)/(ALFA(I,2)*TCLU(I,1))
          DFNCLU(I,1) = UFNCLU(I,1)/ALFA(I,1)
905     CONTINUE
C     NOW OBTAIN DFN AND UFN FOR LEVELS BETWEEN THE CLOUDS
C
C     NOTE THAT MNKBOT, MNKBTM, MXKTOP, MNKTOP, MXKBTM, LSAVE1, AND
C     LSAVE2 WERE ALL SAVED WHEN CALCULATED FOR BAND NO. 1 !
C     THEY DO NOT CHANGE HERE FOR THE CALCULATION OF BANDS 2-12 !
C
        DO 910 K=1,KCLDS+1
        DO 910 I=1,IMAX
          UFNTRN(I,K) = UFNCLU(I,K)*UFNTRN(I,K)
          DFNTRN(I,K) = DFNCLU(I,K)*DFNTRN(I,K)
910     CONTINUE
        DO 930 K=MNKBOT,LP1
          DO 930 I=1,IMAX
            IF(LSAVE0(I,K)) THEN
              UFN(I,K) = UFNTRN(I,1)*TTU(I,K)
              DFN(I,K) = DFNTRN(I,1)*TTD(I,K)
            ENDIF
930     CONTINUE
C       DO 930 I=1,IMAX
C         J2 = KBTMSW(I,2)
C         DO 920 K=J2,LP1
C           UFN(I,K) = UFNTRN(I,1)*TTU(I,K)
C           DFN(I,K) = DFNTRN(I,1)*TTD(I,K)
C920      CONTINUE
C930    CONTINUE
        DO 970 KK=2,KCLDS+1
          DO 940 K=MNKBTM(KK+1),MXKTOP(KK)
            DO 940 I=1,IMAX
              IF(LSAVE1(I,K,KK)) THEN
                UFN(I,K) = UFNTRN(I,KK)*TTU(I,K)
                DFN(I,K) = DFNTRN(I,KK)*TTD(I,K)
              ENDIF
940       CONTINUE
          DO 950 I=1,IMAX
            IF ((KBTM(I,KK)).GT.KTOP(I,KK)) THEN
              TEMPF(I)=(UFNCLU(I,KK)-UFN(I,KBTMSW(I,KK)))*DPCLD(I,KK-1)
              TEMPG(I)=(DFNCLU(I,KK)-DFN(I,KBTMSW(I,KK)))*DPCLD(I,KK-1)
            ENDIF
950       CONTINUE
          DO 960 K=MNKTOP(KK)+1,MXKBTM(KK)-1
            DO 960 I=1,IMAX
              IF(LSAVE2(I,K,KK)) THEN
                UFN(I,K) = UFNCLU(I,KK)+TEMPF(I)*(PP(I,K)-PPTOP(I,KK-1))
                DFN(I,K) = DFNCLU(I,KK)+TEMPG(I)*(PP(I,K)-PPTOP(I,KK-1))
              ENDIF
960       CONTINUE
970     CONTINUE
C       DO 970 KK=2,KCLDS+1
C       DO 965 I=1,IMAX
CDE93     J1 = KTOPSW(I,KK)
C         J1 = KTOP(I,KK)
C         J2 = KBTMSW(I,KK+1)
C         IF (J1.EQ.1) GO TO 965
C         DO 940 K=J2,J1
C           UFN(I,K) = UFNTRN(I,KK)*TTU(I,K)
C           DFN(I,K) = DFNTRN(I,KK)*TTD(I,K)
C940      CONTINUE
C         J3 = KBTMSW(I,KK)
C         IF ((J3-J1).GT.1) THEN
C           TEMPF = (UFNCLU(I,KK)-UFN(I,J3))*DPCLD(I,KK-1)
C           TEMPG = (DFNCLU(I,KK)-DFN(I,J3))*DPCLD(I,KK-1)
C           DO 950 K=J1+1,J3-1
C             UFN(I,K) = UFNCLU(I,KK)+TEMPF*(PP(I,K)-PPTOP(I,KK-1))
C             DFN(I,K) = DFNCLU(I,KK)+TEMPG*(PP(I,K)-PPTOP(I,KK-1))
C950        CONTINUE
C         ENDIF
C965    CONTINUE
C970    CONTINUE
        DO 980 K=1,LP1
        DO 980 I=1,IMAX
          DFSWC(I,K) = DFSWC(I,K) + DFN(I,K)*DFNTOP(I,N)
          UFSWC(I,K) = UFSWC(I,K) + UFN(I,K)*DFNTOP(I,N)
980     CONTINUE
        DO 990 I=1,IMAX
          GDFND(I) = GDFND(I) + CCMAX(I)*DFN(I,LP1)*DFNTOP(I,N)
990     CONTINUE
1000  CONTINUE
      DO 1100 K=1,LP1
      DO 1100 I=1,IMAX
        DFSWC(I,K) = TEMP1(I)*DFSWL(I,K) + CCMAX(I)*DFSWC(I,K)
        UFSWC(I,K) = TEMP1(I)*UFSWL(I,K) + CCMAX(I)*UFSWC(I,K)
1100  CONTINUE
      DO 1200 K=1,LP1
      DO 1200 I=1,IMAX
        FSWC(I,K) = UFSWC(I,K)-DFSWC(I,K)
1200  CONTINUE
      DO 1250 K=1,L
      DO 1250 I=1,IMAX
        HSWC(I,K) = RADCON*(FSWC(I,K+1)-FSWC(I,K))/DP(I,K)
1250  CONTINUE
      RETURN
      END
