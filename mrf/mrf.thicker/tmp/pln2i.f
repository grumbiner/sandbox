CFPP$ NOCONCUR R
      SUBROUTINE PLN2I(QLNT,QLNV,COLRAD,LAT)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    PLN2T       EVALUATES ASSOCIATED LEGENDRE FUNCTIONS.
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 88-10-25
C
C ABSTRACT: EVALUATES THE REQUIRED VALUES OF THE NORMALIZED
C   ASSOCIATED LEGENDRE FUNCTION AT A PRESCRIBED COLATITUDE.
C   A STANDARD RECURSION RELATION IS USED WITH REAL ARITHMETIC.
C
C PROGRAM HISTORY LOG:
C   88-10-25  JOSEPH SELA
C
C USAGE:    CALL PLN2T (QLNT, QLNV, COLRAD, LAT)
C   INPUT ARGUMENT LIST:
C     COLRAD   - HALF PRECISION COLATITUDES IN RADIANS FOR WHICH
C                THE ASSOCIATED LEGENDRE FUNCTIONS ARE TO BE
C                COMPUTED.
C     LAT      - INDEX WHICH INDICATES THE CURRENT LATITUDE.
C
C   OUTPUT ARGUMENT LIST:
C     QLNT     - DOUBLED SCALAR TRIANGLE OF
C                HALF PRECISION ASSOCIATED LEGENDRE FUNCTIONS.
C     QLNV     - DOUBLED VECTOR TRIANGLE OF
C                HALF PRECISION ASSOCIATED LEGENDRE FUNCTIONS.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 200.
C   MACHINE:  CYBER 205.
C
C$$$
CC
      DIMENSION         QLNT( 4032 )
      DIMENSION         QLNV( 4158 )
      DIMENSION       COLRAD( 47 )
CC
      DIMENSION            X( 63 )
      DIMENSION         DPLN( 4158 )
CC
      COMMON /PLN2TI/ DEPS( 4158 ),RDEPS( 4158 ),
     1                DX( 126 ),Y( 63 ),INDXMV( 4158 )
CC
CCC   DATA IFIR /0/
CCC         PART BETWEEN GUARDS MADE INTO SR GPLN2I.
CCC         7 DEC 1990      M. ROZWODOSKI
CC
      COLR   = COLRAD(LAT)
      SINLAT = COS(COLR)
      COS2   = 1.0 - SINLAT * SINLAT
      PROD   = 1.0
      DO 600 LL=1, 63
             X(LL) = 0.5*PROD
CCCC         IF (PROD .LT. 1.0E-75)  PROD=0.0
             PROD = PROD*COS2*Y(LL)
  600 CONTINUE
      DO 620 LL=1, 63
             X(LL) = SQRT(X(LL))
  620 CONTINUE
      DO 640 LL=1, 63
             DPLN(2*LL-1) = X(LL)
             DPLN(2*LL  ) = X(LL)
  640 CONTINUE
      LPLUS =  126
      DO 700 LL=1, 126
             DPLN(LL+LPLUS) = DX(LL) * SINLAT * DPLN(LL)
  700 CONTINUE
      LP2 = 0
      LP1 =      126
      LP0 = 2 *  126
      LEN =      126  - 2
      DO 740  N=3, 64
CDIR$ IVDEP
      DO 720 LL=1,LEN
             DPLN(LL+LP0) = (SINLAT * DPLN(LL+LP1)
     1               - DEPS(LL+LP1) * DPLN(LL+LP2)) * RDEPS(LL+LP0)
  720 CONTINUE
      LP2 = LP1
      LP1 = LP0
      LP0 = LP0 + LEN
      LEN = LEN - 2
  740 CONTINUE
CC
CC    TRANSPOSE VECTOR DPLN ARRAY FROM CRAY ORDER TO IBM ORDER.
      DO 800 I=1, 4158
             QLNV(INDXMV(I)) = DPLN(I)
  800 CONTINUE
CC
      LPV = 0
      LPT = 0
      LEN =  126
      DO 860  N=1, 63
      DO 840 LL=1,LEN
             QLNT(LL+LPT) = QLNV(LL+LPV)
  840 CONTINUE
      LPV = LPV + LEN + 2
      LPT = LPT + LEN
      LEN = LEN - 2
  860 CONTINUE
CC
      RETURN
      END
