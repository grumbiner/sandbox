      SUBROUTINE GGOZRM(EPSI)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GGOZRM      SETS COMMON FOR SUBROUTINE GOZRMT.
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 91-03-14
C
C ABSTRACT: INITIALIZES THE CONSTANT VARIABLES AND ARRAYS
C   OF A COMMON FOR SUBROUTINE GOZRMT.
C
C PROGRAM HISTORY LOG:
C   91-03-14  JOSEPH SELA
C
C USAGE:    CALL GGOZRM ( EPS )
C
C REMARKS: CALL SUBROUTINE ONCE BEFORE CALLS TO GOZRMT.
C          REFER TO GOZRMT FOR ADDITIONAL DOCUMENTATION.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN, CFT77.
C   MACHINE:  CRAY Y-MP.
C
C$$$
CC
      DIMENSION          EPSI( 64 , 63 )
CC
      DIMENSION         DXINT( 4032 )
      DIMENSION            DX( 126 , 64 )
      DIMENSION          DEPS( 126 , 64 )
CC
      COMMON /GOZCOM/ DXA( 4032 ),DXB( 4032 )
CC
      DO 200 LL=1, 126
             DXINT(2*LL-1) = LL
             DXINT(2*LL  ) = LL
  200 CONTINUE
      LP = 0
      DO 240  I=1, 64
      DO 220 LL=1, 126
             DX(LL,I) = DXINT(LL+LP)
  220 CONTINUE
      LP = LP + 2
  240 CONTINUE
      DO 280  I=1, 64
      DO 260 LL=1, 63
             DEPS(2*LL-1,I) = EPSI(I,LL)
             DEPS(2*LL  ,I) = EPSI(I,LL)
  260 CONTINUE
  280 CONTINUE
      DO 300 LL=1, 126
             DXA(LL) = 0.0
  300 CONTINUE
      LP1 =  126
      LEN =  126  - 2
      DO 340  I=1, 62
      DO 320 LL=1,LEN
             DXA(LL+LP1) =  DX(LL,I+1) * DEPS(LL,I+1)
             DXB(LL+LP1) = -DX(LL,I  ) * DEPS(LL,I+2)
  320 CONTINUE
      LP1 = LP1 + LEN
      LEN = LEN - 2
  340 CONTINUE
      DO 380  I=1, 64
      DO 360 LL=1, 126
             DX(LL,I) = DX(LL,I) - 1. E 0
  360 CONTINUE
  380 CONTINUE
      DO 400 LL=1, 126
             DXB(LL) = -DX(LL,1) * DEPS(LL,2)
  400 CONTINUE
CC
CC    TRANSPOSE SCALAR ARRAYS DXA, DXB, DXC, DXD
CC    FROM CRAY ORDER TO IBM ORDER.
      CALL TRANSO (DXA, 2)
CC
      RETURN
      END
