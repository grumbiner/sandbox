      SUBROUTINE KENPRE(CON,COLRAD,IDIM,JDIM2,NFLX)
C....
C  THIS ROUTINE COMPUTES THE KENDATA GRID POINT INDICES IGRD,JGRD FOR
C   FCST GRID AND IGRDR,JGRDR FOR RADIATION GRID, FOR THE
C   NPOINT POINTS,GIVEN THE LONGITUDE AND LATITUDE OF SAME (CON).
C   COLRAD IS THE COLATITUDE OF THE FCST GRID (DIMENSIONED JDIM2,WHICH
C   IS HALF OF THE TOTAL LATITUDINAL GRID POINTS),
C....
      PARAMETER(NVRKEN= 80 + 9 * 28 ,NPTKEN= 50 )
      PARAMETER(NSTKEN= 24 )
      COMMON/COMGPD/ SVDATA(NVRKEN,NPTKEN,NSTKEN),
     1               IGRD(NPTKEN),JGRD(NPTKEN),
     2               IGRDR(NPTKEN),JGRDR(NPTKEN),
     3               ITNUM,NPOINT,ISAVE,ISSHRT,ILSHRT,IKFREQ
      DIMENSION COLRAD(JDIM2),CON(1700)
      DIMENSION ALAT(NPTKEN),ALON(NPTKEN),KPOI(NPTKEN)
      DIMENSION SLIMSK( 192 , 94 ),BLATF( 94 ),BLATR( 62 )
      DIMENSION COLRAR( 31 ),WGR( 31 ),WGRCS( 31 ),RRS2( 31 )
      DATA PI/ 3.141593E+0 /
C....   BEGIN HERE..
      DO 2 K=1,NSTKEN
       DO 2 J=1,NPTKEN
        DO 2 I=1,NVRKEN
         SVDATA(I,J,K) = 0.
    2 CONTINUE
C--- GET SLMSK BECAUSE WE WISH TO GET NEAREST POINT
C       OF SAME SFC TYPE...THIS ARRAY NOT AVAILABLE TIL AFTER STEP1
C       IF WE WERE NOT TRYING TO COVER OURSELVES FOR OUT-BOARD RADI8
C       ,THEN THIS CODE COULD BE CALLED FROM STEP1...
      REWIND NFLX
      READ(NFLX)
      READ(NFLX) GHOUR,ID1,ID2,ID3,ID4
   99 FORMAT(1H ,'FHOUR, IDATE=',F6.2,2X,4(1X,I4))
      PRINT *,'IN KENPRE READ SLMSK FROM UNIT=',NFLX
      PRINT 99,GHOUR, ID1,ID2,ID3,ID4
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
C.....  SKIP CV, CVB, CVT, ALBEDO
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
      READ(NFLX)
      READ(NFLX) SLIMSK
      REWIND NFLX
CCCC           CALL ROW1NS(SLIMSK)
      CALL GLATS ( 31 , COLRAR, WGR, WGRCS, RRS2)
      DXF = 360. /  192
      DXR = 360. /  128
      ILONF =  192
      ILONR =  128
      JLATG2 =  47
      JLATR2 =  31
      JLATG =  94
      JLATR =  62
      JFP1 = JLATG2 + 1
      JRP1 = JLATR2 + 1
C----    GET LATITUDE OF GAUSSIAN GRIDS
      DO 3 J=1,JLATG2
        BLATF(J) =(PI /2. - COLRAD(J)) * 180. / PI
    3 CONTINUE
      BLATF(JFP1) = -BLATF(JLATG2)
      DO 4 J=1,JLATR2
        BLATR(J) =(PI /2. - COLRAR(J)) * 180. / PI
    4 CONTINUE
      BLATR(JRP1) = -BLATR(JLATR2)
C...    PUT LAT/LON INTO USEABLE ARRAYS (MAX=200),WHERE
C         NPOINT GT 0 IMPLIES NPOINT LAT/LON S IN CON AND
C           IF ABS(LAT) BETWEEN   0, 90 LOOK FOR NEAREST POINT
C                       BETWEEN 100,190 LOOK FOR NEAREST LAND POINT
C                       BETWEEN 200,290 LOOK FOR NEAREST SEA POINT
C         NPOINT LT 0 IMPLIES LAT/LON OF CENTER OF REGION ,ONLY..
C                    LAT,LON=CON(1301),CON(1501)
C           LET XY=ABS(NPOINT) AND ALWAYS BE 2 DIGITS
C                              AND DO NOT DIFFERENTIATE LAND/SEA,
C             THEN X BETWEEN 1,9 MEANS CREATE ARRAY OF EVERY X POINTS
C                   (I.E. X=1 MEANS EVERY POINT,X=3 MEANS EVERY 3RD,..
C              AND Y BETWEEN 0,9 MEANS CREATE (Y+1,Y+1) ARRAY..
C           THUS XY CAN HAVE VALUES 10-99
C....
      NPUTE = -1
      IF (NPOINT.LT.0) THEN
       XY = ABS(NPOINT)
       IF (XY.LT.10..OR.XY.GT.99.) THEN
        NPUTE = 0
        PRINT 98,NPOINT
   98   FORMAT(1H ,' NUM(1300)=',I6,'OUT OF -RANGE, SO SET=1')
        NPOINT = 1
       ELSE
        NPOINT = 1
        ISKP = XY/10
        IY   = XY - ISKP*10 + 1
        PRINT 97,IY,IY,ISKP
   97   FORMAT(1H ,' PREPARE REGIONAL (',I2,',',I2,') ARRAY - EVERY',
     1             I2,' POINTS')
        NPUTE = IY * IY
       END IF
      END IF
      DO 5 K = 1, NPOINT
       ILS = -1
       YLAT = ABS(CON(K+1300))
       IF (YLAT.GE.100.AND.YLAT.LE.190.) THEN
C...     LAND POINT IS DESIRED...
        ILS = 1
        SGN = CON(K+1300) / YLAT
        CON(K+1300) = YLAT-100.
        IF (SGN.LT.0.) CON(K+1300) = - (YLAT-100.)
       END IF
       IF (YLAT.GE.200.AND.YLAT.LE.290.) THEN
        ILS = 0
        SGN = CON(K+1300) / YLAT
        CON(K+1300) = YLAT-200.
        IF (SGN.LT.0.) CON(K+1300) = - (YLAT-200.)
       END IF
       XLAT = CON(K+1300)
       XLON = CON(K+1500)
       IF (NPUTE.LT.0.AND.ILS.EQ.-1) PRINT 197,K,XLAT,XLON
       IF (NPUTE.LT.0.AND.ILS.EQ.0) PRINT 198,K,XLAT,XLON
       IF (NPUTE.LT.0.AND.ILS.EQ.1) PRINT 199,K,XLAT,XLON
  197  FORMAT(1H ,' ==== STATION ',I4,' AT LATLON=',2F8.2,
     1            ' DESIRED AS NEAREST POINT')
  198  FORMAT(1H ,' ==== STATION ',I4,' AT LATLON=',2F8.2,
     1            ' DESIRED AS OCEAN PT')
  199  FORMAT(1H ,' ==== STATION ',I4,' AT LATLON=',2F8.2,
     1            ' DESIRED AS LAND PT')
       IF (NPUTE.GT.0.AND.K.GT.1) GO TO 195
        ALAT(K) = CON(K+1300)
        ALON(K) = CON(K+1500)
  195  CONTINUE
       IF (XLON.LT.0) XLON = 360. + CON(K+1500)
       IF (NPUTE.LT.0) THEN
         CALL GETIJ (XLAT,XLON,SLIMSK,BLATF,DXF,
     1               ILS,ILONF,JLATG,KI,KJ)
         ILS = -1
         CALL GETIJ (XLAT,XLON,SLIMSK,BLATR,DXR,
     1               ILS,ILONR,JLATR,KIR,KJR)
       ELSE
         ILS = -1
         CALL GETIJ (XLAT,XLON,SLIMSK,BLATF,DXF,
     1               ILS,ILONF,JLATG,KI,KJ)
         ILS = -1
         CALL GETIJ (XLAT,XLON,SLIMSK,BLATR,DXR,
     1               ILS,ILONR,JLATR,KIR,KJR)
       END IF
       IGRD(K) = KI
       JGRD(K) = KJ
       IGRDR(K) = KIR
       JGRDR(K) = KJR
       IF(NPUTE.GT.0) GO TO 5
        IF(XLAT.LT.0.) THEN
         IGRD(K) = KI + ILONF
         JGRD(K) = JLATG + 1 - KJ
         IGRDR(K) = KIR + ILONR
         JGRDR(K) = JLATR + 1 - KJR
        ENDIF
    5 CONTINUE
C....    REGIONAL BLOCK , I,J STILL IN SINGLE LATITUDE STRUCTURE..
      IF (NPUTE.GT.0) THEN
       IBACK = IY/2
C....    IF IY = 1 THE ALL WE WANT IS 1 POINT
       IF (IBACK.LE.0) THEN
         NPOINT = 1
         GO TO 59
       END IF
       ISTARF = IGRD(1) - IBACK*ISKP
       JSTARF = JGRD(1) - IBACK*ISKP
       ISTARR = IGRDR(1) - IBACK*ISKP
       JSTARR = JGRDR(1) - IBACK*ISKP
       NPOINT = 0
       DO 30 KYJ=1,IY
        DO 30 KXI=1,IY
         NPOINT = NPOINT + 1
         IGRD(NPOINT) = ISTARF + (KXI-1)*ISKP
         JGRD(NPOINT) = JSTARF + (KYJ-1)*ISKP
         IGRDR(NPOINT) = ISTARR + (KXI-1)*ISKP
         JGRDR(NPOINT) = JSTARR + (KYJ-1)*ISKP
   30  CONTINUE
       DO 32 N=1,NPOINT
         KPOI(N) = 0
         IF (JGRD(N).GT.JLATG.OR.JGRD(N).LT.1) GO TO 32
         IF (JGRDR(N).GT.JLATR.OR.JGRDR(N).LT.1) GO TO 32
         IF (IGRD(N).GT.ILONF) IGRD(N) = IGRD(N) - ILONF
         IF (IGRD(N).LT.1) IGRD(N) = IGRD(N) + ILONF
         IF (IGRDR(N).GT.ILONR) IGRDR(N) = IGRDR(N) - ILONR
         IF (IGRDR(N).LT.1) IGRDR(N) = IGRDR(N) + ILONR
         KPOI(N) = N
   32  CONTINUE
C...    SQUEEZE OUT THE OUT OF BOUNDS POINTS(KPOI=0)
       NPP = 0
       DO 33 N=1,NPOINT
        IF (KPOI(N).LE.0) GO TO 33
         NPP = NPP + 1
         IGRD(NPP) = IGRD(KPOI(N))
         IGRDR(NPP) = IGRDR(KPOI(N))
         JGRD(NPP) = JGRD(KPOI(N))
         JGRDR(NPP) = JGRDR(KPOI(N))
         IF (JGRD(NPP).GT.JLATG2) THEN
          IGRD(NPP) = IGRD(NPP) + ILONF
          JGRD(NPP) = JLATG+1-JGRD(NPP)
         END IF
         IF (JGRDR(NPP).GT.JLATR2) THEN
          IGRDR(NPP) = IGRDR(NPP) + ILONR
          JGRDR(NPP) = JLATR+1-JGRDR(NPP)
         END IF
   33  CONTINUE
       NPOINT = NPP
      END IF
C...................  DEBUG PRINT
   59 CONTINUE
      DO 60 K=1,NPOINT
       IG=IGRD(K)
       JG=JGRD(K)
       ICLND=IG
       JCLND=JG
       IF(IGRD(K).LE.ILONF) THEN
        BLAT=90.-COLRAD(JGRD(K))*180./PI
        BLON=(IGRD(K)-1)*360./ILONF
        IF(BLON.GT.180.) BLON=BLON-360.
       ELSE
        BLAT=COLRAD(JGRD(K))*180./PI-90.
        BLON=(IGRD(K)-1-ILONF)*360./ILONF
        IF(BLON.GT.180.) BLON=BLON-360.
        ICLND=IG-ILONF
        JCLND=JLATG+1-JG
       ENDIF
       WRITE(6,61) K,ALAT(K),ALON(K),BLAT,BLON
       WRITE(6,62) JGRD(K),IGRD(K),SLIMSK(ICLND,JCLND)
       WRITE(6,63) JGRDR(K),IGRDR(K)
60    CONTINUE
61    FORMAT(' KENPRE: K,ORIG LAT-LON,COMPT LAT-LON=',I4,4F8.2)
62    FORMAT('          ....JGRD,IGRD,SLMSK=',2I6,F6.1)
63    FORMAT('             ....JGRDR,  IGRDR    =',2I8)
      RETURN
      END
