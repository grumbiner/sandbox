      SUBROUTINE AMBMSV(KM,SI,SL,TOV,AM,BM,SV,GV,CM)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    AMBMSV      COMPUTES CONSTANT MATRICES.
C   PRGMMR: JOSEPH SELA      ORG: W/NMC23    DATE: 88-04-06
C
C ABSTRACT: COMPUTES THE 3 MATRICES AM AND BM AND SV.
C   THE MATRICES REPRESENT THE LINEARIZED GRAVITY WAVE TERMS
C   OF THE EQUATIONS INVOLVED IN THE SEMI-IMPLICIT TIME INTEGRATION.
C   THE MATRICES ARE DEPENDENT ONLY ON THE VERTICAL STRUCTURE.
C   AM IS THE DIVERGENCE EQUATION'S LINEAR DEPENDENCE ON TEMPERATURE.
C   BM IS THE TEMPERATURE EQUATION'S LINEAR DEPENDENCE ON DIVERGENCE.
C   SV IS THE CONTINUITY EQUATION'S LINEAR DEPENDENCE ON DIVERGENCE.
C
C PROGRAM HISTORY LOG:
C   88-04-06  JOSEPH SELA
C   93-02-23  MARK IREDELL   COMPACT VERTICAL FORMULATION
C
C USAGE:    CALL AMBMSV(KM,SL,TOV,AM,BM,SV)
C   INPUT ARGUMENT LIST:
C     KM       - INTEGER NUMBER OF VERTICAL LEVELS.
C     SL       - REAL (KM) SIGMA LEVEL VALUES.
C     TOV      - REAL (KM) REFERENCE TEMPERATURES.
C
C   OUTPUT ARGUMENT LIST:
C     AM       - REAL (KM,KM) SUCH THAT DD(K)/DT = ... + AM(K,J)*T(J)
C     BM       - REAL (KM,KM) SUCH THAT DT(K)/DT = ... + BM(K,J)*D(J)
C     SV       - REAL (KM) SUCH THAT DQ/DT = ... + SV(J)*D(J)
C
C ATTRIBUTES:
C   LANGUAGE: CRAY FORTRAN.
C
C$$$
      DIMENSION SI(KM+1),SL(KM),TOV(KM)
      DIMENSION AM(KM,KM),BM(KM,KM),SV(KM),GV(KM),CM(KM,KM)
      PARAMETER(RD= 2.8705E+2 ,CP= 1.0046E+3 ,RERTH= 6.3712E+6 )
      PARAMETER(ROCP=RD/CP,RAA=RD/(RERTH**2))
      DIMENSION CD(KM,KM+1),CI(KM+1,KM),CQ(KM+1,KM),CQL(KM,KM)
      DIMENSION RNU(KM),RMU(KM),TI(2:KM),DT(KM+1,KM)
      CALL BNMC(KM,SI,SL,CD,CI,CQ,CQL)
      DO 10 J=1,KM
        SV(J)=CQ(KM+1,J)
        GV(J)=RAA*TOV(J)
        DT(1,J)=0.
        DT(KM+1,J)=0.
10    CONTINUE
      DO 20 K=1,KM-1
        TI(K+1)=0.
        DEL=SI(K+1)-SI(K)
        RSL=LOG(SI(K+1)/SI(K))/DEL
        RNU(K)=(1.-RSL*SI(K))/DEL
        RMU(K)=(RSL*SI(K+1)-1.)/DEL
20    CONTINUE
      RNU(KM)=0.
      RMU(KM)=1./SI(KM)
      DO 40 K=2,KM
        DO 30 J=1,KM
          TI(K)=TI(K)+CI(K,J)*TOV(J)
          DT(K,J)=(1-SI(K))*CQ(KM+1,J)-CQ(K,J)
30      CONTINUE
40    CONTINUE
      DO 70 J=1,KM
        DO 50 K=1,KM
          AM(K,J)=-RAA*CQL(K,J)
          BM(K,J)=(ROCP-1)*TOV(K)*CQ(KM+1,J)
     &           +ROCP*TOV(K)*(RNU(K)*DT(K+1,J)+RMU(K)*DT(K,J))
50      CONTINUE
        BM(J,J)=BM(J,J)-TOV(J)
        DO 60 K=1,KM
        DO 60 I=2,KM
          BM(K,J)=BM(K,J)-CD(K,I)*TI(I)*DT(I,J)
60      CONTINUE
70    CONTINUE
CMIC$ DO ALL SHARED(KM,GV,SV,AM,BM,CM) PRIVATE(J,K,I)
      DO 100 J=1,KM
        DO 80 K=1,KM
          CM(K,J)=GV(K)*SV(J)
80      CONTINUE
        DO 90 K=1,KM
        DO 90 I=1,KM
          CM(K,J)=CM(K,J)+AM(K,I)*BM(I,J)
90      CONTINUE
100   CONTINUE
      RETURN
      END
