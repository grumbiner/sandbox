CFPP$ NOCONCUR R
CFPP$ EXPAND(FPKAP,FTDP,FTLCL,FTHE,FTMA)
C-----------------------------------------------------------------------
      SUBROUTINE MSTADB(IM,KM,K1,K2,SL,SLK,PS,TENV,QENV,
     &                  KLCL,KBOT,KTOP,TCLD,QCLD)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: MSTADB       COMPUTE MOIST ADIABATIC CLOUD SOUNDINGS
C
C   AUTHOR: N PHILLIPS    DATE: NOV 1983
C
C ABSTRACT: ATMOSPHERIC COLUMNS OF TEMPERATURE AND SPECIFIC HUMIDITY
C   ARE EXAMINED BY THIS ROUTINE FOR CONDITIONAL INSTABILITY.
C   THE TEST PARCEL IS CHOSEN FROM THE LAYER BETWEEN LAYERS K1 AND K2
C   THAT HAS THE WARMEST POTENTIAL WET-BULB TEMPERATURE.
C   EXCESS CLOUD TEMPERATURES AND SPECIFIC HUMIDITIES ARE RETURNED
C   WHERE THE LIFTED PARCEL IS FOUND TO BE BUOYANT.
C   FAST INLINABLE FUNCTIONS ARE INVOKED TO COMPUTE
C   DEWPOINT AND LIFTING CONDENSATION LEVEL TEMPERATURES,
C   EQUIVALENT POTENTIAL TEMPERATURE AT THE LCL, AND
C   TEMPERATURE AND SPECIFIC HUMIDITY OF THE ASCENDING PARCEL.
C
C PROGRAM HISTORY LOG:
C   83-11     PHILLIPS
C   91-05-07  IREDELL             ARGUMENTS CHANGED, CODE TIDIED
C
C USAGE:    CALL MSTADB(IM,KM,K1,K2,SL,SLK,PS,TENV,QENV,
C    &                  KLCL,KBOT,KTOP,TCLD,QCLD)
C
C   INPUT ARGUMENT LIST:
C     IM       - INTEGER NUMBER OF ATMOSPHERIC COLUMNS
C     KM       - INTEGER NUMBER OF SIGMA LEVELS IN A COLUMN
C     K1       - INTEGER LOWEST LEVEL FROM WHICH A PARCEL CAN ORIGINATE
C     K2       - INTEGER HIGHEST LEVEL FROM WHICH A PARCEL CAN ORIGINATE
C     SL       - REAL (KM) SIGMA VALUES
C     SLK      - REAL (KM) SIGMA VALUES TO THE KAPPA
C     PS       - REAL (IM) SURFACE PRESSURE IN KILOPASCALS (CB)
C     TENV     - REAL (IM,KM) ENVIRONMENT TEMPERATURES
C     QENV     - REAL (IM,KM) ENVIRONMENT SPECIFIC HUMIDITIES
C
C   OUTPUT ARGUMENT LIST:
C     KLCL     - INTEGER (IM) LEVEL JUST ABOVE LCL (KM+1 IF NO LCL)
C     KBOT     - INTEGER (IM) LEVEL JUST ABOVE CLOUD BOTTOM
C     KTOP     - INTEGER (IM) LEVEL JUST BELOW CLOUD TOP
C              - NOTE THAT KBOT(I) GT KTOP(I) IF NO CLOUD.
C     TCLD     - REAL (IM,KM) OF EXCESS CLOUD TEMPERATURES.
C                (PARCEL T MINUS ENVIRON T, OR 0. WHERE NO CLOUD)
C     QCLD     - REAL (IM,KM) OF EXCESS CLOUD SPECIFIC HUMIDITIES.
C                (PARCEL Q MINUS ENVIRON Q, OR 0. WHERE NO CLOUD)
C
C SUBPROGRAMS CALLED:
C     FPKAP    - FUNCTION TO COMPUTE PRESSURE TO THE KAPPA POWER
C     FTDP     - FUNCTION TO COMPUTE DEWPOINT TEMPERATURE
C     FTLCL    - FUNCTION TO COMPUTE LCL TEMPERATURE
C     FTHE     - FUNCTION TO COMPUTE EQUIVALENT POTENTIAL TEMPERATURE
C     FTMA     - FUNCTION TO COMPUTE PARCEL TEMPERATURE AND HUMIDITY
C
C REMARKS: ALL FUNCTIONS ARE INLINED BY FPP.
C          NONSTANDARD AUTOMATIC ARRAYS ARE USED.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      DIMENSION SL(KM),SLK(KM),PS(IM),TENV(IM,KM),QENV(IM,KM),
     &          KLCL(IM),KBOT(IM),KTOP(IM),TCLD(IM,KM),QCLD(IM,KM)
C  PHYSICAL PARAMETERS
      PARAMETER(RD= 2.8705E+2 ,RV= 4.6150E+2 )
      PARAMETER(EPS=RD/RV,EPSM1=RD/RV-1.,FTV=RV/RD-1.)
C  LOCAL ARRAYS
      DIMENSION PSK(IM),SLKMA(IM),THEMA(IM)
C-----------------------------------------------------------------------
C  DETERMINE WARMEST POTENTIAL WET-BULB TEMPERATURE BETWEEN K1 AND K2.
C  COMPUTE ITS LIFTING CONDENSATION LEVEL.
      DO I=1,IM
        PSK(I)=FPKAP(PS(I))
        SLKMA(I)=0.
        THEMA(I)=0.
      ENDDO
      DO K=K1,K2
        DO I=1,IM
          PV=SL(K)*PS(I)*QENV(I,K)/(EPS-EPSM1*QENV(I,K))
          TDPD=TENV(I,K)-FTDP(PV)
          IF(TDPD.GT.0.) THEN
            TLCL=FTLCL(TENV(I,K),TDPD)
            SLKLCL=SLK(K)*TLCL/TENV(I,K)
          ELSE
            TLCL=TENV(I,K)
            SLKLCL=SLK(K)
          ENDIF
          THELCL=FTHE(TLCL,SLKLCL*PSK(I))
          IF(THELCL.GT.THEMA(I)) THEN
            SLKMA(I)=SLKLCL
            THEMA(I)=THELCL
          ENDIF
        ENDDO
      ENDDO
C-----------------------------------------------------------------------
C  SET CLOUD TEMPERATURES AND HUMIDITIES WHEREVER THE PARCEL LIFTED UP
C  THE MOIST ADIABAT IS BUOYANT WITH RESPECT TO THE ENVIRONMENT.
      DO I=1,IM
        KLCL(I)=KM+1
        KBOT(I)=KM+1
        KTOP(I)=0
      ENDDO
      DO K=1,KM
        DO I=1,IM
          TCLD(I,K)=0.
          QCLD(I,K)=0.
        ENDDO
      ENDDO
      DO K=K1,KM
        DO I=1,IM
          IF(SLK(K).LE.SLKMA(I)) THEN
            KLCL(I)=MIN(KLCL(I),K)
            TMA=FTMA(THEMA(I),SLK(K)*PSK(I),QMA)
            TVCLD=TMA*(1.+FTV*QMA)
            TVENV=TENV(I,K)*(1.+FTV*QENV(I,K))
            IF(TVCLD.GT.TVENV) THEN
              KBOT(I)=MIN(KBOT(I),K)
              KTOP(I)=MAX(KTOP(I),K)
              TCLD(I,K)=TMA-TENV(I,K)
              QCLD(I,K)=QMA-QENV(I,K)
            ENDIF
          ENDIF
        ENDDO
      ENDDO
C-----------------------------------------------------------------------
      RETURN
      END
