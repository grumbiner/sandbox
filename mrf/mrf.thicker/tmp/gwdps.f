CFPP$ NOCONCUR R
      SUBROUTINE GWDPS(IDIMT,IDIMT2,KDIM,A,B,
     1                  U1,V1,T1,Q1,
     2                  PSTAR,
     3                  SI,DEL,CL,SL,RCL,DELTIM,LAT,KDT,HPRIME,
     4                  DUSFC,DVSFC)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    GWDPS       INCLUDES GRAVITY WAVE DRAG.
C   PRGMMR: JORDAN C. ALPERT ORG: W/NMC23    DATE: 91-03-12
C
C ABSTRACT: USING THE GWD PARAMETERIZATIONS OF PS-GLAS AND PH-
C   GFDL TECHNIQUE, THE TIME TENDENCIES OF U V
C   ARE ALTERED TO INCLUDE THE EFFECT OF MOUNTAIN INDUCED
C   GRAVITY WAVE DRAG FROM SUB-GRID SCALE OROGRAPHY INCLUDING
C   CONVECTIVE BREAKING, SHEAR BREAKING AND THE PRESENCE OF
C   CRITICAL LEVELS.
C
C PROGRAM HISTORY LOG:
C   87-06-03  JORDAN C. ALPERT  FR30(V3H-MX)
C
C USAGE:    CALL GWDPS(A,B,U1,V1,T1,Q1,PSTAR,
C                      SI,DEL,CL,SL,RCL,DELTIM,LAT,KDT,HPRIME)
C   INPUT ARGUMENT LIST:
C     A        - NEGATIVE NON-LIN TENDENCY FOR V WIND COMPONENT.
C     B        -          NON-LIN TENDENCY FOR U WIND COMPONENT.
C     U1       - ZONAL      WIND COMPONENT *COS(LAT)  M/SEC AT T0-DT.
C     V1       - MERIDIONAL WIND COMPONENT *COS(LAT)  M/SEC AT T0-DT.
C     T1       - TEMPERATURE DEG K AT T0-DT.
C     Q1       - SPECIFIC HUMIDITY AT T0-DT.
C     PSTAR    - SURFACE PRESSURE (CB).
C     SI(N)    - P/PSFC AT BASE OF LAYER N.
C     DEL(N)   - POSITIVE INCREMENT OF P/PSFC ACROSS LAYER N.
C     CL(N)    = 1 - SL(N).
C     SL(N)    - P/PSFC AT MIDDLE OF LAYER N.
C     RCL      - RECIPROCAL OF SQUARE OF COS(LAT).
C     DELTIM   - TIME STEP  SECS.
C     LAT      - LATITUDE  NUMBER.
C     KDT      - TIME STEP NUMBER.
C     HPRIME   - TOPOGRAPHIC STANDARD DEVIATION  (M).
C
C   OUTPUT ARGUMENT LIST:
C     A        - AS AUGMENTED BY TENDENCY DUE TO MIGWD.
C     B        - AS AUGMENTED BY TENDENCY DUE TO MIGWD.
C
C   OUTPUT FILES:
C     FT06F001 - PRINTOUT FILE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN, CFT77.
C   MACHINE:  CRAY Y-MP.
C
C$$$
C ----->  I M P L E M E N T A T I O N    V E R S I O N   <----------
CRAY       SAVE
C
C  VERSION 3  MODIFIED FOR GRAVITY WAVES, LOCATION: .FR30(V3GWD)  *J*
C---       3.1 INCLUDES VARIABLE SATURATION FLUX PROFILE CF ISIGST
C---       3.G INCLUDES PS COMBINED W/ PH (GLAS AND GFDL)
C-----         ALSO INCLUDED IS RI  SMOOTH OVER THICK LOWER LAYER
C-----         ALSO INCLUDED IS DECREASE IN DE-ACC AT TOP BY 1/2
C-----     LOCATION:  FR30(V3GD)          -----
C-----     THE NMC GWD INCORPORATING BOTH GLAS(P&S) AND GFDL(MIGWD)
C-----             MOUNTAIN INDUCED GRAVITY WAVE DRAG
C-----    CODE FROM .FR30(V3MONNX) FOR MONIN3
C-----        THIS VERSION (06 MAR 1987)
C-----        THIS VERSION (26 APR 1987)    3.G
C-----        THIS VERSION (01 MAY 1987)    3.9
C-----   CHANGE TO FORTRAN 77 (FEB 1989)     --- HANN-MING HENRY JUANG
C -------
C    USE
C        ROUTINE IS CALLED FROM GLOOP  (AFTER CALL
C        TO MONNIN IN THE SMF PORTION OF THE MRF MODEL
C
C    PURPOSE
C        USING THE GWD PARAMETERIZATIONS OF PS-GLAS AND PH-
C        GFDL TECHNIQUE.  THE TIME TENDENCIES OF U V
C        ARE ALTERED TO INCLUDE THE EFFECT OF MOUNTAIN INDUCED
C        GRAVITY WAVE DRAG FROM SUB-GRID SCALE OROGRAPHY INCLUDING
C        CONVECTIVE BREAKING, SHEAR BREAKING AND THE PRESENCE OF
C        CRITICAL LEVELS
C
C  INPUT
C        A NEGATIVE NON-LIN TENDENCY FOR V WIND COMPONENT
C        B          NON-LIN TENDENCY FOR U WIND COMPONENT
C          FOR A B DEFS SEE EQ 14 15 OF SELA 1982 NMC SPECTRAL
C          MODEL TECHNICAL REPORT.
C        U1 ZONAL WIND COMPONENT * COS(LAT)   M/SEC  AT T0-DT
C        V1 MERIDIONAL WIND COMPONENT *COS(LAT)  M/SEC AT T0-DT
C        T1 TEMPERATURE DEG K AT T0-DT
C        Q1 SPECIFIC HUMIDITY AT T0-DT
C        NOTE U V T Q AT KDIM LAYERS
C        RCL RECIPROCAL OF SQUARE OF COS(LAT)
C        DELTIM TIME STEP    SECS
C        THE FOLLOWING PARAMETERS ARE SET UP IN SR
C        SETSIG.
C        SI(N) P/PSFC AT BASE OF LAYER N
C        SL(N) P/PSFC AT MIDDLE OF LAYER N
C        DEL(N) POSITIVE INCREMENT OF P/PSFC ACROSS LAYER N
C        CL(N) =1 - SL(N)
C
C  OUTPUT
C        A B         AS AUGMENTED BY TENDENCY DUE TO MIGWD
C        OTHER INPUT VARIABLES UNMODIFIED.
C
C  SRS CALLED    GWDRAG
C
      DIMENSION A(IDIMT,KDIM),  B(IDIMT,KDIM),
     2         U1(IDIMT2,KDIM), V1(IDIMT2,KDIM),
     3         T1(IDIMT2,KDIM), Q1(IDIMT2,KDIM),
     7   SI(KDIM+1),DEL(KDIM),CL(KDIM),SL(KDIM)
C
C     DIMENSION PS     (IDIMT)
      DIMENSION PSTAR  (IDIMT)
C
      PARAMETER(CP= 1.0046E+3 ,G= 9.8000E+0 ,RD= 2.8705E+2 ,RV= 4.6150E+
     12 )
      PARAMETER(GOR=G/RD,GOCP=G/CP,FV=RV/RD-1)
      PARAMETER(DW2MIN=1.,RIMIN=-100.)
      DIMENSION RDZT(KDIM-1)
      DIMENSION VELCO(IDIMT,KDIM-1)
C
      LOGICAL LDRAG(IDIMT)
C
C  DEBUG DIMENSION
C
      DIMENSION RI(KDIM-1)
C
C----         MOUNTAIN INDUCED GRAVITY WAVE DRAG
C----   UNIT14  - SUBGRID SCALE MOUNTAIN VARIANCE HEIGHT INPUT
C----   COMMON TO BE ADDED TO SMF,GLOO FOR MIGWD        *J*
C----   IDIMT = 256 AND KDIM = 18 FOR EXAMPLE...
C
            D I M E N S I O N
     .TAUB(IDIMT),XN(IDIMT),YN(IDIMT),DTAUX(IDIMT,KDIM),
     .UBAR(IDIMT),VBAR(IDIMT),TAUD(IDIMT,KDIM),FR(IDIMT),GF(IDIMT),
     .ULOW(IDIMT),BNV(IDIMT),VTJ(IDIMT,KDIM),DTAUY(IDIMT,KDIM),
     .HPRIME(IDIMT),BNV2(IDIMT,KDIM),SIGK(KDIM),
     .TAUP(IDIMT,KDIM+1),USQJ(IDIMT,KDIM),RO(IDIMT,KDIM),ROLL(IDIMT)
      DIMENSION DUSFC(IDIMT),DVSFC(IDIMT)
      DIMENSION DTFAC(IDIMT)
      DIMENSION VELKO(KDIM-1)
      REAL AKAPPA
      REAL SLREAL
C
C
C--------------------------------------------CONSTANTS FOR MIGWD *J*
C
C-----ONLY DO CONSTANTS FIRST TIME THROUGH MONNIN
C
      RLOWLV =0.7
      XL     =4.0 E 4
      CRITAC =5.0 E -4
      NCNT   =100
C
            AKAPPA = 2. / 7.
C
      DO 18 K=1,KDIM
      SLREAL = SL(K)
      SIGK(K)  = SLREAL**(-AKAPPA)
   18 CONTINUE
C
C------- MKDIMP   THE TOP SIGMA LEVEL OVER WHICH MIGWD WILL OPERATE
C
       MKDIMP = KDIM+1
C
C------- KBJ IS THE BOTTOM OF THE LOW 1/3 LEVEL USUALLY = 1
C
        KBJ = 1
         DO 15 K = KBJ, KDIM
         IF (SI(K) .LT. RLOWLV) THEN
         KSM = K
         GO TO 16
         ENDIF
   15    CONTINUE
   16   CONTINUE
C
C-----KSM -1 INTERVALS IN THE LOWER THIRD OF ATM (SIGMA < .667)
C
         KSMM1 = KSM - 1
         DELKS = SI(KBJ)-SI(KSM)
         DELKS1=SL(KBJ)-SL(KSM)
C
C----ABOVE, THE LOW LAYER DELTA SIGMA
C-----BELOW THE STARTING SIGMA LEVEL FOR PS STRESS CALC DEFAULTS TO 2
C
       KBPS = 2
       LCAP=KDIM
       LCAPP1 = LCAP + 1
       FACTOP=0.5
C
        GRAV = G
        GRAV2 = GRAV * GRAV
        RGAS = RD
        GR2 = 2.0 * GRAV2 / RGAS
        GMAX = 1.
        AJ   = 1.
        XLINV =  1.0 / XL
        VELEPS=1.0
      RCS=SQRT(RCL)
      CS = 1. / RCS
C
C----------SAVING RICHARDSON NUMBER IN USQJ FOR MIGWD        *J*
C
      DO K=1,KDIM-1
        RDZT(K)=GOR*SI(K+1)/(SL(K)-SL(K+1))
      ENDDO
      DO K=1,KDIM
        DO J=1,IDIMT
          VTJ(J,K)=T1(J,K)*(1.+FV*Q1(J,K))
        ENDDO
      ENDDO
      DO K=1,KDIM-1
        DO J=1,IDIMT
          TI=0.5*(T1(J,K)+T1(J,K+1))
          RDZ=RDZT(K)/TI
          DW2=RCL*((U1(J,K)-U1(J,K+1))**2+(V1(J,K)-V1(J,K+1))**2)
          SHR2=MAX(DW2,DW2MIN)*RDZ**2
          BVF2=G*(GOCP+RDZ*(VTJ(J,K+1)-VTJ(J,K)))/TI
          USQJ(J,K)=MAX(BVF2/SHR2,RIMIN)
        ENDDO
      ENDDO
C
C-----VERTICAL STRUCTURE OF RI IN RI(KDIM) FOR DIAGNOSTICS
C
C-----THE LINEAR MOUNTAIN INDUCED GRAVITY MODE P&S PRAMETERIZATION
C      EXPLITLY DONE
C-----THIS ROUTINE COMPUTES THE DECELERATION OF THE ZONAL WIND AND
C-----MERIDIONAL WIND DUE TO MOUNTAIN GRAVITY DRAG.
C
C-----    CODE VARIABLES          DESCRIPTION
C
C-----       XN,YN            PROJECTIONS OF "LOW-LEVEL" WIND
C-----                        IN ZONAL & MERIDIONAL DIRECTIONS
C
C-----       ULOW             "LOW-LEVEL" WIND MAGNITUDE -        (= U)
C-----                        AVERAGED UP TO 2KM ABOVE SURFACE
C
C-----       BNV2             BNV2 = N**2
C
C-----       HPRIME           SUB-GRID SCALE MOUNTAIN HEIGHT      (= H)
C-----                        FROM NAVY TAPE, AVERAGED,'ENVELOPE'STD. VA
C-----                        READ IN IN SMF,COMMON-ED TO GLOO
C
C-----       TAUB             BASE MOMENTUM FLUX
C-----                        = -(RO * U**3/(N*XL)*GF(FR) FOR N**2 > 0
C-----                        = 0.                        FOR N**2 < 0
C
C
C-----       FR               FROUDE    =   N*HPRIME / U
C-----       G                GMAX*FR**2/(FR**2+AJ**2)
C-----       GMAX             = 1.0
C-----       AJ               = 1.0
C
C-----KSM IS DEFINED AS THE NUMBER OF LEVELS UP 1/3 FROM THE LOWEST USED
C-----TO CALCULATE THE "LOW-LEVEL" AVERAGES.
C
C
C-----INITIALIZE ARRAYS     (ON CYBER)
C
       DO 200 I=1,IDIMT
         XN(I) = 0.0
         YN(I) = 0.0
       UBAR (I)=  0.0
       VBAR (I)=  0.0
       ROLL (I)=  0.0
       TAUB (I)=  0.0
       ULOW (I)=  0.0
       TAUP (I,KDIM+1) = 0.0
 200   CONTINUE
C
       DO 250 K=1,KDIM
       DO 250 I=1,IDIMT
        TAUP(I,K) = 0.0
        RO(I,K) = SL(K) * PSTAR(I)
     1                 / ( RGAS * VTJ(I,K) )
 250  CONTINUE
C
C----DENSITY   TONS/METER**3
C--------.---------.---------.---------.---------.---------.---------. .
C-----COMPUTE LOW LEVEL AVERAGES
C-----(U,V)*COS(LAT)  USE UV=(U1,V1) WHICH IS WIND AT T0-1
C----- USE RCS=1/COS(LAT) TO GET WIND FIELD
C----  KSM   THE TOP OF THE LOWEST 1/3 LAYER "THE LOW LEVEL" IS 6
C
        DO 300 K=KBJ,KSMM1
        RCSKS = RCS * DEL(K) / DELKS
        DO 300 I=1,IDIMT
        UBAR(I) = UBAR(I) + RCSKS * U1(I,K)
        VBAR(I) = VBAR(I) + RCSKS * V1(I,K)
 300    CONTINUE
C
C----COMPUTE THE "LOW LEVEL" OR 1/3 WIND MAGNITUDE (M/S)
C
      DO 400 I=1,IDIMT
        ULOW(I) = SQRT( UBAR(I) * UBAR(I) + VBAR(I) * VBAR(I) )
 400  CONTINUE
      DO 450 I=1,IDIMT
        VALUE = 1.0
        ULOW(I) = MAX( ULOW(I), VALUE )
 450  CONTINUE
C
C-----CALCULATE SQUARED LOW LEVEL BRUNT VAISALA FREQUENCY OVER THE
C-----FIRST KSM LEVELS THEN AVERAGE
C---- SIGKM IS RECIP( SIGMA** KAPPA) AT TOP OF LOWER LAYER
C---- SIGKIN IS RECIP( SIGMA** KAPPA) AT BOTTOM OF LOWER LAYER
C---- RDELKS (DEL(K)/DELKS) VERT AVE FACTOR SO WE CAN * INSTEAD OF /
C
        DO 500 I=1,IDIMT
           BNV2(I,1) = 0.
 500    CONTINUE
C
        DO 550 K=KBJ,KSMM1
        DO 550 I=1,IDIMT
            BNV2(I,K) = GR2 * (SL(K) + SL(K+1))
     1    * (VTJ(I,K+1) * SIGK(K+1) - VTJ(I,K) * SIGK(K))
     2    /  (  (VTJ(I,K) * SIGK(K)
     3                 + VTJ(I,K+1) * SIGK(K+1))
     4       * (SL(K) - SL(K+1))
     5                 * (T1(I,K) + T1(I,K+1)) )
 550    CONTINUE
C
        DO 600 K=1,KDIM-1
        DO 600 I=1,IDIMT
        VELCO(I,K) =
     1    (0.5*RCS)*( (U1(I,K) + U1(I,K+1)) *
     2                      UBAR(I) +
     3                       (V1(I,K) + V1(I,K+1)) *
     4                      VBAR(I))
        VELCO(I,K)=VELCO(I,K)/ULOW(I)
        IF ((VELCO(I,K).LT.VELEPS).AND.(VELCO(I,K).GE.0.)) THEN
          VELCO(I,K) = VELEPS
        ENDIF
 600    CONTINUE
C
C  NO DRAG WHEN CRITICAL LEVEL IN THE BASE LAYER
C
        DO 700 I=1,IDIMT
        LDRAG(I)=VELCO(I,1).LE.0.
 700    CONTINUE
        DO 750 K=2,KSMM1
        DO 750 I=1,IDIMT
        LDRAG(I)=LDRAG(I).OR. VELCO(I,K).LE.0.
 750    CONTINUE
C
C  NO DRAG WHEN BNV2.LT.0
C
        DO 800 K=1,KSMM1
        DO 800 I=1,IDIMT
        LDRAG(I)=LDRAG(I).OR. BNV2(I,K).LT.0.
 800    CONTINUE
C
C-----THE LOW LEVEL WEIGHTED AVERAGE RI IS STORED IN USQJ(1,1; IDIMT)
C-----THE LOW LEVEL WEIGHTED AVERAGE N**2 IS STORED IN BNV2(1,1; IDIMT)
C----- THIS IS CALLED BNVL2 IN GWDRAG NOT BNV2
C
        KBJP1 = KBJ + 1
        WTKBJ = (SL(KBJ)-SL(KBJP1))/DELKS1
        DO 900 I=1,IDIMT
        USQJ(I,1) = WTKBJ * USQJ(I,KBJ)
        BNV2(I,1) = WTKBJ * BNV2(I,KBJ)
 900    CONTINUE
C
        DO 1000 K = KBJP1,KSMM1
           RDELKS = (SL(K)-SL(K+1))/DELKS1
           DO 1000 I=1,IDIMT
           BNV2(I,1) =   BNV2(I,1) + BNV2(I,K)  * RDELKS
           USQJ(I,1) =   USQJ(I,1) + USQJ(I,K)   * RDELKS
 1000   CONTINUE
C
        DO 1010 I=1,IDIMT
        LDRAG(I)=LDRAG(I).OR. BNV2(I,1).LE.0.0
        LDRAG(I)=LDRAG(I).OR. ULOW(I).EQ.1.0
 1010   CONTINUE
C
C ----- SET ALL RI LOW LEVEL VALUES TO THE LOW LEVEL VALUE
C
        KBJBEG = KBJ
        IF(KBJ .EQ. 1) KBJBEG = 2
        DO 1020 K=KBJBEG,KSMM1
        DO 1020 I=1,IDIMT
           USQJ(I,K) = USQJ(I,1)
 1020   CONTINUE
C
C-----  LOW LEVEL DENSITY
C
        DO 1030 K=KBJ,KSMM1
           RDELKS =  DEL(K)/DELKS
           DO 1030 I=1,IDIMT
           ROLL(I) =   ROLL(I) + RO(I,K)   * RDELKS
 1030   CONTINUE
C
        DO 1050 I=1,IDIMT
         IF  (.NOT.LDRAG(I) )   THEN
C
C-----VECTOR SQUARE ROOT FUNCTION - VSQRT -  USED TO COMPUTE BNV
C
       BNV(I) = SQRT( BNV2(I,1) )
C
C-----CALCULATE FR  FROUDE    ---- N*HPRIME / U
C
       FR(I) = BNV(I) * HPRIME(I) / ULOW(I)
C
C----CONTINUE W/ WHERE BLOCK
C
C-----CALCULATE G   THE UNIVERSAL FLUX FUNCTION
C
       GF(I) = GMAX * FR(I) * FR(I) /
     1                 ( FR(I) * FR(I) + AJ * AJ )
C
C-----CALCULATE TAUB - (THE BASE FLUX)
C-----REMEMBER - THE LOW LEVEL N IS IN BNV2(1,1;IDIMT) = BNV = BNVL2
C
       TAUB(I) =  -XLINV * ROLL(I) *
     1     ULOW(I) * ULOW(I) * ULOW(I) * GF(I) / BNV(I)
C
C-----CALCULATE XN, YN
C
               XN(I) = UBAR(I) / ULOW(I)
C
               YN(I) = VBAR(I) / ULOW(I)
C
          ELSE
C
              TAUB(I) = 0.0
               XN(I)  = 0.0
               YN(I)  = 0.0
C
          ENDIF
 1050   CONTINUE
C
C------THE CALL TO GWDRAG:
C------      TAUP ARE RETURNED    OTHER PARAMETERS FROM MONN
C
      CALL GWDRAG(IDIMT,IDIMT2,KDIM,
     1            U1,V1,T1,PSTAR,VTJ,USQJ,KSM,KBJ,KBPS,
     1            VELCO,BNV2,ROLL,RO,TAUB,SI,DEL,SL,SIGK,RCL,
     2            LAT,KDT,HPRIME,XLINV,TAUP)
C
           IF(LCAP.LT.KDIM) THEN
C
           DO 1100 KLCAP =  LCAPP1, KDIM
C
            SIRA =          SI(KLCAP) / SI(LCAP)
C
           DO 1100 I=1,IDIMT
           TAUP(I,KLCAP) =  SIRA * TAUP(I,LCAP)
C
 1100      CONTINUE
C
           ENDIF
C
C-----FIX UP THE LEVEL 1 (OR MORE) STRESS TO BE LINEAR WITH LEVEL
C-----KBPS AND THE STRESS AT THE BOTTOM TAUB (IF KBPS IS .GT. 1)
C
        IF (KBPS .GT. KBJ) THEN
           KBPSM1 = KBPS - 1
           KBPSP1 = KBPS + 1
C
           DO 1200 IK1 = KBJ, KBPSM1
                   SAVEM   =  ( (SI(IK1+1) - SI(KBPSP1)) /
     1                         (SI(KBPSP1) - 1.0 ) )
           DO 1200 I=1,IDIMT
           TAUP(I,IK1+1) =  TAUP(I,KBPSP1)   -
     1        SAVEM * ( TAUP(I,KBJ) - TAUP(I,KBPSP1) )
 1200      CONTINUE
C
        ENDIF
C
C---KEEP IN MIND THAT TAUP IS ZERO-ED OUT BEFORE EACH CALL
C-----VERTICALLY DIFFERENCE STRESS FOR D TAU / D SIGMA  FROM SI TO SL
C
      DO 1300 K=1,KDIM
      DO 1300 I=1,IDIMT
C
C-----THE STRESS IN GWSDRAG HAS BEEN CALC USING -TAUB WHICH NOW MUST BE
C-----RETURNED TO -(AMPLITUDE) BELOW THE OLD WAY FOR TAU AS -(RO*U**3/NL
C-----INSTEAD OF RO*UAMP*K*N*H'**2
C
C
      TAUD(I,K) = (TAUP(I,K+1) - TAUP(I,K) ) / DEL(K)
C
C---WHERE DEL= SI(K)-SI(K+1)  (SIGN 'SWITCHED' IN SELA CODE)
C
 1300 CONTINUE
C
C-----CALCULATE DECELERATION TERMS - DTAUX,DTAUY
C
      DO 1400 K=1,KDIM
      DO 1400 I=1,IDIMT
      TAUD(I,K) = TAUD(I,K) / PSTAR(I)
 1400 CONTINUE
C
C------LIMIT DE-ACCELERATION (MOMENTUM DEPOSITION ) AT TOP TO 1/2 VALUE
C------THE IDEA IS SOME STUFF MUST GO OUT THE 'TOP'
C
C------LIMIT DE-ACCELERATION (MOMENTUM DEPOSITION ) AT TOP TO 1/2 VALUE
C------THE IDEA IS SOME STUFF MUST GO OUT THE 'TOP'
C
      DO 1500 KLCAP = LCAP, KDIM
      DO 1500 I=1,IDIMT
      TAUD(I,KLCAP) = TAUD(I,KLCAP) * FACTOP
 1500 CONTINUE
C
C----- *G AND * BY COS(LAT) FOR MRF TENDENCIES
C
      CSGRAV = CS * GRAV
      DO 1600 K=1,KDIM
      DO 1600 I=1,IDIMT
      TAUD(I,K) = TAUD(I,K) * CSGRAV
 1600 CONTINUE
C
C------IF THE GRAVITY WAVE DRAG WOULD FORCE A CRITICAL LINE
C------IN THE LOWER KSMM1 LAYERS DURING THE NEXT 2*DELTIM TIMESTEP,
C------THEN ONLY APPLY DRAG UNTIL THAT CRITICAL LINE IS REACHED.
C
      DO 1610 I=1,IDIMT
      DTFAC(I)=1.
1610  CONTINUE
      DO 1620 K=1,KSMM1
      DO 1620 I=1,IDIMT
      IF(TAUD(I,K).NE.0.)
     &DTFAC(I)=MIN(DTFAC(I),ABS(VELCO(I,K)/(2.*DELTIM*RCS*TAUD(I,K))))
1620  CONTINUE
C     DO 1625 I=1,IDIMT
C     IF(DTFAC(I).LT.0.20) THEN
C     PRINT 1624,KDT,LAT,I,DTFAC(I),VELCO(I,1),2.*DELTIM*RCS*TAUD(I,1),
C    &           TAUB(I),BNV(I),HPRIME(I)
1624  FORMAT(' GWD KDT,LAT,I,DTFAC,V1,DV1',3I4,2PF8.1,0P2F8.2,3E12.4)
C     ENDIF
1625  CONTINUE
      DO 1630 K=1,KDIM
      DO 1630 I=1,IDIMT
      TAUD(I,K)=TAUD(I,K)*DTFAC(I)
1630  CONTINUE
C
C-----FOR OPERATIONS DO THIS USING A BIT VECTOR TO SELECT IN THE ABOVE
C-----WHERE BLOCK AND THEN PRINT ONLY THOSE THAT ARE TRUE.
C
           DO 1660 K=1,KDIM
           DO 1660 I=1,IDIMT
C
            DTAUX(I,K) = XN(I) * TAUD(I,K)
            DTAUY(I,K) = YN(I) * TAUD(I,K)
C
 1660      CONTINUE
C
C    MONITOR FOR EXCESSIVE GRAVITY WAVE DRAG TENDENCIES
C
CP       IF(NCNT.GT.0) THEN
CP       IF(LAT.GE.38.AND.LAT.LE.42) THEN
CP CMIC$ GUARD 37
CP         DO 92 I=1,IDIMT
CP       IF(IKOUNT.GT.NCNT) GO TO 92
CP       IF(I.LT.319.OR.I.GT.320) GO TO 92
CP           DO 91 K=1,KDIM
CP             IF(ABS(RCS*TAUD(I,K)) .GT. CRITAC) THEN
CP             IF(I.LE.IDIM) THEN
CP               IKOUNT=IKOUNT+1
CP               PRINT 123,I,LAT,KDT
CP               PRINT 124,TAUB(I),BNV(I),ULOW(I),GF(I),FR(I),
CP      .                  ROLL(I),HPRIME(I),XN(I),YN(I)
CP               PRINT 124,(TAUD(I,KK),KK=1,KDIM)
CP               PRINT 124,(TAUP(I,KK),KK=1,KDIM+1)
CP               PRINT 124,(USQJ(I,KK),KK=1,KDIM)
CP               DO 93 KK=1,KDIM-1
CP               VELKO(KK)=0.5*RCS*((U1(I,KK)+U1(I,KK+1))*UBAR(I)+
CP      .                  (V1(I,KK)+V1(I,KK+1))*VBAR(I))/ULOW(I)
CP   93          CONTINUE
CP             PRINT 124,(VELKO(KK),KK=1,KDIM-1)
CP             PRINT 124,(A    (I,KK),KK=1,KDIM)
CP             PRINT 124,(DTAUY(I,KK),KK=1,KDIM)
CP             PRINT 124,(B    (I,KK),KK=1,KDIM)
CP             PRINT 124,(DTAUX(I,KK),KK=1,KDIM)
CP             GO TO 92
CP             ENDIF
CP             ENDIF
CP   91      CONTINUE
CP   92    CONTINUE
CP CMIC$ END GUARD 37
CP  123  FORMAT('  *** MIGWD PRINT *** I=',I3,' LAT=',I3,' KDT=',I3)
CP  124  FORMAT(2X,  10E13.6)
CP       ENDIF
CP       ENDIF
C
C-----DONE WITH CALCULATION - ADD IT TO OLD A AND OLD B
C-----A CORRESPONDS TO DTAUY TERM AND B TO DTAUX
C
      DO 2001 I=1,IDIMT
      DUSFC(I)=0.
      DVSFC(I)=0.
 2001 CONTINUE
      DO 2000 K=1,KDIM
      DO 2000 I=1,IDIMT
      A(I,K) =  DTAUY(I,K) + A(I,K)
      B(I,K) =  DTAUX(I,K) + B(I,K)
      DUSFC(I)=DUSFC(I)+DTAUX(I,K)*DEL(K)
      DVSFC(I)=DVSFC(I)+DTAUY(I,K)*DEL(K)
 2000 CONTINUE
      DO 2002 I=1,IDIMT
      DUSFC(I)=-1.E3/G*RCS*PSTAR(I)*DUSFC(I)
      DVSFC(I)=-1.E3/G*RCS*PSTAR(I)*DVSFC(I)
 2002 CONTINUE
C
C-----DIAGNOSTIC FLAG .NE.0 ON, OTHERWISE OFF, NUM(715)=OUTPUT UNIT (=6)
C
      RETURN
      END
