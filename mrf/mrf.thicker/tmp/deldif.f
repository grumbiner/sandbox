      SUBROUTINE DELDIF(RT,W,DELTIM,QM,SL,X,Y)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:    DELDIF      HORIZONTAL DIFFUSION.
C   PRGMMR: MARK IREDELL     ORG: W/NMC23    DATE: 91-03-15
C
C ABSTRACT: HORIZONTAL DIFFUSION OF TEMPERATURE, MOISTURE,
C           VORTICITY AND DIVERGENCE. THE IMPLICIT LINEAR EQUATION
C           IS SOLVED USING THE LAPSE RATES OF GLOBALLY AVERAGED
C           TEMPERATURE AND MOISTURE TO TRANSFORM THE LAPLACIAN
C           FROM CONSTANT PRESSURE TO CONSTANT SIGMA SURFACES.
C           FOR THE T126 OPERATIONAL MODEL, DELDIF INVOKES
C           SECOND ORDER LEITH DIFFUSION ONLY ABOVE WAVENUMBER 69
C           WITH A TIME SCALE OF 13080 SECONDS AT WAVENUMBER 126.
C
C PROGRAM HISTORY LOG:
C   91-03-15  MARK IREDELL
C
C USAGE:    CALL DELDIF (RT,W,DELTIM,QM,SL,X,Y)
C   INPUT ARGUMENT LIST:
C     RT       - SPECIFIC HUMIDITY
C     W        - VORTICITY
C     DELTIM   - TIMESTEP
C     QM       - LN(PSFC)
C     SL       - SIGMA LAYER VALUES
C     X        - DIVERGENCE
C     Y        - TEMPERATURE
C
C   OUTPUT ARGUMENT LIST:
C     RT       - SPECIFIC HUMIDITY
C     W        - VORTICITY
C     X        - DIVERGENCE
C     Y        - TEMPERATURE
C
C REMARKS: LOCAL VARIABLES THAT CAN BE MODIFIED TO CHANGE DIFFUSION ARE:
C          RTNP- RECIPROCAL OF TIME SCALE OF DIFFUSION AT WAVENUMBER NP
C          NP  - WAVENUMBER AT WHICH RTNP DIFFUSION IS APPLIED
C          N0  - MAXIMUM WAVENUMBER FOR ZERO DIFFUSION
C          JDEL- ORDER OF DIFFUSION
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY YMP.
C
C$$$
 
      DIMENSION SL( 28 ),QM( 4033 )
      DIMENSION W( 4033 , 28 ),X( 4033 , 28 ),
     &          Y( 4033 , 28 ),RT( 4033 , 28 )
      DIMENSION DN( 4032 ),SF( 28 )
      DATA IFIRST/0/
      SAVE IFIRST,DN,SF
 
      IF(IFIRST.EQ.0) THEN
        IFIRST=1
C  RECIPROCAL OF TIME SCALE OF DIFFUSION AT REFERENCE WAVENUMBER NP
        RTNP=3.E15/( 6.3712E+6 **4)*FLOAT(80*81)**2
C       NP=80
        NP= 62
C  MAXIMUM WAVENUMBER FOR ZERO DIFFUSION
C       N0=0
        N0=0.55* 62
C  ORDER OF DIFFUSION (EVEN POWER TO RAISE DEL)
C       JDEL=4
        JDEL=2
C
        PRINT 6,RTNP,NP,N0,JDEL
6       FORMAT(' HORIZONTAL DIFFUSION PARAMETERS'/
     &  '   EFFECTIVE ',6PF10.3,' MICROHERTZ AT WAVENUMBER ',I4/
     &  '   MAXIMUM WAVENUMBER FOR ZERO DIFFUSION ',I4/
     &  '   ORDER OF DIFFUSION ',I2)
C
        JDELH=JDEL/2
        NPD=MAX(NP-N0,0)
        DN1=2.*RTNP/FLOAT(NPD*(NPD+1))**JDELH
        I=0
        DO 10 NM=0, 62
        DO 10 M=0, 62 -NM
          ND=MAX(NM+M-N0,0)
          DN(I+1)=DN1*FLOAT(ND*(ND+1))**JDELH
          DN(I+2)=DN(I+1)
          I=I+2
10      CONTINUE
        DO 20 K=1, 28
          KD=MAX(K-1,1)
          KU=MIN(K+1, 28 )
          SF(K)=SL(K)/(SL(KU)-SL(KD))/SQRT(2.)
20      CONTINUE
      ENDIF
 
CFPP$ NODEPCHK
      DO 70 K=1, 28
        KD=MAX(K-1,1)
        KU=MIN(K+1, 28 )
        YSK=(Y(1,KU)-Y(1,KD))*SF(K)
        DO 60 I=3, 4032
          W(I,K)= W(I,K)/(1.+DELTIM*DN(I))
          X(I,K)= X(I,K)/(1.+DELTIM*DN(I))
          Y(I,K)=(Y(I,K)+DELTIM*DN(I)*YSK*QM(I))/(1.+DELTIM*DN(I))
60      CONTINUE
        DO 62 IT=1, 1
          IS=(IT-1)* 28
          RTSK=(RT(1,IS+KU)-RT(1,IS+KD))*SF(K)
          L=IS+K
          DO 61 I=3, 4032
            RT(I,L)=(RT(I,L)+DELTIM*DN(I)*RTSK*QM(I))/(1.+DELTIM*DN(I))
61        CONTINUE
62      CONTINUE
70    CONTINUE
 
      RETURN
      END
