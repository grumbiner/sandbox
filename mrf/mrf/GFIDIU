CFPP$ NOCONCUR R
      SUBROUTINE GFIDIU(DG,TG,ZG,UG,VG,RQG,DPHI,DLAM,
     1 RCL,DEL,RDEL2,CI,P1,P2,H1,H2,TOV,SPDMAX,
     2 DTDF,DTDL,DRDF,DRDL,DUDL,DVDL,DUDF,DVDF,
     2 DQDT,DTDT,DRDT,DUDT,DVDT)
C
C INPUT VARIABLES
C
      DIMENSION
     1  DG(#LONF22,#LEVS),TG(#LONF22,#LEVS), ZG(#LONF22,#LEVS),
     2  UG(#LONF22,#LEVS),VG(#LONF22,#LEVS),RQG(#LONF22,#LEVS,#NTRAC),
     3          DPHI(#LONF22),DLAM(#LONF22)
C
      DIMENSION
     1  DTDF(#LONF22,#LEVS),DTDL(#LONF22,#LEVS),
     1  DRDF(#LONF22,#LEVS,#NTRAC),DRDL(#LONF22,#LEVS,#NTRAC),
     1  DUDL(#LONF22,#LEVS),DVDL(#LONF22,#LEVS),
     1  DUDF(#LONF22,#LEVS),DVDF(#LONF22,#LEVS)
C OUTPUT VARIABLES
C
      DIMENSION SPDMAX(#LEVS),
     1  DUDT(#LONF22,#LEVS),DVDT(#LONF22,#LEVS),
     1  DTDT(#LONF22,#LEVS),DRDT(#LONF22,#LEVS,#NTRAC),
     1  DQDT(#LONF22)
C
C CONSTANT ARRAYS
C
       DIMENSION
     1 DEL(#LEVS),RDEL2(#LEVS),
     2 CI(#LEVP1),TOV(#LEVS),
     3 P1(#LEVS),P2(#LEVS),H1(#LEVS),H2(#LEVS)
C
C LOCAL VARIABLES
C
      DIMENSION
     1     CG (#LONF22,#LEVS), DB(#LONF22,#LEVS),CB(#LONF22,#LEVS),
     2     DOT(#LONF22,#LEVP1),DUP(#LONF22,#LEVS),DVP(#LONF22,#LEVS),
     3     DUM(#LONF22,#LEVS ),DVM(#LONF22,#LEVS), EK(#LONF22,#LEVS),
     4     RMU(#LEVS ),RNU(#LEVS),RHO(#LEVS),SI(#LEVP1),
     5      X1(#LEVS ), X2(#LEVS), X3(#LEVS),X4(#LEVS)
C
      RK= #RD /#CP
      SINRA=SQRT(1.-1./RCL)
      FNOR=2.*#OMEGA*SINRA
      FSOU=-FNOR
      SINRA=SINRA/#RERTH
C
      SI(1)=1.0
      DO 4 K=1,#LEVS
      SI(K+1)=SI(K)-DEL(K)
4     CONTINUE
C
      DO 1 K=1,#LEVM1
      RHO(K)=ALOG(SI(K)/SI(K+1))
1     CONTINUE
      RHO(#LEVS)=0.
C
      DO 2 K=1,#LEVS
      RMU(K)=1.-SI(K+1)*RHO(K)/DEL(K)
2     CONTINUE
C
      DO 3 K=1,#LEVM1
      RNU(K+1)=-1.+SI(K)*RHO(K)/DEL(K)
3     CONTINUE
      RNU(1)=0.
C
      DO 20 K=1,#LEVS
      X1(K)=RMU(K)*(1.-RK*RNU(K))/(RMU(K)+RNU(K))
      X2(K)=1.-X1(K)
      X3(K)=(1.+RK*RMU(K))/(1.-RK*RNU(K))
      X4(K)=1./X3(K)
20    CONTINUE
C
      DO 1234 K=1,#LEVS
      SPDMAX(K)=0.
1234  CONTINUE
      RCL2=.5#E0*RCL
C
      DO 140 K=1,#LEVS
      DO 140 J=1,#LONF2
      EK(J,K)=(UG(J,K)*UG(J,K)+VG(J,K)*VG(J,K))*RCL
  140 CONTINUE
C
      DO 10 K=1,#LEVS
      DO 10 J=1,#LONF2
      IF (EK(J,K) .GT. SPDMAX(K))  SPDMAX(K)=EK(J,K)
   10 CONTINUE
C
C     COMPUTE C=V(TRUE)*DEL(LN(PS)).DIVIDE BY COS FOR DEL, COS FOR V
C
      DO 150 J=1,#LONF2
      DPHI(J)=DPHI(J)*RCL
      DLAM(J)=DLAM(J)*RCL
  150 CONTINUE
      DO 180 K=1,#LEVS
      DO 180 J=1,#LONF2
      CG(J,K)=UG(J,K)*DLAM(J)+VG(J,K)*DPHI(J)
  180 CONTINUE
C
      DO 190 J=1,#LONF2
      DB(J,1)=DEL(1)*DG(J,1)
      CB(J,1)=DEL(1)*CG(J,1)
  190 CONTINUE
      DO 210 K=1,#LEVM1
      DO 210 J=1,#LONF2
      DB(J,K+1)=DB(J,K)+DEL(K+1)*DG(J,K+1)
      CB(J,K+1)=CB(J,K)+DEL(K+1)*CG(J,K+1)
  210 CONTINUE
C
C   STORE INTEGRAL OF CG IN DLAX
C
      DO 220 J=1,#LONF2
      DQDT(J)= -CB(J,#LEVS)
  220 CONTINUE
C
C   SIGMA DOT COMPUTED ONLY AT INTERIOR INTERFACES.
C
      DO 230 J=1,#LONF2
      DOT(J,1)=0.#E0
      DVM(J,1)=0.#E0
      DUM(J,1)=0.#E0
      DOT(J,#LEVP1)=0.#E0
      DVP(J,#LEVS )=0.#E0
      DUP(J,#LEVS )=0.#E0
  230 CONTINUE
C
      DO 240 K=1,#LEVM1
      DO 240 J=1,#LONF2
      DOT(J,K+1)=DOT(J,K)+
     1                 DEL(K)*(DB(J,#LEVS)+CB(J,#LEVS)-
     2                 DG(J,K)-CG(J,K))
  240 CONTINUE
C
C
C
      DO 260 K=1,#LEVM1
      DO 260 J=1,#LONF2
      DVP(J,K  )=VG(J,K+1)-VG(J,K)
      DUP(J,K  )=UG(J,K+1)-UG(J,K)
      DVM(J,K+1)=VG(J,K+1)-VG(J,K)
      DUM(J,K+1)=UG(J,K+1)-UG(J,K)
  260 CONTINUE
      DO J=1,#LONF2
       DPHI(J)=DPHI(J)/RCL
       DLAM(J)=DLAM(J)/RCL
      ENDDO
      DO K=1,#LEVS
       DO J=1,#LONF2
        DUDT(J,K)=-UG(J,K)*DUDL(J,K)-VG(J,K)*DUDF(J,K)
     1 -RDEL2(K)*(DOT(J,K+1)*DUP(J,K)+DOT(J,K)*DUM(J,K))
     2 -#RD*TG(J,K)*DLAM(J)
C
        DVDT(J,K)=-UG(J,K)*DVDL(J,K)-VG(J,K)*DVDF(J,K)
     1 -RDEL2(K)*(DOT(J,K+1)*DVP(J,K)+DOT(J,K)*DVM(J,K))
     2 -#RD*TG(J,K)*DPHI(J)
       ENDDO
      ENDDO
C
      DO K=1,#LEVS
       DO J=1,#LONF
        DUDT(J,K)=DUDT(J,K)+VG(J,K)*FNOR
        DUDT(J+#LONF,K)=DUDT(J+#LONF,K)+VG(J+#LONF,K)*FSOU

        DVDT(J,K)=DVDT(J,K)-UG(J,K)*FNOR
     1 -SINRA*EK(J,K)
        DVDT(J+#LONF,K)=DVDT(J+#LONF,K)-UG(J+#LONF,K)*FSOU
     1 +SINRA*EK(J+#LONF,K)
       ENDDO
      ENDDO
      DO K=1,#LEVS
       DO J=1,#LONF2
        DUDT(J,K)=DUDT(J,K)*RCL
        DVDT(J,K)=DVDT(J,K)*RCL
       ENDDO
      ENDDO
C
C
      DO 280 K=1,#LEVM1
      DO 280 J=1,#LONF2
CECMWF:
      DUP(J,K  )=TG(J,K+1)+TOV(K+1)-TG(J,K)-TOV(K)+2.*RK*RNU(K+1)*
     .(TG(J,K)+TOV(K))
CECMWF:
      DUM(J,K+1)=TG(J,K+1)+TOV(K+1)-TG(J,K)-TOV(K)+2.*RK*RMU(K+1)*
     .(TG(J,K+1)+TOV(K+1))
  280 CONTINUE
C
C
      DO K=1,#LEVS
       DO J=1,#LONF2
C       DTDT(J,K)=
        DTDT(J,K)=-UG(J,K)*DTDL(J,K)-VG(J,K)*DTDF(J,K)
     1 -RDEL2(K)*(DOT(J,K+1)*DUP(J,K)+DOT(J,K)*DUM(J,K))
       ENDDO
      ENDDO
C
      DO K=1,#LEVS
       DO J=1,#LONF2
        DTDT(J,K)=DTDT(J,K)
     1  +RK*(TOV(K)+TG(J,K))*(CG(J,K)-CB(J,#LEVS)-DB(J,#LEVS))
       ENDDO
      ENDDO
C
      DO 330 N=1,#NTRAC
      DO 300 K=1,#LEVM1
      DO 300 J=1,#LONF2
      DUP(J,K  )=RQG(J,K+1,N)-RQG(J,K,N)
      DUM(J,K+1)=RQG(J,K+1,N)-RQG(J,K,N)
  300 CONTINUE
      DO 310 J=1,#LONF2
      DUP(J,#LEVS)=0.#E0
  310 CONTINUE
      DO 320 K=1,#LEVS
      DO 320 J=1,#LONF2
      DRDT(J,K,N)=-UG(J,K)*DRDL(J,K,N)-VG(J,K)*DRDF(J,K,N)
     1 -RDEL2(K)*(DOT(J,K+1)*DUP(J,K)+DOT(J,K)*DUM(J,K))
  320 CONTINUE
  330 CONTINUE
C
      RETURN
      END
