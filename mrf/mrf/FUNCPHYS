C-----------------------------------------------------------------------
CFPP$ EXPAND(FPVSX)
      SUBROUTINE GPVS
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GPVS         COMPUTE SATURATION VAPOR PRESSURE TABLE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE SATURATION VAPOR PRESSURE TABLE AS A FUNCTION OF
C   TEMPERATURE FOR THE TABLE LOOKUP FUNCTION FPVS.
C   EXACT SATURATION VAPOR PRESSURES ARE CALCULATED IN SUBPROGRAM FPVSX.
C   THE CURRENT IMPLEMENTATION COMPUTES A TABLE WITH A LENGTH
C   OF 7501 FOR TEMPERATURES RANGING FROM 180. TO 330. KELVIN.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:  CALL GPVS
C
C SUBPROGRAMS CALLED:
C   (FPVSX)  - INLINABLE FUNCTION TO COMPUTE SATURATION VAPOR PRESSURE
C
C COMMON BLOCKS:
C   COMPVS   - SCALING PARAMETERS AND TABLE FOR FUNCTION FPVS.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=7501)
      DIMENSION TBPVS(NX)
      COMMON/COMPVS/ C1XPVS,C2XPVS,TBPVS
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XMIN=180.0
      XMAX=330.0
      XINC=(XMAX-XMIN)/(NX-1)
      C1XPVS=1.-XMIN/XINC
      C2XPVS=1./XINC
      DO JX=1,NX
        X=XMIN+(JX-1)*XINC
        T=X
        TBPVS(JX)=FPVSX(T)
      ENDDO
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FPVS(T)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FPVS         COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE SATURATION VAPOR PRESSURE FROM THE TEMPERATURE.
C   A LINEAR INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GPVS. SEE DOCUMENTATION FOR FPVSX FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS ALMOST 6 DECIMAL PLACES.
C   ON THE CRAY, FPVS IS ABOUT 4 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:   PVS=FPVS(T)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL TEMPERATURE IN KELVIN
C
C   OUTPUT ARGUMENT LIST:
C     FPVS     - REAL SATURATION VAPOR PRESSURE IN KILOPASCALS (CB)
C
C COMMON BLOCKS:
C   COMPVS   - SCALING PARAMETERS AND TABLE COMPUTED IN GPVS.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=7501)
      DIMENSION TBPVS(NX)
      COMMON/COMPVS/ C1XPVS,C2XPVS,TBPVS
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XPVS+C2XPVS*T,1.),FLOAT(NX))
      JX=MIN(XJ,NX-1.)
      FPVS=TBPVS(JX)+(XJ-JX)*(TBPVS(JX+1)-TBPVS(JX))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FPVSQ(T)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FPVSQ        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE SATURATION VAPOR PRESSURE FROM THE TEMPERATURE.
C   A QUADRATIC INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GPVS. SEE DOCUMENTATION FOR FPVSX FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS ALMOST 9 DECIMAL PLACES.
C   ON THE CRAY, FPVSQ IS ABOUT 3 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             QUADRATIC INTERPOLATION
C
C USAGE:   PVS=FPVSQ(T)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL TEMPERATURE IN KELVIN
C
C   OUTPUT ARGUMENT LIST:
C     FPVSQ    - REAL SATURATION VAPOR PRESSURE IN KILOPASCALS (CB)
C
C COMMON BLOCKS:
C   COMPVS   - SCALING PARAMETERS AND TABLE COMPUTED IN GPVS.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=7501)
      DIMENSION TBPVS(NX)
      COMMON/COMPVS/ C1XPVS,C2XPVS,TBPVS
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XPVS+C2XPVS*T,1.),FLOAT(NX))
      JX=MIN(MAX(NINT(XJ),2),NX-1)
      DXJ=XJ-JX
      FJ1=TBPVS(JX-1)
      FJ2=TBPVS(JX)
      FJ3=TBPVS(JX+1)
      FPVSQ=(((FJ3+FJ1)/2-FJ2)*DXJ+(FJ3-FJ1)/2)*DXJ+FJ2
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FPVSX(T)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FPVSX        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE SATURATION VAPOR PRESSURE FROM TEMPERATURE.
C   THE WATER MODEL ASSUMES A PERFECT GAS, CONSTANT SPECIFIC HEATS
C   FOR GAS AND LIQUID, AND NEGLECTS THE VOLUME OF THE LIQUID.
C   THE MODEL DOES ACCOUNT FOR THE VARIATION OF THE LATENT HEAT
C   OF CONDENSATION WITH TEMPERATURE.  THE ICE OPTION IS NOT INCLUDED.
C   THE CLAUSIUS-CLAPEYRON EQUATION IS INTEGRATED FROM THE TRIPLE POINT
C   TO GET THE FORMULA
C       PVS=PSATK*(TR**XA)*EXP(XB*(1.-TR))
C   WHERE TR IS TTP/T AND OTHER VALUES ARE PHYSICAL CONSTANTS
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   PVS=FPVSX(T)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL TEMPERATURE IN KELVIN
C
C   OUTPUT ARGUMENT LIST:
C     FPVSX    - REAL SATURATION VAPOR PRESSURE IN KILOPASCALS (CB)
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD,RV=#RV,
     &          TTP=#TTP,HVAP=#HVAP,PSAT=#PSAT,
     &          CLIQ=#CLIQ,CVAP=#CVAP)
      PARAMETER(PSATK=PSAT*1.E-3)
      PARAMETER(DLDT=CVAP-CLIQ,XA=-DLDT/RV,XB=XA+HVAP/(RV*TTP))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      TR=TTP/T
      FPVSX=PSATK*(TR**XA)*EXP(XB*(1.-TR))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ EXPAND(FTDPXG)
      SUBROUTINE GTDP
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GTDP         COMPUTE DEWPOINT TEMPERATURE TABLE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE DEWPOINT TEMPERATURE TABLE AS A FUNCTION OF
C   VAPOR PRESSURE FOR INLINABLE FUNCTION FTDP.
C   EXACT DEWPOINT TEMPERATURES ARE CALCULATED IN SUBPROGRAM FTDPXG.
C   THE CURRENT IMPLEMENTATION COMPUTES A TABLE WITH A LENGTH
C   OF 5001 FOR VAPOR PRESSURES RANGING FROM 0.001 TO 10.001 KILOPASCALS
C   GIVING A DEWPOINT TEMPERATURE RANGE OF 208.0 TO 319.0 KELVIN.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:  CALL GTDP
C
C SUBPROGRAMS CALLED:
C   (FTDPXG) - INLINABLE FUNCTION TO COMPUTE DEWPOINT TEMPERATURE
C
C COMMON BLOCKS:
C   COMTDP   - SCALING PARAMETERS AND TABLE FOR FUNCTION FTDP.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=5001)
      DIMENSION TBTDP(NX)
      COMMON/COMTDP/ C1XTDP,C2XTDP,TBTDP
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XMIN= 0.001
      XMAX=10.001
      XINC=(XMAX-XMIN)/(NX-1)
      C1XTDP=1.-XMIN/XINC
      C2XTDP=1./XINC
      T=208.0
      DO JX=1,NX
        X=XMIN+(JX-1)*XINC
        PV=X
        T=FTDPXG(T,PV)
        TBTDP(JX)=T
      ENDDO
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTDP(PV)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTDP         COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE DEWPOINT TEMPERATURE FROM VAPOR PRESSURE.
C   A LINEAR INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTDP. SEE DOCUMENTATION FOR FTDPXG FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.0005 KELVIN
C   FOR DEWPOINT TEMPERATURES GREATER THAN 250 KELVIN,
C   BUT DECREASES TO 0.02 KELVIN FOR A DEWPOINT AROUND 230 KELVIN.
C   ON THE CRAY, FTDP IS ABOUT 75 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:   TDP=FTDP(PV)
C
C   INPUT ARGUMENT LIST:
C     PV       - REAL VAPOR PRESSURE IN KILOPASCALS (CB)
C
C   OUTPUT ARGUMENT LIST:
C     FTDP     - REAL DEWPOINT TEMPERATURE IN KELVIN
C
C COMMON BLOCKS:
C   COMTDP   - SCALING PARAMETERS AND TABLE COMPUTED IN GTDP.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=5001)
      DIMENSION TBTDP(NX)
      COMMON/COMTDP/ C1XTDP,C2XTDP,TBTDP
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XTDP+C2XTDP*PV,1.),FLOAT(NX))
      JX=MIN(XJ,NX-1.)
      FTDP=TBTDP(JX)+(XJ-JX)*(TBTDP(JX+1)-TBTDP(JX))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTDPQ(PV)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTDPQ        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE DEWPOINT TEMPERATURE FROM VAPOR PRESSURE.
C   A QUADRATIC INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTDP. SEE DOCUMENTATION FOR FTDPXG FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.00001 KELVIN
C   FOR DEWPOINT TEMPERATURES GREATER THAN 250 KELVIN,
C   BUT DECREASES TO 0.002 KELVIN FOR A DEWPOINT AROUND 230 KELVIN.
C   ON THE CRAY, FTDPQ IS ABOUT 60 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             QUADRATIC INTERPOLATION
C
C USAGE:   TDP=FTDPQ(PV)
C
C   INPUT ARGUMENT LIST:
C     PV       - REAL VAPOR PRESSURE IN KILOPASCALS (CB)
C
C   OUTPUT ARGUMENT LIST:
C     FTDPQ    - REAL DEWPOINT TEMPERATURE IN KELVIN
C
C COMMON BLOCKS:
C   COMTDP   - SCALING PARAMETERS AND TABLE COMPUTED IN GTDP.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=5001)
      DIMENSION TBTDP(NX)
      COMMON/COMTDP/ C1XTDP,C2XTDP,TBTDP
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XTDP+C2XTDP*PV,1.),FLOAT(NX))
      JX=MIN(MAX(NINT(XJ),2),NX-1)
      DXJ=XJ-JX
      FJ1=TBTDP(JX-1)
      FJ2=TBTDP(JX)
      FJ3=TBTDP(JX+1)
      FTDPQ=(((FJ3+FJ1)/2-FJ2)*DXJ+(FJ3-FJ1)/2)*DXJ+FJ2
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
CFPP$ EXPAND(FTDP,FTDPXG)
      FUNCTION FTDPX(PV)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTDPX        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE DEWPOINT TEMPERATURE FROM VAPOR PRESSURE.
C   AN APPROXIMATE DEWPOINT TEMPERATURE FOR FUNCTION FTDPXG
C   IS OBTAINED USING FTDP SO GTDP MUST BE ALREADY CALLED.
C   SEE DOCUMENTATION FOR FTDPXG FOR DETAILS.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   TDP=FTDPX(PV)
C
C   INPUT ARGUMENT LIST:
C     PV       - REAL VAPOR PRESSURE IN KILOPASCALS (CB)
C
C   OUTPUT ARGUMENT LIST:
C     FTDPX    - REAL DEWPOINT TEMPERATURE IN KELVIN
C
C SUBPROGRAMS CALLED:
C   (FTDP)   - INLINABLE FUNCTION TO COMPUTE DEWPOINT TEMPERATURE
C   (FTDPXG) - INLINABLE FUNCTION TO COMPUTE DEWPOINT TEMPERATURE
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      TG=FTDP(PV)
      FTDPX=FTDPXG(TG,PV)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTDPXG(TG,PV)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTDPXG       COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE DEWPOINT TEMPERATURE FROM VAPOR PRESSURE.
C   A GUESS DEWPOINT TEMPERATURE MUST BE PROVIDED.
C   THE WATER MODEL ASSUMES A PERFECT GAS, CONSTANT SPECIFIC HEATS
C   FOR GAS AND LIQUID, AND NEGLECTS THE VOLUME OF THE LIQUID.
C   THE MODEL DOES ACCOUNT FOR THE VARIATION OF THE LATENT HEAT
C   OF CONDENSATION WITH TEMPERATURE.  THE ICE OPTION IS NOT INCLUDED.
C   THE CLAUSIUS-CLAPEYRON EQUATION IS INTEGRATED FROM THE TRIPLE POINT
C   TO GET THE FORMULA
C       PVS=PSATK*(TR**XA)*EXP(XB*(1.-TR))
C   WHERE TR IS TTP/T AND OTHER VALUES ARE PHYSICAL CONSTANTS
C   THE FORMULA IS INVERTED BY ITERATING NEWTONIAN APPROXIMATIONS
C   FOR EACH PVS UNTIL T IS FOUND TO WITHIN 1.E-6 KELVIN.
C   THIS FUNCTION CAN BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   TDP=FTDPXG(TG,PV)
C
C   INPUT ARGUMENT LIST:
C     TG       - REAL GUESS DEWPOINT TEMPERATURE IN KELVIN
C     PV       - REAL VAPOR PRESSURE IN KILOPASCALS (CB)
C
C   OUTPUT ARGUMENT LIST:
C     FTDPXG   - REAL DEWPOINT TEMPERATURE IN KELVIN
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD,RV=#RV,
     &          TTP=#TTP,HVAP=#HVAP,PSAT=#PSAT,
     &          CLIQ=#CLIQ,CVAP=#CVAP)
      PARAMETER(PSATK=PSAT*1.E-3)
      PARAMETER(DLDT=CVAP-CLIQ,XA=-DLDT/RV,XB=XA+HVAP/(RV*TTP))
      PARAMETER(TERRM=1.E-6)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      T=TG
      TR=TTP/T
      PVT=PSATK*(TR**XA)*EXP(XB*(1.-TR))
      EL=HVAP+DLDT*(T-TTP)
      DPVT=EL*PVT/(RV*T**2)
      TERR=(PVT-PV)/DPVT
      T=T-TERR
      DOWHILE(ABS(TERR).GT.TERRM)
        TR=TTP/T
        PVT=PSATK*(TR**XA)*EXP(XB*(1.-TR))
        EL=HVAP+DLDT*(T-TTP)
        DPVT=EL*PVT/(RV*T**2)
        TERR=(PVT-PV)/DPVT
        T=T-TERR
      ENDDO
      FTDPXG=T
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ EXPAND(FTHEX)
      SUBROUTINE GTHE
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GTHE        COMPUTE EQUIVALENT POTENTIAL TEMPERATURE TABLE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE EQUIVALENT POTENTIAL TEMPERATURE TABLE
C   AS A FUNCTION OF LCL TEMPERATURE AND PRESSURE OVER 100 KPA
C   TO THE KAPPA POWER FOR FUNCTION FTHE.
C   EQUIVALENT POTENTIAL TEMPERATURES ARE CALCULATED IN SUBPROGRAM FTHEX
C   THE CURRENT IMPLEMENTATION COMPUTES A TABLE WITH A FIRST DIMENSION
C   OF 241 FOR TEMPERATURES RANGING FROM 183.16 TO 303.16 KELVIN
C   AND A SECOND DIMENSION OF 151 FOR PRESSURE OVER 100 KPA
C   TO THE KAPPA POWER RANGING FROM 0.04**ROCP TO 1.10**ROCP.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:  CALL GTHE
C
C SUBPROGRAMS CALLED:
C   (FTHEX)  - INLINABLE FUNCTION TO COMPUTE EQUIV. POT. TEMPERATURE
C
C COMMON BLOCKS:
C   COMTHE   - SCALING PARAMETERS AND TABLE FOR FUNCTION FTHE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD,TTP=#TTP)
      PARAMETER(ROCP=RD/CP)
      PARAMETER(NX=241,NY=151)
      DIMENSION TBTHE(NX,NY)
      COMMON/COMTHE/ C1XTHE,C2XTHE,C1YTHE,C2YTHE,TBTHE
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XMIN=TTP-90.
      XMAX=TTP+30.
      XINC=(XMAX-XMIN)/(NX-1)
      C1XTHE=1.-XMIN/XINC
      C2XTHE=1./XINC
      YMIN=0.04**ROCP
      YMAX=1.10**ROCP
      YINC=(YMAX-YMIN)/(NY-1)
      C1YTHE=1.-YMIN/YINC
      C2YTHE=1./YINC
      DO JY=1,NY
        Y=YMIN+(JY-1)*YINC
        PK=Y
        DO JX=1,NX
          X=XMIN+(JX-1)*XINC
          T=X
          TBTHE(JX,JY)=FTHEX(T,PK)
        ENDDO
      ENDDO
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTHE(T,PK)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTHE         COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE EQUIVALENT POTENTIAL TEMPERATURE AT THE LCL
C   FROM TEMPERATURE AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   A BILINEAR INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTHE. SEE DOCUMENTATION FOR FTHEX FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA,
C   EXCEPT ZERO IS RETURNED FOR TOO COLD OR HIGH LCLS.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.01 KELVIN.
C   ON THE CRAY, FTHE IS ALMOST 6 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:   THE=FTHE(PV)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL LCL TEMPERATURE IN KELVIN
C     PK       - REAL LCL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTHE     - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C
C COMMON BLOCKS:
C   COMTHE   - SCALING PARAMETERS AND TABLE COMPUTED IN GTHE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=241,NY=151)
      DIMENSION TBTHE(NX,NY)
      COMMON/COMTHE/ C1XTHE,C2XTHE,C1YTHE,C2YTHE,TBTHE
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(C1XTHE+C2XTHE*T,FLOAT(NX))
      YJ=MIN(C1YTHE+C2YTHE*PK,FLOAT(NY))
      IF(XJ.GE.1..AND.YJ.GE.1.) THEN
        JX=MIN(XJ,NX-1.)
        JY=MIN(YJ,NY-1.)
        FTX1=TBTHE(JX,JY)+(XJ-JX)*(TBTHE(JX+1,JY)-TBTHE(JX,JY))
        FTX2=TBTHE(JX,JY+1)+(XJ-JX)*(TBTHE(JX+1,JY+1)-TBTHE(JX,JY+1))
        FTHE=FTX1+(YJ-JY)*(FTX2-FTX1)
      ELSE
        FTHE=0.
      ENDIF
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTHEQ(T,PK)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTHEQ        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE EQUIVALENT POTENTIAL TEMPERATURE AT THE LCL
C   FROM TEMPERATURE AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   A BIQUADRATIC INTERPOLATION IS DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTHE. SEE DOCUMENTATION FOR FTHEX FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA,
C   EXCEPT ZERO IS RETURNED FOR TOO COLD OR HIGH LCLS.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.0002 KELVIN.
C   ON THE CRAY, FTHEQ IS ALMOST 3 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             QUADRATIC INTERPOLATION
C
C USAGE:   THE=FTHEQ(PV)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL LCL TEMPERATURE IN KELVIN
C     PK       - REAL LCL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTHEQ    - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C
C COMMON BLOCKS:
C   COMTHE   - SCALING PARAMETERS AND TABLE COMPUTED IN GTHE.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=241,NY=151)
      DIMENSION TBTHE(NX,NY)
      COMMON/COMTHE/ C1XTHE,C2XTHE,C1YTHE,C2YTHE,TBTHE
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(C1XTHE+C2XTHE*T,FLOAT(NX))
      YJ=MIN(C1YTHE+C2YTHE*PK,FLOAT(NY))
      IF(XJ.GE.1..AND.YJ.GE.1.) THEN
        JX=MIN(MAX(NINT(XJ),2),NX-1)
        JY=MIN(MAX(NINT(YJ),2),NY-1)
        DXJ=XJ-JX
        DYJ=YJ-JY
        FT11=TBTHE(JX-1,JY-1)
        FT12=TBTHE(JX-1,JY)
        FT13=TBTHE(JX-1,JY+1)
        FT21=TBTHE(JX,JY-1)
        FT22=TBTHE(JX,JY)
        FT23=TBTHE(JX,JY+1)
        FT31=TBTHE(JX+1,JY-1)
        FT32=TBTHE(JX+1,JY)
        FT33=TBTHE(JX+1,JY+1)
        FTX1=(((FT31+FT11)/2-FT21)*DXJ+(FT31-FT11)/2)*DXJ+FT21
        FTX2=(((FT32+FT12)/2-FT22)*DXJ+(FT32-FT12)/2)*DXJ+FT22
        FTX3=(((FT33+FT13)/2-FT23)*DXJ+(FT33-FT13)/2)*DXJ+FT23
        FTHEQ=(((FTX3+FTX1)/2-FTX2)*DYJ+(FTX3-FTX1)/2)*DYJ+FTX2
      ELSE
        FTHEQ=0.
      ENDIF
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTHEX(T,PK)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTHEX        COMPUTE SATURATION VAPOR PRESSURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE EQUIVALENT POTENTIAL TEMPERATURE AT THE LCL
C   FROM TEMPERATURE AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   EQUIVALENT POTENTIAL TEMPERATURE IS CONSTANT FOR A SATURATED PARCEL
C   RISING ADIABATICALLY UP A MOIST ADIABAT WHEN THE HEAT AND MASS
C   OF THE CONDENSED WATER ARE NEGLECTED. THE FORMULA FOR
C   EQUIVALENT POTENTIAL TEMPERATURE (DERIVED IN HOLTON) IS
C       THE=T*(PD**(-ROCP))*EXP(EL*EPS*PV/(CP*T*PD))
C   WHERE T IS THE TEMPERATURE, PV IS THE SATURATED VAPOR PRESSURE,
C   PD IS THE DRY PRESSURE P-PV, EL IS THE TEMPERATURE DEPENDENT
C   LATENT HEAT OF CONDENSATION HVAP+DLDT*(T-TTP), AND OTHER VALUES
C   ARE PHYSICAL CONSTANTS DEFINED IN PARAMETER STATEMENTS IN THE CODE.
C   ZERO IS RETURNED IF THE INPUT VALUES MAKE SATURATION IMPOSSIBLE.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   THE=FTHEX(T,PK)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL LCL TEMPERATURE IN KELVIN
C     PK       - REAL LCL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTHEX    - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD,RV=#RV,
     &          TTP=#TTP,HVAP=#HVAP,PSAT=#PSAT,
     &          CLIQ=#CLIQ,CVAP=#CVAP)
      PARAMETER(PSATK=PSAT*1.E-3)
      PARAMETER(ROCP=RD/CP,CPOR=CP/RD,PSATB=PSATK*1.E-2,EPS=RD/RV,
     &          DLDT=CVAP-CLIQ,XA=-DLDT/RV,XB=XA+HVAP/(RV*TTP))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      P=PK**CPOR
      TR=TTP/T
      PV=PSATB*(TR**XA)*EXP(XB*(1.-TR))
      PD=P-PV
      IF(PD.GT.0.) THEN
        EL=HVAP+DLDT*(T-TTP)
        EXPO=EL*EPS*PV/(CP*T*PD)
        FTHEX=T*PD**(-ROCP)*EXP(EXPO)
      ELSE
        FTHEX=0.
      ENDIF
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ EXPAND(FTMAXG)
      SUBROUTINE GTMA
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: GTMA         COMPUTE MOIST ADIABAT TABLES
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE TEMPERATURE AND SPECIFIC HUMIDITY TABLES
C   AS A FUNCTION OF EQUIVALENT POTENTIAL TEMPERATURE AND
C   PRESSURE OVER 100 KPA TO THE KAPPA POWER FOR FUNCTION FTMA.
C   EXACT PARCEL TEMPERATURES ARE CALCULATED IN SUBPROGRAM FTMAXG.
C   THE CURRENT IMPLEMENTATION COMPUTES A TABLE WITH A FIRST DIMENSION
C   OF 151 FOR EQUIVALENT POTENTIAL TEMPERATURES RANGING FROM 200 TO 500
C   KELVIN AND A SECOND DIMENSION OF 121 FOR PRESSURE OVER 100 KPA
C   TO THE KAPPA POWER RANGING FROM 0.01**ROCP TO 1.10**ROCP.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:  CALL GTMA
C
C SUBPROGRAMS CALLED:
C   (FTMAXG) - INLINABLE FUNCTION TO COMPUTE PARCEL TEMPERATURE
C
C COMMON BLOCKS:
C   COMMA    - SCALING PARAMETERS AND TABLE FOR FUNCTION FTMA.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD)
      PARAMETER(ROCP=RD/CP)
      PARAMETER(NX=151,NY=121)
      DIMENSION TBTMA(NX,NY),TBQMA(NX,NY)
      COMMON/COMMA/ C1XMA,C2XMA,C1YMA,C2YMA,TBTMA,TBQMA
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XMIN=200.
      XMAX=500.
      XINC=(XMAX-XMIN)/(NX-1)
      C1XMA=1.-XMIN/XINC
      C2XMA=1./XINC
      YMIN=0.01**ROCP
      YMAX=1.10**ROCP
      YINC=(YMAX-YMIN)/(NY-1)
      C1YMA=1.-YMIN/YINC
      C2YMA=1./YINC
      DO JY=1,NY
        Y=YMIN+(JY-1)*YINC
        PK=Y
        T=XMIN*Y
        DO JX=1,NX
          X=XMIN+(JX-1)*XINC
          THE=X
          T=FTMAXG(T,THE,PK,Q)
          TBTMA(JX,JY)=T
          TBQMA(JX,JY)=Q
        ENDDO
      ENDDO
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTMA(THE,PK,QMA)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTMA         COMPUTE MOIST ADIABAT TEMPERATURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE TEMPERATURE AND SPECIFIC HUMIDITY OF A PARCEL
C   LIFTED UP A MOIST ADIABAT FROM EQUIVALENT POTENTIAL TEMPERATURE
C   AT THE LCL AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   BILINEAR INTERPOLATIONS ARE DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTMA. SEE DOCUMENTATION FOR FTMAXG FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.01 KELVIN
C   AND 5.E-6 KG/KG FOR TEMPERATURE AND HUMIDITY, RESPECTIVELY.
C   ON THE CRAY, FTMA IS ABOUT 35 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXPAND TABLE
C
C USAGE:   TMA=FTMA(THE,PK,QMA)
C
C   INPUT ARGUMENT LIST:
C     THE      - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C     PK       - REAL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTMA     - REAL PARCEL TEMPERATURE IN KELVIN
C     QMA      - REAL PARCEL SPECIFIC HUMIDITY IN KG/KG
C
C COMMON BLOCKS:
C   COMMA    - SCALING PARAMETERS AND TABLE COMPUTED IN GTMA.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=151,NY=121)
      DIMENSION TBTMA(NX,NY),TBQMA(NX,NY)
      COMMON/COMMA/ C1XMA,C2XMA,C1YMA,C2YMA,TBTMA,TBQMA
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XMA+C2XMA*THE,1.),FLOAT(NX))
      YJ=MIN(MAX(C1YMA+C2YMA*PK,1.),FLOAT(NY))
      JX=MIN(XJ,NX-1.)
      JY=MIN(YJ,NY-1.)
      FTX1=TBTMA(JX,JY)+(XJ-JX)*(TBTMA(JX+1,JY)-TBTMA(JX,JY))
      FTX2=TBTMA(JX,JY+1)+(XJ-JX)*(TBTMA(JX+1,JY+1)-TBTMA(JX,JY+1))
      FTMA=FTX1+(YJ-JY)*(FTX2-FTX1)
      QX1=TBQMA(JX,JY)+(XJ-JX)*(TBQMA(JX+1,JY)-TBQMA(JX,JY))
      QX2=TBQMA(JX,JY+1)+(XJ-JX)*(TBQMA(JX+1,JY+1)-TBQMA(JX,JY+1))
      QMA=QX1+(YJ-JY)*(QX2-QX1)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTMAQ(THE,PK,QMA)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTMAQ        COMPUTE MOIST ADIABAT TEMPERATURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: COMPUTE TEMPERATURE AND SPECIFIC HUMIDITY OF A PARCEL
C   LIFTED UP A MOIST ADIABAT FROM EQUIVALENT POTENTIAL TEMPERATURE
C   AT THE LCL AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   BIQUADRATIC INTERPOLATIONS ARE DONE BETWEEN VALUES IN A LOOKUP TABLE
C   COMPUTED IN GTMA. SEE DOCUMENTATION FOR FTMAXG FOR DETAILS.
C   INPUT VALUES OUTSIDE TABLE RANGE ARE RESET TO TABLE EXTREMA.
C   THE INTERPOLATION ACCURACY IS BETTER THAN 0.0005 KELVIN
C   AND 1.E-7 KG/KG FOR TEMPERATURE AND HUMIDITY, RESPECTIVELY.
C   ON THE CRAY, FTMAQ IS ABOUT 25 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             QUADRATIC INTERPOLATION
C
C USAGE:   TMA=FTMAQ(THE,PK,QMA)
C
C   INPUT ARGUMENT LIST:
C     THE      - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C     PK       - REAL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTMAQ    - REAL PARCEL TEMPERATURE IN KELVIN
C     QMA      - REAL PARCEL SPECIFIC HUMIDITY IN KG/KG
C
C COMMON BLOCKS:
C   COMMA    - SCALING PARAMETERS AND TABLE COMPUTED IN GTMA.
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(NX=151,NY=121)
      DIMENSION TBTMA(NX,NY),TBQMA(NX,NY)
      COMMON/COMMA/ C1XMA,C2XMA,C1YMA,C2YMA,TBTMA,TBQMA
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      XJ=MIN(MAX(C1XMA+C2XMA*THE,1.),FLOAT(NX))
      YJ=MIN(MAX(C1YMA+C2YMA*PK,1.),FLOAT(NY))
      JX=MIN(MAX(NINT(XJ),2),NX-1)
      JY=MIN(MAX(NINT(YJ),2),NY-1)
      DXJ=XJ-JX
      DYJ=YJ-JY
      FT11=TBTMA(JX-1,JY-1)
      FT12=TBTMA(JX-1,JY)
      FT13=TBTMA(JX-1,JY+1)
      FT21=TBTMA(JX,JY-1)
      FT22=TBTMA(JX,JY)
      FT23=TBTMA(JX,JY+1)
      FT31=TBTMA(JX+1,JY-1)
      FT32=TBTMA(JX+1,JY)
      FT33=TBTMA(JX+1,JY+1)
      FTX1=(((FT31+FT11)/2-FT21)*DXJ+(FT31-FT11)/2)*DXJ+FT21
      FTX2=(((FT32+FT12)/2-FT22)*DXJ+(FT32-FT12)/2)*DXJ+FT22
      FTX3=(((FT33+FT13)/2-FT23)*DXJ+(FT33-FT13)/2)*DXJ+FT23
      FTMAQ=(((FTX3+FTX1)/2-FTX2)*DYJ+(FTX3-FTX1)/2)*DYJ+FTX2
      Q11=TBQMA(JX-1,JY-1)
      Q12=TBQMA(JX-1,JY)
      Q13=TBQMA(JX-1,JY+1)
      Q21=TBQMA(JX,JY-1)
      Q22=TBQMA(JX,JY)
      Q23=TBQMA(JX,JY+1)
      Q31=TBQMA(JX+1,JY-1)
      Q32=TBQMA(JX+1,JY)
      Q33=TBQMA(JX+1,JY+1)
      QX1=(((Q31+Q11)/2-Q21)*DXJ+(Q31-Q11)/2)*DXJ+Q21
      QX2=(((Q32+Q12)/2-Q22)*DXJ+(Q32-Q12)/2)*DXJ+Q22
      QX3=(((Q33+Q13)/2-Q23)*DXJ+(Q33-Q13)/2)*DXJ+Q23
      QMA=(((QX3+QX1)/2-QX2)*DYJ+(QX3-QX1)/2)*DYJ+QX2
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
CFPP$ EXPAND(FTMA,FTMAXG)
      FUNCTION FTMAX(THE,PK,QMA)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTMAX        COMPUTE MOIST ADIABAT TEMPERATURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE TEMPERATURE AND HUMIDITY OF A PARCEL
C   LIFTED UP A MOIST ADIABAT FROM EQUIVALENT POTENTIAL TEMPERATURE
C   AT THE LCL AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   AN APPROXIMATE PARCEL TEMPERATURE FOR FUNCTION FTMAXG
C   IS OBTAINED USING FTMA SO GTMA MUST BE ALREADY CALLED.
C   SEE DOCUMENTATION FOR FTMAXG FOR DETAILS.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   TMA=FTMAX(THE,PK,QMA)
C
C   INPUT ARGUMENT LIST:
C     THE      - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C     PK       - REAL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTMAX    - REAL PARCEL TEMPERATURE IN KELVIN
C     QMA      - REAL PARCEL SPECIFIC HUMIDITY IN KG/KG
C
C SUBPROGRAMS CALLED:
C   (FTMA)   - INLINABLE FUNCTION TO COMPUTE PARCEL TEMPERATURE
C   (FTMAXG) - INLINABLE FUNCTION TO COMPUTE PARCEL TEMPERATURE
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      TG=FTMA(THE,PK,QG)
      FTMAX=FTMAXG(TG,THE,PK,QMA)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTMAXG(TG,THE,PK,QMA)
C$$$     SUBPROGRAM DOCUMENTATION BLOCK
C
C SUBPROGRAM: FTMAXG       COMPUTE MOIST ADIABAT TEMPERATURE
C   AUTHOR: N PHILLIPS            W/NMC2X2   DATE: 30 DEC 82
C
C ABSTRACT: EXACTLY COMPUTE TEMPERATURE AND HUMIDITY OF A PARCEL
C   LIFTED UP A MOIST ADIABAT FROM EQUIVALENT POTENTIAL TEMPERATURE
C   AT THE LCL AND PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   A GUESS PARCEL TEMPERATURE MUST BE PROVIDED.
C   EQUIVALENT POTENTIAL TEMPERATURE IS CONSTANT FOR A SATURATED PARCEL
C   RISING ADIABATICALLY UP A MOIST ADIABAT WHEN THE HEAT AND MASS
C   OF THE CONDENSED WATER ARE NEGLECTED. THE FORMULA FOR
C   EQUIVALENT POTENTIAL TEMPERATURE (DERIVED IN HOLTON) IS
C       THE=T*(PD**(-ROCP))*EXP(EL*EPS*PV/(CP*T*PD))
C   WHERE T IS THE TEMPERATURE, PV IS THE SATURATED VAPOR PRESSURE,
C   PD IS THE DRY PRESSURE P-PV, EL IS THE TEMPERATURE DEPENDENT
C   LATENT HEAT OF CONDENSATION HVAP+DLDT*(T-TTP), AND OTHER VALUES
C   ARE PHYSICAL CONSTANTS DEFINED IN PARAMETER STATEMENTS IN THE CODE.
C   THE FORMULA IS INVERTED BY ITERATING NEWTONIAN APPROXIMATIONS
C   FOR EACH THE AND P UNTIL T IS FOUND TO WITHIN 1.E-4 KELVIN.
C   THE SPECIFIC HUMIDITY IS THEN COMPUTED FROM PV AND PD.
C   THIS FUNCTION CAN BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             EXACT COMPUTATION
C
C USAGE:   TMA=FTMAXG(TG,THE,PK,QMA)
C
C   INPUT ARGUMENT LIST:
C     TG       - REAL GUESS PARCEL TEMPERATURE IN KELVIN
C     THE      - REAL EQUIVALENT POTENTIAL TEMPERATURE IN KELVIN
C     PK       - REAL PRESSURE OVER 100 KPA TO THE KAPPA POWER
C
C   OUTPUT ARGUMENT LIST:
C     FTMAXG   - REAL PARCEL TEMPERATURE IN KELVIN
C     QMA      - REAL PARCEL SPECIFIC HUMIDITY IN KG/KG
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD,RV=#RV,
     &          TTP=#TTP,HVAP=#HVAP,PSAT=#PSAT,
     &          CLIQ=#CLIQ,CVAP=#CVAP)
      PARAMETER(PSATK=PSAT*1.E-3)
      PARAMETER(ROCP=RD/CP,CPOR=CP/RD,PSATB=PSATK*1.E-2,EPS=RD/RV,
     &          DLDT=CVAP-CLIQ,XA=-DLDT/RV,XB=XA+HVAP/(RV*TTP))
      PARAMETER(TERRM=1.E-4)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      T=TG
      P=PK**CPOR
      TR=TTP/T
      PV=PSATB*(TR**XA)*EXP(XB*(1.-TR))
      PD=P-PV
      EL=HVAP+DLDT*(T-TTP)
      EXPO=EL*EPS*PV/(CP*T*PD)
      THET=T*PD**(-ROCP)*EXP(EXPO)
      DTHET=THET/T*(1.+EXPO*(DLDT*T/EL+EL*P/(RV*T*PD)))
      TERR=(THET-THE)/DTHET
      T=T-TERR
      DOWHILE(ABS(TERR).GT.TERRM)
        TR=TTP/T
        PV=PSATB*(TR**XA)*EXP(XB*(1.-TR))
        PD=P-PV
        EL=HVAP+DLDT*(T-TTP)
        EXPO=EL*EPS*PV/(CP*T*PD)
        THET=T*PD**(-ROCP)*EXP(EXPO)
        DTHET=THET/T*(1.+EXPO*(DLDT*T/EL+EL*P/(RV*T*PD)))
        TERR=(THET-THE)/DTHET
        T=T-TERR
      ENDDO
      FTMAXG=T
      TR=TTP/T
      PV=PSATB*(TR**XA)*EXP(XB*(1.-TR))
      PD=P-PV
      QMA=EPS*PV/(PD+EPS*PV)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      SUBROUTINE GPKAP
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: GPKAP        COMPUTE COEFFICIENTS FOR P**KAPPA
C   AUTHOR: PHILLIPS         ORG: W/NMC2X2   DATE: 29 DEC 82
C
C ABSTRACT: COMPUTE A RATIONAL WEIGHTED CHEBYSHEV APPROXIMATION TO
C   PRESSURE RAISED TO THE KAPPA POWER FOR USE BY THE FUNCTION FPKAP.
C   THE NUMERATOR IS OF ORDER 2 AND THE DENOMINATOR IS OF ORDER 4.
C   THE PRESSURE RANGE IS 40-110 KPA AND KAPPA IS DEFINED IN FPKAPX.
C   IMSL SUBPROGRAM RATCH IS USED TO COMPUTE COEFFICIENTS.
C
C PROGRAM HISTORY LOG:
C   94-12-30  IREDELL
C
C USAGE:  CALL GPKAP
C
C SUBPROGRAMS CALLED:
C   RATCH    - IMSL RATIONAL WEIGHTED CHEBYSHEV APPROXIMATION
C   FPKAPX   - FUNCTION TO COMPUTE EXACT PRESSURE TO THE KAPPA
C   FIDENT   - IDENTITY FUNCTION
C
C COMMON BLOCKS:
C   COMPKAP  - COEFFICIENTS FOR FUNCTION FPKAP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      COMMON/COMPKAP/ CN0,CN1,CN2,CD0,CD1,CD2,CD3,CD4
      EXTERNAL FPKAPX,FIDENT
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      CALL RATCH(FPKAPX,FIDENT,FPKAPX,40.,110.,2,4,CN0,CD0,ERROR)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
      FUNCTION FIDENT(X)
      FIDENT=X
      RETURN
      END
C-----------------------------------------------------------------------
      BLOCK DATA BDPKAP
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: BDPKAP       SET DEFAULT COEFFICIENTS FOR P**KAPPA
C   AUTHOR: PHILLIPS         ORG: W/NMC2X2   DATE: 29 DEC 82
C
C ABSTRACT: SET DEFAULT COEFFICIENTS FOR P**KAPPA.  SUBPROGRAM GPKAP WAS
C   USED TO COMPUTE A RATIONAL WEIGHTED CHEBYSHEV APPROXIMATION TO
C   PRESSURE RAISED TO THE KAPPA POWER FOR USE BY THE FUNCTION FPKAP.
C   THE NUMERATOR IS OF ORDER 2 AND THE DENOMINATOR IS OF ORDER 4.
C   THE PRESSURE RANGE IS 40-110 KPA AND KAPPA IS 287.05/1004.6.
C   IMSL SUBPROGRAM RATCH IS USED TO COMPUTE COEFFICIENTS.
C   SEE SUBPROGRAMS GPKAP AND FPKAP FOR MORE DETAILS.
C
C PROGRAM HISTORY LOG:
C   94-12-30  IREDELL
C
C COMMON BLOCKS:
C   COMPKAP  - COEFFICIENTS FOR FUNCTION FPKAP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      COMMON/COMPKAP/ CN0,CN1,CN2,CD0,CD1,CD2,CD3,CD4
      DATA CN0,CN1,CN2
     & /   3.13198449E-1,5.78544829E-2, 8.35491871E-4/
      DATA CD0,CD1,CD2,CD3,CD4
     & /1.,8.15968401E-2,5.72839518E-4,-4.86959812E-7,5.24459889E-10/
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FPKAP(P)
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: FPKAP        RAISE SURFACE PRESSURE TO THE KAPPA POWER.
C   AUTHOR: PHILLIPS         ORG: W/NMC2X2   DATE: 29 DEC 82
C
C ABSTRACT: RAISE SURFACE PRESSURE OVER 100 KPA TO THE KAPPA POWER
C   USING A RATIONAL WEIGHTED CHEBYSHEV APPROXIMATION.
C   THE NUMERATOR IS OF ORDER 2 AND THE DENOMINATOR IS OF ORDER 4.
C   THE PRESSURE RANGE IS 40-110 KPA AND KAPPA IS DEFINED IN FPKAPX.
C   THE COEFFIECIENTS ARE SET BY CALLING GPKAP OR INCLUDING BDPKAP.
C   THE ACCURACY OF THIS APPROXIMATION IS ALMOST 8 DECIMAL PLACES.
C   ON THE CRAY, FPKAP IS OVER 10 TIMES FASTER THAN EXACT CALCULATION.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C   94-12-30  IREDELL             STANDARDIZED KAPPA,
C                                 INCREASED RANGE AND ACCURACY
C
C USAGE:  PKAP=FPKAP(P)
C
C   INPUT ARGUMENT LIST:
C     P        - REAL SURFACE PRESSURE IN KILOPASCALS (CB)
C                P SHOULD BE IN THE RANGE 40. TO 110.
C
C   OUTPUT ARGUMENT LIST:
C     FPKAP    - REAL P/100 TO THE KAPPA POWER
C
C COMMON BLOCKS:
C   COMPKAP  - COEFFICIENTS FOR FUNCTION FPKAP
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      COMMON/COMPKAP/ CN0,CN1,CN2,CD0,CD1,CD2,CD3,CD4
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      FPKAP=(CN0+P*(CN1+P*CN2))/(CD0+P*(CD1+P*(CD2+P*(CD3+P*CD4))))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FPKAPX(P)
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: FPKAPX       RAISE SURFACE PRESSURE TO THE KAPPA POWER.
C   AUTHOR: PHILLIPS         ORG: W/NMC2X2   DATE: 29 DEC 82
C
C ABSTRACT: RAISE SURFACE PRESSURE OVER 100 KPA TO THE KAPPA POWER.
C   KAPPA IS EQUAL TO RD/CP WHERE RD AND CP ARE PHYSICAL CONSTANTS.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   94-12-30  IREDELL             MADE INTO INLINABLE FUNCTION
C
C USAGE:  PKAP=FPKAPX(P)
C
C   INPUT ARGUMENT LIST:
C     P        - REAL SURFACE PRESSURE IN KILOPASCALS (CB)
C
C   OUTPUT ARGUMENT LIST:
C     FPKAPX   - REAL P/100 TO THE KAPPA POWER
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CP=#CP,RD=#RD)
      PARAMETER(ROCP=RD/CP)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      FPKAPX=(P/100.)**ROCP
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
C-----------------------------------------------------------------------
CFPP$ NOCONCUR R
      FUNCTION FTLCL(T,TDPD)
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: FTLCL        COMPUTE LCL TEMPERATURE.
C   AUTHOR: PHILLIPS         ORG: W/NMC2X2   DATE: 29 DEC 82
C
C ABSTRACT: COMPUTE TEMPERATURE AT THE LIFTING CONDENSATION LEVEL
C   FROM TEMPERATURE AND DEWPOINT DEPRESSION.  THE FORMULA USED IS
C   A POLYNOMIAL TAKEN FROM PHILLIPS MSTADB ROUTINE WHICH EMPIRICALLY
C   APPROXIMATES THE ORIGINAL EXACT IMPLICIT RELATIONSHIP.
C   (THIS KIND OF APPROXIMATION IS CUSTOMARY (INMAN, 1969), BUT
C   THE ORIGINAL SOURCE FOR THIS PARTICULAR ONE IS NOT YET KNOWN. -MI)
C   ITS ACCURACY IS ABOUT 0.03 KELVIN FOR A DEWPOINT DEPRESSION OF 30.
C   THIS FUNCTION SHOULD BE EXPANDED INLINE IN THE CALLING ROUTINE.
C
C PROGRAM HISTORY LOG:
C   91-05-07  IREDELL             MADE INTO INLINABLE FUNCTION
C
C USAGE:  TLCL=FTLCL(T,TDPD)
C
C   INPUT ARGUMENT LIST:
C     T        - REAL TEMPERATURE IN KELVIN
C     TDPD     - REAL DEWPOINT DEPRESSION IN KELVIN
C
C   OUTPUT ARGUMENT LIST:
C     FTLCL    - REAL TEMPERATURE AT THE LCL IN KELVIN
C
C ATTRIBUTES:
C   LANGUAGE: FORTRAN 77.
C   MACHINE:  CRAY.
C
C$$$
      PARAMETER(CLCL1= 0.954442E+0,CLCL2= 0.967772E-3,
     &          CLCL3=-0.710321E-3,CLCL4=-0.270742E-5)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      FTLCL=T-TDPD*(CLCL1+CLCL2*T+TDPD*(CLCL3+CLCL4*T))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
      RETURN
      END
