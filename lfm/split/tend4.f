       SUBROUTINE TEND4
C$$$   SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: TEND4          RADIATIVE HEATING & COOLING
C   AUTHOR: DENNIS DEAVEN    ORG: W/NMC23    DATE: 21 JUN 83
C
C ABSTRACT: COMPUTES HEATING AND COOLING RATES CAUSED BY RADIATION.
C   TEND4 IS CALLED ONLY ONCE EACH HOUR (MNSTEP=1), HOWEVER THE
C   HEATING RATES ARE APPLIED EVERY TIME STEP DURING THE FORECAST.
C
C USAGE:  CALL TEND4
C
C - - - - - - - - - I N P U T   V A R I A B L E S  - - - - - - - - -
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C     US,VS..     FORECAST VARIABLES PLUS FIXED FIELDS      /DEABLK/
C     LI,LJ..     DOMAIN SIZE POINTERS                      /INDEX/
C     K,KL..      LAYER INDICIES                             DITTO
C 
C - - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C     H2          THREE LAYER FIELD OF HEATING RATES        /DEABLK/
C 
C - - - - - - - - - S U B P R O G R A M S   C A L L E D - - - - - - - -
C     NAME(S)                                               LIBRARY
C     -------                                               -------
C     DSXY,ABSH2O                                           LFMLIB
C     MAX                                                   FORTLIB
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C ATTRIBUTES:
C   LANGUAGE: SiliconGraphics 3.5 FORTRAN 77
C   MACHINE:  SiliconGraphics IRIS-4D/25, 35, INDIGO
C$$$
C
      IMPLICIT    REAL (A-H,O-Z)
      REAL        HOUR1
      REAL        ICE, MF
      REAL        CLDALB(3),PLEV(8),PTHH2O(8),AH2O(8),PFRAC(7)
      CHARACTER*4 LABSIN,LABBND,LABS00,LABS06,LABS12,LABV06,LABV12
      COMMON /CNST/ BTHICK,BTHIK1,BTHK3,DT,RDELX,
     1              DTDS0,DTDS1,DTDS2,DTDX,DS1DX,DS2DX,
     2              CP,R,ROCP,TSTRAT,CPTS,SATRH,RSAT60,
     3              RDT,BTHICH,BTHK3H,BTK398,RBT,A1BRN,
     4              A2BRN,TDTDX4,RPK,VVCNST
      COMMON /DIAG/ SUMK(8),SUMP1(8),SUMP2(8),SUMS(8),SUMF(8),SUMFD(8),
     1     SUMVOR(8),SUMDIV(8),DP(8),SUMP(8),SUMTS(8),SUMZ(8),
     2     SIGB1,SIGT1,SIGT2,SIGS1,SIGS2,RMSSFC,RMSTRP
      COMMON /FASTER/ PIE(1100),AX(120)
      COMMON /DEABLK/ US( 53 , 45 , 21 ),
     A     VS( 53 , 45 , 21 ),
     1     TS( 53 , 45 , 21 ),PSIG( 53 , 45 , 6 ),
     2     ST( 53 , 45 ),ICE( 53 , 45 ),
     3     COSZEN( 53 , 45 ),XM( 53 , 45 ),
     4     F( 53 , 45 ),CD( 53 , 45 ),
     5     SATW( 53 , 45 ,3),WS( 53 , 45 , 9 ),
     6     PREC( 53 , 45 ),ZSTAR( 53 , 45 ),
     7     ALON( 53 , 45 ),ALAT( 53 , 45 ),
     8     MF( 53 , 45 ),H2( 53 , 45 , 3 )
      COMMON /VERTV/ IVVEL( 53 , 45 , 8 )
      COMMON /VTEMP/ SIGDOT( 53 , 45 ,5 ), VT( 53 , 45 ,25 )
      COMMON /INDEX/ LI,LJ,LK,LI1,LJ1,LK1,LI2,NIJ,LOLD,LMID,LNEW,
     1     K7OLD,K7MID,K7NEW,K3OLD,K3MID,K3NEW,K2OLD,K2MID,K2NEW,
     2     K,K1,K2,K3,KL,KH
      COMMON /FDATE/  IYEAR, IMO, IDAYMO, IZTIME, IHR1, IOUT
      COMMON /SVHOUR/ NWDS, IHOUR1( 1205 ), HOUR1(8)
      COMMON /TTME/   MNSTEP,IODD,IHOUR,IMONTH,ITSW,IVEL,NPHOUR,
     1                SSLDC,CSLDC,SSLHR,CSLHR,SHR,CHR,
     2                ALP,XDAYMO
      COMMON /FORTAP/ LUNBND,LUNSIN,LUNS00,LUNS06,LUNS12,LUNV06,LUNV12,
     A                LABSIN(8),LABBND(8),LABS00(8),LABS06(8),LABS12(8),
     B                LABV06(8),LABV12(8),PARM(25)
      COMMON /PBNDPE/ ALARGE( 53, 12, 23 ), BLARGE( 12, 33, 23 )
      COMMON /PBNDRY/ AA( 53, 12, 23 ), BB( 12, 33, 23 )
      SAVE
C
C   STORE LAYER 3 WS BAR XY IN VT(1,1,16)
C   STORE LAND SEA   BAR XY IN VT(1,1,17)
C   STORE SATW       BAR XY IN VT(1,1,18-20)
C
       CALL DSXY(VT(1,1,16),WS(1,1,K3MID+2))
       CALL DSXY2N(VT(1,1,17),XM)
       CALL DSXY2N(VT(1,1,18),SATW(1,1,1))
       CALL DSXY2N(VT(1,1,19),SATW(1,1,2))
       CALL DSXY2N(VT(1,1,20),SATW(1,1,3))
C
C     DETERMINE IF CLOUDS EXIST AND SET CLDALB ACCORDINGLY
C     CLOUDS ASSUMED IF RELATIVE HUMIDITY .GE. 60 PER CENT
      N3IJ = 3 * 2385
      DO 88890 IQ2W6E=1,N3IJ
         H2(IQ2W6E,1,1)=0.E0
88890 CONTINUE
      IF(IHOUR .GE. 48 ) GO TO 3600
      DO 99 J = 1,       44
      DO 99 I = 1,       52
      TOPCLD = 0.E0
      CLDALB(1) = 0.E0
      CLDALB(2) = 0.E0
      CLDALB(3) = 0.E0
      IF (VT(I,J,16)/VT(I,J,20).GE. (-RSAT60)) CLDALB(3) = .5E0
      IF (VT(I,J,KH+7)/VT(I,J,19).GE.(-RSAT60))CLDALB(2) = .75E0
      IF(VT(I,J,KL+7)/VT(I,J,18).GE.(-RSAT60)) CLDALB(1) = 1.E0
C
C     CLOUD TOP ALBEDO
C
      TOPCLD = MAX(CLDALB(1),CLDALB(2),CLDALB(3))
C
C     DETERMINE NATURE OF SURFACE AND SET GNDALB ACCORDINGLY
C     ICE        CONTAINS 1. IF SNOW   0. IF NO SNOW
C     XM         ONTAINS 1.E-4 IF SEA   0. IF LAND
C     GNDALB = .90 OVER LAND   1.0 OVER SNOW OR SEA
C     GNDALB = .9 + 1/2(SNOW + SEA) LT = 1
C
      GNDALB =.9E0+ (ICE(I,J) + VT(I,J,17)*1.E4) * 0.5E0
      GNDALB = MIN(1.E0,GNDALB)
C
C     IF SUN UP DO  SOLAR HEATING CALCULATIONS (DEGREES/MIN)
C
      IF(COSZEN(I,J).LE.0.E0) GO TO 330
C
C     SOLAR RADIATION  1
C     MANABE@S SOLAR RADIATION EQUATION NO CLOUDS
C     FOR 6-LAYER PE MODEL
C     SR/2
C     EFFECT OF CLOUD  ON GROUND WARMING ONLY AND ONLY BY DECREASING SUN
C     SR/3   ---   PRESSURE BROADENING EXPONENT CHANGED TO 1
C     SR/4   ---   ELIMINATE CO2 WARMING  NEGLEGABLE AND TIME CONSUMING
C     COMPUTE            DISTRIBUTOIN OF WATER IN LAYERS 1 - 4
C                 (NONE ABOVE)
C
      PFRAC(1) = VT(I,J,KL+7)
      PFRAC(2) = VT(I,J,KH+7)
      PFRAC(3) = VT(I,J,16)
      PFRAC(4) = 0.E0
      PFRAC(5) = 0.E0
      PFRAC(6) = 0.E0
      PFRAC(7) = 0.E0
C
C     COMPUTE PRESSURE AT EACH SIGMA LEVEL
C
      PLEV(8) = 50.E0
      PLEV(7)=PLEV(8)+VT(I,J,11)
      PLEV(6) = PLEV(7) + VT(I,J,11)
      PLEV(5) = PLEV(6) + VT(I,J,11)
      PLEV(4) = PLEV(5) + VT(I,J,10)
      PLEV(3) = PLEV(4) + VT(I,J,10)
      PLEV(2) = PLEV(3) + VT(I,J,10)
      PLEV(1) = PLEV(2) + BTHICK
C
C     COMPUTE CUMULATIVE PATH LENGTHS FROM BOTTOM UP
C     SUBSCRIPT GIVES LEVEL TO WHICH LENGTH CALCULATED FROM LEV = 1
C     LEV = 1  GROUND     LEV = 7 TOP    (PATH LENGTH TO INFINITY)
C
      PTHH2O(1) = 0.E0
      DO 300 LEV = 1,  7
C     0.68 = PRESSURE BROADENING EXPONENT. SAME FOR ALL GASSES
C     CHANGED TO 1.  IN SR/3
C     PDIF = PLEV(LEV) - PLEV(LEV-1)
C     PBROAD = ((PLEV(LEV-1) + PDIF*0.5)*0.001)**0.68
C     PBROAD = ((PLEV(LEV-1) + PDIF*0.5)*0.001)
C
      PBROAD = (PLEV(LEV) + PLEV(LEV+1)) * .0005E0
C
C     TOTAL PATH LENGTH FROM BELOW TO LAYER MEAN PRESSURE
C
      PTHH2O(LEV+1) = PTHH2O(LEV) + PFRAC(LEV) * PBROAD
  300 CONTINUE
C
C     COMPUTE RADIATION ABSORBED DOWN TO  EACH LEVEL
C
      DO 310 LEV = 1,         8
      PT=(PTHH2O(8)-PTHH2O(LEV))/COSZEN(I,J)
      AH2O(LEV) = 0.E0
C
C     VALUE OF ABSH20 IS LESS THAN ABOUT .2
C
      IF (PT.GT.1.E-5) AH2O(LEV) = ABSH2O(PT)
  310 CONTINUE
C
C     COMPUTE HEATING LAYER BY LAYER  DEGREES/MIN
C     NEGATIVE INDICATES WARMING
C                  980. (= ACCELERATION OF GRAVITY)*(-0.016667)(MIN/SEC)
C  -6.8056E-2 = --------------------------------------------------------
C               0.24 (= SPECIFIC HEAT OF AIR) * E3 (=DYNES/CM*CM PER MB)
       ACOSZ = COSZEN(I,J) * (-6.8056E-2)
      A1 = ACOSZ/BTHICK
C
C     SOLAR RADIATIVE ENERGY ABSORBED BY H20 IN BNDRY LAYER.
C
      H2(I,J,1) = -A1*(AH2O(2)-AH2O(1))
C
C     SAME FOR LAYERS 2,3.
C
      DO 320 LAY=2,3
      H2(I,J,LAY)=(ACOSZ/(PLEV(LAY+1)-PLEV(LAY)))
     1                * (AH2O(LAY+1) - AH2O(LAY))
  320 CONTINUE
C
C     LAYER 1 WARMED ALSO BY NON-REFLECTED RADIATION REACHING THE GROUND
C     COMPUTE DOWNWARD FLUX OF TOTAL RADIATION AT BOTTOM LEVEL
C     1.86 = SOLAR CONSTANT DEPLETED BY 7 PERCENT RAYLEIGH SCATTERING
C     FLUX AT GROUND REDUCED BY CLOUDS (IF ANY)
C
      IF(TOPCLD .EQ.1.E0.OR. GNDALB.EQ.1.E0) GO TO 330
      GNDWRM = A1 *(1.86E0- AH2O(1)) * (1.E0-TOPCLD) * (1.E0-
C
C     A1 IS NEGATIVE
C
     1         GNDALB)
      H2(I,J,1) = H2(I,J,1) + GNDWRM
C
C                            ... AND IT WAS THE EVENING AND THE MORNING
C      COOL ALL LAYERS ABOVE CLOUDS BY 0.06 DEG/HOUR
C                   (= 0.001 DEG/MIN)
C
C     ONE PATH HERE IF NIGHT.
C
  330 KBOT = 1
      TOPCLD = 0.E0
      DO 335 L=1,3
C
C     KBOT IS INDEX FOR FIRST LAYER ABOVE CLOUDS
C
      IF (CLDALB(L) .GT.0.E0) KBOT = L+1
 335  CONTINUE
C
C     GO TO 336 IF NO CLOUDS
C
      IF(KBOT.EQ.1) GO TO 336
      L = KBOT-1
C
C     CLDALB IS ALBEDO OF TOP LAYER OF CLOUDS
C
      TOPCLD = CLDALB(L)
 336  IF (KBOT.GT.3) GO TO 350
C
      DO 340 L = KBOT,3
        H2(I,J,L) = H2(I,J,L) + 1.6667E-5
  340 CONTINUE
C
C     UPPER LEVEL COOLING (LAYERS 4-7, SEE TEND5)
C
 350  CONTINUE
 370  CONTINUE
C     IF NO CLOUDS BUT THERE IS SNOW AND NIGHT TIME (SOLZEN .GT. 80 DEG)
C              (TAKEN CARE OF IN CALCULATION OF COSZEN)
C     COOL BOUNDRY LAYER BY 0.1 DEG/HOUR (= 0.0016667 DEG/MIN)
C      ADDITIONAL
C     COOLING ABOVE CLOUDS ADDED
C
C      CONTINUE SPECIAL   KEEP SNOW COOLING IF APPROPRIATE
C
 360  CONTINUE
      IF (TOPCLD.NE.0.E0)   GO TO 365
      IF (ICE(I,J).EQ.0.E0) GO TO 365
C
C     IF NO CLOUDS AND IF SNOW AND IF NIGHT,
C     THEN COOL LOWEST LAYER
C
      IF (COSZEN(I,J).LE.0.E0)H2(I,J,1) = H2(I,J,1)+2.7778E-5
 365  CONTINUE
 99   CONTINUE
3600  RETURN
      END
