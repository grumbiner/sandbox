      SUBROUTINE FCST
C$$$  SUBPROGRAM  DOCUMENTATION  BLOCK
C
C SUBPROGRAM: FCST           FORECAST FOR ONE TIME  STEP
C   AUTHOR: DENNIS DEAVEN    ORG: W/NMC23    DATE: 17 JUN 83
C
C ABSTRACT: FORECAST LFM FOR ONE TIME STEP.
C
C
C USAGE:  CALL FCST
C
C
C  - - - - - - - - - I N P U T   V A R I A B L E S  - - - - - - - - -
C
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C     ALARGE      ARRAY OF HORIZONTAL BOUNDARY              /PBNDPE/
C     BLARGE      VALUE QUANTITIES (IN AND OUT)
C
C     AA,BB       HORIZ. BOUNDARY VALUE INCREMENTS          /PBNDRY/
C     ALP         TIME SMOOTHER FACTOR                      /TTME/
C     CSLDL,SSLDL COS, SIN SOLAR DECLINATION
C     CSLHR,SSLHR COS, SIN SOLAR HOUR ANGLE
C     IHOUR       CURRENT HOUR OF FORECAST
C     IODD        ODD EVEN TIME STEP FLAG
C     ITSW        TIMESTEP TYPE FLAG
C                 1=FORWARD(INITIAL), 4=CENTERED W/ SMOOTHING
C     MNSTEP      COUNT OF TIME STEP WITHIN HOUR
C                 (=-1 FOR FORWARD, =0 FOR DISASTER)
C     NPHOUR      NO. OF TIMESTEPS PER HOUR
C     BTHICH      BOUND LAYER PRESSURE THICK / 2.           /CNST/
C     BTHICK      BOUND LAYER PRESS THICK
C     RDELX       1/(GRID LENGTH) KM**-1
C     IHR1        CURRENT HOUR OF FORECAST (=IHOUR)         /FDATE/
C     IOUT        HOUR TO STOP THIS FORECAST SEGMENT
C     HOUR1       CURRENT FORECAST HOUR (=IHOUR)            /SVHOUR/
C     LUNS06,S12  UNIT NUMBERS FOR OUTPUT FILES             /FORTAP/
C     LUNV06,V12  FOR SIGMA AND OMEGA OUTPUT
C     LABS00(3)   ON.85 DATE WORD
C     US,VS,TS,   ALL THE FORECAST AND CONSTANT FIELDS      /DEABLK/
C     PSIG,WS,ETC UPDATED AND ADJUSTED
C     LOLD,LMID,LNEW  ROW COUNTERS FOR TIME STEPS           /INDEX/
C     K7OLD...    K COUNTERS FOR 7 LAYER VERTICAL INDEX     /INDEX/
C     K3OLD...        DITTO      3        DITTO
C     K2OLD...        DITTO      2        DITTO
C
C - - - - - - - - - O U T P U T   V A R I A B L E S - - - - - - - - - -
C
C     NAMES       MEANING/CONTENT/PURPOSE/UNITS/TYPE        INTERFACE
C     -----       ----------------------------------        ---------
C     US,VS,TS    FORECAST VARIABLES                        /DEABLK/
C     WS, ETC.    CONVECTIVELY ADJUSTED AND BOUNDARYIZED
C     COSZEN      COS(SOLAR ZENITH ANGLE)
C     LABS06/12   ON. 85 LABLES FOR                    UNIT LUNS06/S12
C     LABV06/12   SIGMA AND VVEL OUTPUT FILES               LUNV06/V12
C     HOUR1       DATE TIME FOR  SIGMA                      LUNS06/S12
C
C - - - - - - - - - S U B P R O G R A M S   C A L L E D - - - - - - - -
C
C     NAME(S)                                               LIBRARY
C     -------                                               -------
C      NFBO1,   SATM,  TENPT,  TENUV,                        LFM
C      DSXY, WATER, STATS, SETBC, TSMO, WRTAPE               LFM
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C ATTRIBUTES:
C   LANGUAGE: SiliconGraphics 3.5 FORTRAN 77
C   MACHINE:  SiliconGraphics IRIS-4D/25, 35, INDIGO
C
C$$$
C
      IMPLICIT    REAL (A-H,O-Z)
      REAL        HOUR1
      REAL        ICE, MF
      LOGICAL     IFWRTE
      CHARACTER*4 LABSIN,LABBND,LABS00,LABS06,LABS12,LABV06,LABV12
      CHARACTER*4 WASH,INGT,ON,LFMS,LFMV,ZERO,LABS2(8)
      COMMON /INDEX/ LI,LJ,LK,LI1,LJ1,LK1,LI2,NIJ,LOLD,LMID,LNEW,
     1     K7OLD,K7MID,K7NEW,K3OLD,K3MID,K3NEW,K2OLD,K2MID,K2NEW,
     2     K,K1,K2,K3,KL,KH
      COMMON /FDATE/  IYEAR, IMO, IDAYMO, IZTIME, IHR1, IOUT
      COMMON /SVHOUR/ NWDS, IHOUR1( 1205 ), HOUR1(8)
      COMMON /TTME/   MNSTEP,IODD,IHOUR,IMONTH,ITSW,IVEL,NPHOUR,
     1                SSLDC,CSLDC,SSLHR,CSLHR,SHR,CHR,
     2                ALP,XDAYMO
      COMMON /FORTAP/ LUNBND,LUNSIN,LUNS00,LUNS06,LUNS12,LUNV06,LUNV12,
     A                LABSIN(8),LABBND(8),LABS00(8),LABS06(8),LABS12(8),
     B                LABV06(8),LABV12(8),PARM(25)
      COMMON /PBNDPE/ ALARGE( 53 ,12, 23 ), BLARGE( 12, 33 ,23 )
      COMMON /PBNDRY/ AA( 53 ,12, 23 ), BB( 12, 33 ,23 )
      COMMON /CNST/ BTHICK,BTHIK1,BTHK3,DT,RDELX,
     1              DTDS0,DTDS1,DTDS2,DTDX,DS1DX,DS2DX,
     2              CP,R,ROCP,TSTRAT,CPTS,SATRH,RSAT60,
     3              RDT,BTHICH,BTHK3H,BTK398,RBT,A1BRN,
     4              A2BRN,TDTDX4,RPK,VVCNST
      COMMON /DIAG/ SUMK(8),SUMP1(8),SUMP2(8),SUMS(8),SUMF(8),SUMFD(8),
     1     SUMVOR(8),SUMDIV(8),DP(8),SUMP(8),SUMTS(8),SUMZ(8),
     2     SIGB1,SIGT1,SIGT2,SIGS1,SIGS2,RMSSFC,RMSTRP
      COMMON /FASTER/ PIE(1100),AX(120)
      COMMON /DEABLK/ US( 53 , 45 , 21 ),
     A     VS( 53 , 45 , 21 ),
     1     TS( 53 , 45 , 21 ),PSIG( 53 , 45 ,6 ),
     2     ST( 53 , 45 ),ICE( 53 , 45 ),
     3     COSZEN( 53 , 45 ),XM( 53 , 45 ),
     4     F( 53 , 45 ),CD( 53 , 45 ),
     5     SATW( 53 , 45 ,3),WS( 53 , 45 ,9 ),
     6     PREC( 53 , 45 ),ZSTAR( 53 , 45 ),
     7     ALON( 53 , 45 ),ALAT( 53 , 45 ),
     8     MF( 53 , 45 ),H2( 53 , 45 , 3 )
      COMMON /VERTV/ IVVEL( 53, 45, 8 )
      SAVE
C
C     PIECES OF ON85 LABELS.
C
C
      DATA  WASH  /'WASH'/, INGT/'INGT'/, ON/'ON  '/,
     A      LFMS  /'LFMS'/, LFMV/'LFMV'/,
     B      ZERO  /'0000'/
      DATA  LABS2 /'06B2','12B2','18B2','24B2',
     A             '30B2','36B2','42B2','48B2'/
C
C         SET UP IWO,IPO
C         IWO IS FOR VERTICAL VELOCITY, IPO FOR SIGMA STRIPS
C           ROWS ARE OUTPUT EVERY SIX HOURS ON THE HOUR
C
C     WHEN IFWRTE = .FALSE. DONT WRITE VVEL OR SIGMA STRIPS
C
       IFWRTE = .FALSE.
       IF (MOD(IHOUR+1,6).NE.0) GO TO 931
       IF (MNSTEP.NE.NPHOUR) GO TO 931
       IFWRTE = .TRUE.
C
C     I6, I12 ARE INDICES IN LABS2 TO CHOOSE CORRECT FILE LABEL
C     FOR UNITS LUNS06, LUNS12, LUNV06 AND LUNV12.
C
         I6  = (IHOUR+1)/6
         I12 = I6
         IF (MOD(IHOUR+1,12).NE.0) GO TO 902
           IWO       = LUNV12
           IPO       = LUNS12
           LABS12(1) = LFMS
           LABS12(2) = LABS2(I12)
           LABS12(3) = LABS00(3)
           LABS12(4) = ZERO
           LABS12(5) = ZERO
           LABS12(6) = WASH
           LABS12(7) = INGT
           LABS12(8) = ON
           LABV12(1) = LFMV
           LABV12(2) = LABS2(I12)
           LABV12(3) = LABS00(3)
           LABV12(4) = ZERO
           LABV12(5) = ZERO
           LABV12(6) = WASH
           LABV12(7) = INGT
           LABV12(8) = ON
C
C     NOW WRITE LABELS.
C
           WRITE (LUNS12) LABS12
           WRITE (LUNV12) LABV12
           GO TO 933
902      CONTINUE
           IWO       = LUNV06
           IPO       = LUNS06
           LABS06(1) = LFMS
           LABS06(2) = LABS2(I6)
           LABS06(3) = LABS00(3)
           LABS06(4) = ZERO
           LABS06(5) = ZERO
           LABS06(6) = WASH
           LABS06(7) = INGT
           LABS06(8) = ON
           LABV06(1) = LFMV
           LABV06(2) = LABS2(I6)
           LABV06(3) = LABS00(3)
           LABV06(4) = ZERO
           LABV06(5) = ZERO
           LABV06(6) = WASH
           LABV06(7) = INGT
           LABV06(8) = ON
C
C     NOW WRITE LABELS.
C
           WRITE (LUNS06) LABS06
           WRITE (LUNV06) LABV06
  933    CONTINUE
931    CONTINUE
       IVEL   = 0
       RMSSFC = 0.0E0
       RMSTRP = 0.0E0
C
C  SET IVEL = 1, TWO HOURS PRIOR TO POST TIME FOR EVEN TIME STEPS
C  IVEL IS THE FLAG FOR COMPUTING VERT VELS, 1 = YES, 0 = NO
C
       IF ((IHOUR+2-IOUT) .LT. 0) GO TO 31
       IF (IODD .EQ. 2) IVEL = 1
       IF (IHR1 .EQ.0 .OR. MOD(IOUT,6) .NE.0) IVEL = 0
31     CONTINUE
C
C   ZERO PRECIP FIELD EVER 12 HOURS, PRECIP COLLECTED EACH 12 HOURS
C
       IF (.NOT.(MNSTEP.LT.2.AND.MOD(IHOUR,12).EQ.0)) GO TO 30
C
       DO 29 J = 1,  45
       DO 29 I = 1,  53
29     PREC(I,J) = 0.0E0
C
30     CONTINUE
C
C  NO PRECIP COLLECTED DURING FIRST 4 HOURS, (ENGINEERING FOR CONV ADJ)
C
       IF (IHOUR .GT. 3) GO TO 8
       DO 9 J = 1,45
       DO 9 I = 1,53
         PREC(I,J) = 0.0E0
9      CONTINUE
8      CONTINUE
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / /
C     ADVANCE PE BOUNDARIES IF NOT FIRST TIME STEP
C
      IF (MNSTEP.LT.0) GO TO 700
      DO 701 K = 1,23
      DO 701 J = 1,12
      DO 701 I = 1,53
        ALARGE(I,J,K) = ALARGE(I,J,K) + AA(I,J,K)
  701 CONTINUE
      DO 702 I = 1,23
      DO 702 J = 1,33
      DO 702 K = 1,12
        BLARGE(K,J,I) = BLARGE(K,J,I) + BB(K,J,I)
  702 CONTINUE
C    CHECK FOR THIN POINTS
      DO 738 K = 1,12
      DO 738 I = 1,  53
      IF (ALARGE(I,K,23).GE.15.E0) GO TO 750
      CHP = 15.E0- ALARGE(I,K,23)
      ALARGE(I,K,22) = ALARGE(I,K,22) - CHP
      ALARGE(I,K,23) = 15.E0
750   CONTINUE
      IF ( I.GT.33 ) GO TO 738
      IF (BLARGE(K,I,23).GE.15.E0) GO TO 760
      CHP = 15.E0- BLARGE(K,I,23)
      BLARGE(K,I,22) = BLARGE(K,I,22) - CHP
      BLARGE(K,I,23) = 15.E0
760   CONTINUE
738   CONTINUE
700   CONTINUE
      DO 1500 I = 1,53
      DO 1500 L = 1,12,11
      DO 1500 K = 1,14
      ALARGE(I,L,K)=SIGN(MIN(ABS(ALARGE(I,L,K)),120.E0),ALARGE(I,L,K))
      IF ( I.GT.33 ) GO TO 1500
      BLARGE(L,I,K)=SIGN(MIN(ABS(BLARGE(L,I,K)),120.E0),BLARGE(L,I,K))
1500  CONTINUE
      DO 1510 I = 1,53,52
      DO 1510 L = 2,11
      DO 1510 K = 1,14
        ALARGE(I,L,K) = SIGN(MIN(ABS(ALARGE(I,L,K)),120.E0),
     A     ALARGE(I,L,K))
1510  CONTINUE
C
C / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / / /
C
C     LOLD,LMID,LNEW ARE TIME COUNTERS WHICH CYCLICALLY TAKE
C     ON THE VALUES 0,1,2.
C     K7OLD,K7MID,K7NEW REFER TO VALUES OF THIRD INDEX.
C     K3OLD,K3MID,K3NEW            DITTO
C     K2OLD,K2MID,K2NEW            DITTO
C
      K = 1
      K7OLD = K + LOLD * 7
      K7MID = K + LMID * 7
      K7NEW = K + LNEW * 7
      K3OLD = K + LOLD * 3
      K3MID = K + LMID * 3
      K3NEW = K + LNEW * 3
      K2OLD = K + LOLD * 2
      K2MID = K + LMID * 2
      K2NEW = K + LNEW * 2
C
C     COMPUTE LATITUDE AND LONGITUDE FOR INITIAL TIME AND ALL RECOVERYS
C     AT CENTER OF GRID SQUARES, SINCE (I,JCOUNT) (IN TEND1)
C     REFERS TO THE LOWER LEFT CORNER OF A GRID SQUARE ADJUST II + JJ
C
      LIHLF =  53 / 2
      IF (MNSTEP.LT.0) GO TO 14
      IF (MNSTEP.EQ.0) GO TO 13
      IF (MNSTEP.EQ.1) GO TO 17
      IF (MNSTEP.GE.2) GO TO 19
   13 CONTINUE
      PRINT 600
  600 FORMAT(//10X,'0MNSTEP = 0 IN FCST AT FORMAT 600'//)
      CALL ERREXT
 14   CONTINUE
C
C  MOVE TAU-1 LEVEL INTO TAU LEVEL AT INITIAL STEP ONLY
C
      N3IJ = 3 * 2385
      N7IJ = 7 * 2385
      N2IJ = 2 * 2385
      DO 88890 IQ2W6E=1,N3IJ
         WS(IQ2W6E,1,K3MID)=WS(IQ2W6E,1,K3OLD)
88890 CONTINUE
      DO 88900 IQ2W6E=1,N7IJ
         TS(IQ2W6E,1,K7MID)=TS(IQ2W6E,1,K7OLD)
         US(IQ2W6E,1,K7MID)=US(IQ2W6E,1,K7OLD)
         VS(IQ2W6E,1,K7MID)=VS(IQ2W6E,1,K7OLD)
88900 CONTINUE
      DO 88930 IQ2W6E=1,N2IJ
         PSIG(IQ2W6E,1,K2MID)=PSIG(IQ2W6E,1,K2OLD)
88930 CONTINUE
C
C  CALCULATE LATITUDE AND LONGITUDE FIRST STEP ONLY
C
      DO 15 JCOUNT = 1,        44
      XJ = JCOUNT
      XJ = XJ + .5E0
      DO 15 I=1,LIHLF
      XI = I
      XI = XI + .5E0
15    CALL NFB01(XI,XJ,ALAT(I,JCOUNT),ALAT(I+LIHLF,JCOUNT),
     1     ALON(I,JCOUNT),ALON(I+LIHLF,JCOUNT))
C
C COMPUTE MAP FACTOR SQUARED
C
      DO 28 JCOUNT = 1,45
      DO 28 I=1,  53
        MF(I,JCOUNT) = REAL(1.86603E0/(1.E0+F(I,JCOUNT)))**2
28    CONTINUE
C
C------ FORM XYBAR ON ST,ICE, AND CD AND STORE BACK IN ORIGINAL----
C
      IF(IHOUR .NE. 0) GO TO 17
      NIJ = 2385
      CALL DSXY2N(US(1,1,K7NEW),ST)
      DO 88940 IQ2W6E=1,NIJ
         ST(IQ2W6E,1)=US(IQ2W6E,1,K7NEW)
88940 CONTINUE
      CALL DSXY2N(US(1,1,K7NEW),ICE)
      DO 88950 IQ2W6E=1,NIJ
         ICE(IQ2W6E,1)=US(IQ2W6E,1,K7NEW)
88950 CONTINUE
      CALL DSXY2N(US(1,1,K7NEW),CD)
      DO 88960 IQ2W6E=1,NIJ
         CD(IQ2W6E,1)=US(IQ2W6E,1,K7NEW)
88960 CONTINUE
   17    CONTINUE
C
C     COMPUTE COSINE OF SOLAR ZENITH ANGLE AT THE CENTER OF EACH GRID SQ
C        COSINE ZENITH ANGLE ONCE EACH HOUR
C
      DO 18 I=1,LIHLF
      DO 18 JCOUNT = 1,        44
      COSZEN(I,JCOUNT)=SSLDC*ALAT(I,JCOUNT)+CSLDC*ALAT(I+LIHLF,JCOUNT) *
     1(CSLHR*ALON(I+LIHLF,JCOUNT)+SSLHR*ALON(I,JCOUNT))
      IF(COSZEN(I,JCOUNT).LE.0.17365E0)COSZEN(I,JCOUNT) = 0.0E0
      COSZEN(I+LIHLF,JCOUNT) = SSLDC*ALAT(LIHLF+1-I,JCOUNT)+CSLDC*ALAT
     1(  53 -I,JCOUNT)*(CSLHR*ALON(  53 -I,JCOUNT)-SSLHR*
     A     ALON(LIHLF+1-I,JCOUNT))
      IF(COSZEN(I+LIHLF,JCOUNT).LE.0.17365E0)COSZEN(I+LIHLF,JCOUNT)
     A  = 0.0E0
 18   CONTINUE
      LJ22 =  45  - 2
      DO 7011 JCOUNT = 1,45
        COSZEN(  53 ,JCOUNT) = 0.0E0
        ALON(  53 ,JCOUNT) = 0.0E0
        ALAT(  53 ,JCOUNT) = 0.0E0
7011  CONTINUE
C
C  ZERO RADIATION HEATING/COOLING START OF EACH HOUR
C
      DO 75 K = 1,3
      DO 75 J = 1,45
      DO 75 I = 1,53
75    H2(I,J,K) = 0.0E0
 19   CONTINUE
C
C        SKIP TO HERE FOR OTHER  THAN FIRST STEP IN HOUR
C ADVANCE MASS VARIABLES AND MOISTURE, TS,WS, AND PSIG FORWARD
C  INCLUDING MOIST PHYSICS (SUB WATER) GRID AND SUB-GRID SCALE
C
      DO 99999 K = 1, 7
      K7OLD = K + LOLD * 7
      K7MID = K + LMID * 7
      K7NEW = K + LNEW * 7
      CALL TENPT
      IF ( K.NE.7 ) GO TO 99999
      CALL WATER
99999 CONTINUE
C
C  COMPUTE INTEGRATION STATISTICS EACH TIME STEP
C
      CALL STATS
C
C  CALCULATE VALUES OF SATURATED PREC WATER (SCALED BY SATRH)
C
      CALL SATM
C
C  ADVANCE MOMENTUM (US AND VS) FORWARD IN TIME
C
      DO 99998 K = 1, 7
      K7OLD = K + LOLD*7
      K7MID = K + LMID*7
      K7NEW = K + LNEW*7
      CALL TENUV
99998 CONTINUE
C
C  SET LATERAL BOUNDARY CONDITIONS ON US AND VS
C
      CALL SETBC
C
C  TIME SMOOTH US,VS,TS,WS, AND PSIG ALL BUT FIRST TIME STEP
C
      IF ( ITSW .EQ. 4 ) CALL TSMO
C
C  WRITE FORECAST (SIGMA STRIPS) TO DISK EVERY 6 (POST INTERVAL) HRS
C
      IF (IFWRTE) CALL WRTAPE
C
       RETURN
       END
