C==================================================================== 
      SUBROUTINE ASTR(PARA) 
C     CONVERT INSOL MAIN TO ASRT SUBROUTINE   8/21/79 
  
      REAL AE(19), BE(19), CE(19),
     1  AOB(47), BOB(47), COB(47),
     2  AOP(78), BOP(78), COP(78) 
  
      COMMON/NORBIT/AT, AXOB, AECC, APRE, APERH 
      REAL AT, AXOB, AECC, APRE, APERH
  
C     COMPUTED CONSTANTS
      REAL PI, PIR, PIRR
  
      INTEGER NEF, NEFF, NOB, NOBB, NOP, NOPP 
      REAL XOD, XOP, XOB
      REAL ARG, PRG, PRM, RP
      REAL ECC, PRE, T, PERH, PARA
      REAL XEC, XES 
      INTEGER I, IB 
      REAL Y, Z 

CD      SAVE AE, BE, CE, AOB, BOB, COB, AOP, BOP, COP,
CD     2      AT, AXOB, AECC, APRE, APERH,
CD     1      NEFF, NOBB, NOPP, PI, PIR, PIRR
      
C     ASTRONOMICAL TIME IN YEARS(PAST TIME NEGATIVE)
      T  = PARA 
 
C     COMPUTATION OF ORBITAL PARAMETERS 
  
C     COMPUTE SERIES FOR ECC ECCENTRICITY TIMES SIN AND COS 
      XES=0.00
      XEC=0.00
      I=NEFF+1
      DO 4 IB=1,NEFF
        I=I-1 
        ARG=BE(I)*T+CE(I) 
        XES=XES+AE(I)*SIN(ARG)
        XEC=XEC+AE(I)*COS(ARG)
    4 CONTINUE
      ECC=SQRT(XES*XES+XEC*XEC) 
  
C     RP IS LONGITUDE OF PERHELION MEASURED 
C     FROM THE EQUINOX OF REFERENCE (FIXED) 
C     NORMALIZED TO 0 TO 360 DEGREES
      IF (ECC .LE. 1.E-6) THEN
        RP = 0.0
       ELSE 
        RP = -ATAN2(XES,-XEC) + PI
      ENDIF 
C     IF (ECC .GT. 1.0E-6) GO TO 203
C     RP = 0.0
C     GO TO 204 
C 203 RP = -ATAN2(XES,-XEC)+PI
  
C     PRG IS GENERAL PRECESSION IN LONGITUDE
  204 PRG = PRM*T 
      I = NOPP+1
      DO 5 IB  = 1,NOPP 
        I = I-1 
        PRG = PRG+AOP(I)*SIN(BOP(I)*T +COP(I))
    5 CONTINUE
      PRG = PRG/3600.+XOP 
  
C     PERH IS LONGITUDE OF PERIHELION 
C     MEASURED FROM THE MOVING AUTUMNAL EQUINOX 
C     NORMALIZED TO 0 TO 360 DEGREES
C      (ANDRE BERGER REFERS TO HIS VARIABLES AS MEASURED FROM 
C     THE VERNAL EQUINOX BUT IN THE PROGRAM USES VALUES 180 
C     DEGREES AWAY FROM THIS. THIS CAN CAUSE CONFUSION. ) 
      PERH = RP/PIR+PRG 
      IF (ABS(PERH).GT.360.) THEN 
        IF (PERH .GT. 360.) THEN
          PERH = PERH - 360.* INT(PERH/ 360.) 
         ELSE 
          PERH = PERH + 360.* (INT(ABS(PERH)/360.)+1) 
        ENDIF 
      ENDIF 
      IF (PERH .LT. 0.0) PERH = PERH + 360. 
C  54 IF (PERH) 51,55,53
C  51 PERH = PERH+360.
C     GO TO 54
C  53 IF (PERH.LT.360) GO TO 55 
C     PERH = PERH-360 
C     GO TO 53
C  55 CONTINUE
  
C     PRE IS PRECESSIONAL PARAMETER 
      PRE = ECC*SIN(PERH*PIR) 
  
C     XOB IS OBLIQUITY
      XOB = XOD 
      I = NOBB+1
      DO 6 IB = 1,NOBB
        I = I-1 
        XOB = XOB+AOB(I)/3600.*COS(BOB(I)*T+COB(I)) 
    6 CONTINUE
C     TRANSFER RESULTS TO COMMON/NORBIT/
      AT    = T 
      AXOB  = XOB 
      AECC  = ECC 
      APRE  = PRE 
      APERH = PERH
  
      RETURN
      
C     ENTRY TO READ DATA CARDS AND INITIALIZE 
      ENTRY ASTSET(PARA)
  
      PI   = 3.1415926535898
      PIR  = PI/180.
      PIRR = PIR/3600.
  
C     READ AMPLITUDE A MEAN RATE B PHASEC 
C     FOR TERMS IN TRIG SERIES. THEY ARE
C     CONVERTED TO RADIANS IMMEDIATELY
C     NEF NOB NOP NUMBER OF TERMS READ IN 
  
C     ECCENTRICITY
      READ(5, 200 ) NEF
  200 FORMAT(I3)
  201 FORMAT(5X, 3F15.10) 
      DO 1 I=1, NEF 
        READ(5, 201 )AE(I), Y, Z
        BE(I)=Y*PIRR
        CE(I)=Z*PIR 
    1 CONTINUE
C 
C     OBLIQUITY 
      READ(5, 200 )NOB
      XOD = 23.320556 
      DO 3 I = 1, NOB 
        READ(5, 201 )AOB(I), Y, Z 
        BOB(I) = Y*PIRR 
        COB(I) = Z*PIR
    3 CONTINUE
  
C     GENERAL PRECESSION IN LONGITUDE 
      XOP = 3.392506
      PRM = 50.439373 
      READ(5,200 )NOP 
      DO 31 I = 1,NOP 
        READ(5,201 )AOP(I),Y,Z
        BOP(I) = Y*PIRR 
        COP(I) = Z*PIR
   31 CONTINUE
  
C     NEFF NOBB NOPP NUMBER OF TERMS KEPT FOR COMPUTATION.
C     THE NUMBER OF TERMS KEPT FOR COMPUTATION IS SET EQUAL 
C     TO THE NUMBER OF TERMS READ IN
      NEFF=NEF
      NOBB=NOB
      NOPP=NOP

      RETURN
  
      END
